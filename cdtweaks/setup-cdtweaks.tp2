BACKUP ~weidu_external/cdtweaks/backup~ // location to store files for
SUPPORT ~https://www.gibberlings3.net/forum/34-tweaks-anthology-forum/~ // URL displayed if install fails
AUTO_EVAL_STRINGS

ALWAYS

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @27 END // DLC Merger check
  ACTION_IF NOT VARIABLE_IS_SET enhanced_edition THEN BEGIN // check to make this happen only once per install
    INCLUDE ~cdtweaks/lib/always.tpa~
  END
  
END

VERSION ~v14~

README ~cdtweaks/languages/%LANGUAGE%/readme-cdtweaks.html~ ~cdtweaks/readme-cdtweaks.html~

AUTO_TRA ~%tra_location%/%s~

LANGUAGE
  ~English~
  ~english~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
LANGUAGE
  ~Czech (Translation by Razfallow, VlasÃ¡k)~
  ~czech~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/czech/game.tra~
  ~cdtweaks/languages/czech/weidu.tra~
LANGUAGE
  ~Francais (Translation by Anomaly, Elgaern the Dragon Slayer, elminster, Isaya)~
  ~french~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/french/game.tra~
  ~cdtweaks/languages/french/weidu.tra~
LANGUAGE
  ~Deutsch (Uebersetzung von Caswallon, Jester, Leonardo Watson)~
  ~german~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/german/game.tra~
  ~cdtweaks/languages/german/weidu.tra~
LANGUAGE
  ~Italiano (translation by Andrea Colombo, ilot, improb@bile; original EoU by Antonio Favata)~
  ~italian~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/italian/game.tra~
  ~cdtweaks/languages/italian/weidu.tra~
LANGUAGE
  ~Korean (Translation by web2air)~
  ~korean~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/korean/game.tra~
  ~cdtweaks/languages/korean/weidu.tra~
LANGUAGE
  ~Polski (Translation by Artanis, Cuttoth, dragionian, Evendur, Grjgori, Neminus, yarpen)~
  ~polish~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/polish/game.tra~
  ~cdtweaks/languages/polish/weidu.tra~
LANGUAGE
  ~Russian (Translation by aerie.ru, abalabokhin, Creepin, Domi, Kulyok, prowler, Saigon1983, Serdrick)~
  ~russian~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/russian/game.tra~
  ~cdtweaks/languages/russian/weidu.tra~
LANGUAGE
  ~Espanol (traducido por Clan DLAN, Bhasbuto, Ghildrean, Immortality, Januskza)~
  ~spanish~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/spanish/game.tra~
  ~cdtweaks/languages/spanish/weidu.tra~
LANGUAGE
  ~Simplified Chinese (Translation by c4_angel, distance20; oriignal BG2 Tweaks by Miranda)~
  ~schinese~
  ~cdtweaks/languages/english/game.tra~
  ~cdtweaks/languages/english/weidu.tra~
  ~cdtweaks/languages/schinese/game.tra~
  ~cdtweaks/languages/schinese/weidu.tra~
  ~cdtweaks/languages/schinese_prompts/prompts-%WEIDU_OS%.tra~
  ~cdtweaks/languages/schinese_prompts/setup-%WEIDU_OS%.tra~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Automated Installer                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/*
BEGIN @100 DESIGNATED 0
REQUIRE_PREDICATE FILE_EXISTS ~cdtweaks/install.txt~ @101

ACTION_MATCH ~%LANGUAGE%~ WITH
  ~english~  BEGIN OUTER_SET lang = 0 END
  ~czech~    BEGIN OUTER_SET lang = 1 END
  ~french~   BEGIN OUTER_SET lang = 2 END
  ~german~   BEGIN OUTER_SET lang = 3 END
  ~italian~  BEGIN OUTER_SET lang = 4 END
  ~korean~   BEGIN OUTER_SET lang = 5 END
  ~polish~   BEGIN OUTER_SET lang = 6 END
  ~russian~  BEGIN OUTER_SET lang = 7 END
  ~spanish~  BEGIN OUTER_SET lang = 8 END
  ~schinese~ BEGIN OUTER_SET lang = 9 END
  DEFAULT OUTER_SET lang = 0
END  

ACTION_IF ("%WEIDU_OS%" STRING_COMPARE_CASE "unix" = 0) BEGIN
  OUTER_SPRINT exe ~weidu~
END ELSE BEGIN
  OUTER_SPRINT exe ~setup-cdtweaks~
END

INCLUDE ~cdtweaks/install.txt~

OUTER_SPRINT component_list ~~

ACTION_PHP_EACH cdtweaks_install AS component => value BEGIN

  ACTION_IF value BEGIN OUTER_SPRINT component_list ~%component_list% %component%~ END // concatenate
  
END

OUTER_INNER_PATCH_SAVE component_list ~%component_list%~ BEGIN
  REPLACE_TEXTUALLY ~comp~ ~~
END

AT_EXIT ~%exe% --language %lang% --force-install-list %component_list% --noautoupdate~
//PRINT ~ would try %exe% --language %lang% --force-install-list %component_list%~
*/

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove Helmet Animations                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1000 DESIGNATED 10
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_remove_helmets~

ACTION_IF enhanced_edition OR game_includes_soa OR game_is_ca OR tutu_gen OR game_is_iwd-in-bg2 BEGIN
  OUTER_SET check_anim = 1
END ELSE BEGIN
  OUTER_SET check_anim = 0
END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c itemtype ELSE 0
  PATCH_IF (itemtype = 7) BEGIN
    PATCH_IF check_anim = 1 BEGIN
      READ_SHORT 0x22 anim ELSE 0
      PATCH_IF NOT ((anim = 0x5759) OR (anim = 0x575a)) BEGIN
        WRITE_SHORT 0x22 0
      END
    END ELSE BEGIN
      WRITE_SHORT 0x22 0
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Imoen's Avatar to Mage                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2000 DESIGNATED 20
GROUP @11
REQUIRE_PREDICATE GAME_IS ~bgt eet bg2ee soa tob~ @25
LABEL ~cd_tweaks_imoen_avatar_change~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Imoen animation adjustments
  READ_LONG   0x08 name  ELSE 0
  READ_SHORT  0x28 anim  ELSE 0
  READ_BYTE  0x273 class ELSE 0
  PATCH_IF (((name = 16453) OR (name = 16454)) AND (class = 13) AND (anim = 25360)) BEGIN
    WRITE_SHORT 0x28 25104
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Nalia's Avatar to Thief                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3000 DESIGNATED 30
GROUP @11
REQUIRE_PREDICATE GAME_IS ~bgt eet bg2ee soa tob~ @25
LABEL ~cd_tweaks_nalia_avatar_change~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Nalia animation adjustments
  READ_LONG   0x08 name  ELSE 0
  READ_SHORT  0x28 anim  ELSE 0
  READ_BYTE  0x273 class ELSE 0
  PATCH_IF (((name = 9102) OR (name = 9103)) AND (class = 13) AND (anim = 25104)) BEGIN
    WRITE_SHORT 0x28 25360
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Viconia's Skin Color to Dark Blue         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @4000 DESIGNATED 40
GROUP @11
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_viconia_skin_color~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~ // Viconia skin color change
  READ_LONG 0x08 "name" ELSE 0
  PATCH_IF (("%name%" = 6132) OR ("%name%" = 9489) OR ("%name%" = 9508)) BEGIN
    WRITE_BYTE 0x2F 96
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Avatar Morphing Script                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @5000 DESIGNATED 50
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~1pp/1pp.tp2~ ~113~ @24 // don't install if 1pp smart avatar/armor switching installed
LABEL ~cd_tweaks_avatar_morphing_script~

COMPILE ~cdtweaks/baf/0ikmorph.baf~
        ~cdtweaks/baf/ikmorph.baf~
        ~cdtweaks/dlg/ikmorph.d~

MOVE ~override/0ikmorph.bcs~ ~scripts/0ikmorph.bs~

APPEND ~scrpdesc.2da~ ~0IKMorph IKMorph1 IKMorph2~

COPY_EXISTING ~scrpdesc.2da~ ~override~
  REPLACE ~IKMorph1~ @5001
  REPLACE ~IKMorph2~ @5002

COPY ~cdtweaks/cre/ikmorph.cre~  ~override~
     ~cdtweaks/eff/ikmorph.eff~  ~override~
     ~cdtweaks/spl/ikmorph.spl~  ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Weapon Animation Tweaks                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @6000 DESIGNATED 60
GROUP @11
LABEL ~cd_tweaks_weapon_animation_tweaks~

ACTION_IF original_bg1 OR original_iwd OR game_is_iwd2 OR game_is_pst THEN BEGIN // exclude pstee
  OUTER_SET use_type = 1
END ELSE BEGIN
  OUTER_SET use_type = 0
END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // animation adjustments
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_BYTE  0x18 flags
    READ_SHORT 0x1c type
    READ_ASCII 0x22 anim (2)
    READ_BYTE  0x31 prof
    READ_LONG  0x64 abil_off
    READ_SHORT 0x68 abil_num
    PATCH_IF ((((use_type = 0) AND ((prof = 100) OR (prof = 101))) OR // for bg2-based games w/ proficiencies
               ((use_type = 1) AND (type = 22))) AND                  // otherwise use item type
               ("%anim%" STRING_COMPARE_CASE "ms" = 0)) BEGIN         // and has the morningstar animation
      WRITE_ASCII 0x22 "MC" // change morningstars to mace animations
    END
    PATCH_IF (((flags & BIT1) = 0) AND                         // one-handed...
              (("%anim%" STRING_COMPARE_CASE "cl" = 0) OR      // club
               ("%anim%" STRING_COMPARE_CASE "mc" = 0) OR      // mace
               ("%anim%" STRING_COMPARE_CASE "ms" = 0))) BEGIN // or morningstar
      FOR (index = 0 ; index < abil_num ; ++index) BEGIN
        READ_BYTE (abil_off + (index * 0x38)) type
        PATCH_IF (type = 1) BEGIN
          WRITE_SHORT (abil_off + 0x2c + (index * 0x38)) 50 // Overhead
          WRITE_SHORT (abil_off + 0x2e + (index * 0x38)) 50 // Backhand
          WRITE_SHORT (abil_off + 0x30 + (index * 0x38))  0 // Thrusting
        END
      END
    END ELSE
    PATCH_IF ((("%anim%" STRING_COMPARE_CASE "sp" = 0) AND ((flags & BIT1) = BIT1)) OR  // two-handed spears
              (("%anim%" STRING_COMPARE_CASE "ss" = 0) AND ((flags & BIT1) = 0))) BEGIN // one-handed short swords
      FOR (index = 0 ; index < abil_num ; ++index) BEGIN
        READ_BYTE (abil_off + (index * 0x38)) type
        PATCH_IF (type = 1) BEGIN
          WRITE_SHORT (abil_off + 0x2c + (index * 0x38)) 10 // Overhead
          WRITE_SHORT (abil_off + 0x2e + (index * 0x38)) 20 // Backhand
          WRITE_SHORT (abil_off + 0x30 + (index * 0x38)) 70 // Thrusting
        END
      END
    END ELSE
    PATCH_IF (("%anim%" STRING_COMPARE_CASE "qs" = 0) AND ((flags & BIT1) = BIT1)) BEGIN // two-handed staves
      FOR (index = 0 ; index < abil_num ; ++index) BEGIN
        READ_BYTE (abil_off + (index * 0x38)) type
        PATCH_IF (type = 1) BEGIN
          WRITE_SHORT (abil_off + 0x2c + (index * 0x38)) 20 // Overhead
          WRITE_SHORT (abil_off + 0x2e + (index * 0x38)) 70 // Backhand
          WRITE_SHORT (abil_off + 0x30 + (index * 0x38)) 10 // Thrusting
        END
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Icewind Dale Casting Graphics                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @7000 DESIGNATED 70
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~iwdification/setup-iwdification.tp2~ ~10~ @16
LABEL ~cd_tweaks_iwd_casting_graphics~

ACTION_IF enhanced_edition BEGIN

  ACTION_FOR_EACH file IN cgabjura cgaltera cgconjur cgdivina cgenchan cgillusi cginvoca cgnecrom BEGIN

    COPY ~cdtweaks/bam/%file%_ee.bam~ ~override/%file%.bam~
    COPY ~cdtweaks/pro/cast_iwd.pro~  ~override/%file%.pro~
      WRITE_ASCIIE 0x104 ~%file%~ #8
         
  END

END ELSE BEGIN

  ACTION_FOR_EACH file IN cgabjura cgaltera cgconjur cgdivina cgenchan cgillusi cginvoca cgnecrom BEGIN

    COPY ~cdtweaks/bam/%file%.bam~ ~override~
         
  END

END  

LAF HANDLE_AUDIO STR_VAR audio_path = ~cdtweaks/wav/casting~ oggdec_path = ~cdtweaks/wav~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BG Casting Graphics                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @7020 DESIGNATED 72
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob ca pst pstee~ @25
LABEL ~cd_tweaks_bg_casting_graphics~

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_map_casting BEGIN
  abjurcg => cgabjura // abjuration
  altercg => cgaltera // alteration
  conjucg => cgconjur // conjuration
  divincg => cgdivina // divination
  enchacg => cgenchan // enchantment
  illuscg => cgillusi // illusion
  invoccg => cginvoca // invocation
  necrocg => cgnecrom // necromancy
END

ACTION_IF enhanced_edition THEN BEGIN

  ACTION_PHP_EACH cd_map_casting AS iwd => bg2 BEGIN

    COPY ~cdtweaks/bam/%iwd%_ee.bam~ ~override/%iwd%.bam~
         ~cdtweaks/bam/%iwd%_ee.bam~ ~override/%bg2%.bam~ // iwdee has extra unused set, but replace anyway
    COPY ~cdtweaks/pro/cast_bg.pro~  ~override/%bg2%.pro~
      WRITE_ASCIIE 0x104 ~%iwd%~ #8
      
  END

END ELSE BEGIN

  ACTION_IF game_is_iwd-in-bg2 THEN BEGIN

    ACTION_PHP_EACH cd_map_casting AS iwd => bg2 BEGIN
    
      COPY ~cdtweaks/bam/%iwd%.bam~ ~override/%bg2%.bam~
      
    END  

  END ELSE BEGIN // original iwd

    ACTION_PHP_EACH cd_map_casting AS iwd => bg2 BEGIN

      COPY ~cdtweaks/bam/%iwd%.bam~ ~override~
  
    END
  
  END

END

LAF HANDLE_AUDIO STR_VAR audio_path = ~cdtweaks/wav/casting_bg~ oggdec_path = ~cdtweaks/wav~ END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Restore SoA Logo for ToB Games                             \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @8000 DESIGNATED 80
GROUP @11
REQUIRE_PREDICATE GAME_IS ~tob~ @25
LABEL ~cd_tweaks_restore_soa_loading_screen~

//Force the game to use the SoA files in the biffs
MOVE ~override/loadcntr.bam~ ~cdtweaks/backup~
     ~override/gprogbar.mos~ ~cdtweaks/backup~
     ~override/gtrspsk.mos~  ~cdtweaks/backup~
     ~override/gtrspsk2.mos~ ~cdtweaks/backup~
     ~override/stoneopt.mos~ ~cdtweaks/backup~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Restore original Logo for HoW Games                        \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @8020 DESIGNATED 82
GROUP @11
REQUIRE_PREDICATE GAME_IS ~how totlm~   @25 // HoW required
LABEL ~cd_tweaks_restore_iwd_loading_screen~

COPY ~cdtweaks/mos/gprogbar.mos~ ~override~
     ~cdtweaks/mos/gtrbpbar.mos~ ~override~
     ~cdtweaks/mos/gtrbpbg.mos~  ~override~
     ~cdtweaks/mos/gtrbpcap.mos~ ~override~
     ~cdtweaks/mos/gtrbpsk2.mos~ ~override~
     ~cdtweaks/mos/gtrbpsk.mos~  ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove icons added by equipping items            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @9000 DESIGNATED 90
GROUP @11
LABEL ~cd_tweaks_remove_portrait_icons~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 142 END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Commoners use drab colors                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @10000 DESIGNATED 100
GROUP @11 
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~iwdification/setup-iwdification.tp2~ ~20~ @16
LABEL ~cd_tweaks_drab_commoners~

COPY_EXISTING ~randcolr.2da~ ~override~
  REPLACE_TEXTUALLY ~HairSet1~ ~CD_DELETE_ME HairSet1~
  SET_2DA_ENTRY  2  9 15 114 // AthPeasantMajor (208)
  SET_2DA_ENTRY  3  9 15 107
  SET_2DA_ENTRY  4  9 15  36
  SET_2DA_ENTRY  5  9 15  37
  SET_2DA_ENTRY  6  9 15  38
  SET_2DA_ENTRY  7  9 15  39
  SET_2DA_ENTRY  8  9 15  40
  SET_2DA_ENTRY  9  9 15  41
  SET_2DA_ENTRY 10  9 15  42
  SET_2DA_ENTRY 11  9 15  43
  SET_2DA_ENTRY  2 10 15  63 // AthPeasantMinor (209)
  SET_2DA_ENTRY  3 10 15  64
  SET_2DA_ENTRY  4 10 15  65
  SET_2DA_ENTRY  5 10 15  66
  SET_2DA_ENTRY  6 10 15  87
  SET_2DA_ENTRY  7 10 15  91
  SET_2DA_ENTRY  8 10 15  92
  SET_2DA_ENTRY  9 10 15  93
  SET_2DA_ENTRY 10 10 15  94
  SET_2DA_ENTRY 11 10 15  95
  SET_2DA_ENTRY  2 13 15 114 // TradePeasantMajor (212)
  SET_2DA_ENTRY  3 13 15 107
  SET_2DA_ENTRY  4 13 15  36
  SET_2DA_ENTRY  5 13 15  37
  SET_2DA_ENTRY  6 13 15  38
  SET_2DA_ENTRY  7 13 15  39
  SET_2DA_ENTRY  8 13 15  40
  SET_2DA_ENTRY  9 13 15  41
  SET_2DA_ENTRY 10 13 15  42
  SET_2DA_ENTRY 11 13 15  43
  SET_2DA_ENTRY  2 14 15  63 // TradePeasantMinor (213)
  SET_2DA_ENTRY  3 14 15  64
  SET_2DA_ENTRY  4 14 15  65
  SET_2DA_ENTRY  5 14 15  66
  SET_2DA_ENTRY  6 14 15  87
  SET_2DA_ENTRY  7 14 15  91
  SET_2DA_ENTRY  8 14 15  92
  SET_2DA_ENTRY  9 14 15  93
  SET_2DA_ENTRY 10 14 15  94
  SET_2DA_ENTRY 11 14 15  95
  REPLACE_TEXTUALLY ~CD_DELETE_ME~ ~~
  PRETTY_PRINT_2DA
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Better Icons                                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @11000 DESIGNATED 110
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_icon_improvements~

COPY ~cdtweaks/bam/cdgchan.bam~  ~override~
     ~cdtweaks/bam/cdgelchn.bam~ ~override~
     ~cdtweaks/bam/cdgdwchn.bam~ ~override~
     ~cdtweaks/bam/cdgleat.bam~  ~override~
     ~cdtweaks/bam/gchan01.bam~  ~override~
     ~cdtweaks/bam/gclck01.bam~  ~override~
     ~cdtweaks/bam/gclck02.bam~  ~override~
     ~cdtweaks/bam/gleat01.bam~  ~override~
     ~cdtweaks/bam/iclck09.bam~  ~override~
     ~cdtweaks/bam/iclck10.bam~  ~override~
     ~cdtweaks/bam/iclck11.bam~  ~override~
     ~cdtweaks/bam/iclck12.bam~  ~override~
     ~cdtweaks/bam/iclck13.bam~  ~override~
     ~cdtweaks/bam/iclck15.bam~  ~override~
     ~cdtweaks/bam/iclck16.bam~  ~override~
     ~cdtweaks/bam/iclck17.bam~  ~override~
     
INCLUDE ~cdtweaks/lib/common_armor_list.tpa~

ACTION_PHP_EACH cd_common_armor_list AS params => item BEGIN
  
  ACTION_IF (("%params_2%" STRING_COMPARE_CASE "leather" = 0) OR
             ("%params_2%" STRING_COMPARE_CASE "chain" = 0) OR
             ("%params_2%" STRING_COMPARE_CASE "chain_elven" = 0) OR
             ("%params_2%" STRING_COMPARE_CASE "chain_drow" = 0)) BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_SHORT 0x1c type
      PATCH_IF type = 2 BEGIN // sanity check
        READ_ASCII 0x44 icon ELSE 0
        PATCH_IF (("%params_2%" STRING_COMPARE_CASE "leather" = 0) AND // leather using generic leather icons
                  (("%icon%" STRING_COMPARE_CASE "gleat01" = 0) OR
                   ("%icon%" STRING_COMPARE_CASE "_gleat01" = 0))) BEGIN
          WRITE_ASCII 0x44 ~cdgleat~ #8
        END
        PATCH_IF (("%icon%" STRING_COMPARE_CASE "gchan01" = 0) OR
                  ("%icon%" STRING_COMPARE_CASE "_gchan01" = 0)) BEGIN // (elven) chain using generic chain icons
          PATCH_IF ("%params_2%" STRING_COMPARE_CASE "chain" = 0) BEGIN // chain
            WRITE_ASCII 0x44 ~cdgchan~ #8
          END ELSE
          PATCH_IF ("%params_2%" STRING_COMPARE_CASE "chain_elven" = 0) BEGIN // elven chain
            WRITE_ASCII 0x44 ~cdgelchn~ #8
          END ELSE
          PATCH_IF ("%params_2%" STRING_COMPARE_CASE "chain_drow" = 0) BEGIN // drow elven chain
            WRITE_ASCII 0x44 ~cdgdwchn~ #8
          END
        END
      END  
      BUT_ONLY
      
  END // appearance check

END // php_each

// if Tutu, move BG2 icons to Tutu defaults
ACTION_IF FILE_EXISTS_IN_GAME ~_gleat01.bam~ THEN BEGIN

  COPY_EXISTING ~gleat01.bam~ ~override/_gleat01.bam~
                ~gchan01.bam~ ~override/_gchan01.bam~

END

// Valygar's armor should use chain graphic on avatars
ACTION_IF FILE_EXISTS_IN_GAME ~npchan.itm~ BEGIN

  COPY_EXISTING ~npchan.itm~ ~override~
    WRITE_ASCII 0x22 ~3A~ #2
    BUT_ONLY

END

ACTION_FOR_EACH item IN quiver02 quiver04 BEGIN

  ACTION_IF ((FILE_EXISTS_IN_GAME ~%item%.itm~) AND (FILE_EXISTS_IN_GAME ~iquiver0.bam~)) THEN BEGIN
  
    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_ASCII 0x3a ~iquiver0~ #8
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Avatar When Wearing Robes or Armor        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @12000 DESIGNATED 120
DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Force all dialogue to pause                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @13000 DESIGNATED 130
GROUP @11 
LABEL ~cd_tweaks_force_all_dialogue_to_pause~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.dlg$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2f) THEN BEGIN // protects against invalid files
    READ_LONG  0x0c offset_state ELSE 0
    PATCH_IF (offset_state = 0x30) BEGIN // adds pause field to older dlg formats
      PATCH_FOR_EACH offset IN 0x18 0x20 0x28 BEGIN // loop through state, response trigger offsets
        READ_LONG offset loop_off
        READ_LONG (offset + 0x04) loop_num
        FOR (index = 0 ; index < loop_num ; ++index) BEGIN
          WRITE_LONG (loop_off + (index * 0x08)) (THIS + 0x04)
        END
      END
      // push all other offsets back 0x04
      PATCH_FOR_EACH offset IN 0x0c 0x14 0x18 0x20 0x28 BEGIN
        WRITE_LONG offset (THIS + 0x04)
      END
      INSERT_BYTES 0x30 0x04
    END ELSE
    PATCH_IF (offset_state = 0x34) BEGIN // otherwise just sets to pause
      WRITE_LONG 0x30 0
    END
  END
  BUT_ONLY

// pausing actually causing issues with drow duels, so:
ACTION_IF FILE_EXISTS_IN_GAME ~udlesa.bcs~ BEGIN

  COPY_EXISTING ~udlesa.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(IF[%TAB% %LNL%%MNL%%WNL%]+!CombatCounter(0)[%TAB% %LNL%%MNL%%WNL%]+Global("DuelOn","AR2202",0)\)\([%TAB% %LNL%%MNL%%WNL%]+\)\(THEN\)~
        ~\1\2Global("PlayerDuelingLesaonar","GLOBAL",0)\2\3~
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Fix Boo's terrible, horrible, awful squeak       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @14000 DESIGNATED 140
GROUP @11 // 
REQUIRE_PREDICATE NOT GAME_IS ~iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee~ @25
LABEL ~cd_tweaks_fix_boo_squeak~

COPY ~cdtweaks/wav/gam_48.wav~ ~override~
/*
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unique item names                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// unique name                                      \\\\\
/////                                                  \\\\\

BEGIN @15001 DESIGNATED 150
SUBCOMPONENT @15000
GROUP @11 // REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee ca~ @25
REQUIRE_PREDICATE FILE_EXISTS ~cdtweaks/languages/%LANGUAGE%/unique.tra~ @7 // skip if tra file for language not available

LOAD_TRA ~%tra_location%/%LANGUAGE%/unique.tra~

INCLUDE ~cdtweaks/lib/unique.tpa~

/////                                                  \\\\\
///// unique name                                      \\\\\
/////                                                  \\\\\

BEGIN @15100 DESIGNATED 151
SUBCOMPONENT @15000
GROUP @11 // REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee ca~ @25
REQUIRE_PREDICATE FILE_EXISTS ~cdtweaks/languages/%LANGUAGE%/uniqueplus.tra~ @7 // skip if tra file for language not available

LOAD_TRA ~%tra_location%/%LANGUAGE%/uniqueplus.tra~

INCLUDE ~cdtweaks/lib/unique.tpa~

/////                                                  \\\\\
///// generic names                                    \\\\\
/////                                                  \\\\\

BEGIN @15200 DESIGNATED 152
SUBCOMPONENT @15000
GROUP @11 // REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee ca~ @25
REQUIRE_PREDICATE FILE_EXISTS ~cdtweaks/languages/%LANGUAGE%/uniquegeneric.tra~ @7 // skip if tra file for language not available

LOAD_TRA ~%tra_location%/%LANGUAGE%/uniquegeneric.tra~

INCLUDE ~cdtweaks/lib/unique.tpa~
*/

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Magic Shields Glow                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @16000 DESIGNATED 160
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_glowing_magical_shields~

OUTER_SET column = 4
INCLUDE ~cdtweaks/lib/mpalette.tpa~

OUTER_SET primary    = 34 // doubles as pulse location
OUTER_SET secondary  = 37
OUTER_SET tertiary   = 32
OUTER_SET quaternary = 36
OUTER_SET quinary    = 33

ACTION_IF original_iwd THEN BEGIN // swap primary/secondary for iwd

  OUTER_SET primary    = 37 // doubles as pulse location
  OUTER_SET secondary  = 34

END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  PATCH_IF ((type = 12) OR (type = 41) OR (type = 47) OR (type = 49) OR (type = 53)) BEGIN // 12 is bg shield type, all others are iwd
    READ_BYTE 0x18 flags
    PATCH_IF ((flags & BIT6) = BIT6) BEGIN // magical
      SET color_primary = "-1"
      SET color_secondary = "-1"
      SET color_tertiary = "-1"
      SET color_quaternary = "-1"
      SET color_quinary = "-1"
      SET color_pulse = 0
      READ_LONG  0x6a fx_off
      READ_SHORT 0x70 fx_num
      FOR (index = 0 ; index < fx_num ; ++index) BEGIN
        READ_SHORT (fx_off +        (index * 0x30)) opcode
        READ_LONG  (fx_off + 0x04 + (index * 0x30)) parameter1
        READ_LONG  (fx_off + 0x08 + (index * 0x30)) parameter2
        PATCH_IF (opcode = 9) BEGIN
          SET color_pulse = 1
          SET index = fx_num // kill search
        END
        PATCH_IF ((opcode = 7) AND (color_primary < 0)) BEGIN // if we have a primary color, stop caring (though keep searching in case of pulse)
          PATCH_IF (parameter2 = primary) BEGIN
            SET color_primary = parameter1
          END ELSE
          PATCH_IF (parameter2 = secondary) BEGIN
            SET color_secondary = parameter1
          END ELSE
          PATCH_IF (parameter2 = tertiary) BEGIN
            SET color_tertiary = parameter1
          END ELSE
          PATCH_IF (parameter2 = quaternary) BEGIN
            SET color_quaternary = parameter1
          END ELSE
          PATCH_IF (parameter2 = quinary) BEGIN
            SET color_quinary = parameter1
          END
        END
      END
      PATCH_IF (color_pulse = 0) BEGIN // don't change anything if we already have a pulse of any kind
        PATCH_IF color_primary < 0 BEGIN // if no primary color set, use one of the existing colors for pulse basis
          PATCH_IF (color_secondary >= 0) BEGIN
            SET color_primary = color_secondary
          END ELSE
          PATCH_IF (color_tertiary >= 0) BEGIN
            SET color_primary = color_tertiary
          END ELSE
          PATCH_IF (color_quaternary >= 0) BEGIN
            SET color_primary = color_quaternary
          END ELSE
          PATCH_IF (color_quinary >= 0) BEGIN
            SET color_primary = color_quinary
          END
        END ELSE BEGIN // if we do have a primary color, remove it
          LPF DELETE_EFFECT INT_VAR check_headers = 0 match_opcode = 7 match_parameter2 = primary END
        END
        PATCH_IF color_primary >= 0 BEGIN // if there's still no color set, it means item has no shield coloring at all and we won't mess with it (think lyre.itm from IWD)
          SET color_pulse = 0x64646400  // set to generic white, try to pick better in next step
          PATCH_IF ((color_primary < height) AND (color_primary >= 0)) BEGIN
            SET color_pulse = $cdcolors(~%color_primary%~) // set pulse to match mpalette entry
          END
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 9 target = 1 parameter1 = color_pulse parameter2 = (primary + (40 << 16)) timing = 2 END // now actually add pulse effect
        END
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unique Icons                                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @17000 DESIGNATED 170
GROUP @11
REQUIRE_PREDICATE GAME_IS ~iwdee iwd how totlm iwd-in-bg2 soa tob bg2ee eet bgt ca bgee tutu tutu_totsc bg1 totsc~ @25
LABEL ~cd_tweaks_unique_icons~

ACTION_IF NOT VARIABLE_IS_SET always_install_unique_icons THEN BEGIN // can be updated in the config style
  OUTER_SET always_install_unique_icons = 0
END

ACTION_IF original_bg1 OR original_iwd BEGIN // old helmet animations
  OUTER_SPRINT h4 ~h3~
  OUTER_SPRINT h5 ~h3~
  OUTER_SPRINT h6 ~h0~
END ELSE BEGIN
  OUTER_SPRINT h4 ~h4~
  ACTION_IF ((MOD_IS_INSTALLED ~1pp/1pp.tp2~ ~208~) OR (enhanced_edition)) BEGIN
    OUTER_SPRINT h5 ~h5~
    OUTER_SPRINT h6 ~h6~
  END ELSE BEGIN
    OUTER_SPRINT h5 ~h0~ // no good plumed helmets in base oBG2
    OUTER_SPRINT h6 ~h0~ // no good vertically horned helmets in base oBG2
  END  
END  

ACTION_IF game_includes_iwd BEGIN

  INCLUDE ~cdtweaks/lib/unique_icons_iwd.tpa~ // load reference array

END ELSE BEGIN  

  INCLUDE ~cdtweaks/lib/unique_icons_bg.tpa~ // load reference array

  COPY_EXISTING ~plat19.itm~ ~override~ // make (wish) full plate +2 look like normal full plate +2, not full plate +1 (plate of the dark)
    WRITE_ASCII 0x58 ~cplat09~ #8
    BUT_ONLY IF_EXISTS

END  

ACTION_PHP_EACH cd_unique_icons AS params => item BEGIN
  
  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ BEGIN
  
    COPY_EXISTING ~%item%.itm~ ~override~
      SET replaced = 0
      READ_ASCII   0x3a icon_old
      PATCH_IF (("%always_install_unique_icons%" = 1) OR
                ("%icon_old%" STRING_COMPARE_CASE "%params_1%" = 0)) BEGIN 
        SET replaced = 1
        PATCH_IF ("%params_3%" STRING_COMPARE_CASE "null") BEGIN // if animation
          WRITE_ASCIIE 0x22 ~%params_3%~
        END  
        READ_ASCII 0x22 anim (2)
        WRITE_ASCIIE 0x3a ~%params_0%~ #8
        READ_LONG    0x64 abil_off ELSE 0
        READ_SHORT   0x68 abil_num ELSE 0
        FOR (index = 0 ; index < abil_num ; ++index) BEGIN
          READ_ASCII (abil_off + 0x04 + (index * 0x38)) icon_abil
          PATCH_IF ((("%always_install_unique_icons%" = 1) AND ("%icon_abil%" STRING_COMPARE_CASE "%icon_old%" = 0)) OR
                    ("%icon_abil%" STRING_COMPARE_CASE "%params_1%" = 0)) BEGIN // if ability uses old icon, update it
            WRITE_ASCIIE (abil_off + 0x04 + (index * 0x38)) ~%params_0%~ #8
          END
        END
        CLEAR_ARRAY cd_unique_icons_coloring
        SET coloring = 0
        PATCH_IF ("%params_4%" STRING_COMPARE_CASE "armor" = 0) BEGIN 
          SET coloring = 1
          DEFINE_ASSOCIATIVE_ARRAY cd_unique_icons_coloring BEGIN    
            5 => ~%params_7%~      
            4 => ~%params_6%~  
            0 => ~%params_5%~    
          END
        END  
        PATCH_IF ("%params_4%" STRING_COMPARE_CASE "weapon" = 0) BEGIN 
          SET coloring = 1
          DEFINE_ASSOCIATIVE_ARRAY cd_unique_icons_coloring BEGIN    
            21 => ~%params_10%~     
            20 => ~%params_9%~   
            16 => ~%params_8%~    
          END 
          PATCH_IF ((original_bg1 OR original_iwd) AND ("%anim%" STRING_COMPARE_CASE "ax" = 0)) BEGIN // original axes
            DEFINE_ASSOCIATIVE_ARRAY cd_unique_icons_coloring BEGIN    
              16 => ~%params_10%~ // 16 is used as main ax color in obg/oiwd, not 21   
            END
          END 
        END   
        PATCH_IF ("%params_4%" STRING_COMPARE_CASE "shield" = 0) BEGIN 
          SET coloring = 1
          DEFINE_ASSOCIATIVE_ARRAY cd_unique_icons_coloring BEGIN    
            37 => ~%params_14%~      
            34 => ~%params_13%~         
            33 => ~%params_12%~  
            32 => ~%params_11%~ 
          END
        END  
        PATCH_IF ("%params_4%" STRING_COMPARE_CASE "helm" = 0) BEGIN 
          SET coloring = 1
          DEFINE_ASSOCIATIVE_ARRAY cd_unique_icons_coloring BEGIN     
            53 => ~%params_17%~    
            52 => ~%params_16%~  
            48 => ~%params_15%~   
          END
          PATCH_IF ((original_bg1 OR original_iwd) AND ("%anim%" STRING_COMPARE_CASE "h3" = 0)) BEGIN // plumed helmets
            DEFINE_ASSOCIATIVE_ARRAY cd_unique_icons_coloring BEGIN    
              50 => ~%params_15%~ // needs a separate plume color
            END
          END 
          PATCH_IF ((!MOD_IS_INSTALLED ~1pp/1pp.tp2~ ~208~) AND (!enhanced_edition)) BEGIN
            DEFINE_ASSOCIATIVE_ARRAY cd_unique_icons_coloring BEGIN    
              48 => ~%params_17%~ // don't use plume as main color
            END
          END 
        END   
        PATCH_IF coloring BEGIN
          LPF DELETE_EFFECT INT_VAR silent = 1 check_headers = 0 match_opcode = 7 END
          PATCH_PHP_EACH cd_unique_icons_coloring AS loc => color BEGIN
            PATCH_IF IS_AN_INT color BEGIN
              LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 7 target = 1 parameter1 = color parameter2 = loc timing = 2 END
            END  
          END
        END  
      END  
      BUT_ONLY
  
    ACTION_IF ((FILE_EXISTS ~cdtweaks/bam/%params_0%.bam~) AND (replaced = 1) AND (!FILE_EXISTS_IN_GAME ~%params_0%.bam~)) THEN BEGIN // in case it's getting an existing icon; also only copy if needed

      COPY ~cdtweaks/bam/%params_0%.bam~ ~override~
    
    END
  
  END
        
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// unique containers                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// fixes only                                       \\\\\
/////                                                  \\\\\

BEGIN @18018 DESIGNATED 180
SUBCOMPONENT @18000
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgee bg2ee eet bgt tutu tutu_totsc how totlm iwdee iwd-in-bg2~ @25
LABEL ~cd_tweaks_unique_containers_fixes_only~

INCLUDE ~cdtweaks/lib/unique_containers_fixes.tpa~ // misc store fixes

/////                                                  \\\\\
///// icons and fixes                                  \\\\\
/////                                                  \\\\\

BEGIN @18021 DESIGNATED 181
SUBCOMPONENT @18000
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgee bg2ee eet bgt tutu tutu_totsc how totlm iwdee iwd-in-bg2~ @25
LABEL ~cd_tweaks_unique_containers_fixes_and_icons~

OUTER_SET names = 0 // don't set names
INCLUDE ~cdtweaks/lib/unique_containers_fixes.tpa~ // misc store fixes 
INCLUDE ~cdtweaks/lib/unique_containers_v2.tpa~ // do the thing

/////                                                  \\\\\
///// fixes, icons, names                              \\\\\
/////                                                  \\\\\

BEGIN @18020 DESIGNATED 182
SUBCOMPONENT @18000
GROUP @11
REQUIRE_PREDICATE GAME_IS ~soa tob bgee bg2ee eet bgt tutu tutu_totsc how totlm iwdee iwd-in-bg2~ @25
REQUIRE_PREDICATE FILE_EXISTS ~cdtweaks/languages/%LANGUAGE%/unique_containers.tra~ @7
LABEL ~cd_tweaks_unique_containers_full~

OUTER_SET names = 1 // set names
INCLUDE ~cdtweaks/lib/unique_containers_fixes.tpa~ // misc store fixes
INCLUDE ~cdtweaks/lib/unique_containers_v2.tpa~ // do the thing

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Maintain character colors                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// all helmets and shields                          \\\\\
/////                                                  \\\\\

BEGIN @19000 DESIGNATED 190
SUBCOMPONENT @19001
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_char_colors_all_helms_shields~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  PATCH_IF ((type = 7) OR (type = 12) OR (type = 41) OR (type = 47) OR (type = 49) OR (type = 53)) BEGIN // item type helm or shield
    PATCH_FOR_EACH loc IN 33 34 49 50 BEGIN // 33 - shield interior, 34 - shield panel, 49 - helm detail/plume, 50 - helm wings
      LPF DELETE_EFFECT INT_VAR match_opcode = 7 match_timing = 2 match_parameter2 = loc END                                    // delete color effect
      LPF ALTER_EFFECT  INT_VAR match_opcode = 8 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white glow
      LPF ALTER_EFFECT  INT_VAR match_opcode = 9 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white pulse
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// non-magical helmets and shields                  \\\\\
/////                                                  \\\\\

BEGIN @19100 DESIGNATED 191
SUBCOMPONENT @19001
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_char_colors_normal_helms_shields~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  READ_LONG 0x18 flags
  PATCH_IF (((flags & BIT6) = 0) AND ((type = 7) OR (type = 12) OR (type = 41) OR (type = 47) OR (type = 49) OR (type = 53))) BEGIN // item type helm or shield, flagged as non-magical
    PATCH_FOR_EACH loc IN 33 34 49 50 BEGIN // 33 - shield interior, 34 - shield panel, 49 - helm detail/plume, 50 - helm wings
      LPF DELETE_EFFECT INT_VAR match_opcode = 7 match_timing = 2 match_parameter2 = loc END                                    // delete color effect
      LPF ALTER_EFFECT  INT_VAR match_opcode = 8 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white glow
      LPF ALTER_EFFECT  INT_VAR match_opcode = 9 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white pulse
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// all helmets                                      \\\\\
/////                                                  \\\\\

BEGIN @19200 DESIGNATED 192
SUBCOMPONENT @19001
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_char_colors_all_helms~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  PATCH_IF (type = 7) BEGIN // item type helm or shield
    PATCH_FOR_EACH loc IN 49 50 BEGIN // 49 - helm detail/plume, 50 - helm wings
      LPF DELETE_EFFECT INT_VAR match_opcode = 7 match_timing = 2 match_parameter2 = loc END                                    // delete color effect
      LPF ALTER_EFFECT  INT_VAR match_opcode = 8 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white glow
      LPF ALTER_EFFECT  INT_VAR match_opcode = 9 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white pulse
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// non-magical helmets                              \\\\\
/////                                                  \\\\\

BEGIN @19300 DESIGNATED 193
SUBCOMPONENT @19001
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_char_colors_normal_helms~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  READ_LONG 0x18 flags
  PATCH_IF (((flags & BIT6) = 0) AND (type = 7)) BEGIN // item type helm, flagged as non-magical
    PATCH_FOR_EACH loc IN 49 50 BEGIN // 49 - helm detail/plume, 50 - helm wings
      LPF DELETE_EFFECT INT_VAR match_opcode = 7 match_timing = 2 match_parameter2 = loc END                                    // delete color effect
      LPF ALTER_EFFECT  INT_VAR match_opcode = 8 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white glow
      LPF ALTER_EFFECT  INT_VAR match_opcode = 9 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white pulse
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// all shields                                      \\\\\
/////                                                  \\\\\

BEGIN @19400 DESIGNATED 194
SUBCOMPONENT @19001
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_char_colors_all_shields~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  PATCH_IF ((type = 12) OR (type = 41) OR (type = 47) OR (type = 49) OR (type = 53)) BEGIN // item type shield
    PATCH_FOR_EACH loc IN 33 34 BEGIN // 33 - shield interior, 34 - shield panel
      LPF DELETE_EFFECT INT_VAR match_opcode = 7 match_timing = 2 match_parameter2 = loc END                                    // delete color effect
      LPF ALTER_EFFECT  INT_VAR match_opcode = 8 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white glow
      LPF ALTER_EFFECT  INT_VAR match_opcode = 9 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white pulse
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// non-magical shields                              \\\\\
/////                                                  \\\\\

BEGIN @19500 DESIGNATED 195
SUBCOMPONENT @19001
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_char_colors_normal_shields~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  READ_LONG 0x18 flags
  PATCH_IF (((flags & BIT6) = 0) AND ((type = 12) OR (type = 41) OR (type = 47) OR (type = 49) OR (type = 53))) BEGIN // item type shield, flagged as non-magical
    PATCH_FOR_EACH loc IN 33 34 BEGIN // 33 - shield interior, 34 - shield panel
      LPF DELETE_EFFECT INT_VAR match_opcode = 7 match_timing = 2 match_parameter2 = loc END                                    // delete color effect
      LPF ALTER_EFFECT  INT_VAR match_opcode = 8 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white glow
      LPF ALTER_EFFECT  INT_VAR match_opcode = 9 match_timing = 2 match_parameter2 = loc parameter1 = 0x64646400 silent = 1 END // change to generic white pulse
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove Annoying Visual Effects fr Equipped Items \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Remove annoying blur effect                      \\\\\
/////                                                  \\\\\

BEGIN @20000 DESIGNATED 200
GROUP @11
SUBCOMPONENT @20001
LABEL ~cd_tweaks_remove_equipped_blur_effects~

OUTER_SET remove_blur      = 1
OUTER_SET remove_spelltrap = 0

INCLUDE ~cdtweaks/lib/annoying_equipped_fx.tpa~

/////                                                  \\\\\
///// Remove Spell Trap and Reflection Effects         \\\\\
/////                                                  \\\\\

BEGIN @20002 DESIGNATED 3150
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
SUBCOMPONENT @20001
LABEL ~cd_tweaks_remove_equipped_spell_trap_effects~

OUTER_SET remove_blur      = 0
OUTER_SET remove_spelltrap = 1

INCLUDE ~cdtweaks/lib/annoying_equipped_fx.tpa~

/////                                                  \\\\\
///// Remove blur and CoM/ST effects                   \\\\\
/////                                                  \\\\\

BEGIN @20003 DESIGNATED 3151 // why 3151 instead of 201? WeiDU presents SUBCOMPONENTs in component order
GROUP @11
SUBCOMPONENT @20001
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_remove_equipped_blur_spell_trap_effects~

OUTER_SET remove_blur      = 1
OUTER_SET remove_spelltrap = 1

INCLUDE ~cdtweaks/lib/annoying_equipped_fx.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Replace Resist Fire/Cold Icon                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @21000 DESIGNATED 2010
GROUP @11
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_replace_fire_cold_resist_icons~

INCLUDE ~cdtweaks/lib/bg2fp_effect_batches.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
                          ~^.+\.itm$~ ~override~
                          ~^.+\.spl$~ ~override~
  LPF cd_apply_batch STR_VAR array_name = cd_cold_icons END
  LPF cd_apply_batch STR_VAR array_name = cd_fire_icons END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Enhanced Overlays for Colorblind Players                   \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @22000 DESIGNATED 220
GROUP @11
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet pstee~ @25
LABEL ~cd_tweaks_colorblind_overlays~

COPY ~cdtweaks/bam/stortint.bam~ ~override~ // red, unusable
     ~cdtweaks/bam/stortin2.bam~ ~override~ // blue, needs ID
     ~cdtweaks/bam/stortin3.bam~ ~override~ // green, learnable scroll

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// More Interjections                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @101000 DESIGNATED 1010
GROUP @13 
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_more_interjections~

ACTION_IF FILE_EXISTS_IN_GAME ~mrimrin1.itm~ THEN BEGIN // imoen romance compatibility code

  COPY_EXISTING ~imoen2j.dlg~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~!I[fs]ValidForPartyDialog\(ue\)?(Player\([2-6]\))~ ~!Range(Player\2,30)~
    END
    BUT_ONLY

END

COPY_EXISTING_REGEXP GLOB ~^.+\.bcs$~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~I[fs]ValidForPartyDialog\(ue\)?(~
                      ~InParty(~
  END
  IF ~16552~
  BUT_ONLY

COPY_EXISTING ~trigger.ids~ ~override~
  REPLACE_TEXTUALLY ~0x40A8~ ~0x4043~
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Alter HP Triggers for NPC Wounded Dialogues      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @102000 DESIGNATED 1020
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_alter_wounded_triggers~

// alter triggers in dialogues...
COMPILE ~cdtweaks/dlg/hptriggers.d~

// and corresponding triggers in scripts
COPY_EXISTING ~edwin.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPPercentLT(Myself,[12]5)~ ~HPPercentLT(Myself,50)~ // in case not fixed
    REPLACE_TEXTUALLY ~Global("BEdwin10","LOCALS",0)\([ %TAB%%LNL%%MNL%%WNL%]+Global("EdwintalksAerieJames","LOCALS",0)\)~ ~Global("BEdwin1","LOCALS",0)\1~
  END
  BUT_ONLY

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPLT("Aerie",10)~ ~HPPercentLT("Aerie",50)~
    REPLACE_TEXTUALLY ~HPLT("Nalia",10)~ ~HPPercentLT("Nalia",50)~
    REPLACE_TEXTUALLY ~HPLT(Myself,15)~  ~HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPGT(Myself,14)~  ~!HPPercentLT(Myself,50)~
  END
  BUT_ONLY

COPY_EXISTING ~mazzy.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPPercentLT("Valygar",20)~ ~HPPercentLT("Valygar",50)~
  END
  BUT_ONLY

COPY_EXISTING ~minsc.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPLT(Myself,20)~    ~HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPGT(Myself,19)~    ~!HPPercentLT(Myself,50)~
    REPLACE_TEXTUALLY ~HPLT("Keldorn",20)~ ~HPPercentLT("Keldorn",50)~
  END
  BUT_ONLY

COPY_EXISTING ~nalia.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPPercentLT("Aerie",25)~ ~HPPercentLT("Aerie",50)~
  END
  BUT_ONLY

COPY_EXISTING ~yoshimo.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~HPLT("Mazzy",20)~ ~HPPercentLT("Mazzy",50)~
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Reveal wilderness areas on map before chapter 6            \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @103000 DESIGNATED 1030
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_reveal_ch6_wilderness~

ACTION_IF ((FILE_EXISTS_IN_GAME ~cdbehbla.pro~) OR (enhanced_edition)) THEN BEGIN // easy fix if fixed worldmap present

  COPY_EXISTING ~worldmap.wmp~ ~override~
    READ_LONG 0x30 "area_num"
    READ_LONG 0x34 "area_off"
    FOR (index = 0 ; index < area_num ; index = index + 1) BEGIN
      READ_ASCII ("%area_off%" + ("%index%" * 0xf0)) "areafile"
      PATCH_IF (("ar1700" STRING_COMPARE_CASE "%areafile%" = 0) OR     // small teeth pass
                ("ar1800" STRING_COMPARE_CASE "%areafile%" = 0) OR     // north forest
                ("ar2600" STRING_COMPARE_CASE "%areafile%" = 0)) BEGIN // forest of tethir
        READ_BYTE  ("%area_off%" + 0x30 + ("%index%" * 0xf0)) "flags"
        WRITE_BYTE ("%area_off%" + 0x30 + ("%index%" * 0xf0)) ("%flags%" BOR 0b00000010)
      END
    END
    BUT_ONLY

END ELSE BEGIN

  // adds new entrance points to wmp areas that don't have one already
  COPY_EXISTING ~ar1700.are~ ~override~
                ~ar1800.are~ ~override~
                ~ar2600.are~ ~override~
    READ_LONG 0x68 ent_off
    PATCH_IF ("ar1700.are" STRING_COMPARE_CASE ~%SOURCE_FILE%~ = 0) BEGIN
      SET x_coord = 3375
      SET y_coord = 142
    END ELSE
    PATCH_IF ("ar1800.are" STRING_COMPARE_CASE ~%SOURCE_FILE%~ = 0) BEGIN
      SET x_coord = 1325
      SET y_coord = 56
    END ELSE BEGIN
      SET x_coord = 3856
      SET y_coord = 115
    END
    WRITE_LONG 0x6c (THIS + 1)
    INSERT_BYTES  (ent_off       ) 0x68
      WRITE_ASCII (ent_off       ) ~CDExit~ // entrance name
      WRITE_SHORT (ent_off + 0x20) x_coord
      WRITE_SHORT (ent_off + 0x22) y_coord
    // fix offsets
    PATCH_FOR_EACH test_off IN 0x54 0x5c 0x60 0x68 0x6c 0x70 0x78 0x7c 0x84 0x88 0xa0 0xa8 0xb0 0xb8 0xbc 0xc0 0xc4 BEGIN  
      READ_LONG test_off val
      PATCH_IF (val < ent_off) BEGIN
        WRITE_LONG test_off (test_off + 0x68) 
      END
    END
    BUT_ONLY

  // need to add link if fixed worldmap not installed
  COPY_EXISTING ~worldmap.wmp~ ~override~
    READ_LONG  0x30 area_num
    READ_LONG  0x34 area_off
    READ_LONG  0x38 link_off
    READ_LONG  0x3c link_num
    WRITE_LONG 0x3c (link_num + 1)
    SET link_idx_nsrt = 999999
    FOR (index = 0 ; index < area_num ; ++index) BEGIN
      READ_ASCII (area_off + (index * 0xf0)) areafile
      PATCH_IF ("ar0020" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // City Gates
        SET area_to = index
      END
      PATCH_IF ("ar1800" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN    // north forest
        WRITE_BYTE (area_off + 0x30 + (index * 0xf0)) (THIS | BIT1)
        SET nf_idx = index
      END
      PATCH_IF ("ar2600" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // forest of tethir
        WRITE_BYTE (area_off + 0x30 + (index * 0xf0)) (THIS | BIT1)
        SET fot_idx = index
      END
      READ_LONG  (area_off + 0x50 + (index * 0xf0)) link_idx_1
      PATCH_IF ("ar1700" STRING_COMPARE_CASE "%areafile%" = 0) BEGIN // Small Teeth Pass
        SET link_idx_nsrt = link_idx_1
        WRITE_BYTE (area_off + 0x30 + (index * 0xf0)) (THIS | BIT1)
        WRITE_LONG (area_off + 0x54 + (index * 0xf0)) (THIS + 1)
        SET stp_idx = index
      END
      PATCH_IF (link_idx_nsrt < link_idx_1) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x50 + (index * 0xf0)) (link_idx_1 + 1)
      END
      READ_LONG  (area_off + 0x58 + (index * 0xf0)) link_idx_2
      PATCH_IF (link_idx_nsrt < link_idx_2) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x58 + (index * 0xf0)) (link_idx_2 + 1)
      END
      READ_LONG  (area_off + 0x60 + (index * 0xf0)) link_idx_3
      PATCH_IF (link_idx_nsrt < link_idx_3) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x60 + (index * 0xf0)) (link_idx_3 + 1)
      END
      READ_LONG  (area_off + 0x68 + (index * 0xf0)) link_idx_4
      PATCH_IF (link_idx_nsrt < link_idx_4) BEGIN // adjusts indices for links after the insert
        WRITE_LONG  (area_off + 0x68 + (index * 0xf0)) (link_idx_4 + 1)
      END
    END
    FOR (index2 = 0 ; index2 < link_num ; ++index2) BEGIN
      READ_LONG (link_off +        (index2 * 0xd8)) target // target index
      READ_LONG (link_off + 0x04 + (index2 * 0xd8)) exit   // exit point
      PATCH_IF ((exit = 0) AND
                ((target = stp_idx) OR
                 (target = nf_idx) OR
                 (target = fot_idx))) BEGIN
        WRITE_ASCII (link_off + 0x04 + (index2 * 0xd8)) "CDExit"   // exit point
      END
    END
    INSERT_BYTES  (link_off +        (link_idx_nsrt * 0xd8)) 0xd8
      WRITE_LONG  (link_off +        (link_idx_nsrt * 0xd8)) "%area_to%" // target index
      WRITE_ASCII (link_off + 0x04 + (link_idx_nsrt * 0xd8)) ~ExitNE~    // exit point
      WRITE_LONG  (link_off + 0x24 + (link_idx_nsrt * 0xd8)) 5           // travel time
      WRITE_LONG  (link_off + 0x28 + (link_idx_nsrt * 0xd8)) 2           // unk, changed for consistency
    FOR (index3 = 0 ; index3 < link_num ; ++index3) BEGIN
      READ_LONG  (link_off +        (index3 * 0xd8)) target // target index
      READ_ASCII (link_off + 0x04 + (index3 * 0xd8)) exit   // exit point
      PATCH_IF (("" STRING_COMPARE_CASE "%exit%" = 0) AND
                ((target = stp_idx) OR
                 (target = nf_idx) OR
                 (target = fot_idx))) BEGIN
        WRITE_ASCII (link_off + 0x04 + (index3 * 0xd8)) "CDExit"   // exit point
      END
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Open up cloakwood                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// first area only                                            \\\\\
/////                                                            \\\\\

BEGIN @103501 DESIGNATED 1035
SUBCOMPONENT @103500
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt bgee eet~ @25 // Tutu, BGEE or BGT only
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~bg1npc/bg1npc.tp2~ ~29~ @24 // don't install if same component from bg1 npc installed
LABEL ~cd_tweaks_reveal_cloakwood_1~

COPY_EXISTING ~worldmap.wmp~ ~override~
  READ_LONG  0x30 "area_num"
  READ_LONG  0x34 "area_off"
  FOR (index = 0; index < area_num; index = index + 1) BEGIN
    READ_ASCII ("%area_off%" + 0x08 + (0xf0 * "%index%")) "area_res"
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%CloakwoodLodge%" = 0) BEGIN // cloakwood 1
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BOR 0b00000010) // adds 'reveal thru linked area' flag
    END ELSE
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%CloakwoodNest%" = 0) BEGIN // cloakwood 2
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BAND 0b11111101) // removes 'reveal thru linked area' flag
    END
  END
  BUT_ONLY

COPY_EXISTING ~%BanditCamp_RaemonsTent_BCS%.bcs~ ~override~ // tutu, bgt. bg
              ~baldur.bcs~                       ~override~ // eet, bgee
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~RevealAreaOnMap("%CloakwoodLodge%")~ ~RevealAreaOnMap("%CloakwoodLodge%") RevealAreaOnMap("%CloakwoodNest%")~
  END
  BUT_ONLY IF_EXISTS // obg doesn't have baldur.bcs

/////                                                            \\\\\
///// all cloakwood save mines                                   \\\\\
/////                                                            \\\\\

BEGIN @103600 DESIGNATED 1036
SUBCOMPONENT @103500
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt bgee eet~ @25 // Tutu, BGEE or BGT only
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~bg1npc/bg1npc.tp2~ ~29~ @24 // don't install if same component from bg1 npc installed
LABEL ~cd_tweaks_reveal_cloakwood_4~

COPY_EXISTING ~worldmap.wmp~ ~override~
  READ_LONG  0x30 "area_num"
  READ_LONG  0x34 "area_off"
  FOR (index = 0; index < area_num; index = index + 1) BEGIN
    READ_ASCII ("%area_off%" + 0x08 + (0xf0 * "%index%")) "area_res"
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%CloakwoodLodge%" = 0) BEGIN // cloakwood 1
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BOR 0b00000010) // adds 'reveal thru linked area' flag
    END ELSE
    PATCH_IF ("%area_res%" STRING_COMPARE_CASE "%CloakwoodMines%" = 0) BEGIN // cloakwood 2
      READ_BYTE  ("%area_off%" + 0x30 + (0xf0 * "%index%")) "flags"
      WRITE_BYTE ("%area_off%" + 0x30 + (0xf0 * "%index%")) ("%flags%" BAND 0b11111101) // removes 'reveal thru linked area' flag
    END
  END
  BUT_ONLY

COPY_EXISTING ~%BanditCamp_RaemonsTent_BCS%.bcs~ ~override~ // tutu, bgt. bg
              ~baldur.bcs~                       ~override~ // eet, bgee
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~RevealAreaOnMap("%CloakwoodLodge%")~ ~RevealAreaOnMap("%CloakwoodLodge%") RevealAreaOnMap("%CloakwoodMines%")~
  END
  BUT_ONLY IF_EXISTS // obg doesn't have baldur.bcs

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Improved Athkatla City Guard                               \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @104000 DESIGNATED 1040
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16 // non-BP
LABEL ~cd_tweaks_improved_athkatla_guard~

COPY_EXISTING ~amncen1.cre~  ~override~ // amnish centurion
              ~amng1.cre~    ~override~ // amnish soldier
              ~amng2.cre~    ~override~ // amnish soldier
              ~amnleg01.cre~ ~override~ // amnish legionary
              ~amnleg1.cre~  ~override~ // amnish legionary
              ~amnlegs.cre~  ~override~ // amnish legionary
              ~bamng01.cre~  ~override~ // amnish guard
              ~bamng02.cre~  ~override~ // amnish guard
              ~bodyg1.cre~   ~override~ // amnish bodyguard
              ~bodyg2.cre~   ~override~ // amnish bodyguard
              ~circg1.cre~   ~override~ // amnish soldier
              ~civamng1.cre~ ~override~ // amnish soldier
              ~docsol01.cre~ ~override~ // amnish soldier
              ~gguard01.cre~ ~override~ // amnish soldier
              ~guamur.cre~   ~override~ // amnish soldier
              ~hactong.cre~  ~override~ // amnish bodyguard
              ~isamng1.cre~  ~override~ // amnish soldier
              ~isamng2.cre~  ~override~ // amnish soldier
              ~isamng3.cre~  ~override~ // amnish soldier
              ~isamng4.cre~  ~override~ // amnish soldier
              ~slamng01.cre~ ~override~ // amnish soldier
              ~slcent.cre~   ~override~ // amnish centurion
              ~stealgrd.cre~ ~override~ // amnish soldier
              ~stolegrd.cre~ ~override~ // amnish soldier
              ~temamn01.cre~ ~override~ // amnish guard
              ~vvamn1.cre~   ~override~ // amnish soldier
              ~vvamn2.cre~   ~override~ // amnish soldier
  WRITE_SHORT 0x24  (THIS + 10) // Current HP
  WRITE_SHORT 0x26  (THIS + 10) // Max HP
  WRITE_SHORT 0x46  (THIS - 1)  // Natural AC
  WRITE_SHORT 0x48  (THIS - 1)  // Effective AC
  WRITE_SHORT 0x52  (THIS - 1)  // thac0
  WRITE_BYTE  0x54  (THIS - 1)  // save vs death
  WRITE_BYTE  0x55  (THIS - 1)  // save vs wands
  WRITE_BYTE  0x56  (THIS - 1)  // save vs polymorph
  WRITE_BYTE  0x57  (THIS - 1)  // save vs breath
  WRITE_BYTE  0x58  (THIS - 1)  // save vs spell
  //search for first empty script slot and overwrite
  FOR (index = 0 ; index < 5 ; index = index + 1) BEGIN
    READ_ASCII (0x248 + ("%index%" * 0x08)) "script"
    PATCH_IF (("None" STRING_COMPARE_CASE "%script%" = 0) OR
              (""     STRING_COMPARE_CASE "%script%" = 0)) BEGIN
      WRITE_ASCII (0x248 + ("%index%" * 0x08)) ~a#amng~ #8
      SET "index" = 5 // kills loop
    END ELSE
    PATCH_IF ("%index%" = 4) BEGIN // if we get here, no script slot open so we overwrite general script
      WRITE_ASCII 0x260 ~a#amng~ #8 // override script
    END
  END
  BUT_ONLY

// Amnish Soldier
COPY_EXISTING ~amng1.cre~ ~override/a#amng.cre~
  SAY NAME1 @104001
  SAY NAME2 @104001
  WRITE_ASCII 0x280 ~a#amng~ #8

//Amnish Soldier - Hostile
COPY_EXISTING ~a#amng.cre~ ~override/a#amngh.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amngh~ #8 // Death Variable

//Sanctioned Wizard
COPY ~cdtweaks/cre/a#amnm.cre~ ~override~
  SAY NAME1 @104002
  SAY NAME2 @104002

//Sanctioned Wizard - Hostile
COPY_EXISTING ~a#amnm.cre~ ~override/a#amnmh.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amnmh~ #8 // Death Variable

//Sanctioned Wizard
COPY ~cdtweaks/cre/a#amnp.cre~ ~override~
  SAY NAME1 @104003
  SAY NAME2 @104003

//Sanctioned Wizard - Hostile
COPY_EXISTING ~a#amnp.cre~ ~override/a#amnph.cre~
  WRITE_BYTE  0x270 255          // Allegiance enemy
  WRITE_ASCII 0x280 ~a#amnph~ // Death Variable

COMPILE ~cdtweaks/baf/a#amng.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Gradual Drow item disintegration                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @105000 DESIGNATED 1050
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_gradual_drow_item_disintegration~

EXTEND_BOTTOM ~ar2500.bcs~ ~cdtweaks/baf/ar2500.baf~
EXTEND_BOTTOM ~baldur.bcs~ ~cdtweaks/baf/baldur.baf~

ACTION_IF ((FILE_EXISTS_IN_GAME ~baldur25.bcs~) AND NOT (game_is_eet)) THEN BEGIN // extend ToB game script if present

  EXTEND_BOTTOM ~baldur25.bcs~ ~cdtweaks/baf/baldur.baf~

END

COPY ~cdtweaks/spl/cdsw1h01.spl~ ~override/cdsw1h01.spl~
     ~cdtweaks/spl/cdsw1h01.spl~ ~override/cdsw1h02.spl~
     ~cdtweaks/spl/cdsw1h01.spl~ ~override/cdblun01.spl~
     ~cdtweaks/spl/cdsw1h01.spl~ ~override/cdhalb01.spl~
     ~cdtweaks/spl/cdsw1h01.spl~ ~override/cdsper01.spl~
  WRITE_ASCIIE 0xae ~%DEST_RES%~ #8
  WRITE_ASCIIE 0xae ~dw~ #2
  SAY 0xfe @105001

COPY ~cdtweaks/spl/cdbreak1.spl~ ~override~ // armor
  SAY 0x9e @105001

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak4.spl~  // cloak
  WRITE_LONG 0xce 4

COPY_EXISTING ~cdbreak1.spl~ ~override/cdbreak9.spl~  // shield
  WRITE_LONG 0xce 9

// melee weapons have chance of failing when used in combat
COPY_EXISTING ~dwsw1h01.itm~ ~override~
              ~dwsw1h02.itm~ ~override~
              ~dwblun01.itm~ ~override~
              ~dwhalb01.itm~ ~override~
              ~dwsper01.itm~ ~override~
  READ_LONG  0x64 "abil_off" ELSE 0
  READ_SHORT 0x68 "abil_num" ELSE 0
  READ_LONG  0x6a "fx_off"   ELSE 0
  SET "abil_length" = 0x38
  SET "fx_delta" = 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN // start iterating through abilities
    READ_SHORT  ("%abil_off%" +        ("%abil_length%" * "%index%")) "type"
    READ_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) "abil_fx_idx"
    SET "abil_fx_idx" = ("%abil_fx_idx%" + "%fx_delta%")
    WRITE_SHORT ("%abil_off%" + 0x20 + ("%abil_length%" * "%index%")) ("%abil_fx_idx%")
    PATCH_IF ("%type%" = 1) BEGIN // melee
      INSERT_BYTES            ("%fx_off%"        + (0x30 * "%abil_fx_idx%")) 0x30
        WRITE_SHORT           ("%fx_off%"        + (0x30 * "%abil_fx_idx%")) 146 // opcode: 146 cast spell
        WRITE_BYTE            ("%fx_off%" + 0x02 + (0x30 * "%abil_fx_idx%")) 1   // target: self
        WRITE_LONG            ("%fx_off%" + 0x04 + (0x30 * "%abil_fx_idx%")) 1   // cast at level: 1
        WRITE_BYTE            ("%fx_off%" + 0x0c + (0x30 * "%abil_fx_idx%")) 1   // timing: instant permanent
        WRITE_BYTE            ("%fx_off%" + 0x12 + (0x30 * "%abil_fx_idx%")) 1   // probability: 1%
        WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~%SOURCE_RES%~ #8 // name of spell to cast (same as item name)
        WRITE_EVALUATED_ASCII ("%fx_off%" + 0x14 + (0x30 * "%abil_fx_idx%")) ~cd~ #2           // replace dw with cd
      SET "abil_fx_num" = "%abil_fx_num%" + 1
      SET "fx_delta" = "%fx_delta%" + 1
    END
    WRITE_SHORT  ("%abil_off%" + 0x1e + ("%abil_length%" * "%index%")) "%abil_fx_num%"
  END
  BUT_ONLY

// armor/shield/cloak has a chance to break when attacked
COPY_EXISTING ~dwchan01.itm~ ~override~
              ~dwchan02.itm~ ~override~
              ~dwclck01.itm~ ~override~
              ~dwplat01.itm~ ~override~
              ~dwshld01.itm~ ~override~
              ~misc9w.itm~   ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_SHORT  0x1c "type"
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    READ_SHORT  0x70 "fx_num"
    WRITE_SHORT 0x70 ("%fx_num%" + 1)
    FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
      READ_SHORT  ("%abil_off%" + 0x20 + (0x38 * "%index%")) "abil_fx_idx"
      WRITE_SHORT ("%abil_off%" + 0x20 + (0x38 * "%index%")) ("%abil_fx_idx%" + 1)
    END
    INSERT_BYTES  ("%fx_off%"       ) 0x30
      WRITE_SHORT ("%fx_off%"       ) 232          // effect type - 232 cast spell on condition
      WRITE_BYTE  ("%fx_off%" + 0x02) 1            // target - 1 targetself
      WRITE_LONG  ("%fx_off%" + 0x04) 1            // cast at level - 1
      WRITE_BYTE  ("%fx_off%" + 0x0c) 2            // duration - 2 instant while equipped
      WRITE_BYTE  ("%fx_off%" + 0x12) 1            // probability 1 - 1%
      PATCH_IF ("%type%" = 2) BEGIN                // if armor
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak1~ // name of spell to cast
      END ELSE
      PATCH_IF ("%type%" = 12) BEGIN               // if shield
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak9~ // name of spell to cast
      END ELSE BEGIN                               // if cloak
        WRITE_ASCII ("%fx_off%" + 0x14) ~cdbreak4~ // name of spell to cast
      END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Breakable nonmagical shields, armor and helms    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @106000 DESIGNATED 1060
GROUP @13
REQUIRE_PREDICATE GAME_IS ~totsc tutu tutu_totsc bgee eet~ @25 // tutu, BGEE only
LABEL ~cd_tweaks_breakable_armor_bg~

ACTION_IF game_is_totsc BEGIN // bg1 does this differently since there's no effect-on-hit opcode

  // PHASE #1 - adds opcode to selected armor/shield/helm, which change stat TRACKING (unused in original game) to 1,2,4 respectively, while equipped. 
  // So we can tell the scripts, if the non-magic armor part is equipped. With disposable in BG1 script's triggers we can only check, if the person 
  // has the item, but not, if the item is equipped or just in inventory. Opcode "store local variable" is not so good, because local variable is set 
  // permanently while an item is equipping, and doesn't return to 0 itself, if the item is unequipped.

  APPEND ~action.ids~ ~160 ApplySpellRES(S:ResRef*,O:Target*)~       UNLESS ~ApplySpellRES~ // needed action
  
  // tracking effect added to items in mutual code section

  //PHASE #2 - script on party-NPC, which on conditions (person was hit, person is wearing an item that sets tracking to value > 0) with small 
  //probability applies a spell to the person. The script DPLAYER3 works continuous on Player1 (all party in multiplayer), but DPLAYER2 could be 
  //turned off with Party AI off. So we must add the script's blocks also to all OVERRIDE scripts of all joinable NPCs, also from mods. There is 
  //unfortunately no possibility to check all party members from DPLAYER3, because trigger HitBy() can be proof only in script on active creature.
  ACTION_FOR_EACH script IN dplayer2 dplayer3 ajantis alora branwen coran dynaheir edwin eldoth faldorn garrick imoen jaheira khalid
    kagain kivan minsc montaron quayle safana sharteel skie tiax viconia xan xzar yeslick BEGIN // scripts for regular NPCs, so no existance check

    EXTEND_BOTTOM ~%script%.bcs~ ~cdtweaks/baf/bg1_break.baf~
  
  END

  ACTION_FOR_EACH script IN amorth aval01 bardo brager bub concho ferthgil jetlaya keiria skeezer thorf willsc wolfshd BEGIN // scripts for NPCs, so existance check included
  
    ACTION_IF FILE_EXISTS_IN_GAME ~%script%.bcs~ THEN BEGIN

      EXTEND_BOTTOM ~%script%.bcs~ ~cdtweaks/baf/bg1_break.baf~
    
    END

  END
  
  //7 NPCs from BG1 don't have OVERRIDE script
  COPY_EXISTING_REGEXP GLOB ~^alora[0-9]*\.cre$~ ~override~
      WRITE_ASCII SCRIPT_OVERRIDE ~alora~
      READ_ASCII SCRIPT_CLASS ~Class_Script_Name~ //Alora has already script ~alora.BCS~ but on CLASS position and with bad blocks
      PATCH_IF ( ~%Class_Script_Name%~ STRING_EQUAL_CASE ~alora~ ) THEN BEGIN  WRITE_ASCII SCRIPT_CLASS ~~ #8  END
    BUT_ONLY
  
  // delete broken junk in alora's script while we're here
  COPY_EXISTING ~alora.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~TimeOfDay(DAY)~ ~False()~
    END
    BUT_ONLY
  
  COPY_EXISTING_REGEXP GLOB ~^branwe[0-9]*\.cre$~ ~override~
      WRITE_ASCII SCRIPT_OVERRIDE ~branwen~
    BUT_ONLY
  
  COPY_EXISTING_REGEXP GLOB ~^faldor[0-9]*\.cre$~ ~override~
      WRITE_ASCII SCRIPT_OVERRIDE ~faldorn~
    BUT_ONLY
  
  COPY_EXISTING_REGEXP GLOB ~^garric[0-9]*\.cre$~  ~override~
      WRITE_ASCII SCRIPT_OVERRIDE ~garrick~
     BUT_ONLY
  
  COPY_EXISTING_REGEXP GLOB ~^imoen[0-9]+\.cre$~   ~override~ // imoen1,2,4,6.CRE but not imoen.CRE
      WRITE_ASCII SCRIPT_OVERRIDE ~imoen~
    BUT_ONLY
  
  COPY_EXISTING_REGEXP GLOB ~^skie[0-9]*\.cre$~    ~override~
      WRITE_ASCII SCRIPT_OVERRIDE ~skie~
    BUT_ONLY
  
  COPY_EXISTING_REGEXP GLOB ~^xan[0-9]*\.cre$~     ~override~
      WRITE_ASCII SCRIPT_OVERRIDE ~xan~ #8
    BUT_ONLY
  
  //PHASE #3 - spell casted from scripts changes the item to the broken one and displays the massage. I don't know, 
  //why in TUTU solution helms are handled different than armors and shields (broken armor and shield replace the 
  //armor/shield direct in their equipping slot, but "broken item", that replace helm, is created in inventory, and 
  //helm slot remains empty). If you prefer version from TUTU solution, I can modify this part of component.
  // done in mutual code section

END

COPY_EXISTING ~misc59.itm~ ~override/cddelhlm.itm~

// bgee freaks out with misc items in shield/armor/helmet slots
ACTION_IF enhanced_edition BEGIN // bgee, eet

  COPY_EXISTING ~misc57.itm~ ~override~ // broken shield
    WRITE_SHORT 0x1c 12
    WRITE_LONG  0x1e `0
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = `0 timing = 2 STR_VAR resource = cdbrak12 END
    BUT_ONLY

  COPY_EXISTING ~misc58.itm~ ~override~ // broken armor
    WRITE_SHORT 0x1c 2
    WRITE_LONG  0x1e `0
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = `0 timing = 2 STR_VAR resource = cdbrak2 END
    BUT_ONLY

  COPY_EXISTING ~cddelhlm.itm~ ~override~ // broken helmet
    WRITE_SHORT 0x1c 7
    WRITE_LONG  0x1e `0
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = `0 timing = 2 STR_VAR resource = cdbrak7 END
    BUT_ONLY

END

COPY ~cdtweaks/spl/cdbreak1.spl~ ~override/cdbrak2.spl~ // armor
  SAY         0x9e @106001

COPY_EXISTING ~cdbrak2.spl~ ~override/cdbrak7.spl~  // helm
  SAY         0x9e @106003
  WRITE_LONG  0xce 6
  WRITE_ASCII 0xde ~cddelhlm~ #8

COPY_EXISTING ~cdbrak2.spl~ ~override/cdbrak12.spl~  // shield
  SAY         0x9e @106002
  WRITE_LONG  0xce 9
  WRITE_ASCII 0xde ~misc57~ #8

ACTION_IF !((enhanced_edition) OR (MOD_IS_INSTALLED ~tobex/tobex.tp2~ ~100~)) BEGIN // if no critical hit aversion from tobex, script to remove broken helm item

  EXTEND_TOP ~baldur.bcs~ ~cdtweaks/baf/break.baf~
  
  ACTION_IF FILE_EXISTS_IN_GAME ~baldur25.bcs~ THEN BEGIN
  
    EXTEND_TOP ~baldur25.bcs~ ~cdtweaks/baf/break.baf~
  
  END

  COPY_EXISTING ~cdbrak7.spl~ ~override~  // create broken misc in inventory
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 122 target = 1 parameter1 = 1 timing = 1 insert_point = 0 STR_VAR resource = misc59 END

END ELSE BEGIN // otherwise add flag to disable critical hit protection for broken helmet

  COPY_EXISTING ~cddelhlm.itm~ ~override~ // broken helmet
    WRITE_BYTE 0x1b THIS | BIT1

END

// give nonmagical iron armor/shield/helmets a chance to break when attacked
ACTION_FOR_EACH file IN
  // helmets
  ~_helm01.itm~ ~_helm08.itm~ ~_helm09.itm~ ~_helm10.itm~ ~_helm11.itm~ ~_helm12.itm~ ~_helm13.itm~
  ~_helm15.itm~ ~_ihelm01.itm~ ~_ihelm10.itm~ ~dhelm01.itm~ ~elfhelm.itm~ ~helm01.itm~ ~helm08.itm~
  ~helm09.itm~ ~helm10.itm~ ~helm11.itm~ ~helm12.itm~ ~helm13.itm~ ~helm15.itm~ ~helm22.itm~
  ~helmskwa.itm~ ~ihelm01.itm~ ~ihelm10.itm~ 
  // shields
  ~_ishld03.itm~ ~_shld01.itm~ ~_shld03.itm~ ~_shld05.itm~ ~_shld08.itm~ ~_shld09.itm~ ~_shld10.itm~
  ~_shld11.itm~ ~_shld12.itm~ ~_shld13.itm~ ~_shld14.itm~ ~_shld15.itm~ ~_shld16.itm~ ~_shld18.itm~
  ~_shld99.itm~ ~dshld01.itm~ ~ishld03.itm~ ~shld01.itm~ ~shld03.itm~ ~shld05.itm~ ~shld08.itm~
  ~shld09.itm~ ~shld10.itm~ ~shld11.itm~ ~shld12.itm~ ~shld13.itm~ ~shld14.itm~ ~shld15.itm~
  ~shld16.itm~ ~shld18.itm~ ~shld99.itm~
  // metal armor
  ~_chan01.itm~ ~_chan04.itm~ ~_ichan01.itm~ ~_ichan04.itm~ ~_iplat01.itm~ ~_plat01.itm~ 
  ~_plat04.itm~ ~_plat07.itm~ ~_plat98.itm~ ~chan01.itm~ ~chan04.itm~ ~ichan01.itm~ ~ichan04.itm~
  ~iplat01.itm~ ~plat01.itm~ ~plat04.itm~ ~plat07.itm~ ~plat98.itm~ ~plat99.itm~ ~vischan1.itm~
  ~vischan2.itm~ ~visplat1.itm~
  BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ THEN BEGIN

    COPY_EXISTING ~%file%~ ~override~
      READ_SHORT  0x1c type
      PATCH_IF game_is_totsc = 1 BEGIN
        PATCH_IF (type =  2) BEGIN SET track = 1 END ELSE // if armor tracking value 1
        PATCH_IF (type = 12) BEGIN SET track = 2 END ELSE // if shield tracking value 2
                             BEGIN SET track = 4 END      // if helm tracking value 4
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 95 target = 1 timing = 2 probability1 = 70 parameter1 = track END // tracking value lets us do this via script
      END ELSE BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 232 target = 1 timing = 2 probability1 = 1 STR_VAR resource = EVAL "cdbrak%type%" END
      END
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multi-player kickout patch                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @107000 DESIGNATED 1070
GROUP @13
REQUIRE_PREDICATE GAME_IS ~totsc soa tob bgt bgee bg2ee iwdee eet tutu tutu_totsc ca iwd-in-bg2~ @25
LABEL ~cd_tweaks_mp_kickout_dialogue~

ACTION_IF game_is_totsc BEGIN // old IE engines - can be extended to IWD?

  INCLUDE ~cdtweaks/lib/BG1areacheck_emulation.tpa~ // clever bit of code from Zed that allows us to check areas; should also work on IWD
  COMPILE ~cdtweaks/dlg/multi_bg.d~ // adds FAI, Nashkel, BG, Beregost options

END ELSE BEGIN // bg2-based engines

  COMPILE ~cdtweaks/dlg/multi_base.d~ // basic kick out/re-join/wait here framework

  // now add additional 'go to x' options for various games
  ACTION_IF game_is_bgt OR game_is_bg2ee OR original_bg2 OR game_is_eet THEN BEGIN

    COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/multi_bg2.d~ // adds copper coronet, pocket plane options

  END

  ACTION_IF game_is_bgee OR game_is_bgt OR game_is_eet OR tutu_gen THEN BEGIN

    COMPILE ~cdtweaks/dlg/multi_bgee.d~ EVALUATE_BUFFER // adds FAI, Nashkel, BG, Beregost options

    ACTION_IF game_is_bgt OR game_is_eet THEN BEGIN

      COMPILE ~cdtweaks/dlg/multi_bgt.d~ // adds EndOfBG var checks

    END

  END

  ACTION_IF game_is_iwdee OR game_is_iwd-in-bg2 THEN BEGIN

    COMPILE ~cdtweaks/dlg/multi_iwdee.d~ // adds winter's cradle, whistling gallows, root tavern cellar options

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Send BioWare NPC's to an Inn                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @107500 DESIGNATED 1075
GROUP @13
REQUIRE_PREDICATE GAME_IS ~totsc tutu tutu_totsc bgt bgee eet~ @25
LABEL ~cd_tweaks_send_bg_npcs_to_inn~

ACTION_IF game_is_totsc BEGIN
   INCLUDE ~cdtweaks/lib/BG1areacheck_emulation.tpa~ // clever bit of code from Zed that allows us to check areas; should also work on IWD
   INCLUDE ~cdtweaks/lib/inns.tpa~
END ELSE BEGIN
    WITH_TRA "%tra_location%/english/dw_components.tra" "%tra_location%/%LANGUAGE%/dw_components.tra" BEGIN
    INCLUDE ~cdtweaks/lib/inns_nonbg.tpa~
  END  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Misc Bags of Holding                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @108000 DESIGNATED 1080
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc soa tob bgt bgee bg2ee eet how totlm iwd-in-bg2~ @25
LABEL ~cd_tweaks_add_bags_of_holding~

ACTION_IF game_is_how OR game_is_totlm OR game_is_iwd-in-bg2 THEN BEGIN // iwd content

  ACTION_IF !game_is_iwd-in-bg2 THEN BEGIN
  
    COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~ // allow sellable items in bags
      READ_LONG 0x18 flags
      PATCH_IF ((flags & BIT11) = BIT11) BEGIN // if bit11 set
        WRITE_LONG 0x18 (THIS BAND `BIT11) // removes bit11
        WRITE_LONG 0x34 0 // set price to zero, preventing selling
      END
      BUT_ONLY
  
    OUTER_SET stor_off = 0x9c // for bottomless bags
  
    // bams
    COPY ~cdtweaks/bam/cdiammo.bam~ ~override~
    
    // ammo belts
    COPY ~cdtweaks/sto/iwd_cdammo.sto~ ~override/cdammo1.sto~
         ~cdtweaks/sto/iwd_cdammo.sto~ ~override/cdammo2.sto~
         ~cdtweaks/sto/iwd_cdammo.sto~ ~override/cdammo3.sto~
      SAY 0x0c @108001
  
    COPY ~cdtweaks/itm/iwd_cdammo.itm~ ~override/cdammo1.itm~
         ~cdtweaks/itm/iwd_cdammo.itm~ ~override/cdammo2.itm~
         ~cdtweaks/itm/iwd_cdammo.itm~ ~override/cdammo3.itm~
      SAY 0x08 @108001
      SAY 0x0c @108001
      SAY 0x50 @108002
      SAY 0x54 @108002

  END ELSE BEGIN
  
    OUTER_SET stor_off = 0x22 // for bottomless bags

    // store files
    COPY_EXISTING ~bag05.sto~ ~override/cdammo1.sto~  // ammo belt
                  ~bag05.sto~ ~override/cdammo2.sto~  // ammo belt #2
                  ~bag05.sto~ ~override/cdammo3.sto~  // ammo belt #3

    // item files
    COPY_EXISTING ~bag05.itm~ ~override/cdammo1.itm~  // ammo belt
                  ~bag05.itm~ ~override/cdammo2.itm~  // ammo belt #2
                  ~bag05.itm~ ~override/cdammo3.itm~  // ammo belt #3

  END

  ACTION_IF game_is_totlm OR game_is_iwd-in-bg2 THEN BEGIN // if totl installed, clone existing boh and add fourth ammo belt

    // first, make existing BoHs take everything
    COPY_EXISTING ~bagh01.sto~ ~override~
                  ~bagh02.sto~ ~override~
      READ_LONG  0x2c buy_off
      WRITE_LONG 0x30 THIS + 3
      INSERT_BYTES buy_off 0x0c
        WRITE_LONG (buy_off       )  2 // armor
        WRITE_LONG (buy_off + 0x04) 47 // lg shield - third remains 0 for miscellaneous

    COPY_EXISTING ~bagh01.itm~  ~override/cdhold1.itm~
                  ~bagh01.itm~  ~override/cdhold2.itm~
                  ~bagh01.itm~  ~override/cdhold3.itm~
                  ~bagh01.sto~  ~override/cdhold1.sto~
                  ~bagh01.sto~  ~override/cdhold2.sto~
                  ~bagh01.sto~  ~override/cdhold3.sto~
                  ~cdammo1.itm~ ~override/cdammo4.itm~
                  ~cdammo1.sto~ ~override/cdammo4.sto~
  
    COPY_EXISTING ~hobart.sto~ ~override~
      ADD_STORE_ITEM ~cdammo4~ LAST #0 #0 #0 ~IDENTIFIED~ #1
      BUT_ONLY

    ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~3040~ THEN BEGIN // if bottomless bags

      COPY_EXISTING ~cdammo4.sto~ ~override~
        WRITE_SHORT stor_off 0xffff
        BUT_ONLY
    
    END
                  
  END ELSE BEGIN
  
    COPY ~cdtweaks/bam/ibaghold.bam~ ~override~

    COPY ~cdtweaks/itm/cdhold.itm~ ~override/cdhold1.itm~
         ~cdtweaks/itm/cdhold.itm~ ~override/cdhold2.itm~
         ~cdtweaks/itm/cdhold.itm~ ~override/cdhold3.itm~
      SAY 0x08 @108005
      SAY 0x0c @108005
      SAY 0x50 @108006
      SAY 0x54 @108006
    
    // bags of holding
    COPY ~cdtweaks/sto/cdhold.sto~ ~override/cdhold1.sto~
         ~cdtweaks/sto/cdhold.sto~ ~override/cdhold2.sto~
         ~cdtweaks/sto/cdhold.sto~ ~override/cdhold3.sto~
      SAY 0x0c @108005
  
  END

  ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~3040~ THEN BEGIN // if bottomless bags

    COPY_EXISTING ~cdammo1.sto~ ~override~
                  ~cdammo2.sto~ ~override~
                  ~cdammo3.sto~ ~override~
                  ~cdhold1.sto~ ~override~
                  ~cdhold2.sto~ ~override~
                  ~cdhold3.sto~ ~override~
      WRITE_SHORT stor_off 0xffff
      BUT_ONLY
      
  END
  
  COPY_EXISTING ~bandoth.sto~ ~override~
    ADD_STORE_ITEM ~cdhold2~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
  
  COPY_EXISTING ~emmeric.sto~ ~override~
    ADD_STORE_ITEM ~cdammo3~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
  
  COPY_EXISTING ~kieran2.sto~ ~override~
    ADD_STORE_ITEM ~cdhold3~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
  
  COPY_EXISTING ~kuork1.sto~ ~override~
    ADD_STORE_ITEM ~cdhold1~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
  
  COPY_EXISTING ~kusmith.sto~ ~override~
    ADD_STORE_ITEM ~cdammo1~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
  
  COPY_EXISTING ~shlehlan.sto~ ~override~
    ADD_STORE_ITEM ~cdammo2~ LAST #0 #0 #0 ~IDENTIFIED~ #1
    BUT_ONLY
  
END ELSE BEGIN // non-iwd content

  ACTION_IF FILE_EXISTS_IN_GAME ~bag05.sto~ THEN BEGIN // if ToB, clone existing items
  
    // store files
    COPY_EXISTING ~bag05.sto~ ~override/cdammo.sto~   // ammo belt
                  ~bag05.sto~ ~override/cdammo2.sto~  // ammo belt #2
                  ~bag06.sto~ ~override/cdpotion.sto~ // potion case
                  ~bag06.sto~ ~override/cdpotio2.sto~ // potion case #2
  
    // item files
    COPY_EXISTING ~bag05.itm~ ~override/cdammo.itm~   // ammo belt
                  ~bag05.itm~ ~override/cdammo2.itm~  // ammo belt #2
                  ~bag06.itm~ ~override/cdpotion.itm~ // potion case
                  ~bag06.itm~ ~override/cdpotio2.itm~ // potion case #2

  END ELSE BEGIN // if no ToB, create potion case & ammo belt
  
    // bams
    COPY ~cdtweaks/bam/ibag05.bam~ ~override~
         ~cdtweaks/bam/ibag06.bam~ ~override~
  
    ACTION_IF game_is_bgee THEN BEGIN

      COPY ~cdtweaks/bmp/pbag05.bmp~ ~override~
           ~cdtweaks/bmp/pbag06.bmp~ ~override~
           
    END ELSE BEGIN
    
      COPY ~cdtweaks/bam/pbag05.bam~ ~override~
           ~cdtweaks/bam/pbag06.bam~ ~override~
    
    END
  
    // ammo belts
    COPY ~cdtweaks/sto/cdammo.sto~ ~override~
         ~cdtweaks/sto/cdammo.sto~ ~override/cdammo2.sto~
      SAY 0x0c @108001
  
    COPY ~cdtweaks/itm/cdammo.itm~ ~override~
         ~cdtweaks/itm/cdammo.itm~ ~override/cdammo2.itm~
      SAY 0x08 @108001
      SAY 0x0c @108001
      SAY 0x50 @108002
      SAY 0x54 @108002
  
    // potion case
    COPY ~cdtweaks/sto/cdpotion.sto~ ~override~
         ~cdtweaks/sto/cdpotion.sto~ ~override/cdpotio2.sto~
      SAY 0x0c @108003
  
    COPY ~cdtweaks/itm/cdpotion.itm~ ~override~
         ~cdtweaks/itm/cdpotion.itm~ ~override/cdpotio2.itm~
      SAY 0x08 @108003
      SAY 0x0c @108003
      SAY 0x50 @108004
      SAY 0x54 @108004
  
    ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~3040~ THEN BEGIN // if bottomless bags
  
      COPY_EXISTING ~cdammo.sto~   ~override~
                    ~cdammo2.sto~  ~override~
                    ~cdpotion.sto~ ~override~
                    ~cdpotio2.sto~ ~override~
        WRITE_SHORT 0x22 32767
        BUT_ONLY
  
    END

  END
  
  ACTION_IF game_includes_soa BEGIN // bg2 content
    
    COPY ~cdtweaks/itm/bag04.itm~ ~override/cdbag04.itm~ // bag of holding
         ~cdtweaks/sto/bag04.sto~ ~override/cdbag04.sto~
  
    COPY_EXISTING ~murcrag.sto~ ~override~ // Cragmoon
      ADD_STORE_ITEM ~cdpotion~ LAST #0 #0 #0 ~identified&unstealable~ #1 // if store is cragmoon add potion case
      BUT_ONLY
  
    COPY_EXISTING ~ribald.sto~  ~override~ // ribald, adventurer's mart
      ADD_STORE_ITEM ~cdbag04~  LAST #0 #0 #0 ~identified&unstealable~ #1 // add bag of holding
      BUT_ONLY
  
    COPY_EXISTING ~roger.sto~   ~override~ // roger
      ADD_STORE_ITEM ~cdpotio2~ LAST #0 #0 #0 ~identified&unstealable~ #1 // if store is roger add potion case 2
      BUT_ONLY
      
    COPY_EXISTING ~shop05.sto~  ~override~ // perter
      ADD_STORE_ITEM ~cdammo~   LAST #0 #0 #0 ~identified&unstealable~ #1 // if store is perter add ammo belt 1
      BUT_ONLY
      
    COPY_EXISTING ~trmer04.sto~ ~override~ // trademeet blacksmith
      ADD_STORE_ITEM ~cdammo2~  LAST #0 #0 #0 ~identified&unstealable~ #1 // if store is Trademeet blacksmith add ammo belt 2
      BUT_ONLY
      
  END
  
  // if BGT, BGEE, EET, or Tutu game, add bags to BG portion
  ACTION_IF game_includes_bg1 THEN BEGIN
  
    COPY_EXISTING ~cdammo.itm~   ~override/c!ammo.itm~   // ammo belt
                  ~cdammo.sto~   ~override/c!ammo.sto~
                  ~bag02g.itm~   ~override/c!bag02.itm~  // gem bag
                  ~bag02g.sto~   ~override/c!bag02.sto~
                  ~cdpotion.itm~ ~override/c!potion.itm~ // potion case
                  ~cdpotion.sto~ ~override/c!potion.sto~
                  ~bag03g.itm~   ~override/c!bag03.itm~  // scroll case
                  ~bag03g.sto~   ~override/c!bag03.sto~

    ACTION_IF game_is_bgee BEGIN // missing bgee resources
    
      COPY ~cdtweaks/itm/bag04.itm~ ~override/c!bag04.itm~
           ~cdtweaks/sto/bag04.sto~ ~override/c!bag04.sto~
           
      // string changes
      COPY_EXISTING ~c!bag02.itm~ ~override~
        WRITE_LONG 0x08 25502
        WRITE_LONG 0x0c 25502
        WRITE_LONG 0x50 25504
        WRITE_LONG 0x54 25504
  
      COPY_EXISTING ~c!bag02.sto~ ~override~
        WRITE_LONG 0x0c 25502
  
      COPY_EXISTING ~c!bag03.itm~ ~override~
        WRITE_LONG 0x08 25505
        WRITE_LONG 0x0c 25505
        WRITE_LONG 0x50 25507
        WRITE_LONG 0x54 25507
  
      COPY_EXISTING ~c!bag03.sto~ ~override~
        WRITE_LONG 0x0c 25505
  
      COPY_EXISTING ~c!bag04.itm~ ~override~
        SAY 0x08 @108005
        SAY 0x0c @108005
        SAY 0x50 @108006
        SAY 0x54 @108006
  
      COPY_EXISTING ~c!bag04.sto~ ~override~
        SAY 0x0c @108005
  
      ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~3040~ THEN BEGIN // if bottomless bags
    
        COPY_EXISTING ~c!bag04.sto~   ~override~
          WRITE_SHORT 0x22 32767
          BUT_ONLY
  
      END
  
    END ELSE BEGIN
    
      COPY_EXISTING ~bag04.itm~    ~override/c!bag04.itm~  // bag of holding
                    ~bag04.sto~    ~override/c!bag04.sto~
    
    END
  
    COPY_EXISTING ~%tutu_var%friend.sto~  ~override~ // FAI
      ADD_STORE_ITEM ~c!bag02~  LAST #0 #0 #0 ~identified&unstealable~ #1 // add c!bag02
      BUT_ONLY
  
    COPY_EXISTING ~%tutu_scripth%ighhedg.sto~  ~override~ // High Hedge
      ADD_STORE_ITEM ~c!bag03~  LAST #0 #0 #0 ~identified&unstealable~ #1 // add c!bag03
      BUT_ONLY
  
    COPY_EXISTING ~%tutu_var%taerum.sto~  ~override~ // Thunderhammer Smithy
      ADD_STORE_ITEM ~c!ammo~  LAST #0 #0 #0 ~identified&unstealable~ #1 // add c!ammo
      BUT_ONLY
  
    COPY_EXISTING ~%tutu_var%sto4803.sto~  ~override~ // Nashkel General Store
      ADD_STORE_ITEM ~c!potion~  LAST #0 #0 #0 ~identified&unstealable~ #1 // add c!potion
      BUT_ONLY
  
    COPY_EXISTING ~%tutu_var%sto0703.sto~  ~override~ // Sorcerous Sundries
      ADD_STORE_ITEM ~c!bag04~  LAST #0 #0 #0 ~identified&unstealable~ #1 // add c!bag04
      BUT_ONLY
  
  END

END

ACTION_IF ((MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~181~) OR (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~182~)) THEN BEGIN // if unique bag icons

  ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~181~ BEGIN
    OUTER_SET names = 0
  END ELSE BEGIN
    OUTER_SET names = 1
  END
  
  INCLUDE ~cdtweaks/lib/unique_containers_v2_macro.tpa~
  
  // gem bag
  OUTER_SPRINT icon          ~t-bgem~
  OUTER_SET    idic          = 29  
  OUTER_FOR (index = 0 ; index < idic ; ++index) BEGIN
    ACTION_IF !FILE_EXISTS_IN_GAME ~%icon%%index%.bam~ BEGIN
      OUTER_SET idic = index  // kill loop
    END
  END  
  OUTER_SET    string_offset = 100
  OUTER_SET    last          = 28
  ACTION_FOR_EACH item IN ~c!bag02~ BEGIN LAM cd_unique_legwork END  

  // scroll cases (normal)
  OUTER_SPRINT icon          ~t-bscr~
  OUTER_SET    idic          = 26
  OUTER_FOR (index = 0 ; index < idic ; ++index) BEGIN
    ACTION_IF !FILE_EXISTS_IN_GAME ~%icon%%index%.bam~ BEGIN
      OUTER_SET idic = index  // kill loop
    END
  END  
  OUTER_SET    string_offset = 200
  OUTER_SET    last          = 25
  ACTION_FOR_EACH item IN ~c!bag03~ BEGIN LAM cd_unique_legwork END  
  
  // bags of holding
  OUTER_SPRINT icon          ~t-bhld~
  OUTER_SET    idic          = 43
  OUTER_FOR (index = 0 ; index < idic ; ++index) BEGIN
    ACTION_IF !FILE_EXISTS_IN_GAME ~%icon%%index%.bam~ BEGIN
      OUTER_SET idic = index  // kill loop
    END
  END  
  OUTER_SET    string_offset = 300
  OUTER_SET    last          = 42
  ACTION_FOR_EACH item IN ~c!bag04~ ~cdbag04~ ~cdhold1~ ~cdhold2~ ~cdhold3~ BEGIN LAM cd_unique_legwork END  
  
  // ammo belts
  OUTER_SPRINT icon          ~t-bamm~
  OUTER_SET    idic          = 30
  OUTER_FOR (index = 0 ; index < idic ; ++index) BEGIN
    ACTION_IF !FILE_EXISTS_IN_GAME ~%icon%%index%.bam~ BEGIN
      OUTER_SET idic = index  // kill loop
      PRINT ~bag idic is %idic%~
    END
  END  
  OUTER_SET    string_offset = 400
  OUTER_SET    last          = 29
  ACTION_FOR_EACH item IN ~c!ammo~ ~cdammo1~ ~cdammo2~ ~cdammo3~ ~cdammo4~ ~cdammo~ BEGIN LAM cd_unique_legwork END   

  // potion cases
  OUTER_SPRINT icon          ~t-bptn~
  OUTER_SET    idic          = 33
  OUTER_FOR (index = 0 ; index < idic ; ++index) BEGIN
    ACTION_IF !FILE_EXISTS_IN_GAME ~%icon%%index%.bam~ BEGIN
      OUTER_SET idic = index  // kill loop
    END
  END  
  OUTER_SET    string_offset = 500
  OUTER_SET    last          = 32
  ACTION_FOR_EACH item IN ~c!potion~ ~cdpotio2~ ~cdpotion~ BEGIN LAM cd_unique_legwork END 

  ACTION_IF names BEGIN

    WITH_TRA ~%tra_location%/%LANGUAGE%/unique_containers.tra~ BEGIN
      INCLUDE ~weidu_external/cdtweaks/unique_containers_working.tpa~
    END  
    
  END  

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Portable Containers                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @108500 DESIGNATED 1085
GROUP @13
REQUIRE_PREDICATE GAME_IS ~totsc~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~twm-01core.cob~ @16
LABEL ~cd_tweaks_portable_containers~

COPY ~cdtweaks/itm/z!bag01.itm~  ~override~ // Gem Bag
  SAY NAME1 @108501
  SAY NAME2 @108502
  SAY UNIDENTIFIED_DESC @108503
  SAY DESC @108504
  SAY 0xDE @108505

COPY ~cdtweaks/itm/z!bag02.itm~  ~override~ // Potion Case
  SAY NAME1 @108506
  SAY NAME2 @108507
  SAY UNIDENTIFIED_DESC @108508
  SAY DESC @108509
  SAY 0xDE @108510

COPY ~cdtweaks/itm/z!bag03.itm~  ~override~ // Scroll Case
  SAY NAME1 @108511
  SAY NAME2 @108512
  SAY UNIDENTIFIED_DESC @108513
  SAY DESC @108514
  SAY 0xDE @108525

COPY ~cdtweaks/itm/z!bag04.itm~  ~override~  // Ammunition Belt
  SAY NAME1 @108515
  SAY NAME2 @108516
  SAY UNIDENTIFIED_DESC @108517
  SAY DESC @108518
  SAY 0xDE @108519

COPY ~cdtweaks/itm/z!bag11.itm~  ~override~ // Bag of Holding
  SAY NAME1 @108520
  SAY NAME2 @108521
  SAY UNIDENTIFIED_DESC @108522
  SAY DESC @108523
  SAY 0xDE @108524

// copy the images needed
COPY ~cdtweaks/bam/cbag02.bam~ ~override~
     ~cdtweaks/bam/cbag03.bam~ ~override~
     ~cdtweaks/bam/cbag04.bam~ ~override~
     ~cdtweaks/bam/pbag05.bam~ ~override~
     ~cdtweaks/bam/pbag06.bam~ ~override~
     ~cdtweaks/bam/ibag02.bam~ ~override~
     ~cdtweaks/bam/ibag03.bam~ ~override~
     ~cdtweaks/bam/ibag04.bam~ ~override~
     ~cdtweaks/bam/ibag05.bam~ ~override~
     ~cdtweaks/bam/ibag06.bam~ ~override~

// modify Dplayer3.bcs to make bags work
EXTEND_BOTTOM ~dplayer3.bcs~ ~cdtweaks/baf/dplayer3_bagsad.baf~

// Create the CRE files that access the stores
COPY ~cdtweaks/cre/z!bag01.cre~ ~override/z!bag01.cre~
     ~cdtweaks/cre/z!bag01.cre~ ~override/z!bag02.cre~
     ~cdtweaks/cre/z!bag01.cre~ ~override/z!bag03.cre~
     ~cdtweaks/cre/z!bag01.cre~ ~override/z!bag04.cre~
     ~cdtweaks/cre/z!bag01.cre~ ~override/z!bag11.cre~
  WRITE_ASCIIE SCRIPT_OVERRIDE ~%DEST_RES%~

COPY ~cdtweaks/cre/z!bag01.cre~ ~override/z!bag0x.cre~
  WRITE_ASCII DEATHVAR ~Z!BAG0X~

// Create the scripts used by the CREs
COMPILE ~cdtweaks/baf/z!bag01.baf~

COPY_EXISTING ~z!bag01.bcs~ ~override/z!bag02.bcs~
              ~z!bag01.bcs~ ~override/z!bag03.bcs~
              ~z!bag01.bcs~ ~override/z!bag04.bcs~
              ~z!bag01.bcs~ ~override/z!bag11.bcs~
  REPLACE_TEXTUALLY ~Z!BAG01~ ~%DEST_RES%~

// Place the stores in the game
COPY ~cdtweaks/sto/z!bag01.sto~ ~override~
  SAY NAME2 @108502
  ADD_STORE_ITEM ~ring13~ #1 #0 #0 ~NONE~ #1
  ADD_STORE_ITEM ~ring17~ #1 #0 #0 ~NONE~ #1
  ADD_STORE_ITEM ~amul10~ #1 #0 #0 ~NONE~ #1

COPY ~cdtweaks/sto/z!bag02.sto~ ~override~
  SAY NAME2 @108507

COPY ~cdtweaks/sto/z!bag03.sto~ ~override~
  SAY NAME2 @108512

COPY ~cdtweaks/sto/z!bag04.sto~ ~override~
  SAY NAME2 @108516
  ADD_STORE_ITEM ~dart04~ #5 #0 #0 ~NONE~ #1

COPY ~cdtweaks/sto/z!bag11.sto~ ~override~
  SAY NAME2 @108521
  ADD_STORE_ITEM ~slng02~ #0 #0 #0 ~NONE~ #1
  ADD_STORE_ITEM ~bull02~ #10 #0 #0 ~NONE~ #1

// Finally, where the items are found
COPY_EXISTING ~tarnes.cre~ ~override~
  ADD_CRE_ITEM ~z!bag01~ #1 #0 #0 ~NONE~ ~INV9~

COPY_EXISTING ~tem3402.sto~ ~override~
  ADD_STORE_ITEM ~z!bag02~ LAST #0 #0 #0 ~IDENTIFIED~ #1

COPY_EXISTING ~highhedg.sto~ ~override~
  ADD_STORE_ITEM ~z!bag03~ LAST #0 #0 #0 ~IDENTIFIED~ #1

COPY_EXISTING ~zal.cre~ ~override~
     ADD_CRE_ITEM ~z!bag04~ #1 #0 #0 ~NONE~ ~BELT~

COPY_EXISTING ~mulahe.cre~ ~override~
  ADD_CRE_ITEM ~z!bag11~ #1 #0 #0 ~NONE~ ~INV9~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Exotic Items Pack                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @109000 DESIGNATED 1090 DEPRECATED @18 // permanently moved to The Calling

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reveal City Areas                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @110000 DESIGNATED 1100
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgee pst pstee iwdee iwd-in-bg2 iwd how totlm bgt~ @25
LABEL ~cd_tweaks_reveal_city_areas~

ACTION_IF game_includes_pst THEN BEGIN

  ACTION_FOR_EACH file IN 0100tear 0600malm ar0101 ar0300 ar0500 ar0700 ar1101 0200strk 1500gul9 ar0109 ar0400 ar0501 BEGIN
    
    EXTEND_TOP ~%file%.bcs~ ~cdtweaks/baf/a!explor.baf~

  END

END

ACTION_IF game_includes_iwd THEN BEGIN
  
  ACTION_FOR_EACH file IN
    ~AR1000~ // EASTHAVEN (PROLOGUE)
//    ~AR1100~ // EASTHAVEN (FINALE)
    ~AR2100~ // KULDAHAR
    ~AR9100~ // LONELYWOOD
    BEGIN
    
    ACTION_IF FILE_EXISTS_IN_GAME ~%file%.bcs~ BEGIN
  
      COPY_EXISTING ~%file%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          APPEND_FILE ~cdtweaks/baf/reveal.baf~
          REPLACE_TEXTUALLY ~CD_MYAREA_VAR_SCOPE~ ~%SOURCE_RES%~ // bg1 doesn't like MYAREA
        END
        BUT_ONLY
    
    END

  END

END

ACTION_IF game_includes_bg1 THEN BEGIN

  COPY_EXISTING ~%NEBaldursGate_BCS%.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~TimeGT(20)~ ~TimeGT(20) Global("CDNarlenExists","GLOBAL",0)~
      REPLACE_TEXTUALLY ~TimeLT(4)~  ~TimeLT(4)  Global("CDNarlenExists","GLOBAL",0)~
      REPLACE_TEXTUALLY ~TimeGT(4)~  ~TimeGT(4)  Global("CDNarlenExists","GLOBAL",1)~
      REPLACE_TEXTUALLY ~\bActivate("narlen")~   ~Activate("narlen") SetGlobal("CDNarlenExists","GLOBAL",1)~
      REPLACE_TEXTUALLY ~Deactivate("narlen")~ ~Deactivate("narlen") SetGlobal("CDNarlenExists","GLOBAL",0)~
    END
    BUT_ONLY IF ~CDNarlenExists~ IF_EXISTS

  COPY_EXISTING ~%NWBaldursGate%.are~      ~override~ // ar0100
                ~%NBaldursGate%.are~       ~override~ // ar0200
                ~%NEBaldursGate%.are~      ~override~ // ar0300
                ~%WBaldursGate%.are~       ~override~ // ar0600
                ~%CentralBaldursGate%.are~ ~override~ // ar0700
                ~%EBaldursGate%.are~       ~override~ // ar0800
                ~%UlgothsBeard%.are~       ~override~ // ar1000
                ~%SWBaldursGate%.are~      ~override~ // ar1100
                ~%BaldursGateDocks%.are~   ~override~ // ar1200
                ~%SEBaldursGate%.are~      ~override~ // ar1300
                ~%Candlekeep%.are~         ~override~ // ar2600
                ~%Candlekeep_Ch6%.are~     ~override~ // ar2626
                ~%Beregost%.are~           ~override~ // ar3300
                ~%Nashkel%.are~            ~override~ // ar4800
    READ_ASCII 0x94 script
    SPRINT scope ~%SOURCE_RES%~
    INNER_ACTION BEGIN

      COPY_EXISTING ~%script%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          APPEND_FILE ~cdtweaks/baf/reveal.baf~
          REPLACE_TEXTUALLY ~CD_MYAREA_VAR_SCOPE~ ~%scope%~ // bg1 doesn't like MYAREA
        END
        BUT_ONLY IF_EXISTS

    END
    BUT_ONLY IF_EXISTS

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Do Not Reveal City Areas                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @110100 DESIGNATED 1101
GROUP @13
REQUIRE_PREDICATE GAME_IS ~eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_do_not_reveal_city_areas~

ACTION_FOR_EACH file IN
  ar0015   // bgt: candlekeep prologue
  ar0300   // bg2: docks
  ar0400   // bg2: slums
  ar0500   // bg2: bridge district
  ar0900   // bg2: temple district
  ar1000   // bg2: government district
  ar2000   // bg2: trademeet
  ar2200   // bg2: ust natha
  ar2800   // bg2: suldanesselar
  ar3700   // bgt: nashkel
  ar5000   // bg2: saradush
  ar5500   // bg2: amkethran
  ar6526   // bgt: candlekeep chapter 6
  ar6700   // bgt: beregost
  ar7200   // bgt: nw baldur's gate
  ar7300   // bgt: n baldur's gate
  ar7400   // bgt: ne baldur's gate
  ar7600   // bgt: w baldur's gate
  ar7700   // bgt: central baldur's gate
  ar7800   // bgt: e baldur's gate
  ar8000   // bgt: sw baldur's gate
  ar8100   // bgt: s baldur's gate
  ar8200   // bgt: se baldur's gate
  aru000   // bgt: ulgoth's beard
  oh4010   // bg2ee
  oh4290   // bg2ee
  oh5400   // bg2ee
  oh6010   // bg2ee
  oh8000   // bg2ee
  cut01    // promenade destruction cutscene
  cut01bgt // promenade destruction cutscene
  cutd1    // promenade destruction cutscene
  cut220a  // pp transport to saradush
  bg2600   // eet: candlekeep prologue
  bg2626   // eet: candlekeep chapter 6
  bg3300   // eet: beregost
  bg0100   // eet: nw baldur's gate
  bg0200   // eet: n baldur's gate
  bg0300   // eet: ne baldur's gate
  bg0600   // eet: w baldur's gate
  bg0700   // eet: central baldur's gate
  bg0800   // eet: e baldur's gate
  bg1100   // eet: sw baldur's gate
  bg1200   // eet: s baldur's gate
  bg1300   // eet: se baldur's gate
  bg1000   // eet: ulgoth's beard

  BEGIN
  
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.bcs~ BEGIN

    COPY_EXISTING ~%file%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~[ %TAB%]Explore()~ ~Continue()~
      END
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Add Map Notes                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @111000 DESIGNATED 1110
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc iwd-in-bg2~ @25 // tutu only
LABEL ~cd_tweaks_add_map_notes~

/*
Note Color key:
Gray    - Caves & Dungeon Entrances
Violet  - Inns & Taverns
Green   - Temples
Orange  - Info points
Red     - Open space locations (i.e. city squares)
Blue    - Buildings of note
Dk Blue - People
Lt Gray - Stores
*/

ACTION_IF game_is_iwd-in-bg2 THEN BEGIN

  COPY_EXISTING ~ar1000.are~ ~override~ //easthaven
    ADD_MAP_NOTE #3050  #224 ~violet~    @111102
    ADD_MAP_NOTE #3552  #416 ~lightgray~ @111104
    ADD_MAP_NOTE #3392 #1077 ~violet~    @111103
    ADD_MAP_NOTE  #448  #330 ~green~     @111100
    ADD_MAP_NOTE #1002 #2090 ~blue~      @111107
    ADD_MAP_NOTE #1002 #1546 ~blue~      @111106
    ADD_MAP_NOTE #2080 #1184 ~blue~      @111105
    ADD_MAP_NOTE #1973  #202 ~blue~      @111101
    ADD_MAP_NOTE #2463  #391 ~blue~      @111108

  COPY_EXISTING ~ar2100.are~ ~override~ // kuldahar
    ADD_MAP_NOTE  #358  #666 ~lightgray~ @111109 // Orrick's tower
    ADD_MAP_NOTE #1381 #1972 ~blue~      @111110 // Arundel's House
    ADD_MAP_NOTE #1063 #1787 ~lightgray~ @111111 // Gerth's Equipment Shoppe
    ADD_MAP_NOTE #1961 #1539 ~blue~      @111112 // Potter's
    ADD_MAP_NOTE #2033  #749 ~violet~    @111113 // Root Cellar Tavern
    ADD_MAP_NOTE #1555  #369 ~lightgray~ @111114 // Blacksmith's
    ADD_MAP_NOTE #1161  #552 ~violet~    @111115 // Evening Shade Inn
    ADD_MAP_NOTE #2731 #1245 ~blue~      @111116 // Barbarian Shaman's House
    ADD_MAP_NOTE #2845  #663 ~green~     @111117 // Temple of Ilmater
    ADD_MAP_NOTE #3156  #321 ~lightgray~ @111118 // Flying Ship
    ADD_MAP_NOTE #2475 #1754 ~red~       @111119 // Tolben's Statue

  COPY_EXISTING ~ar9100.are~ ~override~ // lonelywood
    ADD_MAP_NOTE  #450  #335 ~blue~   @111120 // Brothers' Cabin
    ADD_MAP_NOTE #1525  #475 ~blue~   @111121 // Cartwright's
    ADD_MAP_NOTE #2933  #357 ~blue~   @111122 // Cooper's
    ADD_MAP_NOTE  #873 #1403 ~blue~   @111123 // Boathouse
    ADD_MAP_NOTE #1937 #1202 ~violet~ @111124 // Whistling Gallows Inn
    ADD_MAP_NOTE #3387 #1250 ~blue~   @111125 // Purvis's Shack
    ADD_MAP_NOTE #2477 #1721 ~green~  @111097 // Shrine of Waukeen
    ADD_MAP_NOTE #3193 #2157 ~blue~   @111098 // Bowyer's
    ADD_MAP_NOTE #2181 #2510 ~blue~   @111099 // Merchant's Warehouse
  
END ELSE BEGIN
  
  COPY_EXISTING ~fw0100.are~ ~override~ // NW Baldur's Gate
    ADD_MAP_NOTE #1230 #1706 ~blue~   @111010
    ADD_MAP_NOTE #3610 #3007 ~violet~ @111011
    ADD_MAP_NOTE #2940 #1716 ~violet~ @111012
    BUT_ONLY
  
  COPY_EXISTING ~fw0123.are~ ~override~ // Temple of Bhaal
    ADD_MAP_NOTE #1668 #1297 ~blue~ @111096
    BUT_ONLY
  
  COPY_EXISTING ~fw0146.are~ ~override~ // Thieves' Maze
    ADD_MAP_NOTE #583 #1148 ~gray~ @111095
    ADD_MAP_NOTE #414 #2763 ~gray~ @111095
    BUT_ONLY
  
  COPY_EXISTING ~fw0200.are~ ~override~ // N Baldur's Gate
    ADD_MAP_NOTE #1550 #1123 ~green~  @111013
    ADD_MAP_NOTE #200  #1462 ~red~    @111014
    ADD_MAP_NOTE #3581 #1464 ~blue~   @111015
    ADD_MAP_NOTE #1963 #2795 ~blue~   @111016
    ADD_MAP_NOTE #3782 #2418 ~violet~ @111017
    BUT_ONLY
  
  COPY_EXISTING ~fw0300.are~ ~override~ // NE Baldur's Gate
    ADD_MAP_NOTE #144  #1799 ~violet~ @111018
    ADD_MAP_NOTE #541  #2854 ~violet~ @111019
    ADD_MAP_NOTE #1215 #1345 ~green~  @111020
    ADD_MAP_NOTE #2276 #3134 ~blue~   @111021
    BUT_ONLY
  
  COPY_EXISTING ~fw0400.are~ ~override~ // Zombie Farm
    ADD_MAP_NOTE #638 #454 ~blue~ @111058
    BUT_ONLY
  
  COPY_EXISTING ~fw0600.are~ ~override~ // W Baldur's Gate
    ADD_MAP_NOTE #1110 #1080 ~green~ @111083
    ADD_MAP_NOTE #1520 #2289 ~blue~  @111084
    ADD_MAP_NOTE #3439 #873  ~blue~  @111085
    ADD_MAP_NOTE #2914 #551  ~blue~  @111086
    BUT_ONLY
  
  COPY_EXISTING ~fw0700.are~ ~override~ // Central Baldur's Gate
    ADD_MAP_NOTE #2536 #873 ~blue~      @111087
    ADD_MAP_NOTE #730  #567 ~lightgray~ @111088
    ADD_MAP_NOTE #1729 #1230 ~violet~   @111089
    ADD_MAP_NOTE #1993 #720 ~blue~      @111090
    BUT_ONLY
  
  COPY_EXISTING ~fw0800.are~ ~override~ // E Baldur's Gate
    ADD_MAP_NOTE #1780 #2451 ~green~     @111065
    ADD_MAP_NOTE #1055 #2015 ~lightgray~ @111066
    ADD_MAP_NOTE #3151 #1093 ~lightgray~ @111067
    ADD_MAP_NOTE #1073 #3017 ~lightgray~ @111068
    ADD_MAP_NOTE #1728 #1354 ~violet~    @111069
    ADD_MAP_NOTE #3259 #1974 ~lightgray~ @111070
    BUT_ONLY
  
  COPY_EXISTING ~fw0900.are~ ~override~ // Wyrm's Crossing
    ADD_MAP_NOTE #2342 #2028 ~gray~ @111059
    ADD_MAP_NOTE #4093 #708  ~blue~ @111033
    BUT_ONLY
  
  COPY_EXISTING ~fw1100.are~ ~override~ // SW Baldur's Gate
    ADD_MAP_NOTE #3535 #1327 ~blue~      @111079
    ADD_MAP_NOTE #1556 #1385 ~blue~      @111080
    ADD_MAP_NOTE #3255 #2534 ~blue~      @111081
    ADD_MAP_NOTE #2742 #1063 ~lightgray~ @111072
    ADD_MAP_NOTE #2487 #2377 ~lightgray~ @111072
    ADD_MAP_NOTE #2526 #823  ~violet~    @111082
    BUT_ONLY
  
  COPY_EXISTING ~fw1200.are~ ~override~ // S Baldur's Gate
    ADD_MAP_NOTE #2568 #1858 ~blue~   @111074
    ADD_MAP_NOTE #3654 #2976 ~violet~ @111075
    ADD_MAP_NOTE #931  #2415 ~green~  @111076
    ADD_MAP_NOTE #400  #677  ~blue~   @111077
    ADD_MAP_NOTE #2624 #588 ~violet~  @111078
    BUT_ONLY

  COPY_EXISTING ~fw1300.are~ ~override~ // SE Baldur's Gate
    ADD_MAP_NOTE #166  #729  ~violet~    @111071
    ADD_MAP_NOTE #2135 #316  ~lightgray~ @111072
    ADD_MAP_NOTE #960  #2561 ~lightgray~ @111072
    ADD_MAP_NOTE #1699 #2399 ~lightgray~ @111073
    BUT_ONLY
  
  COPY_EXISTING ~fw1400.are~ ~override~ // Fishing Village
    ADD_MAP_NOTE #1899 #2402 ~gray~ @111057
    ADD_MAP_NOTE #3504 #2233 ~blue~ @111058
    BUT_ONLY
  
  COPY_EXISTING ~fw1600.are~ ~override~ // Cloakwood Druids
    ADD_MAP_NOTE #2194 #1001 ~gray~ @111034
    ADD_MAP_NOTE #804  #2174 ~blue~ @111062
    BUT_ONLY
  
  COPY_EXISTING ~fw1700.are~ ~override~ // Cloakwood Wyverns
    ADD_MAP_NOTE #4222 #1709 ~gray~ @111034
    BUT_ONLY

  COPY_EXISTING ~fw1800.are~ ~override~ // Cloakwood Mines Exterior
    ADD_MAP_NOTE #3247 #793  ~blue~ @111042
    ADD_MAP_NOTE #1375 #1852 ~blue~ @111008
    ADD_MAP_NOTE #1904 #1554 ~blue~ @111004
    BUT_ONLY

  COPY_EXISTING ~fw1801.are~ ~override~ // Cloakwood Mines Lvl 1
    ADD_MAP_NOTE #1975 #71   ~orange~ @111063
    ADD_MAP_NOTE #341  #133  ~gray~   @111064
    ADD_MAP_NOTE #1406 #1563 ~gray~   @111050
    BUT_ONLY
  
  COPY_EXISTING ~fw1802.are~ ~override~ // Cloakwood Mines Lvl 3
    ADD_MAP_NOTE #2612 #2916 ~gray~ @111050
    ADD_MAP_NOTE #756  #1219 ~gray~ @111051
    BUT_ONLY

  COPY_EXISTING ~fw1803.are~ ~override~ // Cloakwood Mines Lvl 4
    ADD_MAP_NOTE #205  #1177 ~orange~ @111064
    ADD_MAP_NOTE #1711 #209  ~gray~   @111051
    BUT_ONLY
  
  COPY_EXISTING ~fw1804.are~ ~override~ // Cloakwood Mines Lvl 2
    ADD_MAP_NOTE #436  #1564 ~gray~ @111050
    ADD_MAP_NOTE #1157 #110  ~gray~ @111051
    BUT_ONLY
  
  COPY_EXISTING ~fw1900.are~ ~override~ // Bandit Camp
    ADD_MAP_NOTE #2326 #773 ~gray~ @111034
    ADD_MAP_NOTE #3655 #957 ~blue~ @111060
    BUT_ONLY
  
  COPY_EXISTING ~fw2100.are~ ~override~ // Cloakwood Spider Nest
    ADD_MAP_NOTE #1817 #1240 ~blue~ @111061
    BUT_ONLY
  
  COPY_EXISTING ~fw2200.are~ ~override~ // Cloakwood Lodge
    ADD_MAP_NOTE #2477 #1790 ~blue~ @111043
    BUT_ONLY
  
  COPY_EXISTING ~fw2300.are~ ~override~ // Friendly Arm Inn
    ADD_MAP_NOTE #1550 #2970 ~red~    @111005
    ADD_MAP_NOTE #3511 #1903 ~violet~ @111024
    ADD_MAP_NOTE #3873 #2463 ~green~  @111025
    BUT_ONLY
  
  COPY_EXISTING ~fw2600.are~ ~override~ // Candlekeep Prologue
                ~fw2626.are~ ~override~ // Candlekeep Chapter 6
    ADD_MAP_NOTE #1071 #557  ~violet~ @111001
    ADD_MAP_NOTE #2959 #464  ~blue~   @111002
    ADD_MAP_NOTE #3843 #728  ~green~  @111003
    ADD_MAP_NOTE #4303 #1410 ~blue~   @111004
    ADD_MAP_NOTE #4396 #2561 ~red~    @111005
    ADD_MAP_NOTE #2607 #1679 ~blue~   @111006
    ADD_MAP_NOTE #2053 #2696 ~blue~   @111007
    ADD_MAP_NOTE #1569 #2487 ~blue~   @111008
    ADD_MAP_NOTE #3531 #2916 ~blue~   @111009
    ADD_MAP_NOTE #3063 #2981 ~blue~   @111022
    BUT_ONLY

  COPY_EXISTING ~fw2615.are~ ~override~ // Candlekeep Catacombs Lvl 1
    ADD_MAP_NOTE #4026 #1712 ~gray~ @111049
    ADD_MAP_NOTE #3826 #921 ~gray~  @111049
    BUT_ONLY
  
  COPY_EXISTING ~fw2619.are~ ~override~ // Candlekeep Catacombs Lvl 2
    ADD_MAP_NOTE #3552 #2308 ~gray~ @111049
    ADD_MAP_NOTE #240  #1040 ~gray~ @111049
    BUT_ONLY

  COPY_EXISTING ~fw2800.are~ ~override~ // Coast Way
    ADD_MAP_NOTE #2359 #3335 ~orange~ @111023
    BUT_ONLY
  
  COPY_EXISTING ~fw3100.are~ ~override~ // Shipwreck Coast
    ADD_MAP_NOTE #958 #1646 ~orange~ @111055
    BUT_ONLY
  
  COPY_EXISTING ~fw3200.are~ ~override~ // High Hedge
    ADD_MAP_NOTE #2893 #2671 ~lightgray~ @111032
    ADD_MAP_NOTE #1732 #1583 ~blue~      @111033
    BUT_ONLY
  
  COPY_EXISTING ~fw3300.are~ ~override~ // Beregost
    ADD_MAP_NOTE #3711 #3754 ~violet~    @111026
    ADD_MAP_NOTE #1470 #2731 ~violet~    @111027
    ADD_MAP_NOTE #4612 #2906 ~lightgray~ @111028
    ADD_MAP_NOTE #3403 #2039 ~violet~    @111029
    ADD_MAP_NOTE #2596 #2189 ~violet~    @111030
    ADD_MAP_NOTE #3643 #993  ~blue~      @111031
    BUT_ONLY
  
  COPY_EXISTING ~fw3400.are~ ~override~ // Beregost Temple
    ADD_MAP_NOTE #414  #916 ~green~ @111046
    ADD_MAP_NOTE #1463 #893 ~green~ @111047
    BUT_ONLY
  
  COPY_EXISTING ~fw3600.are~ ~override~ // Lighthouse
    ADD_MAP_NOTE #393 #991  ~gray~   @111034
    ADD_MAP_NOTE #883 #2702 ~orange~ @111056
    BUT_ONLY
  
  COPY_EXISTING ~fw3800.are~ ~override~ // South Beregost Road
    ADD_MAP_NOTE #1861 #2269 ~gray~   @111034
    ADD_MAP_NOTE #3172 #1447 ~orange~ @111035
    BUT_ONLY
  
  COPY_EXISTING ~fw3900.are~ ~override~ // Ulcaster Ruins
    ADD_MAP_NOTE #3159 #663 ~gray~ @111048
    BUT_ONLY
  
  COPY_EXISTING ~fw4000.are~ ~override~ // Gullykin
    ADD_MAP_NOTE #882 #721 ~orange~ @111053
    BUT_ONLY

  COPY_EXISTING ~fw4100.are~ ~override~ // Archaelogical Dig
    ADD_MAP_NOTE #3035 #1285 ~gray~   @111034
    ADD_MAP_NOTE #3751 #2173 ~orange~ @111023
    BUT_ONLY
  
  COPY_EXISTING ~fw4200.are~ ~override~ // Fisherman's Lake
    ADD_MAP_NOTE #1379 #714 ~orange~ @111045
    BUT_ONLY
  
  COPY_EXISTING ~fw4400.are~ ~override~ // Lonely Peaks
    ADD_MAP_NOTE #1485 #1199 ~gray~   @111034
    ADD_MAP_NOTE #2711 #2694 ~orange~ @111045
    BUT_ONLY
  
  COPY_EXISTING ~fw4500.are~ ~override~ // Firewine Bridge
    ADD_MAP_NOTE #4027 #1983 ~gray~ @111054
    BUT_ONLY
  
  COPY_EXISTING ~fw4700.are~ ~override~ // Xvart Village
    ADD_MAP_NOTE #4217 #1025 ~gray~ @111034
    BUT_ONLY
  
  COPY_EXISTING ~fw4800.are~ ~override~ // Nashkel
    ADD_MAP_NOTE #2725 #949  ~green~     @111013
    ADD_MAP_NOTE #1308 #674  ~violet~    @111036
    ADD_MAP_NOTE #1665 #902  ~lightgray~ @111037
    ADD_MAP_NOTE #2689 #2441 ~violet~    @111038
    ADD_MAP_NOTE #4765 #558  ~violet~    @111039
    ADD_MAP_NOTE #3331 #1780 ~blue~      @111040
    BUT_ONLY
  
  COPY_EXISTING ~fw5000.are~ ~override~ // Valley of the Tombs
    ADD_MAP_NOTE #944  #1793 ~gray~   @111034
    ADD_MAP_NOTE #1868 #2897 ~gray~   @111034
    ADD_MAP_NOTE #4494 #2855 ~gray~   @111034
    ADD_MAP_NOTE #1771 #886  ~orange~ @111094
    BUT_ONLY
  
  COPY_EXISTING ~fw5100.are~ ~override~ // Gnoll Stronghold
    ADD_MAP_NOTE #591  #2147 ~gray~ @111034
    ADD_MAP_NOTE #152  #3376 ~gray~ @111034
    ADD_MAP_NOTE #818  #2420 ~gray~ @111034
    ADD_MAP_NOTE #2564 #2341 ~red~  @111041
    BUT_ONLY
  
  COPY_EXISTING ~fw5200.are~ ~override~ // Cloudpeak Mountains
    ADD_MAP_NOTE #3163 #2674 ~gray~ @111034
    BUT_ONLY
  
  COPY_EXISTING ~fw5201.are~ ~override~ // Firewine Ruins
    ADD_MAP_NOTE #2256 #1673 ~gray~ @111049
    ADD_MAP_NOTE #538  #215  ~gray~ @111049
    ADD_MAP_NOTE #1052 #1655 ~gray~ @111049
    BUT_ONLY

  COPY_EXISTING ~fw5400.are~ ~override~ // Nashkel Mines Exterior
    ADD_MAP_NOTE #1202 #717  ~gray~   @111042
    ADD_MAP_NOTE #2796 #430  ~blue~   @111043
    ADD_MAP_NOTE #641  #2713 ~orange~ @111044
    BUT_ONLY

  COPY_EXISTING ~fw5401.are~ ~override~ // Nashkel Mines Lvl 1
    ADD_MAP_NOTE #1333 #187  ~gray~ @111049
    ADD_MAP_NOTE #2076 #1884 ~gray~ @111050
    BUT_ONLY
  
  COPY_EXISTING ~fw5402.are~ ~override~ // Nashkel Mines Lvl 2
    ADD_MAP_NOTE #2306 #2710 ~gray~ @111051
    ADD_MAP_NOTE #3610 #2516 ~gray~ @111050
    BUT_ONLY
  
  COPY_EXISTING ~fw5403.are~ ~override~ // Nashkel Mines Lvl 3
    ADD_MAP_NOTE #1593 #120  ~gray~ @111051
    ADD_MAP_NOTE #3357 #2416 ~gray~ @111050
    BUT_ONLY
  
  COPY_EXISTING ~fw5404.are~ ~override~ // Nashkel Mines Lvl 4
    ADD_MAP_NOTE #424  #1893 ~gray~ @111051
    ADD_MAP_NOTE #1416 #1211 ~gray~ @111034
    ADD_MAP_NOTE #2975 #908  ~gray~ @111049
    BUT_ONLY

  COPY_EXISTING ~fw5506.are~ ~override~ // Candlekeep Catacombs Lvl 3
    ADD_MAP_NOTE #689 #124  ~gray~ @111049
    ADD_MAP_NOTE #279 #2107 ~gray~ @111052
    BUT_ONLY
  
  // the following areas are TotSC only
  ACTION_IF FILE_EXISTS_IN_GAME ~fw1500.are~ THEN BEGIN // if TotSC is installed
  
    COPY_EXISTING ~fw0500.are~ ~override~ // Durlag's Tower Exterior
      ADD_MAP_NOTE #2703 #2163 ~gray~ @111093
      BUT_ONLY
  
    COPY_EXISTING ~fw1000.are~ ~override~ // Ulgoth's Beard
      ADD_MAP_NOTE #2310 #612 ~blue~ @111091
      ADD_MAP_NOTE #350  #626 ~blue~ @111092
      BUT_ONLY
  
    COPY_EXISTING ~fw1009.are~ ~override~ // Ice Island Dungeon
      ADD_MAP_NOTE #2389 #1670 ~gray~ @111052
      ADD_MAP_NOTE #620  #170  ~gray~ @111052
      BUT_ONLY
  
    COPY_EXISTING ~fw1500.are~ ~override~ // N Werewolf Island
      ADD_MAP_NOTE #4200 #1683 ~blue~ @111043
      ADD_MAP_NOTE #3697 #1143 ~red~  @111055
      BUT_ONLY

    COPY_EXISTING ~fw2012.are~ ~override~ // Werewolf Caverns
      ADD_MAP_NOTE #974  #1392 ~gray~ @111052
      ADD_MAP_NOTE #2977 #401  ~gray~ @111052
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stores Sell Higher Stacks of Items               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @112000 DESIGNATED 1120
GROUP @13
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_stores_sell_higher_stacks~

ACTION_CLEAR_ARRAY  cd_higher_stacks
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_higher_stacks BEGIN
  arow01 => 120 // arrows
  bolt01 => 120 // bolts
  bull01 => 120 // bullets
  dart01 => 120 // darts
  dagg05 =>  25 // throwing daggers
  ax1h04 =>  10 // throwing axes
END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off ELSE 0
  READ_LONG 0x38 sale_num ELSE 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) "item"
    PHP_EACH cd_higher_stacks AS item2 => stack2 BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_REGEXP "^_?\(00\)?%item2%$" = 0) BEGIN
        READ_SHORT (sale_off + 0x0a + (index * 0x1c)) stack
        PATCH_IF (stack < stack2) BEGIN // sanity check; don't change to smaller stacks
          WRITE_SHORT (sale_off + 0x0a + (index * 0x1c)) stack2
        END
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Launchers have infinite nonmagical ammo          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @112500 DESIGNATED 1125 DEPRECATED @18
GROUP @13
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_infinite_nonmagical_ammo~

OUTER_SET arow01_name = RESOLVE_STR_REF (@112501)
OUTER_SET bolt01_name = RESOLVE_STR_REF (@112502)
OUTER_SET bull01_name = RESOLVE_STR_REF (@112503)

// grab info from nonmagical ammo
COPY_EXISTING ~arow01.itm~ ~override~
              ~bolt01.itm~ ~override~
              ~bull01.itm~ ~override~
  READ_LONG 0x64 abil_off       
  READ_ASCII (abil_off       ) "ability" (56) // read in whole ability
  READ_SHORT (abil_off + 0x1e) "fx_num"
  PATCH_IF ("fx_num" != 0) BEGIN
    READ_SHORT  (abil_off + 0x20) "fx_idx"
    READ_LONG 0x6a fx_off
    READ_ASCII (fx_off + (fx_idx * 0x30)) "effects" (fx_num * 0x30)
  END ELSE BEGIN
    SPRINT "effects" ""
  END
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "arow01" = 0) BEGIN
    SPRINT arow01_ability "%ability%"
    SPRINT arow01_effects "%effects%"
    SET    arow01_fx_num = fx_num
  END ELSE  
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "bull01" = 0) BEGIN
    SPRINT bull01_ability "%ability%"
    SPRINT bull01_effects "%effects%"
    SET    bull01_fx_num = fx_num
  END ELSE BEGIN  
    SPRINT bolt01_ability "%ability%"
    SPRINT bolt01_effects "%effects%"
    SET    bolt01_fx_num = fx_num
  END
  BUT_ONLY  

// now insert ability to any launcher
PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c item_type
  PATCH_MATCH "%item_type%" WITH
    15 BEGIN // bows
      SPRINT ability "%arow01_ability%"
      SPRINT imported_effects "%arow01_effects%" 
      SET imported_fx_num = "%arow01_fx_num%"
      SET name = "%arow01_name%"
    END
    18 BEGIN // sling   
      SPRINT ability "%bull01_ability%"
      SPRINT imported_effects "%bull01_effects%" 
      SET imported_fx_num = "%bull01_fx_num%"
      SET name = "%bull01_name%"
    END
    27 BEGIN // crossbows
      SPRINT ability "%bolt01_ability%"
      SPRINT imported_effects "%bolt01_effects%" 
      SET imported_fx_num = "%bolt01_fx_num%"
      SET name = "%bolt01_name%"
    END
    DEFAULT SET item_type = 0
  END
  PATCH_IF item_type BEGIN
    READ_LONG   0x64 abil_off
    READ_SHORT  0x68 abil_num
    SET inserted = 0
    SET new_fx = 0
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      PATCH_IF inserted BEGIN 
        WRITE_SHORT (abil_off + 0x20 + (index * 0x38)) (THIS + new_fx) // if already inserted, just correct the effect index
      END ELSE BEGIN
        READ_BYTE (abil_off +        (index * 0x38)) type
        PATCH_IF type = 4 BEGIN // insert before first launcher type
          READ_SHORT (abil_off + 0x1e + (index * 0x38)) abil_fx_num
          READ_SHORT (abil_off + 0x20 + (index * 0x38)) abil_fx_idx
          SET abil_num += 1
          SET inserted = 1
          SET new_fx = abil_fx_num + imported_fx_num
          PATCH_IF (index < 2) BEGIN // for adjusting tooltip
            DEFINE_ASSOCIATIVE_ARRAY cd_abil_table BEGIN "%SOURCE_RES%","%name%","%index%" => 0 END
          END  
          PATCH_IF new_fx BEGIN // if we also clone existing effects
            READ_LONG 0x6a fx_off
            PATCH_IF abil_fx_num BEGIN // if we also need to clone existing effects
              READ_ASCII   (fx_off + (abil_fx_idx * 0x30)) effects (abil_fx_num * 0x30)
              INSERT_BYTES (fx_off + (abil_fx_idx * 0x30)) (abil_fx_num * 0x30) // create new effect(s)
              WRITE_ASCIIE (fx_off + (abil_fx_idx * 0x30)) "%effects%"          // write in info
            END    
            PATCH_IF imported_fx_num BEGIN // if we also clone existing effects
              INSERT_BYTES (fx_off + (abil_fx_idx * 0x30)) (imported_fx_num * 0x30) // create new effect(s)
              WRITE_ASCIIE (fx_off + (abil_fx_idx * 0x30)) "%imported_effects%"     // write in info
            END     
          END   
          INSERT_BYTES (abil_off +        (index * 0x38)) 0x38        // create new ability
          WRITE_ASCIIE (abil_off +        (index * 0x38)) "%ability%" // write in info
          WRITE_BYTE   (abil_off + 0x10 + (index * 0x38)) 0           // no launcher required
          WRITE_SHORT  (abil_off + 0x1e + (index * 0x38)) new_fx      // combine existing + imported effects
          WRITE_SHORT  (abil_off + 0x20 + (index * 0x38)) abil_fx_idx // use correct index
          WRITE_SHORT  (abil_off + 0x22 + (index * 0x38)) 0           // no charges             
        END 
      END
    END
    PATCH_IF inserted BEGIN
      WRITE_SHORT 0x68 abil_num
      WRITE_LONG  0x6a (THIS + 0x38)
    END  
  END
  BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~tooltip.2da~ BEGIN

  COPY_EXISTING ~tooltip.2da~ ~override~
    PATCH_PHP_EACH cd_abil_table AS info => key BEGIN
      REPLACE_EVALUATE ~^%info_0%[ %TAB%%LNL%%MNL%%WNL%]+\(-?[0-9]+\)[ %TAB%%LNL%%MNL%%WNL%]+\(-?[0-9]+\)[ %TAB%%LNL%%MNL%%WNL%]+-?[0-9]+~ BEGIN
        PATCH_PRINT "%info_0% found in tooltip table, proceeding"
        DEFINE_ASSOCIATIVE_ARRAY cd_abil_table BEGIN "%info_0%","%info_1%","%info_2%" => 1 END // => 1 tells appends in next step to ignore this entry
        PATCH_IF info_2 = 0 BEGIN
          SPRINT replacement ~%info_0% %info_1% %MATCH1% %MATCH2%~
        END ELSE  
        PATCH_IF info_2 = 1 BEGIN
          SPRINT replacement ~%info_0% %MATCH1% %info_1% %MATCH2%~
        END ELSE BEGIN // info_2 = 2
          SPRINT replacement ~%info_0% %MATCH1% %MATCH2% %info_1%~
        END   
      END ~%replacement%~  
    END
    BUT_ONLY

  ACTION_PHP_EACH cd_abil_table AS info => key BEGIN

    ACTION_IF key = 0 BEGIN
    
      ACTION_IF info_2 = 0 BEGIN APPEND ~tooltip.2da~ ~%info_0% %info_1% -1 -1~ END ELSE
      ACTION_IF info_2 = 1 BEGIN APPEND ~tooltip.2da~ ~%info_0% -1 %info_1% -1~ END ELSE BEGIN
                                 APPEND ~tooltip.2da~ ~%info_0% -1 -1 %info_1%~ END

    END

  END

  COPY_EXISTING ~tooltip.2da~ ~override~ 
    PRETTY_PRINT_2DA

END  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reset rep at beginning of BG2 (BGT)              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @113000 DESIGNATED 1130
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bgt~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdt01130.g3~ @16 // already installed by BGT tweaks
LABEL ~cd_tweaks_rep_reset_in_bg2~

EXTEND_BOTTOM ~ar0602.bcs~ ~cdtweaks/baf/ar0602.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Gems and potions have lore                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Gems and potions                                 \\\\\
/////                                                  \\\\\

BEGIN @114002 DESIGNATED 1140
SUBCOMPONENT @114000
GROUP @13
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_lore_for_gems_potions~

OUTER_SET match_jewelry = 1
OUTER_SET match_potion  = 1

INCLUDE ~cdtweaks/lib/lore.tpa~

/////                                                  \\\\\
///// just gems                                        \\\\\
/////                                                  \\\\\

BEGIN @114100 DESIGNATED 1141
SUBCOMPONENT @114000
GROUP @13
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_lore_for_gems~

OUTER_SET match_jewelry = 1
OUTER_SET match_potion  = 0

INCLUDE ~cdtweaks/lib/lore.tpa~

/////                                                  \\\\\
///// just potions                                     \\\\\
/////                                                  \\\\\

BEGIN @114200 DESIGNATED 1142
SUBCOMPONENT @114000
GROUP @13
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_lore_for_potions~

OUTER_SET match_jewelry = 0
OUTER_SET match_potion  = 1

INCLUDE ~cdtweaks/lib/lore.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Shapeshifter rebalancing                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @115000 DESIGNATED 1150
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bgee tutu tutu_totsc eet bgt bg2ee soa tob iwd-in-bg2 ca iwdee~ @25
LABEL ~cd_tweaks_weimer_shapeshifter_rebalancing~

ACTION_IF tutu_gen THEN BEGIN // Tutu

  COPY_EXISTING ~_b1-3.itm~    ~override~ // adds magical flag to misc weapons
                ~_b2-16.itm~   ~override~
                ~_b1-20.itm~   ~override~
                ~_drizzts.itm~ ~override~
                ~_fblade.itm~  ~override~
                ~_s1-10.itm~   ~override~
                ~_s1-12.itm~   ~override~
                ~_s2-16.itm~   ~override~
                ~_sarevo.itm~  ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
      READ_BYTE  0x18 "flags"
      WRITE_BYTE 0x18 ("%flags%" BOR 0b01000000)
    END
    BUT_ONLY

END

COPY_EXISTING ~sppr604.spl~ ~override~
  WRITE_SHORT 0x20 16384
  SAY IDENTIFIED_DESC @115001
  BUT_ONLY

COPY ~cdtweaks/cre/wwbearwe.cre~ ~override~
  SAY NAME1 @115002
  SAY NAME2 @115002

COPY ~cdtweaks/cre/wwbear.cre~ ~override~
  SAY NAME1 @115003
  SAY NAME2 @115003

COPY_EXISTING ~wwbearwe.cre~ ~override~
              ~wwbear.cre~   ~override~
  PATCH_IF ((IDS_OF_SYMBOL (~animate~ ~WEREBEAR_D2~)) >= 0) BEGIN // if werebear animation from Infinity animations available...
    WRITE_LONG 0x28 (IDS_OF_SYMBOL (~animate~ ~WEREBEAR_D2~))     // then use it
  END ELSE BEGIN
    PATCH_IF game_is_iwdee OR game_is_iwd-in-bg2 BEGIN // if not, use polar bears for IWD games
      WRITE_LONG 0x28 29187 // bear_polar
    END
  END
  BUT_ONLY  

COPY ~cdtweaks/2da/anisum04.2da~ ~override~
     ~cdtweaks/2da/anisum05.2da~ ~override~
     ~cdtweaks/eff/spanim5.eff~  ~override~

// patch shapeshifter table instead of overwriting it
APPEND ~clabdr03.2da~ ~ABILITY1 CDnewreplace3 **** **** **** **** **** **** **** **** **** **** **** CDnewreplace4 CDREPLACE~
COPY_EXISTING ~clabdr03.2da~ ~override~
  REPLACE_TEXTUALLY ~GA_SPCL64[34]~ ~****~
  REPLACE_TEXTUALLY ~CDnewreplace~ ~GA_SPCL64~
  COUNT_2DA_COLS cols
  FOR (index = 15 ; index < cols ; ++index) BEGIN
    REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
  END
  REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
  PRETTY_PRINT_2DA  
  
ACTION_FOR_EACH spell IN spcl643 spcl644 BEGIN // grab existing strrefs

  COPY_EXISTING ~%spell%.spl~ ~override~
    READ_ASCII 0x08 name (8)
    READ_ASCII 0x50 desc (8)

  COPY ~cdtweaks/spl/%spell%.spl~  ~override~
    WRITE_ASCIIE 0x08 ~%name%~
    WRITE_ASCIIE 0x50 ~%desc%~

END

ACTION_IF enhanced_edition BEGIN // since descript actually matters in EE innates

  COPY_EXISTING ~spcl643.spl~ ~override~
    SAY 0x50 @115006

  COPY_EXISTING ~spcl644.spl~ ~override~
    SAY 0x50 @115007

END

// if DR > v3  is installed prior to srb
ACTION_IF ((FILE_EXISTS_IN_GAME ~cdnegpp.spl~) AND (FILE_EXISTS ~Divine_Remix/lib/macro_reindex_clab.tph~)) THEN BEGIN

  INCLUDE ~Divine_Remix/lib/macro_reindex_clab.tph~
  INCLUDE ~Divine_Remix/lib/macro_add_druid_spells.tph~
  COPY_EXISTING ~clabdr03.2da~ ~override~
    LAUNCH_PATCH_MACRO ~add_druid_spells~ // add generic druid spells
    LAUNCH_PATCH_MACRO ~reindex_clab~     // re-index lines
    BUT_ONLY

END ELSE BEGIN

  // makes spells castable by rangers and druids
  COPY_EXISTING ~sppr109.spl~ ~override~
                ~sppr203.spl~ ~override~
                ~sppr304.spl~ ~override~
                ~sppr318.spl~ ~override~
                ~sppr513.spl~ ~override~
    WRITE_BYTE 0x21 (THIS & `BIT7)
    BUT_ONLY

  // update spellbooks for druids for new druid-allowed spells
  COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
    READ_LONG 0x1cc joinable ELSE 0
    READ_BYTE 0x273 class
    PATCH_IF ((joinable > 0) AND ((class = 11) OR (class = 16))) BEGIN // check for non-null bio string, druid class
      PATCH_IF (class = 16) BEGIN // fighter-druids
        READ_BYTE 0x235 level
      END ELSE BEGIN              // vanilla druids
        READ_BYTE 0x234 level
      END
      ADD_KNOWN_SPELL ~sppr109~ #0 ~priest~
      PATCH_IF (level > 2) BEGIN
        ADD_KNOWN_SPELL ~sppr203~ #1 ~priest~
      END
      PATCH_IF (level > 4) BEGIN
        ADD_KNOWN_SPELL ~sppr304~ #2 ~priest~
        ADD_KNOWN_SPELL ~sppr318~ #2 ~priest~
      END
      PATCH_IF (level > 8) BEGIN
        ADD_KNOWN_SPELL ~sppr513~ #4 ~priest~
      END
    END
    BUT_ONLY

END

COPY ~cdtweaks/itm/wwwere.itm~ ~override~
  SAY 0x08 @115008
  SAY 0x0c @115008
  SAY 0x50 @115004
  SAY 0x54 @115004

COPY ~cdtweaks/itm/wwweregr.itm~ ~override~
  SAY 0x08 @115009
  SAY 0x0c @115009
  SAY 0x50 @115005
  SAY 0x54 @115005

// if something installed that allows shapeshifters to be non-TN alignments, adjust the paw usability
COPY_EXISTING ~alignmnt.2da~ ~override~
  READ_2DA_ENTRY 42 1 10 "lg"
  READ_2DA_ENTRY 42 2 10 "ln"
  READ_2DA_ENTRY 42 3 10 "le"
  READ_2DA_ENTRY 42 4 10 "ng"
  READ_2DA_ENTRY 42 6 10 "ne"
  READ_2DA_ENTRY 42 7 10 "cg"
  READ_2DA_ENTRY 42 8 10 "cn"
  READ_2DA_ENTRY 42 9 10 "ce"

ACTION_IF (("%lg%" = 1) OR ("%ln%" = 1) OR ("%le%" = 1)) THEN BEGIN // remove lawful flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11101111) // removes lawful flag
    BUT_ONLY

END

ACTION_IF (("%cg%" = 1) OR ("%cn%" = 1) OR ("%ce%" = 1)) THEN BEGIN // remove chaotic flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111110) // removes chaotic flag
    BUT_ONLY

END

ACTION_IF (("%lg%" = 1) OR ("%ng%" = 1) OR ("%cg%" = 1)) THEN BEGIN // remove good flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111011) // removes good flag
    BUT_ONLY

END

ACTION_IF (("%le%" = 1) OR ("%ne%" = 1) OR ("%ce%" = 1)) THEN BEGIN // remove evil flag

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    READ_BYTE  0x1e "use"
    WRITE_BYTE 0x1e ("%use%" BAND 0b11111101) // removes evil flag
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multiple Strongholds                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// no restrictions                                  \\\\\
/////                                                  \\\\\

BEGIN @116001 DESIGNATED 1160
GROUP @13
SUBCOMPONENT @116000 // multi strongholds
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
//REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~thecalling/setup-thecalling.tp2~ ~10~) AND (FILE_EXISTS ~thecalling/baf/cheat2.baf~)) @25
LABEL ~cd_tweaks_multi_stronghold_no_restriction~
  
COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/stronghold1.d~                 // removes stronghold var
                        ~cdtweaks/dlg/stronghold_cabin_choice.d~     // allows choice for ranger stronghold
                        ~cdtweaks/dlg/stronghold_grove_choice.d~     // allows choice for druid stronghold
                        ~cdtweaks/dlg/stronghold_playhouse_choice.d~ // allows choice for bard stronghold
                        ~cdtweaks/dlg/stronghold_cabin.d~            // removes class check: ranger
                        ~cdtweaks/dlg/stronghold_dearnise.d~         // removes class check: fighter, blackguard
                        ~cdtweaks/dlg/stronghold_grove.d~            // removes class check: druid
                        ~cdtweaks/dlg/stronghold_guild.d~            // removes class check: thief
                        ~cdtweaks/dlg/stronghold_norh.d~             // removes class check: paladin
                        ~cdtweaks/dlg/stronghold_playhouse.d~        // removes class check: bard
                        ~cdtweaks/dlg/stronghold_sphere.d~           // removes class check: mage, sorcerer
                        ~cdtweaks/dlg/stronghold_temples.d~          // removes class check: cleric

COPY_EXISTING ~ar0900.bcs~ ~override~
              ~ar0901.bcs~ ~override~
              ~ar0902.bcs~ ~override~
              ~ar0904.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR([56])[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_MAGE)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_THIEF)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_MAGE_CLERIC)\([%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_RANGER)\)?~
      ~~ // deletes positive class checks for oisig/arval/nalla/telwyn spawn
    REPLACE_TEXTUALLY ~!Class(Player1,CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_MAGE)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_THIEF)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_MAGE_CLERIC)\([%TAB% %LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_RANGER)\)?~
      ~False()~ // removes oisig spawn for non-clerics, who should get the regular spawn from the previous change
  END

COPY_EXISTING ~baldur.bcs~ ~override~ // non-druids not getting great druid messenger
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,DRUID_ALL)[%TAB% %LNL%%MNL%%WNL%]+\(LevelGT(Player1,13)[%TAB% %LNL%%MNL%%WNL%]+Global("DruidADSpawn","GLOBAL",0)\)~ ~\1~
  END
  BUT_ONLY

/////                                                  \\\\\
///// keep class restrictions                          \\\\\
/////                                                  \\\\\

BEGIN @116100 DESIGNATED 1161
GROUP @13
SUBCOMPONENT @116000 // multi strongholds
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
//REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~thecalling/setup-thecalling.tp2~ ~10~) AND (FILE_EXISTS ~thecalling/baf/cheat2.baf~)) @25
LABEL ~cd_tweaks_multi_stronghold_class_restriction~

COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/stronghold1.d~                 // removes stronghold var
                        ~cdtweaks/dlg/stronghold_cabin_choice.d~     // allows choice for ranger stronghold
                        ~cdtweaks/dlg/stronghold_grove_choice.d~     // allows choice for druid stronghold
                        ~cdtweaks/dlg/stronghold_playhouse_choice.d~ // allows choice for bard stronghold

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// individual Stronghold allowances                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// planar sphere (mage)                             \\\\\
/////                                                  \\\\\

BEGIN @134000 DESIGNATED 1340
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_mage~

COMPILE ~cdtweaks/dlg/stronghold1.d~       // removes stronghold var
        ~cdtweaks/dlg/stronghold_sphere.d~ // removes class check

/////                                                  \\\\\
///// de'arnise keep (fighter, monk)                   \\\\\
/////                                                  \\\\\

BEGIN @134100 DESIGNATED 1341
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_fighter~

COMPILE ~cdtweaks/dlg/stronghold1.d~         // removes stronghold var
        ~cdtweaks/dlg/stronghold_dearnise.d~ // removes class check

/////                                                  \\\\\
///// temples (cleric)                                 \\\\\
/////                                                  \\\\\

BEGIN @134200 DESIGNATED 1342
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_cleric~

COMPILE ~cdtweaks/dlg/stronghold1.d~        // removes stronghold var
        ~cdtweaks/dlg/stronghold_temples.d~ // removes class check
  
COPY_EXISTING ~ar0900.bcs~ ~override~
              ~ar0901.bcs~ ~override~
              ~ar0902.bcs~ ~override~
              ~ar0904.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~OR([56])[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_CLERIC)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_MAGE)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_THIEF)[%TAB% %LNL%%MNL%%WNL%]+Class(Player1,FIGHTER_MAGE_CLERIC)\([%TAB% %LNL%%MNL%%WNL%]+Class(Player1,CLERIC_RANGER)\)?~
      ~~ // deletes positive class checks for oisig/arval/nalla/telwyn spawn
    REPLACE_TEXTUALLY ~!Class(Player1,CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_CLERIC)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_MAGE)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_THIEF)[ %TAB%%LNL%%MNL%%WNL%]+!Class(Player1,FIGHTER_MAGE_CLERIC)\([%TAB% %LNL%%MNL%%WNL%]+!Class(Player1,CLERIC_RANGER)\)?~
      ~False()~ // removes oisig spawn for non-clerics, who should get the regular spawn from the previous change
  END

/////                                                  \\\\\
///// thieves guild                                    \\\\\
/////                                                  \\\\\

BEGIN @134300 DESIGNATED 1343
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_thief~

COMPILE ~cdtweaks/dlg/stronghold1.d~      // removes stronghold var
        ~cdtweaks/dlg/stronghold_guild.d~ // removes class check

/////                                                  \\\\\
///// playhouse (bard)                                 \\\\\
/////                                                  \\\\\

BEGIN @134400 DESIGNATED 1344
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_bard~

COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/stronghold1.d~                 // removes stronghold var
                        ~cdtweaks/dlg/stronghold_playhouse.d~        // removes class check
                        ~cdtweaks/dlg/stronghold_playhouse_choice.d~ // adds option to decline stronghold

/////                                                  \\\\\
///// norh (paladin)                                   \\\\\
/////                                                  \\\\\

BEGIN @134500 DESIGNATED 1345
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_paladin~

COMPILE ~cdtweaks/dlg/stronghold1.d~     // removes stronghold var
        ~cdtweaks/dlg/stronghold_norh.d~ // removes class check

/////                                                  \\\\\
///// druid grove                                      \\\\\
/////                                                  \\\\\

BEGIN @134600 DESIGNATED 1346
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_druid~

COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/stronghold1.d~             // removes stronghold var
                        ~cdtweaks/dlg/stronghold_grove.d~        // removes class check
                        ~cdtweaks/dlg/stronghold_grove_choice.d~ // adds option to decline stronghold
  
COPY_EXISTING ~baldur.bcs~ ~override~ // non-druids not getting great druid messenger
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Class(Player1,DRUID_ALL)[%TAB% %LNL%%MNL%%WNL%]+\(LevelGT(Player1,13)[%TAB% %LNL%%MNL%%WNL%]+Global("DruidADSpawn","GLOBAL",0)\)~ ~\1~
  END
  BUT_ONLY

/////                                                  \\\\\
///// imnesvale cabin (ranger)                         \\\\\
/////                                                  \\\\\

BEGIN @134700 DESIGNATED 1347
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
REQUIRE_PREDICATE ((!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1160~) AND (!MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1161~)) @24
LABEL ~cd_tweaks_sole_stronghold_ranger~

COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/stronghold1.d~             // removes stronghold var
                        ~cdtweaks/dlg/stronghold_cabin.d~        // removes class check
                        ~cdtweaks/dlg/stronghold_cabin_choice.d~ // adds option to decline stronghold

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Bonus Merchants                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @117000 DESIGNATED 1170
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16 // non-BP
LABEL ~cd_tweaks_bonus_merchants~

ACTION_FOR_EACH file IN ar0406 ar0702 BEGIN

  COPY_EXISTING ~%file%.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      APPEND_FILE ~cdtweaks/baf/%file%.baf~
    END
    UNLESS ~wmart[12]~
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Edwina                                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @118000 DESIGNATED 1180
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_edwina~

ACTION_IF enhanced_edition THEN BEGIN

  COPY ~cdtweaks/bmp/royo4_330.bmp~ ~override/royo4l.bmp~
       ~cdtweaks/bmp/royo4_266.bmp~ ~override/royo4m.bmp~

END ELSE BEGIN

  COPY ~cdtweaks/bmp/royo4_170.bmp~ ~override/royo4l.bmp~
       ~cdtweaks/bmp/royo4_60.bmp~  ~override/royo4m.bmp~

END

// add portrait change opcodes to edwin transform spells
COPY_EXISTING ~spin662.spl~ ~override~ // return to edwin
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 107 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = nedwinm END // large portrait
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 107 target = 2 parameter2 = 0 timing = 1 STR_VAR resource = nedwins END // small portrait

// add portrait change opcodes to edwin transform spells
COPY_EXISTING ~spin916.spl~ ~override~ // change to edwina
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 107 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = royo4l END // large portrait
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 107 target = 2 parameter2 = 0 timing = 1 STR_VAR resource = royo4m END // small portrait

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Ease of Use Romance Fixes                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @119000 DESIGNATED 1190
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16 // only if Fixpack not installed
LABEL ~cd_tweaks_eou_romance_fixes~

COMPILE ~cdtweaks/dlg/jagalvar.d~

// Anomen's title change; effects should be permanent and persist through resurrection
COPY_EXISTING ~spin678.spl~ ~override~
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  WHILE ("%abil_num%" > 0) BEGIN
    SET "abil_num" = ("%abil_num%" - 1)
    READ_SHORT ("%abil_off%" + (0x28 * "%abil_num%")) "type"
    PATCH_IF ("%type%" = 1) BEGIN // if melee
      READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%abil_num%")) "abil_fx_num"
      READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%abil_num%")) "abil_fx_idx"
      WHILE ("%abil_fx_num%" > 0) BEGIN
        SET "abil_fx_num" = ("%abil_fx_num%" - 1)
        WRITE_BYTE ("%fx_off%" + 0x0c + (0x30 * ("%abil_fx_idx%" + "%abil_fx_num%"))) 1 // instant/permanent
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~aerie.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)~
      ~SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        Global("HaerDalisRomanceActive","GLOBAL",2)
        Global("AerieRomanceActive","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",1)
          Continue()
      END

      IF
        InParty("HaerDalis")
        !Dead("HaerDalis")
        !Global("HaerDalisRomanceActive","GLOBAL",0)
        !Global("HaerDalisRomanceActive","GLOBAL",3)
        Global("AerieRomanceActive","GLOBAL",2)
      THEN
        RESPONSE #100
          SetGlobal("HaerDalisRomanceActive","GLOBAL",3)
          Continue()~
    REPLACE_TEXTUALLY ~AreaType(0)~ ~AreaType(OUTDOOR)~
  END
EXTEND_TOP ~aerie.bcs~ ~cdtweaks/baf/romfix_aerie.baf~

COPY_EXISTING ~anomen.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)~
      ~SetGlobal("AnomenIsNotKnight","GLOBAL",1)
      ChangeAlignment("Anomen",CHAOTIC_NEUTRAL)~
    REPLACE_TEXTUALLY ~"TALKEDTOCOR","GLOBAL"~ ~"TALKEDCOR","GLOBAL"~
  END
EXTEND_TOP ~anomen.bcs~ ~cdtweaks/baf/romfix_anomen.baf~

COPY_EXISTING ~jaheira.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~GlobalLT("DerminSpawn","GLOBAL",5)~
      ~  GlobalLT("DerminSpawn","GLOBAL",5)
        !InParty(Myself)
      THEN
        RESPONSE #100
          RealSetGlobalTimer("JaheiraRomance","GLOBAL",3600)
          SetGlobal("DerminSpawn","GLOBAL",5)
          SetGlobalTimer("DerminAppear","GLOBAL",17280)
          StartDialogueNoSet([PC])
      END

      IF
        False()~
    REPLACE_TEXTUALLY ~\bSetGlobalTimer("TerminselAppear","GLOBAL",FIVE_DAYS)~
                      ~RealSetGlobalTimer("TerminselAppear","GLOBAL",3600)~ // originally 36000 seems to be more than random.
  END

COPY_EXISTING ~viconia.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("LoveTalk","LOCALS",71)~ ~False()~
    REPLACE_TEXTUALLY ~Global("ViconiaKeldornFight","GLOBAL",1)~
      ~Global("ViconiaKeldornFight","GLOBAL",1)
      THEN
        RESPONSE #100
          SetGlobal("ViconiaKeldornFight","GLOBAL",0)
      END

      IF
        False()~
  END

// If ToB installed, patch ToB BCS files:
ACTION_IF game_is_tob THEN BEGIN

  COPY_EXISTING ~anom25.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("AnomenSummoned","GLOBAL",1)~
      ~Global("AnomenSummoned","GLOBAL",1)
      !Global("AnomenIsKnight","GLOBAL",1)~
    END
  EXTEND_TOP ~anom25.bcs~ ~cdtweaks/baf/romfix_anom25.baf~

  COPY_EXISTING ~jahe25.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~OR(2)~ ~~
      REPLACE_TEXTUALLY ~GlobalGT("LoveTalk","LOCALS",50)~ ~~
      REPLACE_TEXTUALLY ~SetGlobal("JaheiraRomanceActive","GLOBAL",2)~ ~~
    END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Imoen ToB-Dialogue Fixes                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @120000 DESIGNATED 1200
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bgt tob~    @25 // ToB required, already fixed in bg2ee
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16 // only if Fixpack not installed
LABEL ~cd_tweaks_tob_imoen_dialogue_fix~

COMPILE ~cdtweaks/dlg/immyfix.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Use BG Walking Speeds                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @121000 DESIGNATED 1210
GROUP @13 
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_use_bg_walking_speeds~

ACTION_IF enhanced_edition THEN BEGIN // in ee, can adjust the animation speed directly
   
  COPY_EXISTING_REGEXP GLOB ~^[56][012345][01][0-9]\.ini$~ ~override~ 
    REPLACE_TEXTUALLY ~^move_scale=9~ ~move_scale=7~
    BUT_ONLY

END ELSE BEGIN

  COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
    READ_SHORT 0x28 "anim" ELSE 0
    PATCH_IF (
         ("%SOURCE_FILE%" STRING_COMPARE_CASE   "charbase.cre" = 0)
      OR ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^AERIE[0-9][0-9]?\.cre$" = 0) // aerie comes as a ogre, so gets missed
      OR ("%anim%" = 0x5000)
      OR ("%anim%" = 0x5001)
      OR ("%anim%" = 0x5002)
      OR ("%anim%" = 0x5003)
      OR ("%anim%" = 0x5010)
      OR ("%anim%" = 0x5011)
      OR ("%anim%" = 0x5012)
      OR ("%anim%" = 0x5013)
      OR ("%anim%" = 0x5100)
      OR ("%anim%" = 0x5101)
      OR ("%anim%" = 0x5102)
      OR ("%anim%" = 0x5103)
      OR ("%anim%" = 0x5110)
      OR ("%anim%" = 0x5111)
      OR ("%anim%" = 0x5112)
      OR ("%anim%" = 0x5113)
      OR ("%anim%" = 0x5200)
      OR ("%anim%" = 0x5201)
      OR ("%anim%" = 0x5202)
      OR ("%anim%" = 0x5210)
      OR ("%anim%" = 0x5211)
      OR ("%anim%" = 0x5212)
      OR ("%anim%" = 0x5300)
      OR ("%anim%" = 0x5301)
      OR ("%anim%" = 0x5302)
      OR ("%anim%" = 0x5303)
      OR ("%anim%" = 0x5310)
      OR ("%anim%" = 0x5311)
      OR ("%anim%" = 0x5312)
      OR ("%anim%" = 0x5313)
      OR ("%anim%" = 0x6000)
      OR ("%anim%" = 0x6001)
      OR ("%anim%" = 0x6002)
      OR ("%anim%" = 0x6003)
      OR ("%anim%" = 0x6004)
      OR ("%anim%" = 0x6005)
      OR ("%anim%" = 0x6010)
      OR ("%anim%" = 0x6011)
      OR ("%anim%" = 0x6012)
      OR ("%anim%" = 0x6013)
      OR ("%anim%" = 0x6014)
      OR ("%anim%" = 0x6015)
      OR ("%anim%" = 0x6100)
      OR ("%anim%" = 0x6101)
      OR ("%anim%" = 0x6102)
      OR ("%anim%" = 0x6103)
      OR ("%anim%" = 0x6104)
      OR ("%anim%" = 0x6105)
      OR ("%anim%" = 0x6110)
      OR ("%anim%" = 0x6111)
      OR ("%anim%" = 0x6112)
      OR ("%anim%" = 0x6113)
      OR ("%anim%" = 0x6114)
      OR ("%anim%" = 0x6115)
      OR ("%anim%" = 0x6200)
      OR ("%anim%" = 0x6201)
      OR ("%anim%" = 0x6202)
      OR ("%anim%" = 0x6204)
      OR ("%anim%" = 0x6205)
      OR ("%anim%" = 0x6210)
      OR ("%anim%" = 0x6211)
      OR ("%anim%" = 0x6212)
      OR ("%anim%" = 0x6214)
      OR ("%anim%" = 0x6215)
      OR ("%anim%" = 0x6300)
      OR ("%anim%" = 0x6301)
      OR ("%anim%" = 0x6302)
      OR ("%anim%" = 0x6303)
      OR ("%anim%" = 0x6304)
      OR ("%anim%" = 0x6305)
      OR ("%anim%" = 0x6310)
      OR ("%anim%" = 0x6311)
      OR ("%anim%" = 0x6312)
      OR ("%anim%" = 0x6313)
      OR ("%anim%" = 0x6314)
      OR ("%anim%" = 0x6315)
      OR ("%anim%" = 0x6500)
      OR ("%anim%" = 0x6510)) BEGIN
      READ_BYTE   0x33 fx_flag
      READ_LONG  0x2c4 fx_off
      READ_LONG  0x2c8 fx_num
      WRITE_LONG 0x2c8 (fx_num + 1)
      // fix offsets after inserted effects
      PATCH_FOR_EACH offset IN 0x2a0 0x2a8 0x2b0 0x2b8 0x2bc BEGIN
        READ_LONG offset offset_val
        PATCH_IF (fx_off <= offset_val) BEGIN
          WRITE_LONG offset (offset_val + 0x30 + (fx_flag * 0xd8))
        END
      END
      INSERT_BYTES fx_off (0x30 + (fx_flag * 0xd8))
      WRITE_SHORT (fx_off +        (fx_flag * 0x08)) 176
      WRITE_BYTE  (fx_off + 0x02 + (fx_flag * 0x0a)) 1
      WRITE_LONG  (fx_off + 0x04 + (fx_flag * 0x10)) 0xfffffffd
      WRITE_BYTE  (fx_off + 0x0c + (fx_flag * 0x10)) 9
      WRITE_BYTE  (fx_off + 0x12 + (fx_flag * 0x12)) 100
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cromwell can upgrade WK items                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @122000 DESIGNATED 1220
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_cromwell_updates_wk_items~

STRING_SET 66764 @122043

ACTION_IF NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1230~ BEGIN

  EXTEND_BOTTOM ~botsmith.bcs~ ~cdtweaks/baf/botsmith.baf~
  
  COPY_EXISTING ~botsmith.dlg~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~OR(2)\([%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun14")[%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun30c")\)~
                        ~OR(3)\1 PartyHasItem("blun30d")~
    END
    BUT_ONLY

  COMPILE ~cdtweaks/dlg/cespy0.d~ // cespenar FoA head order

END

// compiled in serial so that COPY_TRANS will get the one before it
COMPILE ~cdtweaks/dlg/crom1.d~
COMPILE ~cdtweaks/dlg/crom2.d~
COMPILE ~cdtweaks/dlg/crom3.d~
COMPILE ~cdtweaks/dlg/crom4.d~
COMPILE ~cdtweaks/dlg/crom5.d~
COMPILE ~cdtweaks/dlg/crom6.d~
COMPILE ~cdtweaks/dlg/crom7.d~
COMPILE ~cdtweaks/dlg/crom8.d~
COMPILE ~cdtweaks/dlg/crom9.d~
ACTION_IF MOD_IS_INSTALLED ~item_rev/item_rev.tp2~ ~0~ THEN BEGIN // ir detection
  COMPILE ~cdtweaks/dlg/crom10_irr.d~ // ir adds ring of energy to erienne sling upgrade
END ELSE BEGIN  
  COMPILE ~cdtweaks/dlg/crom10.d~ // regular
END  
COMPILE ~cdtweaks/dlg/crom11.d~
COMPILE ~cdtweaks/dlg/crom12.d~
COMPILE ~cdtweaks/dlg/crom13.d~
COMPILE ~cdtweaks/dlg/crom14.d~
COMPILE ~cdtweaks/dlg/crom15.d~

EXTEND_BOTTOM ~ar0334.bcs~ ~cdtweaks/baf/ar0334.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust Cromwell forging time                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// weidu gets stupid about this, so we're doing something a little different here
// all will be displayed, but changes only made if it's not the default for that game

/////                                                  \\\\\
///// instant                                          \\\\\
/////                                                  \\\\\

BEGIN @122501 DESIGNATED 1225
SUBCOMPONENT @122500
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_cromwell_forge_time_0~

ACTION_IF enhanced_edition BEGIN

  COPY_EXISTING ~cromwell.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~RestParty\(Ex\)?(\(0,0,FALSE\)?)[ %TAB%%LNL%%MNL%%WNL%]+AdvanceTime(SIXTEEN_HOURS)~ ~~ // remove rest/advancetime
    END
    BUT_ONLY

END  

/////                                                  \\\\\
///// eight hours                                      \\\\\
/////                                                  \\\\\

BEGIN @122600 DESIGNATED 1226
SUBCOMPONENT @122500
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_cromwell_forge_time_8~

ACTION_IF enhanced_edition THEN BEGIN

  COPY_EXISTING ~cromwell.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~RestParty\(Ex\)?(\(0,0,FALSE\)?)[ %TAB%%LNL%%MNL%%WNL%]+AdvanceTime(SIXTEEN_HOURS)~ ~AdvanceTime(EIGHT_HOURS)~ // remove rest, change advance to 8
    END
    BUT_ONLY

END ELSE BEGIN
  
  OUTER_FOR (index = 0 ; index < 24 ; ++index) BEGIN
  
    OUTER_SET index2 = index + 8
    ACTION_IF index2 > 23 BEGIN OUTER_SET index2 -= 24 END

    EXTEND_TOP ~ar0334.bcs~ ~cdtweaks/baf/ar0334_nosleep.baf~ EVALUATE_BUFFER
  
  END
  
  EXTEND_TOP ~ar0334.bcs~ ~cdtweaks/baf/ar0334_cromwell.baf~

END

/////                                                  \\\\\
///// 24 hours, including rest                         \\\\\
/////                                                  \\\\\

BEGIN @122700 DESIGNATED 1227
SUBCOMPONENT @122500
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_cromwell_forge_time_24~

ACTION_IF !enhanced_edition THEN BEGIN

  OUTER_FOR (index = 0 ; index < 24 ; ++index) BEGIN
 
    OUTER_SET index2 = index + 16

    ACTION_IF index2 > 23 BEGIN OUTER_SET index2 -= 24 END

    EXTEND_TOP ~ar0334.bcs~ ~cdtweaks/baf/ar0334_sleep.baf~ EVALUATE_BUFFER

  END

  EXTEND_TOP ~ar0334.bcs~ ~cdtweaks/baf/ar0334_cromwell.baf~

END
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// cespenar gets cromwell recipes                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @123000 DESIGNATED 1230
GROUP @13
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_cespenar_updates_soa_items~

ACTION_IF NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1220~ BEGIN

  EXTEND_BOTTOM ~botsmith.bcs~ ~cdtweaks/baf/botsmith.baf~

  COPY_EXISTING ~botsmith.dlg~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~OR(2)\([%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun14")[%TAB% %LNL%%MNL%%WNL%]*PartyHasItem("blun30c")\)~
                        ~OR(3)\1 PartyHasItem("blun30d")~
    END
    BUT_ONLY

  COMPILE ~cdtweaks/dlg/cespy0.d~ // cespenar FoA head order

END

// compiled in serial so that COPY_TRANS will get the one before it
COMPILE ~cdtweaks/dlg/cespy1.d~ // red & shadow dragon scale (bg2ee already has shadow)
COMPILE ~cdtweaks/dlg/cespy2.d~ // ankheg scale
COMPILE ~cdtweaks/dlg/cespy3.d~ // crom faeyr
COMPILE ~cdtweaks/dlg/cespy4.d~ // equalizer
COMPILE ~cdtweaks/dlg/cespy5.d~ // gesen bow
COMPILE ~cdtweaks/dlg/cespy6.d~ // wave halberd
COMPILE ~cdtweaks/dlg/cespy7.d~ // silver sword
COMPILE ~cdtweaks/dlg/cespy8.d~ // mace of disruption

EXTEND_BOTTOM ~botsmith.bcs~ ~cdtweaks/baf/cespy.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Friendly Arm Inn Hidden Container Restoration    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @124000 DESIGNATED 1240
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1~ @25 // not even totsc
LABEL ~cd_tweaks_fai_container_restoration~

COPY_EXISTING ~ar2300.are~ ~override~
  //first determine that the container truly is not there  if it is, we don't do anything
  SET container_exists = 0
  READ_LONG 0x70 container_off
  READ_SHORT 0x74 container_num
  FOR (i = 0; i < %container_num%; i += 1) BEGIN
    READ_ASCII (%container_off% + %i% * 0xc0) container_name (12)
    READ_SHORT (%container_off% + (%i% * 0xc0) + 0x20) locX //2527 //locX
    READ_SHORT (%container_off% + (%i% * 0xc0) + 0x22) locY //3781 //locY
    PATCH_IF ("%container_name%" STRING_EQUAL_CASE "Container 1")
             OR ( (%locX% =2527) AND (%locY% = 3781) ) BEGIN
     PATCH_PRINT ~A container already exists with the same name and/or at the same location. Canceling installation...~
     SET container_exists = 1
    END
  END
  //so long as container does not exist
  PATCH_IF (%container_exists% = 0 ) BEGIN
    // PATCH_PRINT ~Adding hidden container~
    // add new container
    LAUNCH_PATCH_FUNCTION ~fj_are_structure~
    INT_VAR fj_loc_x = 2527 //locX
                    fj_loc_y = 3781 //locY
                    fj_type = 1 //Type
                    fj_lock_diff = 100 //Lock difficulty
                    fj_trap_remove_diff = 100 //Trap removal difficulty
                    fj_trap_loc_x = 2528 //Trap locX
                    fj_trap_loc_y = 3775 //Trap locY
                    fj_box_left = 2551 //Bound left
                    fj_box_top = 3757 //Bound top
                    fj_box_right = 2555 //Bound right
                    fj_box_bottom = 3762 //Bound bottom
                    fj_vertex_0 = (2552 + (3757 << 16))
                    fj_vertex_1 = (2555 + (3757 << 16))
                    fj_vertex_2 = (2555 + (3759 << 16))
                    fj_vertex_3 = (2553 + (3762 << 16))
                    fj_vertex_4 = (2551 + (3759 << 16))
    STR_VAR fj_structure_type = ~container~
                    fj_name = ~Container 1~ //Name
    END //ends lpf
    // PATCH_PRINT ~Placing Ring of Wizardy into hidden container~
    // put ring into container
    LAUNCH_PATCH_FUNCTION ~fj_are_structure~
          INT_VAR fj_charge0 = 1
                          fj_con_itm_idx  = SHORT_AT 0x74 - 1
          STR_VAR fj_name = ~RING08~
                          fj_structure_type = ~itm~
    END //ends lpf
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Move NPCs From Baldur's Gate                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @125000 DESIGNATED 1250 DEPRECATED @18 // deprecated in favor of individual components, below

/////                                                  \\\\\
///// Move NPCs From Baldur's Gate: alora              \\\\\
/////                                                  \\\\\

BEGIN @125100 DESIGNATED 1251
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS ~override/dw#npc_start_loc.mrk~ @16 // scs version of same
REQUIRE_PREDICATE !FILE_EXISTS ~override/x#aloramove.g3~ @16 // bg1 npc does these separately
LABEL ~cd_tweaks_move_bg_npcs_alora~

COPY_EXISTING ~%ALORA_BCS%.bcs~ ~override~ // catching and updating area variables
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~,"%WBaldursGate_HallofWonders%",~ ~,"%Gullykin%",~
  END
  BUT_ONLY      

ACTION_IF original_bg1 THEN BEGIN

  INCLUDE ~cdtweaks/lib/BG1areacheck_emulation.tpa~ // clever bit of code from Zed that allows us to check areas; should also work on IWD

  COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/x#aloramoves.d~

END ELSE BEGIN

  COPY ~cdtweaks/dlg/x#aloramoves.d~ ~cdtweaks/dlg/x#aloramoves_ac.d~
    REPLACE_TEXTUALLY ~Global("Z!EmulAreaCheck","GLOBAL",4000)~ ~AreaCheck("%Gullykin%")~

  COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/x#aloramoves_ac.d~

END

/* Alora Starts in Gullykin - VERIFY IF THIS BLOCK IS NEEDED */
/* Trying to prevent Alora's CTD */
COPY_EXISTING ~%WBaldursGate_HallofWonders_BCS%.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~!InParty("alora")~
      ~!InParty("alora") Global("X#AloraDontBeDifficult","GLOBAL",1)~
    END
  BUT_ONLY

ACTION_IF !game_is_bgt THEN BEGIN // all other platforms use GAM file

  COPY_EXISTING ~baldur.gam~ ~override~
    READ_LONG 0x30 npc_off
    READ_LONG 0x34 npc_num
    FOR (index = 0; index < npc_num; ++index) BEGIN
      READ_ASCII (npc_off + 0x0c + (index * 0x160)) ~CREName~
      PATCH_IF (~%CREName%~ STRING_COMPARE_CASE ~%tutu_var%ALORA~ = 0) BEGIN // move alora to gullykin
        WRITE_ASCIIE (npc_off + 0x18 + (index * 0x160)) ~%Gullykin%~ #8
        WRITE_SHORT  (npc_off + 0x20 + (index * 0x160)) 553
        WRITE_SHORT  (npc_off + 0x22 + (index * 0x160)) 506
      END
    END
    BUT_ONLY

END ELSE BEGIN // bgt

  // disable spawns
  COPY_EXISTING ~%WBaldursGate_HallofWonders_BCS%.bcs~ ~override~ // ar7230
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("BGTNPC[0-9]+","GLOBAL",0)~ ~False()~
    END
    BUT_ONLY
  
  EXTEND_BOTTOM ~%Gullykin_BCS%.bcs~                  ~cdtweaks/baf/bgt_spawn_alora.baf~

END

/////                                                  \\\\\
///// Move NPCs From Baldur's Gate: eldoth             \\\\\
/////                                                  \\\\\

BEGIN @125200 DESIGNATED 1252
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS ~override/dw#npc_start_loc.mrk~ @16 // scs version of same
REQUIRE_PREDICATE !FILE_EXISTS ~override/x#eldothmove.g3~ @16 // bg1 npc does these separately
LABEL ~cd_tweaks_move_bg_npcs_eldoth~

COPY_EXISTING ~%ELDOTH_BCS%.bcs~ ~override~ // bg1npc compat
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~,"%CloakwoodDruids%",~ ~,"%CoastWay%",~ // catching and updating area variables
    REPLACE_TEXTUALLY ~!Global("X#SkieLeftEldoth","GLOBAL",1)~ ~OR(2) !Global("X#SkieLeftEldoth","GLOBAL",1) !Global("EldothMove","GLOBAL",0)~
  END
  BUT_ONLY

ACTION_IF !game_is_bgt THEN BEGIN // all other platforms use GAM file

  COPY_EXISTING ~baldur.gam~ ~override~
    READ_LONG 0x30 npc_off
    READ_LONG 0x34 npc_num
    FOR (index = 0; index < npc_num; ++index) BEGIN
      READ_ASCII (npc_off + 0x0c + (index * 0x160)) ~CREName~
      PATCH_IF (~%CREName%~ STRING_COMPARE_CASE ~%tutu_var%ELDOTH~ = 0) BEGIN // move eldoth to coast way
        WRITE_ASCIIE (npc_off + 0x18 + (index * 0x160)) ~%CoastWay%~ #8
        WRITE_SHORT  (npc_off + 0x20 + (index * 0x160)) 1064
        WRITE_SHORT  (npc_off + 0x22 + (index * 0x160)) 2086
      END
    END
    BUT_ONLY

END ELSE BEGIN // bgt

  // disable spawns
  COPY_EXISTING ~%CloakwoodDruids_BCS%.bcs~ ~override~ // ar7230
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~CreateCreature("ELDOTH5?",[^)]+)~ ~~ // blank this action rather than false()ing entire block, since same block spawns faldorn
    END
    BUT_ONLY
  
  EXTEND_BOTTOM ~%CoastWay_BCS%.bcs~                  ~cdtweaks/baf/bgt_spawn_eldoth.baf~

END

/////                                                  \\\\\
///// Move NPCs From Baldur's Gate: quayle             \\\\\
/////                                                  \\\\\

BEGIN @125300 DESIGNATED 1253
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS ~override/dw#npc_start_loc.mrk~ @16 // scs version of same
REQUIRE_PREDICATE !FILE_EXISTS ~override/x#quaylemove.g3~ @16 // bg1 npc does these separately
LABEL ~cd_tweaks_move_bg_npcs_quayle~

COPY_EXISTING ~%QUAYLE_BCS%.bcs~ ~override~ // catching and updating area variables
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~,"%WyrmsCrossing%",~ ~,"%NashkelCarnival%",~
  END
  BUT_ONLY      

ACTION_IF !game_is_bgt THEN BEGIN // all other platforms use GAM file

  COPY_EXISTING ~baldur.gam~ ~override~
    READ_LONG 0x30 npc_off
    READ_LONG 0x34 npc_num
    FOR (index = 0; index < npc_num; ++index) BEGIN
      READ_ASCII (npc_off + 0x0c + (index * 0x160)) ~CREName~
      PATCH_IF (~%CREName%~ STRING_COMPARE_CASE ~%tutu_var%QUAYLE%eet_var%~ = 0) BEGIN // Quayle Starts at the Nashkel Carnival
        WRITE_ASCIIE (npc_off + 0x18 + (index * 0x160)) ~%NashkelCarnival%~ #8
        WRITE_SHORT  (npc_off + 0x20 + (index * 0x160)) 1067
        WRITE_SHORT  (npc_off + 0x22 + (index * 0x160)) 3784
      END
    END
    BUT_ONLY

END ELSE BEGIN // bgt

  // disable spawns
  COPY_EXISTING ~%WyrmsCrossing_BCS%.bcs~     ~override~ // ar7900
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("BGTNPC[0-9]+","GLOBAL",0)~ ~False()~
    END
    BUT_ONLY

  EXTEND_BOTTOM ~%NashkelCarnival_BCS%.bcs~           ~cdtweaks/baf/bgt_spawn_quayle.baf~

END

/////                                                  \\\\\
///// Move NPCs From Baldur's Gate: shar-teel          \\\\\
/////                                                  \\\\\

BEGIN @125400 DESIGNATED 1254
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS ~override/dw#npc_start_loc.mrk~ @16 // scs version of same
//REQUIRE_PREDICATE !FILE_EXISTS ~override/x#quaylemove.g3~ @16 // bg1 npc doesn't move her
LABEL ~cd_tweaks_move_bg_npcs_sharteel~

COPY_EXISTING ~%SHARTEEL_BCS%.bcs~ ~override~ // catching and updating area variables
              ~shartel2.bcs~       ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~,"%MutaminsGarden%",~ ~,"%NorthNashkelRoad%",~
  END
  BUT_ONLY      

ACTION_IF !game_is_bgt THEN BEGIN // all other platforms use GAM file

  COPY_EXISTING ~baldur.gam~ ~override~
    READ_LONG 0x30 npc_off
    READ_LONG 0x34 npc_num
    FOR (index = 0; index < npc_num; ++index) BEGIN
      READ_ASCII (npc_off + 0x0c + (index * 0x160)) ~CREName~
      PATCH_IF (~%CREName%~ STRING_COMPARE_CASE ~%tutu_var%SHARTE~ = 0) BEGIN // Quayle Starts at the Nashkel Carnival
        WRITE_ASCIIE (npc_off + 0x18 + (index * 0x160)) ~%NorthNashkelRoad%~ #8
        WRITE_SHORT  (npc_off + 0x20 + (index * 0x160)) 1391
        WRITE_SHORT  (npc_off + 0x22 + (index * 0x160)) 2524
      END
    END
    BUT_ONLY
    
  ACTION_IF enhanced_edition BEGIN // her joining combat script is in the area script

    COPY_EXISTING ~shartel2.bcs~ ~override~ // shar-teel's combat script for the initial fight
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~"%MutaminsGarden%"~ ~"%NorthNashkelRoad%"~ // change area variable to new area
      END
      BUT_ONLY

    COPY_EXISTING ~%MutaminsGarden_BCS%.bcs~     ~override~ // ar3500, mutamin's garden
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(IF[ %TAB%%LNL%%MNL%%WNL%]+\)\(Global("SharteelFight","GLOBAL",[12])\)~ ~\1 False() \2~ // disable existing scripting
      END
      BUT_ONLY
      
    EXTEND_BOTTOM ~%NorthNashkelRoad_BCS%.bcs~ ~cdtweaks/baf/bgee_spawn_sharteel.baf~ EVALUATE_BUFFER

  END
  
END ELSE BEGIN // bgt

  // disable spawns
  COPY_EXISTING ~%MutaminsGarden_BCS%.bcs~     ~override~ // ar9400, mutamin's garden
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("BGTNPC[0-9]+","GLOBAL",0)~ ~False()~
    END
    BUT_ONLY

  EXTEND_BOTTOM ~%NorthNashkelRoad_BCS%.bcs~           ~cdtweaks/baf/bgt_spawn_sharteel.baf~

END

/////                                                  \\\\\
///// Move NPCs From Baldur's Gate: tiax               \\\\\
/////                                                  \\\\\

BEGIN @125500 DESIGNATED 1255
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS ~override/dw#npc_start_loc.mrk~ @16 // scs version of same
REQUIRE_PREDICATE !FILE_EXISTS ~override/x#tiaxmove.g3~ @16 // bg1 npc does these separately
LABEL ~cd_tweaks_move_bg_npcs_tiax~

COPY_EXISTING ~%TIAX_BCS%.bcs~ ~override~ // catching and updating area variables
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~,"%SWBaldursGate%",~ ~,"%Beregost_FeldepostsInn_L1%",~
  END
  BUT_ONLY      

ACTION_IF !game_is_bgt THEN BEGIN // all other platforms use GAM file

  COPY_EXISTING ~baldur.gam~ ~override~
    READ_LONG 0x30 npc_off
    READ_LONG 0x34 npc_num
    FOR (index = 0; index < npc_num; ++index) BEGIN
      READ_ASCII (npc_off + 0x0c + (index * 0x160)) ~CREName~
      PATCH_IF (~%CREName%~ STRING_COMPARE_CASE ~%tutu_var%TIAX~ = 0) BEGIN // Tiax Starts in Beregost
        WRITE_ASCIIE (npc_off + 0x18 + (index * 0x160)) ~%Beregost_FeldepostsInn_L1%~ #8
        WRITE_SHORT  (npc_off + 0x20 + (index * 0x160)) 500
        WRITE_SHORT  (npc_off + 0x22 + (index * 0x160)) 630
      END
    END
    BUT_ONLY

END ELSE BEGIN // bgt

  // disable spawns
  COPY_EXISTING ~%SWBaldursGate_BCS%.bcs~     ~override~ // ar8000
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("BGTNPC[0-9]+","GLOBAL",0)~ ~False()~
    END
    BUT_ONLY

  EXTEND_BOTTOM ~%Beregost_FeldepostsInn_L1_BCS%.bcs~ ~cdtweaks/baf/bgt_spawn_tiax.baf~

END

/////                                                  \\\\\
///// Move NPCs From Baldur's Gate: viconia            \\\\\
/////                                                  \\\\\

BEGIN @125600 DESIGNATED 1256
GROUP @13
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS ~override/dw#npc_start_loc.mrk~ @16 // scs version of same
//REQUIRE_PREDICATE !FILE_EXISTS ~override/x#quaylemove.g3~ @16 // bg1 npc doesn't move her
LABEL ~cd_tweaks_move_bg_npcs_viconia~

COPY_EXISTING ~%VICONIA_BCS%.bcs~ ~override~ // catching and updating area variables
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~,"%Peldvale%",~ ~,"%SouthBeregostRoad%",~
  END
  BUT_ONLY      

ACTION_IF !game_is_bgt THEN BEGIN // all other platforms use GAM file

  COPY_EXISTING ~baldur.gam~ ~override~
    READ_LONG 0x30 npc_off
    READ_LONG 0x34 npc_num
    FOR (index = 0; index < npc_num; ++index) BEGIN
      READ_ASCII (npc_off + 0x0c + (index * 0x160)) ~CREName~
      PATCH_IF (~%CREName%~ STRING_COMPARE_CASE ~%tutu_var%VICONI~ = 0) BEGIN // Quayle Starts at the Nashkel Carnival
        WRITE_ASCIIE (npc_off + 0x18 + (index * 0x160)) ~%SouthBeregostRoad%~ #8
        WRITE_SHORT  (npc_off + 0x20 + (index * 0x160)) 1455
        WRITE_SHORT  (npc_off + 0x22 + (index * 0x160)) 253
      END
    END
    BUT_ONLY

END ELSE BEGIN // bgt

  // disable spawns
  COPY_EXISTING ~%Peldvale_BCS%.bcs~     ~override~ // ar8900, peldvale
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("BGTNPC[0-9]+","GLOBAL",0)~ ~False()~
    END
    BUT_ONLY

  EXTEND_BOTTOM ~%SouthBeregostRoad_BCS%.bcs~           ~cdtweaks/baf/bgt_spawn_viconia.baf~

END

COPY_EXISTING ~%tutu_scriptbg%viconi%eet_var%.dlg~ ~override~ // viconi in bg/bgee, bgviconi in bgt, _viconi in tutu, viconi_ in eet
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~CreateCreature("FLAM2",\[605\.901\])~ ~CreateCreature("FLAM2",[1620.420],0)~  // BG lacks a direction parameter, fix syntax
    REPLACE_TEXTUALLY ~CreateCreature("FLAM2",\[605\.901\]~ ~CreateCreature("FLAM2",[1620.420]~  // move the FF spawn to reflect her new coords
    REPLACE_TEXTUALLY ~MoveToPoint(\[507\.806\])~ ~MoveToPoint([1520.320])~ 
  END
  BUT_ONLY  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Bardic Reputation Adjustment                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @126000 DESIGNATED 1260
GROUP @13
REQUIRE_PREDICATE GAME_IS ~totsc~ @25 // leave bgee/tutu/bgee/etc. to BG1 NPC
LABEL ~cd_tweaks_bardic_rep_adjustment~

APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~
CLEAR_IDS_MAP

/* Lake Poets "Quest"; Bard in party encounter */
/* creatures */
COPY_EXISTING ~volo.cre~ ~override/x#lp1rep.cre~
              ~volo.cre~ ~override/x#lp2rep.cre~
              ~volo.cre~ ~override/x#lp3rep.cre~
  SAY BATTLE_CRY2 #4521
  SAY BATTLE_CRY3 #4521
  SAY BATTLE_CRY4 #4521
  SAY BATTLE_CRY5 #4521
  SAY DIALOGUE_DEFAULT #4522
  WRITE_EVALUATED_ASCII 0x2CC ~%DEST_RES%~ #8 // dialog
  WRITE_EVALUATED_ASCII 0x280 ~%DEST_RES%~ #32 // death variable
  PATCH_IF (~%DEST_RES%~ STRING_EQUAL ~x#lp1rep~) THEN BEGIN
    SAY NAME1 @126001
    SAY NAME2 @126002
    WRITE_ASCII 0x248 ~x#lp1r~ #8 // override script
  END ELSE PATCH_IF (~%DEST_RES%~ STRING_EQUAL ~x#lp2rep~) THEN BEGIN
    SAY NAME1 @126003
    SAY NAME2 @126004
    WRITE_ASCII 0x248 ~x#lp2r~ #8 // override script
  END ELSE PATCH_IF (~%DEST_RES%~ STRING_EQUAL ~x#lp3rep~) THEN BEGIN
    SAY NAME1 @126005
    SAY NAME2 @126006
    WRITE_ASCII 0x248 ~x#lp3r~ #8 // override script
  END

/* scripts */
COMPILE EVALUATE_BUFFER ~cdtweaks/baf/x#lp1r.baf~
  USING ~%tra_location%/%LANGUAGE%/x#lp1r.tra~
COMPILE EVALUATE_BUFFER ~cdtweaks/baf/x#lp2r.baf~
  USING ~%tra_location%/%LANGUAGE%/x#lp2r.tra~
COMPILE EVALUATE_BUFFER ~cdtweaks/baf/x#lp3r.baf~
  USING ~%tra_location%/%LANGUAGE%/x#lp3r.tra~

/* dialogue */
COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/x#lp1rep.d~
  USING ~%tra_location%/%LANGUAGE%/x#lp1rep.tra~
COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/x#lp2rep.d~
  USING ~%tra_location%/%LANGUAGE%/x#lp2rep.tra~
COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/x#lp3rep.d~
  USING ~%tra_location%/%LANGUAGE%/x#lp2rep.tra~

/* Area Scripts for spawning */
EXTEND_TOP ~ar2301.bcs~ ~cdtweaks/baf/x#lp1repas.baf~ // The Friendly Arm Inn
EXTEND_TOP ~ar4904.bcs~ ~cdtweaks/baf/x#lp2repas.baf~ // The Carnival
EXTEND_TOP ~ar0705.bcs~ ~cdtweaks/baf/x#lp3repas.baf~ // The Elfsong

/* Ajantis Reputation Seller Reactions */
COMPILE EVALUATE_BUFFER ~cdtweaks/dlg/x#lprcad.d~
  USING ~%tra_location%/%LANGUAGE%/x#lprcad.tra~

EXTEND_BOTTOM ~ajantis.bcs~ ~cdtweaks/baf/x#lprcajr.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Change Cloakwood Mines Chapter End Trigger                 \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @127000 DESIGNATED 1270 // ~Revert Chapter Change in Cloakwood Mines to Non-ToTSC BG Behavior~
GROUP @13
REQUIRE_PREDICATE GAME_IS ~totsc bgt eet bgee tutu tutu_totsc~ @25
LABEL ~cd_tweaks_change_cloakwood_trigger~

COPY_EXISTING ~%CloakwoodMines_L4%.are~ override // AR1803, 8603bgt
  READ_ASCII 0x94 areascript (8) NULL
  PATCH_IF FILE_EXISTS_IN_GAME ~%areascript%.bcs~ BEGIN
    INNER_ACTION BEGIN
     COPY_EXISTING ~%areascript%.bcs~ override
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~Dead("Davaeorn")~ ~False() Dead("Davaeorn")~
//        REPLACE_EVALUATE
//          ~\(IF[ %TAB%%LNL%%MNL%%WLN%]+Dead("Davaeorn")[ %TAB%%LNL%%MNL%%WLN%]+Global("Chapter","GLOBAL",[45])[ %TAB%%LNL%%MNL%%WLN%]+THEN[ %TAB%%LNL%%MNL%%WLN%]+RESPONSE #100[ %TAB%%LNL%%MNL%%WLN%]+\)\(.+END\)~
//          BEGIN SPRINT actions "%MATCH2%" END
//          ~\1\2~
      END
     BUT_ONLY
    END
  END
  LPF fj_are_structure
      INT_VAR
      fj_type              = 0    //trap
      fj_box_left  = 380
      fj_box_top    = 1062
      fj_box_right    = 541
      fj_box_bottom   = 1277
      fj_cursor_idx   = 0   //no cursor
      fj_vertex_0  = 380 + (1076 << 16)
      fj_vertex_1  = 510 + (1160 << 16)
      fj_vertex_2  = 439 + (1263 << 16)
      fj_vertex_3  = 468 + (1277 << 16)
      fj_vertex_4  = 541 + (1148 << 16)
      fj_vertex_5  = 393 + (1062 << 16)
      fj_flags        = 0b0000000000000010
      fj_trap_detect  = 100
      fj_trap_remove  = 100
      fj_trap_active  = 1
      fj_trap_status  = 0
      fj_loc_x        = 404
      fj_loc_y        = 1300
      STR_VAR
      fj_structure_type   = region
      fj_name                      = ~Chapter trigger 1~
      fj_reg_script          = endch4
  END
  LPF fj_are_structure
      INT_VAR
      fj_type              = 0    //trap
      fj_box_left  = 1523
      fj_box_top    = 272
      fj_box_right    = 1629
      fj_box_bottom   = 356
      fj_cursor_idx   = 0   //no cursor
      fj_vertex_0  = 1523 + (292 << 16)
      fj_vertex_1  = 1607 + (356 << 16)
      fj_vertex_2  = 1629 + (334 << 16)
      fj_vertex_3  = 1550 + (272 << 16)
      fj_flags        = 0b0000000000000010
      fj_trap_detect  = 100
      fj_trap_remove  = 100
      fj_trap_active  = 1
      fj_trap_status  = 0
      fj_loc_x        = 1620
      fj_loc_y        = 340
      STR_VAR
      fj_structure_type   = region
      fj_name                      = ~Chapter trigger 2~
      fj_reg_script          = endch4
  END
  BUT_ONLY

//endch4.bcs should exist in game, but in case it does not
ACTION_IF !FILE_EXISTS_IN_GAME ~endch4.bcs~ THEN BEGIN
  COMPILE ~cdtweaks/baf/endch4.baf~ EVALUATE_BUFFER
END

// non-bg games also purge journal strings here
ACTION_IF !game_is_totsc THEN BEGIN

  COPY_EXISTING ~endch4.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      PATCH_IF tutu_gen BEGIN
        PATCH_FOR_EACH string IN 78162 85754 85767 80561 80565 86317 86297 85810 85824 85868 BEGIN
          REPLACE_TEXTUALLY ~\(IncrementChapter(\)~ ~EraseJournalEntry(%string%) \1~
        END
      END ELSE
      PATCH_IF game_is_bgt BEGIN
        PATCH_FOR_EACH string IN 
          74951 74952 74953 74954 74955 74956 74957 74958 74959 74960 74961 74962 74963
          74964 74965 74966 74967 74968 74969 74970 74971 74972 74973 74974 74975 74976
          74977 74978 74979 74980 74981 74982 74983 74984 74985 74986 74987 74988 74989
          74990 74991 74992 74993 74994 74995 74773 74775 74777 74996 74997 74784
        BEGIN
          REPLACE_TEXTUALLY ~\(IncrementChapter(\)~ ~EraseJournalEntry(%string%) \1~
        END
      END ELSE
      PATCH_IF game_is_bgee BEGIN
        PATCH_FOR_EACH string IN 27047 27046 27060 27059 26829 27455 27173 BEGIN
          REPLACE_TEXTUALLY ~\(IncrementChapter(\)~ ~EraseJournalEntry(%string%) \1~
        END
      END ELSE
      PATCH_IF game_is_eet BEGIN
        PATCH_FOR_EACH string IN 227047 227046 227060 227059 226829 227455 227173 BEGIN
          REPLACE_TEXTUALLY ~\(IncrementChapter(\)~ ~EraseJournalEntry(%string%) \1~
        END
      END
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// end game when Player1 dies                                 \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @128000 DESIGNATED 1280
GROUP @13
REQUIRE_PREDICATE GAME_IS ~iwdee iwd-in-bg2~ @25
LABEL ~cd_tweaks_game_ends_with_player1~

ACTION_IF game_is_iwd-in-bg2 THEN BEGIN // easy solution!

  COPY ~tobex_ini/tobextweak.ini~ ~tobex_ini~
    REPLACE_TEXTUALLY ~Engine:Disable End On Player1 Dead=1~ ~Engine:Disable End On Player1 Dead=0~

END ELSE BEGIN // iwdee

  COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
    WRITE_BYTE 0x14 (THIS & `BIT4) // remove 'player1 can die' bit from all alreas
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Protagonist replacement                                    \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @129000 DESIGNATED 1290
GROUP @13
REQUIRE_PREDICATE GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
LABEL ~cd_tweaks_protagonist_replacement~

ACTION_FOR_EACH file IN
  daccalia dalbion daldwin dambere damelia dangaar dapsel darden darundel dbaldemr dbandoth dbeorn dcallard
  dcallian dchalp dchals dchalw dconlan dcusthan ddenaini ddigby ddirtyll ddoogal dedion degenia dehealer 
  delibrar delisia demmrch derevain deverard dfalsear dfengla dferg dfgg dflozem dgareth dgerth dgina2 
  dginafae dgoblinc dgrisell dguello dhailee dharken dharpy dhildrth dhjollde dhobart dhroth dicasa 
  dilmadia djermsy djhonen djoril djorn dkerish dkieran dkieran2 dkutowng dlehland dlysanpr dmalasim 
  dmarketh dmirek dmurdaug dmytos dnorl dnorlino dnym dogre doldjed dorcchie dorrick doswald dperdiem 
  dpomab dpomend dquimby dquinn drawl dredtoe drikasha dseer dseth dsheemis dsoth dtest dthom dtiernon 
  dtiersto dtowngen dtybald dundlt1 dundlt2 dvaargln dvexing dvoiceda dweenog dwhitcom // dwinona // dwinona is a testing dlg and throws warnings to boot
  dwylf dxactile dyxun
  BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.dlg~ THEN BEGIN
    
    COPY_EXISTING ~%file%.dlg~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~LastTalkedToBy~  ~Protagonist~
      END
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// HoW accessible at 1st level                                \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @130000 DESIGNATED 1300
GROUP @13
REQUIRE_PREDICATE GAME_IS ~iwdee how totlm iwd-in-bg2~ @25
LABEL ~cd_tweaks_how_accessible_at_l1~

COPY_EXISTING ~dhjollde.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~LevelPartyGT(8)~      ~True()~  // iwd-in-bg2, also converts inverse to !True
    REPLACE_TEXTUALLY ~CheckPartyLevel(9)~   ~True()~  // iwd; also converts inverse check to !True
    REPLACE_TEXTUALLY ~XPLT(Player1,250000)~ ~False()~ // iwdee
    REPLACE_TEXTUALLY ~XPGT(Player1,249999)~ ~True()~  // iwdee
  END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// restore all  BG2 spells, and make scrolls available        \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @131000 DESIGNATED 1310
GROUP @13
REQUIRE_PREDICATE GAME_IS ~iwd-in-bg2~ @25
LABEL ~cd_tweaks_restore_bg2_spells~

//LAF make_label STR_VAR label=~i#bg2spells~ END

      COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
          PATCH_IF ~SOURCE_SIZE~ >0x71 BEGIN

              READ_SHORT 0x1c ~type~
              PATCH_IF ~type~=11 BEGIN // only check scrolls
                       GET_OFFSET_ARRAY ab_array 0x64 4 0x68 2 0 0 0x38
                       PHP_EACH ab_array AS int => ab_off BEGIN
                                CLEAR_ARRAY fx_array
                                GET_OFFSET_ARRAY2 fx_array ab_off ITM_V10_HEAD_EFFECTS
                                PHP_EACH fx_array AS int => fx_off BEGIN
                                         READ_SHORT fx_off fx_type
                                         PATCH_IF fx_type = 147 BEGIN
                                             READ_ASCII fx_off + 0x14 spell
                                             TO_LOWER ~spell~
                                             SPRINT $scrollmap(~%spell%~) ~%SOURCE_RES%~
                                         END
                                END
                       END
              END
          END
      BUT_ONLY

ACTION_FOR_EACH ~magespell~ IN
120 123 125
220 221 223 224
301 302 307 318 319 320 321 322 324 325
403 409 415 416 417 418 419 420 421 423 424 425
505 510 511 512 513 515 517 518 519 520 522
606 607 608 609 611 613 617 618 619 620 621 622 623 624
701 702 703 704 705 707 708 710 711 712 717 718 719 720 722
803 804 805 807 808 809 811 813 816 817 818 // not odd Spell Deflection (802)
902 903 905 907 908 909 910 911 913 914 915 916 917 918 919
BEGIN
     OUTER_SET ~level~=~magespell~ / 100
     COPY_EXISTING ~hidespl.2da~ ~override~
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPWI%magespell%[ ]+[\*]+~ ~~
     BUT_ONLY
     ACTION_IF ~level~=1 OR ~level~=2 BEGIN
         OUTER_SPRINT ~store~ ~kuork1~
         OUTER_SPRINT ~after~ ~spwi223a~
     END
     ACTION_IF ~level~=3 OR ~level~=4 BEGIN
         OUTER_SPRINT ~store~ ~kuork2~
         OUTER_SPRINT ~after~ ~spwi517x~
     END
     ACTION_IF ~level~=5 OR ~level~=6 BEGIN
         OUTER_SPRINT ~store~ ~kuork3~
         OUTER_SPRINT ~after~ ~scstor~
     END
     ACTION_IF ~level~>6 BEGIN
        RANDOM_SEED 3.14
        OUTER_SET ~randnum~=RANDOM (1 4)
        ACTION_IF ~randnum~=1 BEGIN
            OUTER_SPRINT ~store~ ~edion~
            OUTER_SPRINT ~after~ ~spwi808x~
        END
        ACTION_IF ~randnum~=2 BEGIN
            OUTER_SPRINT ~store~ ~bandoth~
            OUTER_SPRINT ~after~ ~scssha~
        END
        ACTION_IF ~randnum~=3 BEGIN
            OUTER_SPRINT ~store~ ~ldd_nym~
            OUTER_SPRINT ~after~ ~scprism~
        END
        ACTION_IF ~randnum~=4 BEGIN
            OUTER_SPRINT ~store~ ~kieran2~
            OUTER_SPRINT ~after~ ~spwi805x~
        END
     END
     OUTER_SPRINT ~file~ ~spwi%magespell%~
     OUTER_SPRINT ~scroll~ $scrollmap(EVALUATE_BUFFER ~%file%~)
     COPY_EXISTING ~%store%.sto~ ~override~
         ADD_STORE_ITEM ~%scroll%~ AFTER ~%after%~ #0 #0 #0 ~IDENTIFIED~ #2
END

ACTION_FOR_EACH ~priestspell~ IN
111 113 315 318 319 409 410 412 413 415 417 505 506
513 514 515 516 601 604 609 610 611 612 613 701 703
704 706 708 711 713 718 719
BEGIN
     COPY_EXISTING ~hidespl.2da~ ~override~
        REPLACE_TEXTUALLY CASE_INSENSITIVE ~SPPR%priestspell%[ ]+[\*]+~ ~~
     BUT_ONLY
END

COPY_EXISTING ~splsrckn.2da~ ~override~
      FOR (i=20;i<25;i=i+1) BEGIN
         SET_2DA_ENTRY i 9 9 3
      END
      FOR (i=25;i<36;i=i+1) BEGIN
         SET_2DA_ENTRY i 9 9 4
      END
      FOR (i=36;i<41;i=i+1) BEGIN
         SET_2DA_ENTRY i 9 9 5
      END
      
COPY_EXISTING ~tobex_ini/tobexcore.ini~ ~tobex_ini~ 
   REPLACE_TEXTUALLY ~Scrollable Mage Spellbook=0~ ~Scrollable Mage Spellbook=1~
   REPLACE_TEXTUALLY ~Scrollable Priest Spellbook=0~ ~Scrollable Priest Spellbook=1~
   
COMPILE ~cdtweaks/dlg/iib_famil.d~ // disables BG2 advice from familiars

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// BG2-style journal                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/*
BEGIN @132000 DESIGNATED 1320
GROUP @13
REQUIRE_PREDICATE GAME_IS ~iwd-in-bg2~ @25

INCLUDE ~cdtweaks/lib/journal_entries.tpa~
LAM ~journal_data~
LAF journal_edit_dlg END
LAF residual_journal_hacks END
*/

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// NPCs can not pass doors                                    \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @133000 DESIGNATED 1330
GROUP @13
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd-in-bg2 bgee bg2ee eet iwdee~ @25
LABEL ~cd_tweaks_I_am_a_servant_of_the_Secret_Fire_wielder_of_the_flame_of_Anor_You_cannot_pass_The_dark_fire_will_not_avail_you_flame_of_Udun_Go_back_to_the_Shadow_You_cannot_pass~

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
  PATCH_IF ("%SOURCE_RES%" STRING_COMPARE_CASE "ar0028") BEGIN
    READ_SHORT 0x5a trig_num
    READ_SHORT 0x5c trig_off
    FOR (index = 0 ; index < trig_num ; ++index) BEGIN
      READ_SHORT (trig_off + 0x20 + (index * 0xc4)) type
      PATCH_IF type = 2 BEGIN // travel triggers only
        WRITE_LONG (trig_off + 0x60 + (index * 0xc4)) (THIS | BIT9) // adds 'cannot be passed by NPC' flag
      END
    END 
  END  
  BUT_ONLY
  
// 1340 series is reserved for the individual stronghold components, but moved to be near the other stronghold components  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Bastard Swords                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @202000 DESIGNATED 2020
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_2h_bastard_swords~

ACTION_IF original_bg1 OR original_iwd THEN BEGIN OUTER_SET use_type = 1 END ELSE BEGIN OUTER_SET use_type = 0 END
INCLUDE ~cdtweaks/lib/2h_weapons.tpa~  // text replacement macros

ACTION_IF original_iwd THEN BEGIN

  LAUNCH_ACTION_FUNCTION 2h_weapons INT_VAR bg1_type = 57 END // diff item type in iwd

END ELSE BEGIN

  LAUNCH_ACTION_FUNCTION 2h_weapons END // defaults are bastard sword settings

END

// if breakable weapons from tutufix installed or BGEE/BGT breakable weapons
ACTION_IF ((game_is_eet = 0) AND ((FILE_EXISTS_IN_GAME ~_sw1h01.spl~) OR (FILE_EXISTS_IN_GAME ~sw1h01.spl~))) THEN BEGIN

  ACTION_CLEAR_ARRAY cd_breakables
  ACTION_IF FILE_EXISTS_IN_GAME ~sw1h01.spl~  BEGIN ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_breakables BEGIN ~c!bs1~ => ~sw1h01~ END END
  ACTION_IF FILE_EXISTS_IN_GAME ~_sw1h01.spl~ BEGIN ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_breakables BEGIN ~c!bs2~ => ~_sw1h01~ END END
  ACTION_IF !game_is_bgt BEGIN OUTER_SET op = 146 END ELSE BEGIN OUTER_SET op = 309 END
  
  ACTION_PHP_EACH cd_breakables AS item => spell BEGIN
  
    COPY_EXISTING ~%item%.itm~ ~override~
      LPF ALTER_EFFECT INT_VAR check_headers = 1 check_globals = 0 match_opcode = op STR_VAR match_resource = EVAL "%spell%" resource = EVAL "%item%" END

    // make new breaking spell
    COPY_EXISTING ~%spell%.spl~ ~override/%item%.spl~ // create breakage spell for new swords
      LPF ALTER_EFFECT INT_VAR match_opcode = 112 STR_VAR match_resource = EVAL "%spell%" resource = EVAL "%item%" END

    ACTION_IF game_is_bgt BEGIN
      
      COPY_EXISTING ~dplayer2.bcs~ ~override~
                    ~dplayer3.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          APPEND_FILE ~cdtweaks/baf/bgt_breakage.baf~
          REPLACE_TEXTUALLY ~CD_REPLACE_ME~ ~%item%~
        END
        BUT_ONLY
        
    END    

  END

END

// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~cdtweaks/dlg/2hbastard.d~

END
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Katanas                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @203000 DESIGNATED 2030
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_2h_katanas~

ACTION_IF original_bg1 OR original_iwd THEN BEGIN OUTER_SET use_type = 1 END ELSE BEGIN OUTER_SET use_type = 0 END
INCLUDE ~cdtweaks/lib/2h_weapons.tpa~  // text replacement macros

LAUNCH_ACTION_FUNCTION 2h_weapons // defaults are bastard sword settings
  INT_VAR prof_check   = 94
  STR_VAR 2da_file     = cdkatana
          prefix       = kt
          2h_bam       = isw2h01
          1h_bam       = isw1h43
          2h_paperdoll = S2
END

// if breakable weapons from tutufix installed or BGEE/BGT breakable weapons
ACTION_IF ((game_is_eet = 0) AND (FILE_EXISTS_IN_GAME ~sw1h43.spl~)) THEN BEGIN

  // change existing breakage spell to point to new spell
  COPY_EXISTING ~c!kt1.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 match_opcode = 146 STR_VAR resource = "c!kt1" END

  // make new breaking spell
  COPY_EXISTING ~sw1h43.spl~ ~override/c!kt1.spl~ // create breakage spell for new swords
    LPF ALTER_EFFECT INT_VAR match_opcode = 112 STR_VAR match_resource = EVAL "%break_spell%" resource = "cdkt1" END

END

ACTION_IF game_is_bgt BEGIN

  OUTER_SPRINT ref ~c!kt1~

  // change existing breakage spell to point to new spell
  COPY_EXISTING ~%ref%.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR check_headers = 1 check_globals = 0 match_opcode = 309 STR_VAR resource = EVAL ~%ref%~ END
    
  COPY_EXISTING ~dplayer2.bcs~ ~override~
                ~dplayer3.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      APPEND_FILE ~cdtweaks/baf/bgt_breakage.baf~
      REPLACE_TEXTUALLY ~CD_REPLACE_ME~ ~%ref%~
    END
    BUT_ONLY

END

// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~cdtweaks/dlg/2hkatanas.d~
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Two-Handed Axes                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @203500 DESIGNATED 2035
GROUP @9 
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_2h_axes~

ACTION_IF original_bg1 OR original_iwd THEN BEGIN OUTER_SET use_type = 1 END ELSE BEGIN OUTER_SET use_type = 0 END
INCLUDE ~cdtweaks/lib/2h_weapons.tpa~  // text replacement macros

LAUNCH_ACTION_FUNCTION 2h_weapons // defaults are bastard sword settings
  INT_VAR prof_check   = 92
          bg1_type     = 25
          overhead     = 0
          slashing     = 35
          thrusting    = 65
  STR_VAR 2da_file     = cdaxe
          prefix       = ax
          2h_bam       = ihalb01
          1h_bam       = iax1h01
          2h_paperdoll = hb
END

// if breakable weapons from tutufix installed or BGEE/BGT breakable weapons
ACTION_IF ((game_is_eet = 0) AND ((FILE_EXISTS_IN_GAME ~_ax1h01.spl~) OR (FILE_EXISTS_IN_GAME ~ax1h01.spl~))) THEN BEGIN

  ACTION_CLEAR_ARRAY cd_breakables
  ACTION_IF FILE_EXISTS_IN_GAME ~ax1h01.spl~  BEGIN ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_breakables BEGIN ~c!ax1~  => ~ax1h01~ END END
  ACTION_IF FILE_EXISTS_IN_GAME ~_ax1h01.spl~ BEGIN ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_breakables BEGIN ~c!ax14~ => ~_ax1h01~ END END
  ACTION_IF !game_is_bgt BEGIN OUTER_SET op = 146 END ELSE BEGIN OUTER_SET op = 309 END
  
  ACTION_PHP_EACH cd_breakables AS item => spell BEGIN
  
    COPY_EXISTING ~%item%.itm~ ~override~
      LPF ALTER_EFFECT INT_VAR check_headers = 1 check_globals = 0 match_opcode = op STR_VAR match_resource = EVAL "%spell%" resource = EVAL "%item%" END

    // make new breaking spell
    COPY_EXISTING ~%spell%.spl~ ~override/%item%.spl~ // create breakage spell for new swords
      LPF ALTER_EFFECT INT_VAR match_opcode = 112 STR_VAR match_resource = EVAL "%spell%" resource = EVAL "%item%" END

    ACTION_IF game_is_bgt BEGIN
      
      COPY_EXISTING ~dplayer2.bcs~ ~override~
                    ~dplayer3.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          APPEND_FILE ~cdtweaks/baf/bgt_breakage.baf~
          REPLACE_TEXTUALLY ~CD_REPLACE_ME~ ~%item%~
        END
        BUT_ONLY
        
    END    

  END

END

// tutorial obe also destroys illusionary alternate axes
ACTION_IF FILE_EXISTS_IN_GAME obe.dlg THEN BEGIN

  COMPILE ~cdtweaks/dlg/2haxes_obe.d~
  
END

// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~cdtweaks/dlg/2haxes.d~

END

// edion/tiernon can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME dedion.dlg THEN BEGIN

  COMPILE ~cdtweaks/dlg/2haxes_iwd.d~
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Universal Clubs                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @204000 DESIGNATED 2040
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_universal_clubs~

OUTER_SET type_match = 0
OUTER_SET modify_descript = 0

ACTION_IF !enhanced_edition BEGIN

  ACTION_IF original_bg1 OR original_iwd OR game_is_iwd2 THEN BEGIN
    ACTION_IF original_iwd THEN BEGIN
      OUTER_SET type_match = 44
    END ELSE BEGIN
      OUTER_SET type_match = 17
    END
  END
  
  ACTION_IF FILE_CONTAINS_EVALUATED (~%tra_location%/%LANGUAGE%/setup.tra~ ~^[ %TAB%]*@204001[ %TAB%]*=~) BEGIN // if translated strings are available...

    OUTER_SET modify_descript = 1 // ...then try to update item descriptions
  
    OUTER_SPRINT unusable_regexp_text      @204001
    OUTER_SPRINT unusable_replacement_text @204002
    OUTER_SPRINT mage_regexp_text          @204003
  
  END

END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE  0x31 prof ELSE 0
  READ_SHORT 0x1c type
  READ_ASCII 0x22 anim (2)
  PATCH_IF ((((type_match = 0) AND (prof = 115)) OR          // for bg2-based games w/ proficiencies
              (type = type_match)) AND                       // games w/o profs
              ("%anim%" STRING_COMPARE_CASE "cl" = 0)) BEGIN // animation must match
    READ_BYTE 0x1f fighter
    PATCH_IF ((fighter BAND BIT3) = 0) BEGIN // if usable by single-class fighter
      WRITE_BYTE 0x20 (THIS BAND `BIT2)      // make usable by single-class mages
      PATCH_IF modify_descript BEGIN // skip this junk for EEs or if translation not available

        // update descriptions
        PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
              REPLACE_TEXTUALLY ~%unusable_regexp_text%~ ~%unusable_replacement_text%~
            END
            PATCH_IF ((~%desc%~ STRING_CONTAINS_REGEXP ~^%unusable_replacement_text%~) == 0) BEGIN
              // extract usability info
              INNER_PATCH_SAVE usab_block ~%desc%~ BEGIN
                REPLACE_TEXTUALLY ~\(.*[%LNL%%MNL%%WNL%]\)*%unusable_replacement_text%~ ~~
              END
              // update info
              INNER_PATCH_SAVE usab_block_new ~%usab_block%~ BEGIN
                REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%][- %TAB%]*$~ ~~ // remove any empty lines
                REPLACE_TEXTUALLY ~^[- %TAB%]*~ ~ ~ // make sure entries are indented by one space
                REPLACE_TEXTUALLY ~%mage_regexp_text%~ ~~ // remove mage line
              END
              // replace old usability info with new
              INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                REPLACE_TEXTUALLY EXACT_MATCH ~%usab_block%~ ~%usab_block_new%~
              END
              
              // check if mage was the only thing that couldn't use
              INNER_PATCH_SAVE compare ~%desc%~ BEGIN 
                REPLACE_TEXTUALLY ~^%unusable_replacement_text%.*[%LNL%%MNL%%WNL%] [^- %TAB%]~ ~~
              END
              // if it was...
              PATCH_IF (~%desc%~ STRING_EQUAL ~%compare%~) BEGIN
                INNER_PATCH_SAVE desc ~%desc%~ BEGIN
                  // remove 'not usable by' line, so it won't be sitting all by its lonesome now
                  REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]%unusable_replacement_text%.*~ ~~
                END
              END
          
              // write changes
              SAY_EVALUATED ~%offset%~ ~%desc%~
            END

          END
        END
        
      END
    END
  END
  BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~weapprof.2da~ THEN BEGIN

  ACTION_CLEAR_ARRAY style_kits
  OUTER_SET $style_kits(MAGE)     =  1 // add straight mage
  OUTER_SET $style_kits(SORCERER) = 19 // shouldn't be in weapprof, but let's be safe
  COPY_EXISTING ~kitlist.2da~ ~override~
    SET col_check = (9 + enhanced_edition)
    COUNT_2DA_ROWS col_check rows
    FOR (row = 1 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 8 col_check class
      PATCH_IF ((class = 1) OR (class = 19)) BEGIN // mage or sorcerer kit
        READ_2DA_ENTRY row 1 col_check kit
        SET $style_kits("%kit%") = class
      END
    END   
    BUT_ONLY

  COPY_EXISTING ~weapprof.2da~ ~override~
    REPLACE_TEXTUALLY ~\(ID[ %TAB%]+NAME_REF\)~ ~CD_DELETE_ME \1~ // adding column to header column
    COUNT_2DA_COLS col_count
    FOR (col = 4 ; col < col_count ; ++col) BEGIN
      READ_2DA_ENTRY 0 col col_count kit
      PATCH_IF VARIABLE_IS_SET $style_kits("%kit%") BEGIN
        READ_2DA_ENTRY 18 col col_count club_check
        PATCH_IF !club_check BEGIN // only if 0 pips
          SET_2DA_ENTRY 18 col col_count 1
        END  
      END
    END  
    REPLACE_TEXTUALLY ~CD_DELETE_ME~ ~~ // removing it
    PRETTY_PRINT_2DA
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Weapon Styles for All (Idobek)                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @206000 DESIGNATED 2060
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2162~ @16 // bg profs w/o weapon styles
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2164~ @16 // iwd profs w/o weapon styles
LABEL ~cd_tweaks_weapon_styles_for_all~

ACTION_CLEAR_ARRAY style_kits
OUTER_SET $style_kits(MAGE) = 1
OUTER_SET $style_kits(SORCERER) = 19 // shouldn't be in weapprof, but let's be safe
OUTER_SET $style_kits(MONK) = 20 
COPY_EXISTING ~kitlist.2da~ ~override~
  SET col_check = (9 + enhanced_edition)
  COUNT_2DA_ROWS col_check rows
  FOR (row = 1 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 8 col_check class
    PATCH_IF ((class = 1) OR (class = 19) OR (class = 20)) BEGIN // mage or sorcerer or monk
      READ_2DA_ENTRY row 1 col_check kit
      SET $style_kits("%kit%") = class
    END
  END   
  BUT_ONLY

COPY_EXISTING ~weapprof.2da~ ~override~
  REPLACE_TEXTUALLY ~\(ID[ %TAB%]+NAME_REF\)~ ~CD_DELETE_ME \1~ // adding column to header column
  COUNT_2DA_COLS col_count
  READ_2DA_ENTRY 30  4 col_count mage_sns
  READ_2DA_ENTRY 30 32 col_count kensai_sns  
  SET single  = 2 // if a kit/class ever needs exceptions here, move these into the loop   
  SET dual    = 3
  // if mages can use bucklers installed, or some other mod has given mages points for sns
  PATCH_IF mage_sns OR MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2520~ BEGIN
    SET mage_sns = 2
  END ELSE BEGIN
    SET mage_sns = 0
  END  
  FOR (col = 4 ; col < col_count ; ++col) BEGIN
    READ_2DA_ENTRY 0 col col_check kit
    SET 2handed = 2
    SET sns     = 2 
    PATCH_IF VARIABLE_IS_SET $style_kits("%kit%") BEGIN
      PATCH_IF $style_kits("%kit%") = 20 BEGIN
        SET 2handed = 0
        SET sns     = 0 
      END ELSE BEGIN // for mage or sorcerer
        SET sns     = mage_sns  
      END        
    END
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 29 col 2handed
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 col sns
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 31 col single
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 32 col dual
  END  
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 32 kensai_sns
  SET_2DA_ENTRIES_NOW ~s2el_weapprof~ col_count
  REPLACE_TEXTUALLY ~CD_DELETE_ME~ ~~ // removing it
  PRETTY_PRINT_2DA
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Delay High Level Abilities                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @208000 DESIGNATED 2080
GROUP @9
REQUIRE_PREDICATE GAME_IS ~tob bg2ee bgt eet~   @25 // ToB required
LABEL ~cd_tweaks_delay_hlas~

COPY_EXISTING ~lunumab.2da~ ~override~
  COUNT_2DA_ROWS 5 rows
  FOR (row = 0; row < rows; ++row) BEGIN
    READ_2DA_ENTRY row 1 5 level
    PATCH_IF (level < 21) BEGIN
      SET_2DA_ENTRY row 1 5 ~21~
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change XP Cap                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// remove cap                                       \\\\\
/////                                                  \\\\\

BEGIN @209001 DESIGNATED 2090 // remove xp cap
GROUP @9
SUBCOMPONENT @209000 // change xp cap
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_remove_xp_cap~

COPY ~cdtweaks/2da/xplevel.2da~ ~override/xplevel.2da~
INCLUDE ~cdtweaks/lib/xp_common.tpa~

// extend bard, ranger pickpocketing to 200 and levels to 50
INCLUDE ~cdtweaks/lib/thieving_to_50.tpa~

ACTION_CLEAR_ARRAY  cd_extend_tables // use array so we can push HP table entries into it
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_extend_tables BEGIN
  lvlmodwm => 0 // wild mage level modulation for wild surges
  monkfist => 0 // monk fist progression
  mxsplbrd => 0 // bard spell table
  mxspldd  => 0 // dragon disciple spell table
  mxspldru => 0 // druid spell table
  mxsplpal => 0 // paladin spell table
  mxsplprs => 0 // priest spell table
  mxsplran => 0 // ranger spell table
  mxsplshm => 0 // shaman spell table
  mxsplsrc => 0 // sorcerer spell table
  mxsplwiz => 0 // mage spell table
  raisdead => 0 // raise dead temple prices
  splsrckn => 0 // sorcerer spell table
  splshmkn => 0 // shaman spell table

  // manually add hp tables for non-ee games; being added below again harms nothing
  hpbarb   => 0 // barbarian hp table
  hpmonk   => 0 // monk hp table
  hpprs    => 0 // priest hp table
  hprog    => 0 // rogue hp table
  hpwar    => 0 // warrior hp table
  hpwiz    => 0 // mage hp table

END

// extend hp tables for ee games
ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN

  // add hp tables to list of tables to extend
  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS ~2~ "rows"
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      SET "exists" = 0
      READ_2DA_ENTRY "%index%" 1 2 "table"
      DEFINE_ASSOCIATIVE_ARRAY cd_extend_tables BEGIN "%table%" => 0 END
    END

END

// easy replacements for 2das with levels as rows
ACTION_PHP_EACH cd_extend_tables AS file => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN

    APPEND ~%file%.2da~ ~CD_EOF_CUSTOM~ // also normalizes excessive/missing line breaks at EOF

    // why the chicanery with CD_EOF_CUSTOM and REPLACE_EVALUATE? The 2DA functions get unreliable on two-column tables such as raisdead
    COPY_EXISTING ~%file%.2da~ ~override~ // wild mage level modulation for wild surges
      FOR (index = 1 ; index < 50 ; ++index) BEGIN
        REPLACE_EVALUATE ~^%index%\([ %TAB%].*\)\([%LNL%%MNL%%WNL%]+CD_EOF_CUSTOM\)~ BEGIN
          SET new = index + 1
        END
~%index%\1
%new%\1\2~
      END
      REPLACE_TEXTUALLY ~CD_EOF_CUSTOM~ ~~
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END

// extend level-based abilities to 50
ACTION_FOR_EACH file IN backstab crippstr layhands savemonk saveprs saverog savewar savewiz sneakatt thac0 xpbonus wspatck BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN

    COPY_EXISTING ~%file%.2da~ ~override~
      COUNT_2DA_COLS col_num
      COUNT_2DA_ROWS (col_num - 1) row_num
      READ_2DA_ENTRY 0 (col_num - 2) (col_num - 1) level    // read last enry of top line as level
      READ_2DA_ENTRY 0 0 (col_num - 1) val                  // weidu seems to freak out with a top line w/ one less entry
      SET_2DA_ENTRY  0 0 (col_num - 1) ~CD_DELETE_ME %val%~ // temp
      FOR (index = level ; index < 50 ; ++index) BEGIN
        SET new = index + 1
        FOR (index2 = 1 ; index2 < row_num ; ++index2) BEGIN // extend all other rows with previous column's value
          READ_2DA_ENTRY index2 (col_num - 1) (col_num) val
          SET_2DA_ENTRY  index2 (col_num - 1) (col_num) ~%val% %val%~
        END
        SET_2DA_ENTRY 0 (col_num - 1) (col_num) ~%index% %new%~ // extend level row
        SET col_num += 1
      END
      REPLACE_TEXTUALLY ~CD_DELETE_ME~ ~~
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END

ACTION_IF FILE_EXISTS_IN_GAME ~kitlist.2da~ THEN BEGIN // if kits are present

  ACTION_CLEAR_ARRAY  cd_extend_clabs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_extend_clabs BEGIN // start with base class tables
    clabba01 => 0 // generic bard abilities
    clabdr01 => 0 // generic druid abilities
    clabfi01 => 0 // generic fighter abilities
    clabmo01 => 0 // generic monk abilities
    clabpa01 => 0 // generic paladin abilities
    clabpa05 => 0 // fallen paladin abilities
    clabpr01 => 0 // generic priest abilities
    clabrn01 => 0 // generic ranger abilities
    clabrn05 => 0 // fallen ranger abilities
    clabth01 => 0 // generic thief abilities
  END
  
  // add kit tables dynamically by reading kitlist
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      SET "exists" = 0
      READ_2DA_ENTRY "%index%" 5 9 "clab"
      DEFINE_ASSOCIATIVE_ARRAY cd_extend_clabs BEGIN "%clab%" => 0 END
    END
    BUT_ONLY
  
  // extend clab abilities to 50
  ACTION_PHP_EACH cd_extend_clabs AS file => foo BEGIN
  
    ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN
  
      COPY_EXISTING ~%file%.2da~ ~override~      
        // this is adapted from argent77's a7#add_kit_ex#normalize_table to both fix and extend tables
        // https://github.com/Argent77/A7-ImprovedArcher/blob/153f3258f9b884a96ff58537175062bd6b280000/A7%23ImprovedArcher/lib/a7%23add_kit_ex.tpa#L233
        SET num_added = 0
        COUNT_2DA_COLS num_cols
        READ_2DA_ENTRIES_NOW table 1
        READ_2DA_ENTRY_FORMER table 1 0 default
        FOR (row = 0; row < table; ++row) BEGIN
          READ_2DA_ENTRY_FORMER table row 0 line
          FOR (col = 1; col < num_cols && VARIABLE_IS_SET $table(~%row%~ ~%col%~); ++col) BEGIN
            READ_2DA_ENTRY_FORMER table row col v
            TEXT_SPRINT line ~%line% %v%~
          END
          // fill in missing data
          PATCH_IF (row = 2) BEGIN  // header
            FOR (; col < 50; ++col) BEGIN
              SET col_header = col + 1
              TEXT_SPRINT line ~%line% %col_header%~
              SET num_added += 1
            END
          END ELSE PATCH_IF (row > 2) BEGIN // content
            FOR (; col < 51; ++col) BEGIN
              TEXT_SPRINT line ~%line% %default%~
              SET num_added += 1
            END
          END
          TEXT_SPRINT $lines(~%row%~) ~%line%%LNL%~
          SET $lines(~%row%~ ~size~) = STRING_LENGTH $lines(~%row%~)
        END

        // write normalized table to buffer
        PATCH_IF (num_added > 0) BEGIN
          DELETE_BYTES 0 BUFFER_LENGTH
          FOR (row = 0 pos = 0; row < table; ++row) BEGIN
            SET size = $lines(~%row%~ ~size~)
            INSERT_BYTES pos size
            WRITE_ASCIIE pos $lines(~%row%~) (size)
            SET pos += size
          END
          PRETTY_PRINT_2DA
        END
        BUT_ONLY
  
    END
  
  END

END

ACTION_CLEAR_ARRAY  cd_start_areas
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_start_areas BEGIN // start with base class tables
  startare => 0 // generic bard abilities
  startbp  => 0 // generic druid abilities
END

ACTION_IF FILE_EXISTS_IN_GAME ~campaign.2da~ THEN BEGIN

  COPY_EXISTING ~campaign.2da~ ~override~
    COUNT_2DA_COLS col_num
    COUNT_2DA_ROWS (col_num - 1) row_num
    FOR (index = 1 ; index < row_num ; ++index) BEGIN // start at 1 to skip header row
      READ_2DA_ENTRY index 13 13 file
      DEFINE_ASSOCIATIVE_ARRAY cd_start_areas BEGIN "%file%" => 0 END
    END
    BUT_ONLY

END

// for EE
ACTION_PHP_EACH cd_start_areas AS file => val BEGIN

  PRINT ~patching campaign file %file%.2da~

  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ THEN BEGIN

    COPY_EXISTING ~%file%.2da~ ~override~
      REPLACE_TEXTUALLY ~\(START_\(MP_\)?XP_CAP[ %TAB%]+\)[0-9]+~ ~\1-1~
      BUT_ONLY

  END

END

ACTION_IF ((FILE_EXISTS_IN_GAME ~lunumab.2da~) AND (!FILE_EXISTS_IN_GAME ~spcl900.spl~)) THEN BEGIN // games with hla tables but no hlas (looking at you bgee, sod)
    
  COPY_EXISTING ~lunumab.2da~ ~override~
    COUNT_2DA_ROWS 5 rows
    FOR (row = 0; row < rows; ++row) BEGIN
      SET_2DA_ENTRY row 1 5 ~99~
    END
    BUT_ONLY

END

/////                                                  \\\\\
///// cap at level 20                                  \\\\\
/////                                                  \\\\\

BEGIN @209100 DESIGNATED 2091
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee~ @25
SUBCOMPONENT @209000 // change xp cap
LABEL ~cd_tweaks_cap_xp_at_l20~

COPY ~cdtweaks/2da/xplevel20.2da~ ~override/xplevel.2da~
INCLUDE ~cdtweaks/lib/xp_common.tpa~

/////                                                  \\\\\
///// cap at level 30                                  \\\\\
/////                                                  \\\\\

BEGIN @209200 DESIGNATED 2092
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee~ @25
SUBCOMPONENT @209000 // change xp cap
LABEL ~cd_tweaks_cap_xp_at_l30~

COPY ~cdtweaks/2da/xplevel30.2da~ ~override/xplevel.2da~
INCLUDE ~cdtweaks/lib/xp_common.tpa~

/* 
neat idea inspired by similar components in EET, but unworkable for dual-classes

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BGT: Additional Experience Cap While in BG       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @257000 DESIGNATED 2570
GROUP @9
REQUIRE_PREDICATE GAME_IS ~bgt~ @25
LABEL ~cd_tweaks_bgt_xp_cap_in_bg~

OUTER_SET xp_cap = 161000 // modify as desired

OUTER_SPRINT trigger ~GlobalLT("ENDOFBG1","GLOBAL",2)~ // don't modify as desired
OUTER_SPRINT spell   ~cdxpbg~ 
INCLUDE ~cdtweaks/lib/bgt_xp_caps.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// BGT: Additional Experience Cap While in SoA      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @257100 DESIGNATED 2571
GROUP @9
REQUIRE_PREDICATE GAME_IS ~bgt~ @25
LABEL ~cd_tweaks_bgt_xp_cap_in_soa~

OUTER_SET xp_cap = 2950000 // modify as desired

OUTER_SPRINT trigger ~Global("ENDOFBG1","GLOBAL",2) GlobalLT("Chapter","GLOBAL",8)~ // don't modify as desired
OUTER_SPRINT spell   ~cdxpsoa~ 
INCLUDE ~cdtweaks/lib/bgt_xp_caps.tpa~

*/

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow Thief Skills and Stealth in Heavy Armor    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @210000 DESIGNATED 2100
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_thieving_in_heavy_armor~

INCLUDE ~cdtweaks/lib/thieving_descripts.tpa~ // text replacement macros
INCLUDE ~cdtweaks/lib/thieving_to_50.tpa~     // extend bard, ranger pickpocketing to 200 and levels to 50

// to get unarmored bonuses to work, bonuses are added in the race bonus tables
// all non-robe armor offsets these
COPY_EXISTING ~skillrac.2da~ ~override~
  PATCH_IF original_bg1 OR original_iwd OR game_is_pst OR game_is_pstee BEGIN // games that use stealth
    SET use_stealth = 1
    SET max_col = 5
  END ELSE BEGIN
    SET use_stealth = 0
    SET max_col = 8
  END
  COUNT_2DA_ROWS max_col rows // five columns in bg/iwd, eight otherwise
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 1 max_col val
    SET_2DA_ENTRY  index 1 max_col (val + 5) // pickpocket
    READ_2DA_ENTRY index 4 max_col val
    SET_2DA_ENTRY  index 4 max_col (val + 10) // move silently
    PATCH_IF max_col > 5 BEGIN // skip old games that use stealth
      READ_2DA_ENTRY index 5 max_col val
      SET_2DA_ENTRY  index 5 max_col (val + 5) // HiS
    END
  END
  BUT_ONLY
     
INCLUDE ~cdtweaks/lib/common_armor_list.tpa~

ACTION_PHP_EACH cd_common_armor_list AS params => item BEGIN

  ACTION_IF use_stealth BEGIN
    OUTER_SET params_7 = params_9 // use 'stealth' value for move silently
    OUTER_SET params_8 = 255      // ignore hide in shadows
  END 

  // make sure we have something to do, e.g. not futzing with a robe
  ACTION_IF ((params_4 != 255) OR     // pickpocketing 
             (params_5 != 255) OR     // locks
             (params_6 != 255) OR     // traps
             (params_7 != 255) OR     // move silently
             (params_8 != 255)) BEGIN // hide in shadows
  
    COPY_EXISTING ~%item%.itm~ ~override~
      READ_SHORT 0x1c sanity_check
      PATCH_IF sanity_check = 2 BEGIN // type 2 is armor
        LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 144 END // delete disable thieving buttons
        PATCH_IF (use_stealth = 0) BEGIN
          PATCH_IF (params_8 != 255) BEGIN
            LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 275 END
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 275 target = 1 parameter1 = (0 - params_8 - 5) timing = 2 END // extra 5 to offset unarmored bonus
          END ELSE BEGIN
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 275 target = 1 parameter1 = "-5"               timing = 2 END // extra 5 to offset unarmored bonus
          END
        END
        PATCH_IF (params_4 != 255) BEGIN
          LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 92 END
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 92 target = 1 parameter1 = (0 - params_4 - 5) timing = 2 END // extra 5 to offset unarmored bonus
        END ELSE BEGIN
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 92 target = 1 parameter1 = "-5"               timing = 2 END // extra 5 to offset unarmored bonus
        END
        PATCH_IF (params_6 != 255) BEGIN
          LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 91 END
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 91  target = 1 parameter1 = (0 - params_6) timing = 2 END
        END
        PATCH_IF (params_5 != 255) BEGIN
          LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 90 END
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 90  target = 1 parameter1 = (0 - params_5) timing = 2 END
        END
        PATCH_IF (params_7 != 255) BEGIN
          LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 59 END
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 59  target = 1 parameter1 = (0 - params_7 - 10) timing = 2 END // extra 10 to offset unarmored bonus
        END ELSE BEGIN
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 59 target = 1 parameter1 = "-10"                timing = 2 END // extra 10 to offset unarmored bonus
        END
        // adjust descriptions
        PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
              LAUNCH_PATCH_MACRO ~thieving_descripts~
            END
            SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
          END
        END
      END
      BUT_ONLY

  END // end skill check
    
END // end php_each

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow Arcane Spellcasting in Armor               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @212000 DESIGNATED 2120
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_casting_in_armor~

INCLUDE ~cdtweaks/lib/arcane_descripts.tpa~  // text replacement macros     
INCLUDE ~cdtweaks/lib/common_armor_list.tpa~

ACTION_PHP_EACH cd_common_armor_list AS params => item BEGIN

  ACTION_IF ((params_3 != 0) AND (params_3 != 255)) BEGIN // if penalty is needed
  
    COPY_EXISTING ~%item%.itm~ ~override~
      READ_SHORT 0x1c type
      PATCH_IF ((type = 2) OR (type = 12) OR (type = 41) OR (type = 47) OR (type = 49) OR (type = 53)) BEGIN // 12 is bg shield type, all others are iwd; 2 is armor
        LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete =  60 END
        LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 145 END
        PATCH_FOR_EACH res IN d5arcal d5arcac d5arcap BEGIN // compat with subtledoctor
          LPF DELETE_EFFECT INT_VAR match_opcode = 177 STR_VAR match_resource = EVAL ~%res%~ END
        END 
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 60 target = 1 parameter1 = params_3 timing = 2 END
        // adjust descriptions
        PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
              LAUNCH_PATCH_MACRO ~arcane_descripts~
            END
            SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
          END
        END
      END
      BUT_ONLY

  END // miscast check
  
END // php_each

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// More dual-classes                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @214000 DESIGNATED 2140
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_more_dual_classes~

INCLUDE ~cdtweaks/lib/kit_ids_fixer.tpa~

// first it replaces WILDMAGE and BARBARIAN values in these files if they're present
COPY_EXISTING ~dualclas.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                1       1       0       1       0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               0       1       1       1       1       0~
  BUT_ONLY

COPY_EXISTING ~abdcdsrq.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                0       0       0       17      0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               17      0       0       0       0       0~
  BUT_ONLY

COPY_EXISTING ~abdcscrq.2da~ ~override~
  REPLACE_TEXTUALLY
    ~\bWILDMAGE\b.*~
    ~WILDMAGE                0       0       0       15      0       0~
  REPLACE_TEXTUALLY
    ~\bBARBARIAN\b.*~
    ~BARBARIAN               15      0       0       0       0       0~
  BUT_ONLY

// if they're not then it appends them to the files
APPEND ~dualclas.2da~ ~WILDMAGE                1       1       0       1       0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~abdcdsrq.2da~ ~WILDMAGE                0       0       0       17      0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~abdcscrq.2da~ ~WILDMAGE                0       0       0       15      0       0~
  UNLESS ~\bWILDMAGE\b~

APPEND ~dualclas.2da~ ~BARBARIAN               0       1       1       1       1       0~
  UNLESS ~\bBARBARIAN\b~

APPEND ~abdcdsrq.2da~ ~BARBARIAN               17      0       0       0       0       0~
  UNLESS ~\bBARBARIAN\b~

APPEND ~abdcscrq.2da~ ~BARBARIAN               15      0       0       0       0       0~
  UNLESS ~\bBARBARIAN\b~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    READ_BYTE 0x1f "usa2"
    READ_BYTE 0x29 "kit1"
    PATCH_IF ((("%kit1%" BAND 0b01000000) = 0b01000000) AND // if unusable by fighters and barbarians
              (("%usa2%" BAND 0b00001000) = 0b00001000)) THEN BEGIN
      WRITE_BYTE 0x29 ("%kit1%" BAND 0b10111111) //remove redundant barb flag
    END
  END
  BUT_ONLY


ACTION_IF FILE_EXISTS ~refinements/hlab/thief/clab/clabempt.2da~ AND NOT FILE_EXISTS ~refinements/lib/kit.tpa~ THEN BEGIN

  INCLUDE ~cdtweaks/lib/sod_25stweap_fix.tpa~ // for broken 25stweap.2da in SoD
  INCLUDE ~cdtweaks/lib/iwdee_25stweap_fix.tpa~ // for broken 25stweap.2da in SoD

  COPY ~cdtweaks/spl/cdtitle0.spl~ ~override~ // spell to fix title and class name for d/c barbarians. Calls one of the EFFs below as appropriate
  
  COPY ~cdtweaks/eff/cdtitle1.eff~ ~override~
    SAY 0x1C @214001
  
  COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle2.eff~
    SAY 0x1C @214002
  
  COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle3.eff~
    SAY 0x1C @214003
  
  COPY_EXISTING ~cdtitle1.eff~ ~override/cdtitle4.eff~
    SAY 0x1C @214004

  ADD_KIT ~barbtF~
    ~barbtF   1 1 1 1 1 0 0 0~
    ~barbtF  0 1 0 0 1 0 0 1 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    ~barbtF     0       9       0       0       0       0~
    ~barbtF     0       0       0       0       0       0~
    ~barbtF     0       17      0       0       0       0~
    ~barbtF     0       15      0       0       0       0~
    ~barbtF     0       1       1       1       1       1       1       1       1~
    ~barbtF     1       1       1       0       0       0~
    ~Refinements/Hlab/Thief/clab/CLABEMPT.2DA~
    ~~
    ~0x40000000 2~
    ~BRB~
    ~* * * * * * * * * * * * * * * * * * * *~
    SAY #45855
    SAY #45859
    SAY #45869

  ADD_KIT ~barbtw~
    ~barbtw   1 1 1 1 1 0 0 0~
    ~barbtw  0 1 0 0 1 0 0 1 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 1 0 1 1 1 1 1 1 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
    ~barbtw     0       9       0       0       0       0~
    ~barbtw     0       0       0       0       0       0~
    ~barbtw     0       17      0       0       0       0~
    ~barbtw     0       15      0       0       0       0~
    ~barbtw     0       1       1       1       1       1       1       1       1~
    ~barbtw     1       1       1       0       0       0~
    ~Refinements/Hlab/Thief/clab/CLABTEMP.2DA~
    ~~
    ~0x40000000 2~
    ~BRB~
    ~* * * * * * * * * * * * * * * * * * * *~
    SAY #45855
    SAY #45859
    SAY #45869

  COPY_EXISTING ~kitlist.2da~ ~override~
    REPLACE_TEXTUALLY ~45855 45859 45869 CLABTEMP ~     ~45855 45859 45869 CLABFI05 ~
    SET_2DA_ENTRY 34 7 1 0x400000020

  EXTEND_TOP ~dplayer.bcs~  ~cdtweaks/baf/dplayer.baf~
  EXTEND_TOP ~dplayer2.bcs~ ~cdtweaks/baf/dplayer.baf~
  EXTEND_TOP ~dplayer3.bcs~ ~cdtweaks/baf/dplayer.baf~
  //EXTEND_BOTTOM ~baldur.bcs~   ~cdtweaks/baf/Runcheck.baf~
  //EXTEND_BOTTOM ~baldur25.bcs~ ~cdtweaks/baf/Runcheck.baf~
  EXTEND_BOTTOM ~li#stri.bcs~  ~cdtweaks/baf/li#stri.baf~

  COPY_EXISTING ~weapprof.2da~ ~override~
  // Full compatibility with all weapprof modifiers (like, say, AOE)
    FOR (row = 3; row < 35; row = row + 1) BEGIN
      READ_2DA_ENTRY "%row%" 52 1 "barb"
      SET_2DA_ENTRY "%row%" (22+"%barbtF%") 1 "%barb%"
      SET_2DA_ENTRY "%row%" (22+"%barbtw%") 1 "%barb%"
    END

  COPY_EXISTING  ~kitlist.2da~ ~override~
    READ_2DA_ENTRY 34 2 1 "lower"
    READ_2DA_ENTRY 34 3 1 "mixed"
    READ_2DA_ENTRY 34 4 1 "help"
    SET_2DA_ENTRY "%barbtF%"+3 2 1 "%lower%"
    SET_2DA_ENTRY "%barbtw%"+3 2 1 "%lower%"
    SET_2DA_ENTRY "%barbtF%"+3 3 1 "%mixed%"
    SET_2DA_ENTRY "%barbtw%"+3 3 1 "%mixed%"
    SET_2DA_ENTRY "%barbtF%"+3 4 1 "%help%"
    SET_2DA_ENTRY "%barbtw%"+3 4 1 "%help%"

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// wear multiple protection items                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// PnP restrictions                                 \\\\\
/////                                                  \\\\\

BEGIN @215001 DESIGNATED 2150 // PnP restrictions
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
SUBCOMPONENT @215000 // wear multiple protection items
LABEL ~cd_tweaks_protection_items_pnp~
  
ACTION_CLEAR_ARRAY cd_pnp_check
ACTION_CLEAR_ARRAY cd_pnp_table
ACTION_CLEAR_ARRAY cd_pnp_charges

INCLUDE ~cdtweaks/lib/pnp_descripts.tpa~  // text replacement macros
INCLUDE ~cdtweaks/lib/fj_spl_itm_reindex.tpa~

// first, eliminate iwd items on list without AC bonuses
COPY_EXISTING ~itemexcl.2da~ ~override~
  REPLACE_TEXTUALLY ~^bootgr01[ %TAB%]+1[ %TAB%%LNL%%MNL%%WNL%]+~ ~~
  REPLACE_TEXTUALLY ~^shldrng[ %TAB%]+1[ %TAB%%LNL%%MNL%%WNL%]+~ ~~
  
ACTION_IF FILE_EXISTS ~weidu_external/cdtweaks/2da/cdpnp_final.2da~ BEGIN
  COPY ~weidu_external/cdtweaks/2da/cdpnp_final.2da~ ~weidu_external/cdtweaks/2da/cdpnp.2da~
END ELSE BEGIN
  COPY ~cdtweaks/2da/cdpnp.2da~ ~weidu_external/cdtweaks/2da/cdpnp.2da~
END  

// lookup table
COPY ~weidu_external/cdtweaks/2da/cdpnp.2da~ ~weidu_external/cdtweaks/2da/cdpnp.2da~
  COUNT_2DA_ROWS 3 cd_count
  FOR (row = 0 ; row < cd_count ; ++row) BEGIN // skip header
    READ_2DA_ENTRY row 0 3 item
    TO_LOWER item
    SET $cd_pnppi("%item%") = 1
  END  

COPY_EXISTING ~itemexcl.2da~ ~override~
  COUNT_2DA_ROWS 2 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 2 itemcheck
    TO_LOWER itemcheck
    PATCH_IF !VARIABLE_IS_SET $cd_pnppi("%itemcheck%") BEGIN
      DEFINE_ASSOCIATIVE_ARRAY cd_pnp_check BEGIN "%itemcheck%" => 0 END
    END
  END
  BUT_ONLY

ACTION_PHP_EACH cd_pnp_check AS item => foo BEGIN 

  // don't bother if file isn't present or if it's already on the list
  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ THEN BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_SHORT 0x1c type
      SET add = 0
      PATCH_IF (type != 2) BEGIN // don't care if armor
        READ_LONG  0x6a fx_off
        READ_SHORT 0x70 fx_num
        FOR (index2 = 0 ; index2 < fx_num ; ++index2) BEGIN
          READ_SHORT (fx_off + (0x30 * index2)) opcode
          PATCH_IF (opcode = 0) BEGIN
            SET add = 1
            SET index2 = fx_num // kill loop
          END
        END
      END
      BUT_ONLY

    ACTION_IF (add = 1) BEGIN

      OUTER_SET cd_count += 1
      APPEND_OUTER ~weidu_external/cdtweaks/2da/cdpnp.2da~ ~%SOURCE_RES%  cdpp%cd_count%  c!pp%cd_count%~

    END

  END

END

COPY ~weidu_external/cdtweaks/2da/cdpnp.2da~ ~weidu_external/cdtweaks/2da/cdpnp.2da~
  COUNT_2DA_ROWS 3 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 3 orig
    READ_2DA_ENTRY index 1 3 no_ac
    READ_2DA_ENTRY index 2 3 0_lore
    DEFINE_ASSOCIATIVE_ARRAY cd_pnp_table BEGIN "%no_ac%", "%0_lore%" => "%orig%" END
  END
  BUT_ONLY  
  
ACTION_PHP_EACH cd_pnp_table AS params => orig BEGIN 

  ACTION_IF FILE_EXISTS_IN_GAME ~%orig%.itm~ THEN BEGIN

    COPY_EXISTING ~%orig%.itm~ ~override~
      LPF ~FJ_SPL_ITM_REINDEX~ END
      READ_ASCII  0x3a bam (8)
      READ_SHORT  0x42 lore
      PATCH_IF (lore = 0) BEGIN
        SPRINT params_1 ~%orig%~ // if original has no lore, don't bother with a second new item
      END
      READ_SHORT  0x68 abil_num
      WRITE_SHORT 0x68 (abil_num + 1)
      READ_LONG   0x6a fx_off
      WRITE_LONG  0x6a (fx_off + 0x38)
      PATCH_IF (abil_num > 0) BEGIN
        READ_LONG   0x64 abil_off
        READ_SHORT (abil_off + 0x1e + ((abil_num - 1) * 0x38)) last_fx_num
        READ_SHORT (abil_off + 0x20 + ((abil_num - 1) * 0x38)) last_fx_idx
        SET new_fx = (last_fx_idx + last_fx_num)
      END ELSE BEGIN
        READ_SHORT  0x70 new_fx
      END
      // because create inventory item doesn't have a way to set charges, create new and delete current
      INSERT_BYTES   (fx_off +        (new_fx * 0x30)) 0x30 // new effect
        WRITE_SHORT  (fx_off +        (new_fx * 0x30)) 122  // create inventory item
        WRITE_BYTE   (fx_off + 0x02 + (new_fx * 0x30)) 1    // target: self
        WRITE_BYTE   (fx_off + 0x0c + (new_fx * 0x30)) 1    // instant/permanent till death
        WRITE_BYTE   (fx_off + 0x12 + (new_fx * 0x30)) 100  // probability
        WRITE_ASCIIE (fx_off + 0x14 + (new_fx * 0x30)) ~%params_0%~ #8
      INSERT_BYTES   (fx_off       ) 0x38         // new ability
        WRITE_BYTE   (fx_off       ) 3            // magical
        WRITE_BYTE   (fx_off + 0x01) 1            // ID to use
        WRITE_SHORT  (fx_off + 0x02) 3            // in item slots
        WRITE_ASCIIE (fx_off + 0x04) ~%bam%~ #8   // item bam
        WRITE_SHORT  (fx_off + 0x0c) 5            // target: caster
        WRITE_SHORT  (fx_off + 0x0e) 1            // range: 1
        WRITE_SHORT  (fx_off + 0x1e) 1            // num effects: 1
        WRITE_SHORT  (fx_off + 0x20) new_fx       // fx index
        WRITE_SHORT  (fx_off + 0x22) 1            // num charges
        WRITE_BYTE   (fx_off + 0x24) 1            // vanishes when drained
        WRITE_LONG   (fx_off + 0x26) BIT11        // recharge after resting
        WRITE_SHORT  (fx_off + 0x2a) 1            // projectile: none
      BUT_ONLY

    ACTION_IF (lore != 0) BEGIN

      APPEND ~itemexcl.2da~ ~%params_1% 1~

    END

    ACTION_IF (abil_num < 3) BEGIN // don't bother if new ability is #4 (charges only for first 3 abils)

      ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_pnp_charges BEGIN "%orig%" => "%abil_num%" END
    
    END

    COPY_EXISTING ~%orig%.itm~ ~override/%params_0%.itm~
                  ~%orig%.itm~ ~override/%params_1%.itm~
      WRITE_SHORT 0x42 0 // no lore

    COPY_EXISTING ~%params_0%.itm~ ~override~
      WRITE_ASCIIE (fx_off + 0x4c + (new_fx * 0x30)) ~%params_1%~ #8 // exploit the fact that fx_off and new_fx carry over
      LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete = 0 END
      // update descriptions
      PATCH_FOR_EACH offset IN  0x54 BEGIN
        READ_LONG ~%offset%~ desc_strref
        PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
          READ_STRREF ~%offset%~ desc
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            LAUNCH_PATCH_MACRO ~pnp_descripts~
          END
          SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
        END
      END

  END

END

// add charges to items in game
COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG 0x2bc itm_off ELSE 0
    READ_LONG 0x2c0 itm_num ELSE 0
  END ELSE BEGIN
    READ_LONG  0x78 itm_off ELSE 0
    READ_SHORT 0x76 itm_num ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; ++index) BEGIN
    READ_ASCII (itm_off +        (0x14 * index)) item ELSE ""
    PHP_EACH cd_pnp_charges AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (itm_off + 0x0a + (charge * 0x02) + (0x14 * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY

// add charges to items being sold
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off  ELSE 0
  READ_LONG 0x38 sale_num  ELSE 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PHP_EACH cd_pnp_charges AS itemname => charge BEGIN
      PATCH_IF ("%item%" STRING_COMPARE_CASE "%itemname%" = 0) BEGIN
        WRITE_SHORT (sale_off + 0x0a + (charge * 0x02) + (0x1c * index)) 1 // add charge
      END
    END
  END
  BUT_ONLY
  
// cespenar can use alternate items for upgrades
ACTION_IF FILE_EXISTS_IN_GAME botsmith.dlg THEN BEGIN

  COMPILE ~cdtweaks/dlg/cdpnp.d~
  
END

COPY + ~weidu_external/cdtweaks/2da/cdpnp.2da~ ~weidu_external/cdtweaks/2da/cdpnp_final.2da~

/////                                                  \\\\\
///// no restrictions                                  \\\\\
/////                                                  \\\\\

BEGIN @215100 DESIGNATED 2151 // no restrictions
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
SUBCOMPONENT @215000 // wear multiple protection items
LABEL ~cd_tweaks_protection_items_all~

COPY  ~cdtweaks/2da/itemexcl.2da~ ~override~

/////                                                  \\\\\
///// armor + one item                                 \\\\\
/////                                                  \\\\\

BEGIN @215200 DESIGNATED 2152 // no restrictions
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
SUBCOMPONENT @215000 // wear multiple protection items
LABEL ~cd_tweaks_protection_items_armor_plus_one~

COPY_EXISTING ~itemexcl.2da~ ~override~
  COUNT_2DA_ROWS 2 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN // start at 1 to skip header row
    SET type = 0
    READ_2DA_ENTRY index 0 2 file
    PATCH_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN
      INNER_ACTION BEGIN

        COPY_EXISTING ~%file%.itm~ ~override~
          READ_SHORT 0x1c type
          BUT_ONLY

      END
    END
    PATCH_IF type = 2 BEGIN
      SET_2DA_ENTRY index 1 2 CD_DELETE_ME
    END
  END
  REPLACE_TEXTUALLY ~^.+[ %TAB%]+CD_DELETE_ME[ %TAB%%LNL%%MNL%%WNL%]+~ ~~
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Altered weapon proficiencies                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Rebalanced weapon proficiencies                  \\\\\
/////                                                  \\\\\

BEGIN @216000 DESIGNATED 2160
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
SUBCOMPONENT @216006
LABEL ~cd_tweaks_alter_prof_rebalanced~

INCLUDE ~cdtweaks/lib/profs_racethac.tpa~
INCLUDE ~cdtweaks/lib/profs_common.tpa~
INCLUDE ~cdtweaks/lib/rebalanced_profs.tpa~

/////                                                  \\\\\
///// BG Weapon profs with weapon styles               \\\\\
/////                                                  \\\\\

BEGIN @216100 DESIGNATED 2161
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
SUBCOMPONENT @216006
LABEL ~cd_tweaks_alter_prof_bg_with_styles~

OUTER_SET "tb#tutu_realloc_style" = 0
INCLUDE ~cdtweaks/lib/profs_racethac.tpa~
INCLUDE ~cdtweaks/lib/profs_common.tpa~
INCLUDE ~cdtweaks/lib/bg_style_profs.tpa~

/////                                                  \\\\\
///// BG Weapon profs without weapon styles            \\\\\
/////                                                  \\\\\

BEGIN @216200 DESIGNATED 2162
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
SUBCOMPONENT @216006
LABEL ~cd_tweaks_alter_prof_bg_no_styles~

OUTER_SET "tb#tutu_realloc_style" = 1
INCLUDE ~cdtweaks/lib/profs_racethac.tpa~
INCLUDE ~cdtweaks/lib/profs_common.tpa~
INCLUDE ~cdtweaks/lib/bg_style_profs.tpa~

/////                                                  \\\\\
///// IWD Weapon profs with weapon styles              \\\\\
/////                                                  \\\\\

BEGIN @216300 DESIGNATED 2163
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
SUBCOMPONENT @216006
LABEL ~cd_tweaks_alter_prof_iwd_with_styles~

OUTER_SET "tb#tutu_realloc_style" = 0
INCLUDE ~cdtweaks/lib/profs_racethac.tpa~
INCLUDE ~cdtweaks/lib/profs_common.tpa~
INCLUDE ~cdtweaks/lib/iwd_style_profs.tpa~

/////                                                  \\\\\
///// IWD Weapon profs without weapon styles           \\\\\
/////                                                  \\\\\

BEGIN @216400 DESIGNATED 2164
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
SUBCOMPONENT @216006
LABEL ~cd_tweaks_alter_prof_iwd_no_styles~

OUTER_SET "tb#tutu_realloc_style" = 1
INCLUDE ~cdtweaks/lib/profs_racethac.tpa~
INCLUDE ~cdtweaks/lib/profs_common.tpa~
INCLUDE ~cdtweaks/lib/iwd_style_profs.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Cast Spells from Scrolls at Character Level      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @217000 DESIGNATED 2170
GROUP @9
LABEL ~cd_tweaks_item_spells_at_char_level~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_FOR_EACH op IN 146 148 BEGIN
    LPF ALTER_EFFECT INT_VAR headers = 1 check_globals = 0 header_type = 3 match_opcode = op parameter1 = 0 silent = 1 END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// limited storekeeper id ability                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// only mages and bards                             \\\\\
/////                                                  \\\\\

BEGIN @219001 DESIGNATED 2190 // only mages n bards
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
SUBCOMPONENT @219000 // limited storekeeper id ability
LABEL ~cd_tweaks_shopkeeper_id_mages~

OUTER_SET lore_class = 1
OUTER_SET lore_calc  = 0
INCLUDE ~cdtweaks/lib/cdstores.tpa~

/////                                                  \\\\\
///// based on shopkeeper                              \\\\\
/////                                                  \\\\\

BEGIN @219100 DESIGNATED 2191 // only mages n bards
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
SUBCOMPONENT @219000 // limited storekeeper id ability
LABEL ~cd_tweaks_shopkeeper_id_lore~

OUTER_SET lore_class = 0
OUTER_SET lore_calc  = 1
INCLUDE ~cdtweaks/lib/cdstores.tpa~

/////                                                  \\\\\
///// hybrid approach                                  \\\\\
/////                                                  \\\\\

BEGIN @219200 DESIGNATED 2192 // only mages n bards
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
SUBCOMPONENT @219000 // limited storekeeper id ability
LABEL ~cd_tweaks_shopkeeper_id_hybrid~

OUTER_SET lore_class = 1
OUTER_SET lore_calc  = 1
INCLUDE ~cdtweaks/lib/cdstores.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Multi-Class Grand Mastery                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @220000 DESIGNATED 2200
GROUP @9 
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_multiclass_gm~

ACTION_IF original_bg1 OR original_iwd THEN BEGIN

  COPY_EXISTING ~profsmax.2da~  ~override~
    SET_2DA_ENTRY_LATER ~profsmax~  9 2 ~5~ // fm
    SET_2DA_ENTRY_LATER ~profsmax~ 10 2 ~5~ // fc
    SET_2DA_ENTRY_LATER ~profsmax~ 11 2 ~5~ // ft
    SET_2DA_ENTRY_LATER ~profsmax~ 12 2 ~5~ // fmt
    SET_2DA_ENTRY_LATER ~profsmax~ 18 2 ~5~ // fd
    SET_2DA_ENTRY_LATER ~profsmax~ 19 2 ~5~ // fmc
    SET_2DA_ENTRIES_NOW ~profsmax~ 1
    BUT_ONLY

END ELSE BEGIN

  ACTION_CLEAR_ARRAY cd_mc_gm
  ACTION_CLEAR_ARRAY cd_mc_kitnames // multiclass kits
  OUTER_FOR (index = 12 ; index < 21 ; ++index) BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_mc_gm BEGIN ~%index%~ => ~%index%~ END // bogstandard multis
  END  

  ACTION_IF enhanced_edition BEGIN // only bother checking for MC kits on EE games

    COPY_EXISTING ~kitlist.2da~ ~override~
      COUNT_2DA_ROWS 9 rows
      FOR (row = 0 ; row < rows ; ++row) BEGIN
        READ_2DA_ENTRY row 8 9 class
        PATCH_IF (IS_AN_INT class) AND ((class = 7) OR (class = 8) OR (class = 9) OR (class = 10) OR (class = 16) OR (class = 17)) BEGIN
          READ_2DA_ENTRY row 1 9 kitname
          DEFINE_ASSOCIATIVE_ARRAY cd_mc_kitnames BEGIN ~%kitname%~ => ~%kitname%~ END
        END
      END
      BUT_ONLY   

  END      
        
  COPY_EXISTING ~weapprof.2da~  ~override~
    COUNT_2DA_COLS col_count
    SET col_check = col_count - 1
    PATCH_IF enhanced_edition BEGIN // only bother checking for MC kits on EE games
      COUNT_2DA_COLS col_count
      PATCH_PHP_EACH cd_mc_kitnames AS match_name => foo BEGIN
        FOR (column = 50; column < col_check; ++column) BEGIN
          READ_2DA_ENTRY 0 column col_check name
          PATCH_IF ("%name%" STRING_COMPARE_CASE "%match_name%" = 0) BEGIN
            SET column += 1 // row with names has an empty entry for the first column
            DEFINE_ASSOCIATIVE_ARRAY cd_mc_gm BEGIN ~%column%~ => ~%column%~ END
            SET column = col_count // kill loop
          END
        END
      END
    END
    PATCH_PHP_EACH cd_mc_gm AS column => foo BEGIN
      FOR (row = 8; row < 28; ++row) BEGIN
        READ_2DA_ENTRY row column col_count stars
        PATCH_IF (stars > 1) BEGIN
          SET_2DA_ENTRY row column col_count 5
        END
      END
    END
    PRETTY_PRINT_2DA
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change grandmastery bonuses                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// true grandmastery                                \\\\\
/////                                                  \\\\\

BEGIN @221000 DESIGNATED 2210
GROUP @9
SUBCOMPONENT @221001 // Change Grandmastery Bonuses
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_grandmastery_bonus_true~

ACTION_IF FILE_CONTAINS_EVALUATED (~wspecial.2da~ ~[ %TAB%]*SPEED[ %TAB%]*~) THEN BEGIN OUTER_SET dump_speed = 0 END ELSE BEGIN OUTER_SET dump_speed = 1 END

COPY ~cdtweaks/2da/wspatck.2da~  ~override~
     ~cdtweaks/2da/wspecial.2da~ ~override~

ACTION_IF dump_speed THEN BEGIN

  COPY_EXISTING ~wspecial.2da~ ~override~ // remove speed column
    REPLACE_TEXTUALLY ~[ %TAB%]*SPEED[ %TAB%]*~ ~~
    REPLACE_TEXTUALLY ~^\([0-5]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+\)[ %TAB%]+-?[0-9]+~ ~\1~
    BUT_ONLY

END

/////                                                  \\\\\
///// bg2 grandmastery                                 \\\\\
/////                                                  \\\\\

BEGIN @221100 DESIGNATED 2211
GROUP @9
SUBCOMPONENT @221001 // Change Grandmastery Bonuses
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_grandmastery_bonus_bg2~

ACTION_IF FILE_CONTAINS_EVALUATED (~wspecial.2da~ ~[ %TAB%]*SPEED[ %TAB%]*~) THEN BEGIN OUTER_SET dump_speed = 0 END ELSE BEGIN OUTER_SET dump_speed = 1 END

COPY ~cdtweaks/2da/bg2_wspatck.2da~  ~override/wspatck.2da~
     ~cdtweaks/2da/bg2_wspecial.2da~ ~override/wspecial.2da~

ACTION_IF dump_speed THEN BEGIN

  COPY_EXISTING ~wspecial.2da~ ~override~ // remove speed column
    REPLACE_TEXTUALLY ~[ %TAB%]*SPEED[ %TAB%]*~ ~~
    REPLACE_TEXTUALLY ~^\([0-5]+[ %TAB%]+-?[0-9]+[ %TAB%]+-?[0-9]+\)[ %TAB%]+-?[0-9]+~ ~\1~
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Magically Created Weapons to Zero Weight  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @222000 DESIGNATED 2220
GROUP @9 
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee~ @25
LABEL ~cd_tweaks_0_weight_created_weapons~

ACTION_IF enhanced_edition OR tutu_gen OR game_is_bgt OR original_bg2 OR game_is_iwd-in-bg2 OR game_is_ca BEGIN
  OUTER_SET check_255 = 1
END ELSE BEGIN
  OUTER_SET check_255 = 0
END

OUTER_SPRINT weight_temp @212001 // use existing weight string from arcane magic in heavy armor, but...
OUTER_INNER_PATCH_SAVE weight ~%weight_temp%~ BEGIN
  REPLACE_TEXTUALLY ~\[0-9\]\+~ ~~ // we'll remove actual numerical weight from match
END

ACTION_CLEAR_ARRAY  cd_magical_items
ACTION_DEFINE_ARRAY cd_magical_items BEGIN END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
                          ~^.+\.spl$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.spl$" = 0) BEGIN
    SET "abil_length" = 0x28
  END ELSE BEGIN
    SET "abil_length" = 0x38
  END
  READ_LONG  0x64 abil_off ELSE 0
  READ_SHORT 0x68 abil_num ELSE 0
  READ_LONG  0x6a fx_off   ELSE 0
  FOR (index = 0 ; index < abil_num ; ++index) BEGIN
    READ_SHORT  (abil_off + 0x1e + (abil_length * index)) abil_fx_num
    READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
    FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
      READ_SHORT (fx_off +        (0x30 * (abil_fx_idx + index2))) opcode
      PATCH_IF ((opcode = 111) OR ((opcode = 255) AND (check_255 = 1))) BEGIN // create magic item
        READ_ASCII (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) item
        DEFINE_ASSOCIATIVE_ARRAY cd_magical_items BEGIN "%item%" => 0 END
      END
    END
  END
  BUT_ONLY

ACTION_PHP_EACH cd_magical_items AS item => foo BEGIN
      
  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ THEN BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_LONG 0x4c weight_itm
      PATCH_IF (weight_itm != 0) BEGIN // don't bother if not non-zero
        WRITE_LONG 0x4c 0        
        // adjust descriptions
        PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
          READ_LONG ~%offset%~ desc_strref
          PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
            READ_STRREF ~%offset%~ desc
            INNER_PATCH_SAVE desc ~%desc%~ BEGIN
              REPLACE_TEXTUALLY ~%weight%[0-9]+~  ~\10~
            END
            SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
          END
        END
      END  
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Make +x/+y weapons consistent                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @223000 DESIGNATED 2230
GROUP @9
REQUIRE_PREDICATE GAME_IS ~soa tob bgt~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ @16
FORBID_COMPONENT ~item_rev/item_rev.tp2~ ~0~ @16
LABEL ~cd_tweaks_consistent_x_y_items~

// to update descriptions; if not translated it'll try in English and not change anything
OUTER_SPRINT text_stats @223001
OUTER_SPRINT text_ench  @223002

// get enchantments from lookup table
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_enchantment BEGIN

  blun10 => 3 // Root of the Problem +1/+3 vs unnatural
  blun23 => 3 // Bone Club +2/+3 vs undead
  hamm04 => 4 // hammer +1/+4 vs giantkin
  sw1h03 => 3 // bastard sword +1/+3 vs shapeshifters
  sw1h31 => 4 // daystar
  sw1h54 => 3 // Equalizer
  wamace => 5 // jerrod's mace

END

ACTION_PHP_EACH cd_enchantment AS item => ench BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ THEN BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_LONG 0x60 ench
      READ_LONG  0x54 desc_strref
      PATCH_IF (desc_strref >= 0 && desc_strref < 2147483646) BEGIN
        READ_STRREF 0x54 desc
        INNER_PATCH_SAVE desc ~%desc%~ BEGIN
          REPLACE_TEXTUALLY ~\(%text_stats%[ %TAB%%LNL%%MNL%%WNL%]+\)\([^ %TAB%%LNL%%MNL%%WNL%]\)~
            ~\1%text_ench%
\2~
        END
        SAY_EVALUATED 0x54 ~%desc%~ // write changes
      END
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Description updates for consistent +x/+y weapons           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @223100 DESIGNATED 2231 DEPRECATED @18

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter THAC0 Table                                          \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @224000 DESIGNATED 2240
GROUP @9
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT (GAME_IS ~bg2 tob bgt~ AND FILE_EXISTS ~data/rot-rule.bif~) @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_alter_thac0_table~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/iwd2_thac0.2da~ ~override~

END ELSE BEGIN

  ACTION_IF FILE_CONTAINS_EVALUATED (~thac0.2da~ ~^SHAMAN~) THEN BEGIN OUTER_SET dump_shaman = 0 END ELSE BEGIN OUTER_SET dump_shaman = 1 END
  ACTION_IF FILE_CONTAINS_EVALUATED (~xpcap.2da~ ~^MONK~)   THEN BEGIN OUTER_SET dump_monk   = 0 END ELSE BEGIN OUTER_SET dump_monk   = 1 END

  COPY ~cdtweaks/2da/thac0.2da~ ~override~
    PATCH_IF dump_monk BEGIN
      REPLACE_TEXTUALLY ~^\(MONK\|SORCERER\).+[0-9]+[ %TAB%%LNL%%MNL%%WNL%]+~ ~~
    END
    PATCH_IF dump_shaman BEGIN
      REPLACE_TEXTUALLY ~^SHAMAN.+[0-9]+[ %TAB%%LNL%%MNL%%WNL%]+~ ~~
    END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter sorcerer spell progression                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @225000 DESIGNATED 2250
GROUP @9
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxsplsrc.2da~) OR (FILE_EXISTS_IN_GAME ~mxsplsor.2da~)) @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT (GAME_IS ~bg2 tob bgt~ AND FILE_EXISTS ~data/rot-rule.bif~) @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_sorc_spell_progression~

ACTION_IF FILE_EXISTS_IN_GAME ~mxsplsrc.2da~ THEN BEGIN

  COPY ~cdtweaks/2da/un_mxsplsrc.2da~ ~override/mxsplsrc.2da~

  ACTION_IF FILE_EXISTS_IN_GAME mxspldd.2da THEN BEGIN
  
    COPY_EXISTING ~mxsplsrc.2da~ ~override/mxspldd.2da~
      FOR (row = 0 ; row < 50 ; ++row) BEGIN
        FOR (col = 1 ; col < 10 ; ++col) BEGIN
          READ_2DA_ENTRY row col 10 spells
          PATCH_IF (spells > 2) BEGIN
            SET_2DA_ENTRY row col 10 (spells - 2)
          END
        END
      END  
  END

  ACTION_IF (MOD_IS_INSTALLED ~tomeandblood.tp2~ ~95~) BEGIN
    ACTION_FOR_EACH sorc_table IN ~mxsplsrc~ ~mxspldd~ BEGIN
      COPY_EXISTING ~%sorc_table%.2da~ ~override~
        COUNT_2DA_COLS cols
        READ_2DA_ENTRIES_NOW rows cols
        FOR (row = 0; row < rows; ++row) BEGIN
          FOR (col = 1; col < 6; ++col) BEGIN
            READ_2DA_ENTRY_FORMER rows row col slots
            PATCH_IF (slots > 0) BEGIN
              SET_2DA_ENTRY row col cols (slots + 2)
            END
          END
        END
        IF_EXISTS BUT_ONLY
    END
  END

END ELSE BEGIN

  COPY ~cdtweaks/2da/un_mxsplsrc.2da~ ~override/mxsplsor.2da~ // iwd2

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Mage Spell Progression Table                         \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2260
GROUP @9
SUBCOMPONENT @226000 // alter mage progression
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~mxsplwiz.2da~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT (GAME_IS ~bg2 tob bgt~ AND FILE_EXISTS ~data/rot-rule.bif~) @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_mage_spell_progression_unnerfed~

COPY ~cdtweaks/2da/un_mxsplwiz.2da~ ~override/mxsplwiz.2da~

ACTION_IF (MOD_IS_INSTALLED ~tomeandblood.tp2~ ~95~) BEGIN
  COPY_EXISTING ~mxsplwiz.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 0; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 1 slots
      PATCH_IF !(FILE_EXISTS_IN_GAME ~qdtnb_l1cantrips.qd~) BEGIN
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row 1 cols (slots + 2)
        END
      END
        PATCH_IF (FILE_EXISTS_IN_GAME ~qdtnb_l1cantrips.qd~) BEGIN
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row 1 cols (slots + 1)
        END
      END
      FOR (col = 2; col < 8; ++col) BEGIN
        READ_2DA_ENTRY_FORMER rows row col slots
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row col cols (slots + 2)
        END
      END
    END
    IF_EXISTS BUT_ONLY
END

ACTION_IF game_includes_tob THEN BEGIN

  // add mage alchemy, scribe scrolls
  COPY_EXISTING ~lucm0.2da~ ~override~
                ~luma0.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    SET "patch" = 0
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "abil"
      PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN
        PATCH_IF ("%patch%" = 0) BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBSCRBMG~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "patch" = 1
        END ELSE BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBALCHMG~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "index" = "%rows%" // kills loop
        END
      END
    END
    BUT_ONLY

  COPY ~cdtweaks/spl/gbalchmg.spl~ ~override~
    SAY 0x08 @218001
    SAY 0x50 @218004

  COPY ~cdtweaks/spl/gbscrbmg.spl~ ~override~
    SAY 0x08 @218007
    SAY 0x50 @218010

  ACTION_IF NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2280~ THEN BEGIN // if un-nerfed cleric progression hasn't already made this change...

    COPY_EXISTING ~spcl918.spl~ ~override~
      SAY        0x08 @218003
      SAY        0x50 @218006
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      READ_LONG  0x6a "fx_off"
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
        READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
        FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
          READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
          PATCH_IF ("%opcode%" = 122) BEGIN // create item
            READ_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "item"
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn52" = 0) BEGIN // potion of extra healing
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn10~ #8 // potion of invis
            END ELSE
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn36" = 0) BEGIN // potion of master thieving
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn37~ #8 // potion of mind focusing
            END
          END
        END
      END
      BUT_ONLY

    COPY_EXISTING ~spcl919.spl~ ~override~
      SAY 0x008 @218009
      SAY 0x050 @218012

  END

END

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2261
GROUP @9
SUBCOMPONENT @226000 // alter mage progression
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~mxsplwiz.2da~ @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_mage_spell_progression_pnp~

COPY ~cdtweaks/2da/mxsplwiz.2da~ ~override~

ACTION_IF (MOD_IS_INSTALLED ~tomeandblood.tp2~ ~95~) BEGIN
  COPY_EXISTING ~mxsplwiz.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 0; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 1 slots
      PATCH_IF !(FILE_EXISTS_IN_GAME ~qdtnb_l1cantrips.qd~) BEGIN
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row 1 cols (slots + 2)
        END
      END
      PATCH_IF (FILE_EXISTS_IN_GAME ~qdtnb_l1cantrips.qd~) BEGIN
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row 1 cols (slots + 1)
        END
      END
      FOR (col = 2; col < 8; ++col) BEGIN
        READ_2DA_ENTRY_FORMER rows row col slots
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row col cols (slots + 2)
        END
      END
    END
    IF_EXISTS BUT_ONLY
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter bard Spell Progression Table                         \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2270
GROUP @9
SUBCOMPONENT @227000 // alter bard progression
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~mxsplbrd.2da~ @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT (GAME_IS ~bg2 tob bgt~ AND FILE_EXISTS ~data/rot-rule.bif~) @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_bard_spell_progression_unnerfed~

COPY ~cdtweaks/2da/un_mxsplbrd.2da~ ~override/mxsplbrd.2da~

ACTION_IF (MOD_IS_INSTALLED ~tomeandblood.tp2~ ~95~) BEGIN
  COPY_EXISTING ~mxsplbrd.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 0; row < rows; ++row) BEGIN
      FOR (col = 1; col < 7; ++col) BEGIN
        READ_2DA_ENTRY_FORMER rows row col slots
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row col cols (slots + 2)
        END
      END
    END
    IF_EXISTS BUT_ONLY
END

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2271
GROUP @9
SUBCOMPONENT @227000 // alter bard progression
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~mxsplbrd.2da~ @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_bard_spell_progression_pnp~

COPY ~cdtweaks/2da/mxsplbrd.2da~ ~override~

ACTION_IF (MOD_IS_INSTALLED ~tomeandblood.tp2~ ~95~) BEGIN
  COPY_EXISTING ~mxsplbrd.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 0; row < rows; ++row) BEGIN
      FOR (col = 1; col < 7; ++col) BEGIN
        READ_2DA_ENTRY_FORMER rows row col slots
        PATCH_IF (slots > 0) BEGIN
          SET_2DA_ENTRY row col cols (slots + 2)
        END
      END
    END
    IF_EXISTS BUT_ONLY
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter cleric Spell Progression Table                       \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// un-nerfed                                                  \\\\\
/////                                                            \\\\\

BEGIN @226001 DESIGNATED 2280
GROUP @9
SUBCOMPONENT @228000 // alter cleric progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxsplclr.2da~) OR (FILE_EXISTS_IN_GAME ~mxsplprs.2da~)) @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT (GAME_IS ~bg2 tob bgt~ AND FILE_EXISTS ~data/rot-rule.bif~) @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_cleric_spell_progression_unnerfed~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/un_mxsplprs.2da~ ~override/mxsplclr.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/un_mxsplprs.2da~ ~override/mxsplprs.2da~

END

ACTION_IF game_includes_tob THEN BEGIN

  // add clerical alchemy, scribe scrolls
  COPY_EXISTING ~lucl0.2da~ ~override~
                ~lucm0.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    SET "patch" = 0
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "abil"
      PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN
        PATCH_IF ("%patch%" = 0) BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBSCRBPR~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "patch" = 1
        END ELSE BEGIN
          SET_2DA_ENTRY "%index%" 1 9 ~GA_GBALCHPR~
          SET_2DA_ENTRY "%index%" 4 9 ~1~
          SET_2DA_ENTRY "%index%" 5 9 ~99~
          SET_2DA_ENTRY "%index%" 6 9 ~99~
          SET "index" = "%rows%" // kills loop
        END
      END
    END
    BUT_ONLY

  COPY ~cdtweaks/spl/gbalchpr.spl~ ~override~
    SAY 0x08 @218002
    SAY 0x50 @218005

  COPY ~cdtweaks/spl/gbscrbpr.spl~ ~override~
    SAY 0x08 @218008
    SAY 0x50 @218011

  ACTION_IF NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2260~ THEN BEGIN // if un-nerfed mage progression hasn't already made this change...

    COPY_EXISTING ~spcl918.spl~ ~override~
      SAY        0x08 @218003
      SAY        0x50 @218006
      READ_LONG  0x64 "abil_off"
      READ_SHORT 0x68 "abil_num"
      READ_LONG  0x6a "fx_off"
      FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
        READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
        READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
        FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
          READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
          PATCH_IF ("%opcode%" = 122) BEGIN // create item
            READ_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) "item"
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn52" = 0) BEGIN // potion of extra healing
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn10~ #8 // potion of invis
            END ELSE
            PATCH_IF ("%item%" STRING_COMPARE_CASE "potn36" = 0) BEGIN // potion of master thieving
              WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%abil_fx_idx%" + "%index2%"))) ~potn37~ #8 // potion of mind focusing
            END
          END
        END
      END
      BUT_ONLY

    COPY_EXISTING ~spcl919.spl~ ~override~
      SAY 0x008 @218009
      SAY 0x050 @218012

  END

END

/////                                                            \\\\\
///// pnp                                                        \\\\\
/////                                                            \\\\\

BEGIN @226100 DESIGNATED 2281
GROUP @9
SUBCOMPONENT @228000 // alter cleric progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxsplclr.2da~) OR (FILE_EXISTS_IN_GAME ~mxsplprs.2da~)) @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_cleric_spell_progression_pnp~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/mxsplprs.2da~ ~override/mxsplclr.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/mxsplprs.2da~ ~override~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter druid Spell/level Progression Table                  \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// druid level, un-nerfed druid spells                        \\\\\
/////                                                            \\\\\

BEGIN @229001 DESIGNATED 2290
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxspldrd.2da~) OR (FILE_EXISTS_IN_GAME ~mxspldru.2da~)) @25
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~bpduearc.spl~ @16      // non-BP
REQUIRE_PREDICATE NOT (GAME_IS ~bg2 tob bgt~ AND FILE_EXISTS ~data/rot-rule.bif~) @16 // non-RoT
REQUIRE_PREDICATE NOT FILE_EXISTS         ~data/tdd-rule.bif~ @16 // non-TDD
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_druid_spell_progression_unnerfed_druid_level~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/un_mxspldru.2da~ ~override/mxspldrd.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/un_mxspldru.2da~ ~override/mxspldru.2da~

END

/////                                                            \\\\\
///// druid level, pnp cleric/druid spells                       \\\\\
/////                                                            \\\\\

BEGIN @229100 DESIGNATED 2291
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxspldrd.2da~) OR (FILE_EXISTS_IN_GAME ~mxspldru.2da~)) @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_druid_spell_progression_pnp_druid_level~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/mxsplprs.2da~ ~override/mxspldrd.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/mxsplprs.2da~ ~override/mxspldru.2da~

END

/////                                                            \\\\\
///// cleric level, normal druid spells                          \\\\\
/////                                                            \\\\\

BEGIN @229200 DESIGNATED 2292
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxspldrd.2da~) OR (FILE_EXISTS_IN_GAME ~mxspldru.2da~)) @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_druid_spell_progression_unchanged_cleric_level~

// use cleric level progression
INCLUDE ~cdtweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, un-nerfed druid spells                       \\\\\
/////                                                            \\\\\

BEGIN @229300 DESIGNATED 2293
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxspldrd.2da~) OR (FILE_EXISTS_IN_GAME ~mxspldru.2da~)) @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_druid_spell_progression_unnerfed_cleric_level~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/un_mxspldru.2da~ ~override/mxspldrd.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/un_mxspldru.2da~ ~override/mxspldru.2da~

END

// use cleric level progression
INCLUDE ~cdtweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, pnp cleric/druid spells                      \\\\\
/////                                                            \\\\\

BEGIN @229400 DESIGNATED 2294
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxspldrd.2da~) OR (FILE_EXISTS_IN_GAME ~mxspldru.2da~)) @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_druid_spell_progression_pnp_cleric_level~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/mxsplprs.2da~ ~override/mxspldrd.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/mxsplprs.2da~ ~override/mxspldru.2da~

END

// use cleric level progression
INCLUDE ~cdtweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, normal cleric spells                         \\\\\
/////                                                            \\\\\

BEGIN @229500 DESIGNATED 2295
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxspldrd.2da~) OR (FILE_EXISTS_IN_GAME ~mxspldru.2da~)) @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_druid_spell_progression_cleric_normal_cleric_level~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY_EXISTING ~mxsplclr.2da~ ~override/mxspldrd.2da~

END ELSE BEGIN

  COPY_EXISTING ~mxsplprs.2da~ ~override/mxspldru.2da~

END

// use cleric level progression
INCLUDE ~cdtweaks/lib/druid_2_cleric_levels.tpa~

/////                                                            \\\\\
///// cleric level, un-nerfed cleric spells                      \\\\\
/////                                                            \\\\\

BEGIN @229600 DESIGNATED 2296
GROUP @9
SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~mxspldrd.2da~) OR (FILE_EXISTS_IN_GAME ~mxspldru.2da~)) @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_druid_spell_progression_cleric_unnerfed_cleric_level~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/un_mxsplprs.2da~ ~override/mxspldrd.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/un_mxsplprs.2da~ ~override/mxspldru.2da~

END

// use cleric level progression
INCLUDE ~cdtweaks/lib/druid_2_cleric_levels.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter druid Spell/level Progression Table (BG/IWD)         \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// cleric level, normal druid spells                          \\\\\
/////                                                            \\\\\

// why is this here separately? Because WeiDU doesn't handle a pile of
// subcomponents with different REQUIRE_PREDICATEs very well.
// this allows this component to be installed for BG/IWD. 

BEGIN @229200 DESIGNATED 2297
GROUP @9
//SUBCOMPONENT @229000 // alter druid progression
REQUIRE_PREDICATE GAME_IS ~bg1 totsc iwd how totlm~ @25
LABEL ~cd_tweaks_druid_spell_progression_unchanged_cleric_level_obg_version~

// use cleric level progression
INCLUDE ~cdtweaks/lib/druid_2_cleric_levels.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// triple-class HLA tables                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @230000 DESIGNATED 2300
GROUP @9
REQUIRE_PREDICATE     MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2090~ @21 // xp cap must be removed
REQUIRE_PREDICATE     GAME_IS ~tob bgt bg2ee eet~   @25 // ToB required
LABEL ~cd_tweaks_triple_class_hla_table~

// gives f/m/c and f/m/t unique HLAs; tables created below
COPY_EXISTING ~LUABBR.2DA~ ~override~
  SET_2DA_ENTRY 14 1 1 ~FMT~
  SET_2DA_ENTRY 19 1 1 ~FMC~
  BUT_ONLY

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~lufmc.2da~) BEGIN

  COPY ~cdtweaks/2da/lufmc.2da~ ~override~

END ELSE BEGIN

  COPY_EXISTING ~lufmc.2da~ ~override~
    READ_2DA_ENTRIES_NOW ~r2en_lufmc~ 10
    SET planetar = 0
    SET dark_planetar = 0
    SET improved_alacrity = 0
    SET dragons_breath = 0
    SET comet = 0
    FOR (i = 0; i < r2en_lufmc; i += 1) BEGIN // for each row
      READ_2DA_ENTRY_FORMER ~r2en_lufmc~ i 1 old_ability
      SPRINT new_ability ~~
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL902~) BEGIN // deathblow
        PATCH_IF (planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI923~ // summon planetar
          SET planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL903~) BEGIN // greater deathblow
        PATCH_IF (dark_planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI924~ // summon dark planetar
          SET dark_planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL908~) BEGIN // war cry
        PATCH_IF (improved_alacrity == 0) BEGIN
          SPRINT new_ability ~GA_SPWI921~ // improved alacrity
          SET improved_alacrity = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~*~) BEGIN // unused
        PATCH_IF (dragons_breath == 0) BEGIN
          SPRINT new_ability ~GA_SPWI922~ // dragon's breath
          SET dragons_breath = 1
        END ELSE
        PATCH_IF (comet == 0) BEGIN
          SPRINT new_ability ~GA_SPWI925~ // comet
          SET comet = 1
        END
      END
      PATCH_IF (STRING_LENGTH ~%new_ability%~ > 0) BEGIN // ability to be replaced here
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 1 ~%new_ability%~ // ability
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 2 ~*~ // icon
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 3 ~*~ // strref
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 4 ~32~ // min_level
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 5 ~99~ // max_level
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 6 ~1~ // num_allowed
        SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 7 ~*~ // prerequisite
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI923~) BEGIN // summon planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~GA_SPWI924~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~ALL_EVIL~ // alignment_restrict
        END ELSE
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI924~) BEGIN // summon dark planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~GA_SPWI923~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~ALL_GOOD~ // alignment_restrict
        END ELSE BEGIN // restrictions for others
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 8 ~*~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmc~ i 9 ~*~ // alignment_restrict
        END
      END
    END
    SET_2DA_ENTRIES_NOW ~s2el_lufmc~ 10 // actually write changes!
    PRETTY_PRINT_2DA

END

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~lufmt.2da~) BEGIN

  COPY ~cdtweaks/2da/lufmt.2da~ ~override~

END ELSE BEGIN

  COPY_EXISTING ~lufmt.2da~ ~override~
    READ_2DA_ENTRIES_NOW ~r2en_lufmt~ 10
    SET planetar = 0
    SET dark_planetar = 0
    SET improved_alacrity = 0
    SET dragons_breath = 0
    SET energy_blades = 0
    SET comet = 0
    FOR (i = 0; i < r2en_lufmt; i += 1) BEGIN // for each row
      READ_2DA_ENTRY_FORMER ~r2en_lufmt~ i 1 old_ability
      SPRINT new_ability ~~
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL902~) BEGIN // deathblow
        PATCH_IF (planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI923~ // summon planetar
          SET planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL903~) BEGIN // greater deathblow
        PATCH_IF (dark_planetar == 0) BEGIN
          SPRINT new_ability ~GA_SPWI924~ // summon dark planetar
          SET dark_planetar = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL908~) BEGIN // war cry
        PATCH_IF (improved_alacrity == 0) BEGIN
          SPRINT new_ability ~GA_SPWI921~ // improved alacrity
          SET improved_alacrity = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL918~) BEGIN // alchemy
        PATCH_IF (energy_blades == 0) BEGIN
          SPRINT new_ability ~GA_SPWI920~ // energy blades
          SET energy_blades = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~GA_SPCL919~) BEGIN // scribe scrolls
        PATCH_IF (comet == 0) BEGIN
          SPRINT new_ability ~GA_SPWI925~ // comet
          SET comet = 1
        END
      END ELSE
      PATCH_IF (~%old_ability%~ STRING_EQUAL_CASE ~*~) BEGIN // unused
        PATCH_IF (dragons_breath == 0) BEGIN
          SPRINT new_ability ~GA_SPWI922~ // dragon's breath
          SET dragons_breath = 1
        END
      END
      PATCH_IF (STRING_LENGTH ~%new_ability%~ > 0) BEGIN // ability to be replaced here
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 1 ~%new_ability%~ // ability
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 2 ~*~ // icon
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 3 ~*~ // strref
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 4 ~32~ // min_level
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 5 ~99~ // max_level
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 6 ~1~ // num_allowed
        SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 7 ~*~ // prerequisite
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI923~) BEGIN // summon planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~GA_SPWI924~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~ALL_EVIL~ // alignment_restrict
        END ELSE
        PATCH_IF (~%new_ability%~ STRING_EQUAL_CASE ~GA_SPWI924~) BEGIN // summon dark planetar restrictions
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~GA_SPWI923~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~ALL_GOOD~ // alignment_restrict
        END ELSE BEGIN // restrictions for others
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 8 ~*~ // excluded_by
          SET_2DA_ENTRY_LATER ~s2el_lufmt~ i 9 ~*~ // alignment_restrict
        END
      END
    END
    SET_2DA_ENTRIES_NOW ~s2el_lufmt~ 10 // actually write changes!
    PRETTY_PRINT_2DA

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// save penalties for powerful spellcasters                   \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// arcane only                                                \\\\\
/////                                                            \\\\\

BEGIN @231001 DESIGNATED 2310
GROUP @9
SUBCOMPONENT @231000 // arcane save penalties
LABEL ~cd_tweaks_arcane_save_penalties~

INCLUDE ~cdtweaks/lib/saves_macro.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF ("%spell_type%" = 1) BEGIN // arcane only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY

/////                                                            \\\\\
///// divine only                                                \\\\\
/////                                                            \\\\\

BEGIN @231100 DESIGNATED 2311
GROUP @9
SUBCOMPONENT @231000 // divine
LABEL ~cd_tweaks_divine_save_penalties~

INCLUDE ~cdtweaks/lib/saves_macro.tpa~

// first fix spin692 so it won't muck up the search
ACTION_IF FILE_EXISTS_IN_GAME spin692.spl THEN BEGIN

  COPY_EXISTING ~spin692.spl~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
      READ_LONG 0x64 "abil_off"
      WRITE_SHORT ("%abil_off%" + 0x10) 1 // set first ability header to have minimum level of 1
    END
    BUT_ONLY

END

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF ("%spell_type%" = 2) BEGIN // divine only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY

/////                                                            \\\\\
///// arcane & divine                                            \\\\\
/////                                                            \\\\\

BEGIN @231200 DESIGNATED 2312
GROUP @9
SUBCOMPONENT @231000 // both
LABEL ~cd_tweaks_arcane_divine_save_penalties~

INCLUDE ~cdtweaks/lib/saves_macro.tpa~

// first fix spin692 so it won't muck up the search
ACTION_IF FILE_EXISTS_IN_GAME spin692.spl THEN BEGIN

  COPY_EXISTING ~spin692.spl~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
      READ_LONG 0x64 "abil_off"
      WRITE_SHORT ("%abil_off%" + 0x10) 1 // set first ability header to have minimum level of 1
    END
    BUT_ONLY

END

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_SHORT 0x1c "spell_type" ELSE 0
    PATCH_IF (("%spell_type%" = 1) OR ("%spell_type%" = 2)) BEGIN // arcane & divine only
      LAUNCH_PATCH_MACRO ~save_via_level~ // contains rest of patch
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// trap cap removal                                           \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @232000 DESIGNATED 2320
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_trap_cap_removal~

ACTION_IF FILE_EXISTS_IN_GAME ~traplimt.2da~ THEN BEGIN 

  COPY_EXISTING ~traplimt.2da~ ~override~
    COUNT_2DA_ROWS 2 rows
    FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 1 2 limit
      PATCH_IF IS_AN_INT limit BEGIN // sanity check
        SET_2DA_ENTRY row 1 2 999
      END
    END
    BUT_ONLY

END ELSE BEGIN

  COPY_EXISTING ~trapsnar.pro~ ~override/ag#snar.pro~
  ADD_PROJECTILE ~override/ag#snar.pro~
  
  COPY_EXISTING ~spcl411.spl~ ~override~
                ~spcl415.spl~ ~override~
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) ag#snar
    END
    BUT_ONLY
  
  ACTION_IF FILE_EXISTS_IN_GAME ~trapboom.pro~ THEN BEGIN // if ToB

    COPY_EXISTING ~trapboom.pro~ ~override/ag#boom.pro~
    COPY_EXISTING ~trapspik.pro~ ~override/ag#spik.pro~
    COPY_EXISTING ~traptime.pro~ ~override/ag#time.pro~
  
    ADD_PROJECTILE ~override/ag#boom.pro~
    ADD_PROJECTILE ~override/ag#spik.pro~
    ADD_PROJECTILE ~override/ag#time.pro~
  
    COPY_EXISTING ~spcl910b.spl~ ~override~
      READ_LONG  0x64 ab_off
      READ_SHORT 0x68 ab_num
      FOR(i=0; i<ab_num; i+=1) BEGIN
        WRITE_SHORT (ab_off+i*0x28+0x26) ag#spik
      END
  
    COPY_EXISTING ~spcl911b.spl~ ~override~
      READ_LONG  0x64 ab_off
      READ_SHORT 0x68 ab_num
      FOR(i=0; i<ab_num; i+=1) BEGIN
        WRITE_SHORT (ab_off+i*0x28+0x26) ag#boom
      END
      BUT_ONLY
  
    COPY_EXISTING ~spcl912b.spl~ ~override~
      READ_LONG  0x64 ab_off
      READ_SHORT 0x68 ab_num
      FOR(i=0; i<ab_num; i+=1) BEGIN
        WRITE_SHORT (ab_off+i*0x28+0x26) ag#time
      END
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// remove delay on magical traps                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @233000 DESIGNATED 2330
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_remove_magical_trap_delay~

ACTION_FOR_EACH proj IN dfirebl iceglyp idpro96 trapglyp trapskul BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%proj%.pro~ THEN BEGIN

    COPY_EXISTING ~%proj%.pro~ ~override~
      WRITE_SHORT 0x210 1
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// summoning cap removal                                      \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @233900 DESIGNATED 2339
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~summlimt.2da~ @25 // ToB required
GROUP @9
LABEL ~cd_tweaks_summoning_cap_removal~

COPY_EXISTING ~summlimt.2da~ ~override~
  REPLACE_TEXTUALLY ~NORMAL[ %TAB%]+[0-9]+~ ~NORMAL 999~
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// celestial cap removal                                      \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @234000 DESIGNATED 2340
REQUIRE_PREDICATE     GAME_IS ~tob bg2ee eet bgt~   @25 // ToB required
GROUP @9
LABEL ~cd_tweaks_celestial_cap_removal~

ACTION_IF FILE_EXISTS_IN_GAME ~summlimt.2da~ THEN BEGIN

  COPY_EXISTING ~summlimt.2da~ ~override~
    REPLACE_TEXTUALLY ~CELESTIAL[ %TAB%]+[0-9]+~ ~CELESTIAL 999~
    BUT_ONLY

END ELSE BEGIN

  COPY_EXISTING ~devagood.cre~ ~override/ag#dgood.cre~
                ~devaevil.cre~ ~override/ag#devil.cre~
                ~plangood.cre~ ~override/ag#pgood.cre~
                ~planevil.cre~ ~override/ag#pevil.cre~

  COPY_EXISTING ~spdeva.eff~ ~override~
    WRITE_ASCII 0x30 ag#dgood

  COPY_EXISTING ~spdeva2.eff~ ~override~
    WRITE_ASCII 0x30 ag#devil

  COPY_EXISTING ~spplan.eff~ ~override~
    WRITE_ASCII 0x30 ag#pgood

  COPY_EXISTING ~spplan2.eff~ ~override~
    WRITE_ASCII 0x30 ag#pevil

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Remove Racial Restrictions for Single Classes              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @236000 DESIGNATED 2360
GROUP @9
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
LABEL ~cd_tweaks_remove_class_racial_restrictions~

INCLUDE ~cdtweaks/lib/class-race_removal.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Remove Racial Restrictions for Kits                        \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
  
BEGIN @238000 DESIGNATED 2380
GROUP @9
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
LABEL ~cd_tweaks_remove_kit_racial_restrictions~

INCLUDE ~cdtweaks/lib/kit-race_removal.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Dual-class Restrictions                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Allow Non-Humans to Dual-Class                             \\\\\
/////                                                            \\\\\

BEGIN @237100 DESIGNATED 2371
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx
GROUP @9
SUBCOMPONENT @237000 // Alter Dual-class Restrictions
LABEL ~cd_tweaks_alter_dualclass_humans_yes_nonhumans_yes~

ACTION_IF FILE_EXISTS ~tobex_ini/tobextweak.ini~ THEN BEGIN

  COPY ~tobex_ini/tobextweak.ini~ ~tobex_ini/tobextweak.ini~
    REPLACE_TEXTUALLY ~Engine:Allow All Races to Dual Class=0~ ~Engine:Allow All Races to Dual Class=1~
    BUT_ONLY

END ELSE BEGIN

  COPY_EXISTING ~clsrcreq.2da~ ~override~
    REPLACE_TEXTUALLY ~^\(DUALCLASS[ %TAB%]+[0-9]+[ %TAB%]+\).+$~ ~\1 1 1 1 1 1 1~
    PRETTY_PRINT_2DA
    BUT_ONLY

END

INCLUDE ~cdtweaks/lib/dualclass_text.tpa~ // update text that mentions only humans can dual-class

/////                                                            \\\\\
///// Humans can no longer dual-class                            \\\\\
/////                                                            \\\\\

BEGIN @237001 DESIGNATED 2370
GROUP @9
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~clsrcreq.2da~) AND (!MOD_IS_INSTALLED ~tobex/tobex.tp2~ ~100~)) @25 // tobex has no option for this unfortunately
SUBCOMPONENT @237000 // Alter Dual-class Restrictions
LABEL ~cd_tweaks_alter_dualclass_humans_no_nonhumans_no~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  REPLACE_TEXTUALLY ~^DUALCLASS[ %TAB%]+1~ ~DUALCLASS 0~
  PRETTY_PRINT_2DA
  BUT_ONLY

INCLUDE ~cdtweaks/lib/dualclass_text.tpa~ // update text that mentions only humans can dual-class

/////                                                            \\\\\
///// Change both                                                 \\\\\
/////                                                            \\\\\

BEGIN @237200 DESIGNATED 2372
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~clsrcreq.2da~) AND (!MOD_IS_INSTALLED ~tobex/tobex.tp2~ ~100~)) @25 // tobex has no option for this unfortunately
GROUP @9
SUBCOMPONENT @237000 // Alter Dual-class Restrictions
LABEL ~cd_tweaks_alter_dualclass_humans_no_nonhumans_yes~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(DUALCLASS[ %TAB%]+\).+$~ ~\1 0 1 1 1 1 1 1~
  PRETTY_PRINT_2DA
  BUT_ONLY

INCLUDE ~cdtweaks/lib/dualclass_text.tpa~ // update text that mentions only humans can dual-class

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Multiclass Restrictions                              \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Allow humans to multiclass                                 \\\\\
/////                                                            \\\\\

BEGIN @235001 DESIGNATED 2350
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up
LABEL ~cd_tweaks_multiclass_allow_humans~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0)) BEGIN // allow human multi-classes in these by default
      SET_2DA_ENTRY  row 1 8 1
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY
  
INCLUDE ~cdtweaks/lib/multiclass_text.tpa~

/////                                                            \\\\\
///// Allow non-humans every multi-class combo                   \\\\\
/////                                                            \\\\\

BEGIN @235100 DESIGNATED 2351
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up
LABEL ~cd_tweaks_multiclass_allow_nonhumans_all~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0)) BEGIN
      FOR (col = 2; col < 8 ; ++col) BEGIN
        SET_2DA_ENTRY  row col 8 1
      END
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

/////                                                            \\\\\
///// deprecated                                                 \\\\\
/////                                                            \\\\\

BEGIN @235200 DESIGNATED 2352
DEPRECATED @18

// this used to be the multiclass madness component, but with the new multiclass option the component number had to be changed

/////                                                            \\\\\
///// Allow non-humans selected multi-class combos               \\\\\
/////                                                            \\\\\

BEGIN @235300 DESIGNATED 2353
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up
LABEL ~cd_tweaks_multiclass_allow_nonhumans_limited~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (col = 2; col < 8 ; ++col) BEGIN
    SET fighter = 0
    SET mage = 0
    SET cleric = 0
    SET thief = 0
    SET druid = 0
    SET ranger = 0
    FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 0 8 entry
      READ_2DA_ENTRY row col 8 value
      PATCH_IF value = 1 BEGIN
        PATCH_MATCH ~%entry%~ WITH
          ~^FIGHTER$~ BEGIN SET fighter = 1 END
          ~^MAGE$~    BEGIN SET mage = 1 END
          ~^CLERIC$~  BEGIN SET cleric = 1 END
          ~^THIEF$~   BEGIN SET thief = 1 END
          ~^DRUID$~   BEGIN SET druid = 1 END
          ~^RANGER$~  BEGIN SET ranger = 1 END
          DEFAULT
        END  
      END
    END 
    FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 0 8 entry
      PATCH_MATCH ~%entry%~ WITH
        ~^FIGHTER_MAGE$~        BEGIN PATCH_IF fighter AND mage            BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_CLERIC$~      BEGIN PATCH_IF fighter AND cleric          BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_THIEF$~       BEGIN PATCH_IF fighter AND thief           BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_MAGE_THIEF$~  BEGIN PATCH_IF fighter AND mage AND thief  BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^MAGE_THIEF$~          BEGIN PATCH_IF mage AND thief              BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^CLERIC_MAGE$~         BEGIN PATCH_IF cleric AND mage             BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^CLERIC_THIEF$~        BEGIN PATCH_IF cleric AND thief            BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_DRUID$~       BEGIN PATCH_IF fighter AND druid           BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_MAGE_CLERIC$~ BEGIN PATCH_IF fighter AND mage AND cleric BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^CLERIC_RANGER$~       BEGIN PATCH_IF cleric AND ranger           BEGIN SET_2DA_ENTRY row col 8 1 END END
        DEFAULT
      END
    END  
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

/////                                                            \\\\\
///// multiclass madness                                         \\\\\
/////                                                            \\\\\

BEGIN @235200 DESIGNATED 2357
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up
LABEL ~cd_tweaks_multiclass_allow_all_all~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (row = 0 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 0 8 entry
    PATCH_IF (("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_CLERIC"      = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_THIEF"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_THIEF"  = 0) OR
              ("%entry%" STRING_COMPARE_CASE "MAGE_THIEF"          = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_MAGE"         = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_THIEF"        = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_DRUID"       = 0) OR
              ("%entry%" STRING_COMPARE_CASE "FIGHTER_MAGE_CLERIC" = 0) OR
              ("%entry%" STRING_COMPARE_CASE "CLERIC_RANGER"       = 0)) BEGIN
      FOR (col = 1; col < 8 ; ++col) BEGIN
        SET_2DA_ENTRY row col 8 1
      END
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY
  
INCLUDE ~cdtweaks/lib/multiclass_text.tpa~

/////                                                            \\\\\
///// multiclass semimadness                                     \\\\\
/////                                                            \\\\\

BEGIN @235800 DESIGNATED 2358
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
GROUP @9
SUBCOMPONENT @235000 // Higher HP on Level Up
LABEL ~cd_tweaks_multiclass_allow_all_limited~

COPY_EXISTING ~clsrcreq.2da~ ~override~
  COUNT_2DA_ROWS 8 rows
  FOR (col = 1; col < 8 ; ++col) BEGIN // since humans get all of these classes, starting at 1 will open them all up
    SET fighter = 0
    SET mage = 0
    SET cleric = 0
    SET thief = 0
    SET druid = 0
    SET ranger = 0
    FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 0 8 entry
      READ_2DA_ENTRY row col 8 value
      PATCH_IF value = 1 BEGIN
        PATCH_MATCH ~%entry%~ WITH
        ~^FIGHTER$~ BEGIN SET fighter = 1 END
        ~^MAGE$~    BEGIN SET mage = 1 END
        ~^CLERIC$~  BEGIN SET cleric = 1 END
        ~^THIEF$~   BEGIN SET thief = 1 END
        ~^DRUID$~   BEGIN SET druid = 1 END
        ~^RANGER$~  BEGIN SET ranger = 1 END
        DEFAULT
        END  
      END
    END 
    FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 0 8 entry
      PATCH_MATCH ~%entry%~ WITH
        ~^FIGHTER_MAGE$~        BEGIN PATCH_IF fighter AND mage            BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_CLERIC$~      BEGIN PATCH_IF fighter AND cleric          BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_THIEF$~       BEGIN PATCH_IF fighter AND thief           BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_MAGE_THIEF$~  BEGIN PATCH_IF fighter AND mage AND thief  BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^MAGE_THIEF$~          BEGIN PATCH_IF mage AND thief              BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^CLERIC_MAGE$~         BEGIN PATCH_IF cleric AND mage             BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^CLERIC_THIEF$~        BEGIN PATCH_IF cleric AND thief            BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_DRUID$~       BEGIN PATCH_IF fighter AND druid           BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^FIGHTER_MAGE_CLERIC$~ BEGIN PATCH_IF fighter AND mage AND cleric BEGIN SET_2DA_ENTRY row col 8 1 END END
        ~^CLERIC_RANGER$~       BEGIN PATCH_IF cleric AND ranger           BEGIN SET_2DA_ENTRY row col 8 1 END END
      DEFAULT
      END
    END  
  END
  PRETTY_PRINT_2DA
  BUT_ONLY
  
INCLUDE ~cdtweaks/lib/multiclass_text.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Gnome Mage Kit/Multiclass Options                    \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Keep Illusionist Multi, Enable All Kits for Single-Class   \\\\\
/////                                                            \\\\\

BEGIN @255001 DESIGNATED 2550
GROUP @9
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @25 // EE/TobEx required
SUBCOMPONENT @255000
LABEL ~cd_tweaks_gnome_multiclass_illusionist_kits_all~

OUTER_SET gnome_base = 1
OUTER_SET gnome_kits = 1
INCLUDE ~cdtweaks/lib/kit-gnome.tpa~

/////                                                            \\\\\
///// Generic Mage Multi, Keep Only Illusionist Kit for Single   \\\\\
/////                                                            \\\\\

BEGIN @255100 DESIGNATED 2551
GROUP @9
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~clsrcreq.2da~) AND (!MOD_IS_INSTALLED ~tobex/tobex.tp2~ ~100~)) @25 // tobex has no option for this unfortunately
SUBCOMPONENT @255000
LABEL ~cd_tweaks_gnome_multiclass_mage_kits_illusionist~

OUTER_SET gnome_base = 0
OUTER_SET gnome_kits = 0
INCLUDE ~cdtweaks/lib/kit-gnome.tpa~

/////                                                            \\\\\
///// Generic Mage Multiclass, Enable All Kits for Single-Class  \\\\\
/////                                                            \\\\\

BEGIN @255200 DESIGNATED 2552
GROUP @9
REQUIRE_PREDICATE ((FILE_EXISTS_IN_GAME ~clsrcreq.2da~) AND (!MOD_IS_INSTALLED ~tobex/tobex.tp2~ ~100~)) @25 // tobex has no option for this unfortunately
SUBCOMPONENT @255000
LABEL ~cd_tweaks_gnome_multiclass_mage_kits_all~

OUTER_SET gnome_base = 0
OUTER_SET gnome_kits = 1
INCLUDE ~cdtweaks/lib/kit-gnome.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Paladin Spell Progression Table                      \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Paladins Use HoW Spell Progression                         \\\\\
/////                                                            \\\\\

BEGIN @239000 DESIGNATED 2390
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~iwdification/setup-iwdification.tp2~ ~160~ @16
SUBCOMPONENT @239001
LABEL ~cd_tweaks_paladin_spell_progression_iwd~

COPY ~cdtweaks/2da/iwd_mxsplpal.2da~ ~override/mxsplpal.2da~

/////                                                            \\\\\
///// BG2-Style Progression (Up To Level Six Spells)             \\\\\
/////                                                            \\\\\

BEGIN @239100 DESIGNATED 2391
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~iwdification/setup-iwdification.tp2~ ~160~ @16
SUBCOMPONENT @239001
LABEL ~cd_tweaks_paladin_spell_progression_alt~

COPY ~cdtweaks/2da/alt_mxsplpal.2da~ ~override/mxsplpal.2da~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Ranger Spell Progression Table                       \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Rangers Use HoW Spell Progression                          \\\\\
/////                                                            \\\\\

BEGIN @240000 DESIGNATED 2400
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~iwdification/setup-iwdification.tp2~ ~170~ @16
SUBCOMPONENT @240001
LABEL ~cd_tweaks_ranger_spell_progression_iwd~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/iwd_mxsplran.2da~ ~override/mxsplrgr.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/iwd_mxsplran.2da~ ~override/mxsplran.2da~

END

/////                                                            \\\\\
///// BG2-Style Progression (Up To Level Five Spells)            \\\\\
/////                                                            \\\\\

BEGIN @240100 DESIGNATED 2401
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~iwdification/setup-iwdification.tp2~ ~170~ @16
SUBCOMPONENT @240001
LABEL ~cd_tweaks_ranger_spell_progression_alt~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~cdtweaks/2da/alt_mxsplran.2da~ ~override/mxsplrgr.2da~

END ELSE BEGIN

  COPY ~cdtweaks/2da/alt_mxsplran.2da~ ~override/mxsplran.2da~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Druids Use 3e Alignment Restrictions                       \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @241000 DESIGNATED 2410
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_druids_use_3e_alignment~

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_3e_druid_aligns BEGIN

  DRUID         => 0
  FIGHTER_DRUID => 0
  
END

// now get kits, if available
ACTION_IF FILE_EXISTS_IN_GAME ~kitlist.2da~ THEN BEGIN

  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS 9 rows
    FOR (row = 0 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 8 9 class
      PATCH_IF (IS_AN_INT class) BEGIN // in case of junk rows being caught
        PATCH_IF ((class = 11) OR (class = 16)) BEGIN
          READ_2DA_ENTRY row 1 9 kittext
          DEFINE_ASSOCIATIVE_ARRAY cd_3e_druid_aligns BEGIN "%kittext%" => 0 END
        END
      END
    END
    BUT_ONLY    

END
  
// Alignment changes
COPY_EXISTING ~alignmnt.2da~ ~override~
  PATCH_PHP_EACH cd_3e_druid_aligns AS text => foo BEGIN
    REPLACE_TEXTUALLY ~^%text%[ %TAB%]+0[ %TAB%]+0[ %TAB%]+0[ %TAB%]+0[ %TAB%]+1[ %TAB%]+0[ %TAB%]+0[ %TAB%]+0[ %TAB%]+0~
                      ~%text% 0 1 0 1 1 1 0 1 0~
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~1150~ THEN BEGIN // shapeshifter rebalancing

  COPY_EXISTING ~wwwere.itm~   ~override~
                ~wwweregr.itm~ ~override~
    WRITE_BYTE 0x1e (THIS BAND `BIT4) // removes lawful
    WRITE_BYTE 0x1e (THIS BAND `BIT2) // removes good
    WRITE_BYTE 0x1e (THIS BAND `BIT1) // removes evil
    WRITE_BYTE 0x1e (THIS BAND `BIT0) // removes chaotic
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Loosen Equipment Restrictions for Cleric Multis            \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @242000 DESIGNATED 2420
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_cleric_multis_loosen_equipment_restrictions~

// basically, only mess with kit flags if they still have their original usability and have not been hijacked by kit mods as free flags
OUTER_SET lathander = 0
OUTER_SET helm = 0
OUTER_SET talos = 0
ACTION_IF FILE_EXISTS_IN_GAME ~kitlist.2da~ BEGIN
  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS ~9~ "rows"
    FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
      READ_2DA_ENTRY "%index%" 1 9 "kitname"
      READ_2DA_ENTRY "%index%" 7 9 "usability"
      //PATCH_PRINT ~on index %index%, kitname is %kitname% and usability is %usability%~
      PATCH_IF (("%kitname%" STRING_COMPARE_CASE "lathander" = 0) AND ("%usability%" STRING_COMPARE_CASE "0x04000000" = 0)) BEGIN 
        SET lathander = 1
      END  
      PATCH_IF (("%kitname%" STRING_COMPARE_CASE "helm" = 0) AND ("%usability%" STRING_COMPARE_CASE "0x02000000" = 0)) BEGIN
        SET helm = 1
      END  
      PATCH_IF (("%kitname%" STRING_COMPARE_CASE "talos" = 0) AND ("%usability%" STRING_COMPARE_CASE "0x01000000" = 0)) BEGIN
        SET talos = 1
      END  
    END
    BUT_ONLY

END

//Alignment changes
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE 0x1f "use2"
  READ_BYTE 0x20 "use3"
  SET patched = 0
  PATCH_IF (("%use2%" BAND 0b00001000) = 0b00000000) BEGIN // if usable by single-class fighters...
    SET "use2" = ("%use2%" BAND 0b00111111) // make usable by fc, fmc
    SET patched = 1
  END
  PATCH_IF (("%use3%" BAND 0b00000100) = 0b00000000) BEGIN // if usable by single-class mages...
    SET "use2" = ("%use2%" BAND 0b01111110) // make usable by mc, fmc
    SET patched = 1
  END
  PATCH_IF (("%use3%" BAND 0b01000000) = 0b00000000) BEGIN // if usable by single-class thieves...
    SET "use2" = ("%use2%" BAND 0b11111101) // make usable by ct
    SET patched = 1
  END
  PATCH_IF (("%use3%" BAND 0b00100000) = 0b00000000) BEGIN // if usable by single-class rangers...
    SET "use2" = ("%use2%" BAND 0b11111011) // make usable by cr
    SET patched = 1
  END
  WRITE_BYTE 0x1f "%use2%"
  WRITE_BYTE 0x20 "%use3%"
  PATCH_IF (patched AND (lathander OR helm OR talos)) BEGIN
    READ_BYTE 0x29 use4
    PATCH_IF lathander BEGIN
      SET use4 = (use4 BAND `BIT2)
    END  
    PATCH_IF helm BEGIN
      SET use4 = (use4 BAND `BIT1)
    END  
    PATCH_IF talos BEGIN
      SET use4 = (use4 BAND `BIT0)
    END  
    WRITE_BYTE 0x29 use4
  END  
  BUT_ONLY

COPY_EXISTING ~clasweap.2da~ ~override~
  PATCH_IF original_iwd BEGIN
    SET col_num = 16
  END ELSE BEGIN
    SET col_num = 9
  END
  READ_2DA_ENTRY 0 0 (col_num - 1) val                  // weidu seems to freak out with a top line w/ one less entry
  SET_2DA_ENTRY  0 0 (col_num - 1) ~CD_DELETE_ME %val%~ // temp
  FOR (col = 1; col < col_num; ++col) BEGIN
    READ_2DA_ENTRY  1 col col_num "mage"
    READ_2DA_ENTRY  2 col col_num "fighter"
    READ_2DA_ENTRY  4 col col_num "thief"
    READ_2DA_ENTRY  8 col col_num "ranger"
    PATCH_IF ("%fighter%" = 1) BEGIN
      SET_2DA_ENTRY 10 col col_num ~1~ // fc
    END
    PATCH_IF ("%mage%" = 1) BEGIN
      SET_2DA_ENTRY 14 col col_num ~1~ // cm
    END
    PATCH_IF ("%thief%" = 1) BEGIN
      SET_2DA_ENTRY 15 col col_num ~1~ // ct
    END
    PATCH_IF ("%ranger%" = 1) BEGIN
      SET_2DA_ENTRY 18 col col_num ~1~ // cr
    END
    PATCH_IF (("%fighter%" + "%mage%") > 0) BEGIN
      SET_2DA_ENTRY 17 col col_num ~1~ // fmc
    END
  END
  REPLACE_TEXTUALLY ~CD_DELETE_ME ~ ~~
  PRETTY_PRINT_2DA
  BUT_ONLY


ACTION_IF FILE_EXISTS_IN_GAME ~weapprof.2da~ THEN BEGIN

  ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2200~ THEN BEGIN // multiclass grandmastery
  
    OUTER_SET profmax = 5
  
  END ELSE BEGIN
  
    OUTER_SET profmax = 2
  
  END

  COPY_EXISTING ~weapprof.2da~  ~override~
    FOR (row = 9; row < 32; ++row) BEGIN
      READ_2DA_ENTRY row  4 22 mag // mage stars
      READ_2DA_ENTRY row  5 22 fgt // fighter stars
      READ_2DA_ENTRY row  7 22 thf // thief stars
      READ_2DA_ENTRY row 11 22 rgr // ranger stars
      READ_2DA_ENTRY row 13 22 fc  // fighter/cleric stars
      READ_2DA_ENTRY row 17 22 cm  // mage/cleric stars
      READ_2DA_ENTRY row 18 22 ct  // thief/cleric stars
      READ_2DA_ENTRY row 20 22 fmc // fighter/mage/cleric stars
      READ_2DA_ENTRY row 21 22 cr  // cleric/ranger stars
      PATCH_IF (fgt > profmax) BEGIN SET fgt = profmax END
      PATCH_IF (mag > profmax) BEGIN SET mag = profmax END
      PATCH_IF (thf > profmax) BEGIN SET thf = profmax END
      PATCH_IF (rgr > profmax) BEGIN SET rgr = profmax END
      PATCH_IF (fgt > fc) BEGIN
        SET_2DA_ENTRY row 13 22 ~%fgt%~
      END
      PATCH_IF (fgt > fmc) BEGIN
        SET_2DA_ENTRY row 20 22 ~%fgt%~
      END
      PATCH_IF (mag > cm) BEGIN
        SET_2DA_ENTRY row 17 22 ~%mag%~
      END
      PATCH_IF (thf > ct) BEGIN
        SET_2DA_ENTRY row 18 22 ~%thf%~
      END
      PATCH_IF (rgr > cr) BEGIN
        SET_2DA_ENTRY row 21 22 ~%rgr%~
      END
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Change Equipment Restrictions for Druid Multis             \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                            \\\\\
///// Loosen Equipment Restrictions for Druid Multis             \\\\\
/////                                                            \\\\\

BEGIN @243000 DESIGNATED 2430
GROUP @9
SUBCOMPONENT @243001
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_druid_multis_loosen_equipment_restrictions~
  
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_BYTE 0x1f use
  PATCH_IF ((use BAND BIT3) = 0) BEGIN // if usable by single-class fighters...
    WRITE_BYTE 0x1f (THIS BAND `BIT4)  // removes f/d flag
  END
  BUT_ONLY

COPY_EXISTING ~clasweap.2da~ ~override~
  PATCH_IF original_iwd BEGIN
    SET col_num = 16
  END ELSE BEGIN
    SET col_num = 9
  END
  READ_2DA_ENTRY 0 0 (col_num - 1) val                  // weidu seems to freak out with a top line w/ one less entry
  SET_2DA_ENTRY  0 0 (col_num - 1) ~CD_DELETE_ME %val%~ // temp
  FOR (col = 1; col < col_num; ++col) BEGIN
    READ_2DA_ENTRY  2 col col_num "fighter"
    PATCH_IF ("%fighter%" = 1) BEGIN
      SET_2DA_ENTRY 16 col col_num ~1~ // fd
    END
  END
  REPLACE_TEXTUALLY ~CD_DELETE_ME ~ ~~
  PRETTY_PRINT_2DA
  BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~weapprof.2da~ THEN BEGIN

  ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2200~ THEN BEGIN // multiclass grandmastery

    OUTER_SET profmax = 5
  
  END ELSE BEGIN
  
    OUTER_SET profmax = 2
  
  END

  COPY_EXISTING ~weapprof.2da~  ~override~
    FOR (row = 9; row < 32; ++row) BEGIN
      READ_2DA_ENTRY row  5 22 fgt // fighter stars
      READ_2DA_ENTRY row 19 22 fd  // fighter/druid stars
      PATCH_IF (fgt > profmax) BEGIN SET fgt = profmax END
      PATCH_IF (fgt > fd) BEGIN
        SET_2DA_ENTRY row 19 22 ~%fgt%~
      END
    END
    BUT_ONLY

END

/////                                                            \\\\\
///// Tighten Equipment Restrictions for Druid Multis            \\\\\
/////                                                            \\\\\
 
BEGIN @243100 DESIGNATED 2431
GROUP @9
SUBCOMPONENT @243001
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_druid_multis_tighten_equipment_restrictions~
  
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (("%SOURCE_RES%" STRING_COMPARE_CASE "cdrelm") AND
            ("%SOURCE_RES%" STRING_COMPARE_CASE "d1brac02") AND
            ("%SOURCE_RES%" STRING_COMPARE_CASE "gauntem") AND
            ("%SOURCE_RES%" STRING_COMPARE_CASE "helm34") AND
            ("%SOURCE_RES%" STRING_COMPARE_CASE "mh#ioun1") AND
            ("%SOURCE_RES%" STRING_COMPARE_CASE "misc5x")) BEGIN // exclude jaheira's harper pin
    READ_SHORT 0x1c type
    READ_LONG  0x1e use
    PATCH_IF type = 7 BEGIN // helmets
      PATCH_IF ((use BAND BIT6) = BIT6) BEGIN        // if unusable by bards (regular helmet)...
        WRITE_LONG 0x1e ((THIS BOR BIT12) BOR BIT30) // adds f/d, druid flags
      END ELSE BEGIN                                 // if not helmet, assume ioun stone
        PATCH_IF ((use BAND BIT30) = BIT30) BEGIN    // if unusable by druids...
          WRITE_LONG 0x1e (THIS BOR BIT12)           // adds f/d flag
        END 
      END
    END ELSE
    PATCH_IF ((type = 1) OR      // amulets
              (type = 3) OR      // belts
              (type = 4) OR      // boots
              (type = 6) OR      // bracers
              (type = 9) OR      // potions
              (type = 10) OR     // rings
              (type = 32) OR     // cloaks
              (type = 35)) BEGIN // wands
      PATCH_IF (((use BAND BIT30) = BIT30) AND    // if unusable by druids...
                ((use BAND BIT11) = BIT11)) BEGIN // ...and unusable by fighters...
        WRITE_LONG 0x1e (THIS BOR BIT12)          // adds f/d flag
      END
    END ELSE BEGIN // all other item types
      PATCH_IF ((use BAND BIT30) = BIT30) BEGIN // if unusable by druids...
        WRITE_LONG 0x1e (THIS BOR BIT12)        // adds f/d flag
      END
    END
  END
  BUT_ONLY

COPY_EXISTING ~clasweap.2da~ ~override~
  PATCH_IF original_iwd BEGIN
    SET col_num = 16
  END ELSE BEGIN
    SET col_num = 9
  END
  READ_2DA_ENTRY 0 0 (col_num - 1) val                  // weidu seems to freak out with a top line w/ one less entry
  SET_2DA_ENTRY  0 0 (col_num - 1) ~CD_DELETE_ME %val%~ // temp
  FOR (col = 1; col < col_num; ++col) BEGIN
    READ_2DA_ENTRY  7 col col_num druid
    PATCH_IF (druid = 0) BEGIN
      SET_2DA_ENTRY 16 col col_num 0 // fd
    END
  END
  REPLACE_TEXTUALLY ~CD_DELETE_ME ~ ~~
  PRETTY_PRINT_2DA
  BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME ~weapprof.2da~ THEN BEGIN

  COPY_EXISTING ~weapprof.2da~  ~override~
    FOR (row = 9; row < 28; ++row) BEGIN // exclude weapon styles
      READ_2DA_ENTRY row 10 22 dru // druid stars
      PATCH_IF (dru = 0) BEGIN
        SET_2DA_ENTRY row 19 22 0
      END
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Everyone Gets Bonus APR from Specialization                \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @244000 DESIGNATED 2440
GROUP @9
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet~ @25
LABEL ~cd_tweaks_specialization_apr_bonus_for_all~

INCLUDE ~cdtweaks/lib/remove_lines.tpa~

COPY ~cdtweaks/spl/d5_numat.spl~ ~override~

// sets GETS_PROF_APR to 1 for all classes/kits
COPY_EXISTING ~clswpbon.2da~ ~override~
  COUNT_2DA_ROWS 4 rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    SET_2DA_ENTRY index 1 4 1
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

COPY_EXISTING ~wspatck.2da~ ~override~
  COUNT_2DA_COLS col_num
  READ_2DA_ENTRY 0 1 col_num row0 // read in level 1 bonuses
  READ_2DA_ENTRY 1 1 col_num row1
  READ_2DA_ENTRY 2 1 col_num row2
  READ_2DA_ENTRY 3 1 col_num row3
  READ_2DA_ENTRY 4 1 col_num row4
  READ_2DA_ENTRY 5 1 col_num row5
  FOR (index = 2 ; index < col_num ; ++index) BEGIN // write level 1 bonuses to all other levels (no apr from levelling)
    SET_2DA_ENTRY 0 index col_num row0
    SET_2DA_ENTRY 1 index col_num row1
    SET_2DA_ENTRY 2 index col_num row2
    SET_2DA_ENTRY 3 index col_num row3
    SET_2DA_ENTRY 4 index col_num row4
    SET_2DA_ENTRY 5 index col_num row5
  END
  PRETTY_PRINT_2DA
  BUT_ONLY

// update prof strings in game
ACTION_CLEAR_ARRAY cd_prof_strings
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_prof_strings BEGIN
  dots0 => 9588  // default iwdee/bg2ee/bgee prof string
  dots2 => 32117 // default iwdee/bg2ee specialization string
  dots3 => 32118 // default iwdee/bg2ee mastery string
  dots4 => 32119 // default iwdee/bg2ee high mastery string
  dots5 => 32120 // default iwdee/bg2ee grandmastery string
END

ACTION_IF NOT (game_is_iwdee OR game_is_bg2ee OR game_is_eet) BEGIN // override dots2-5 for bgee, sod

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_prof_strings BEGIN
    dots2 => 24220 // default iwdee/bg2ee specialization string
    dots3 => 24221 // default iwdee/bg2ee mastery string
    dots4 => 24222 // default iwdee/bg2ee high mastery string
    dots5 => 24223 // default iwdee/bg2ee grandmastery string
  END

END

ACTION_IF FILE_EXISTS_IN_GAME ~enginest.2da~ BEGIN // if ee v2 and above, override above with actual lookups

  COPY_EXISTING ~enginest.2da~ ~override~
    REPLACE_EVALUATE ~^\(STRREF_GUI_HELP_PROFICIENCIES[ TAB%]+\)\([0-9]+\)~ BEGIN DEFINE_ASSOCIATIVE_ARRAY cd_prof_strings BEGIN dots0 => "%MATCH2%" END END ~%MATCH1%%MATCH2%~
    REPLACE_EVALUATE ~^\(STRREF_GUI_MIXED_DOTS2[ TAB%]+\)\([0-9]+\)~        BEGIN DEFINE_ASSOCIATIVE_ARRAY cd_prof_strings BEGIN dots2 => "%MATCH2%" END END ~%MATCH1%%MATCH2%~
    REPLACE_EVALUATE ~^\(STRREF_GUI_MIXED_DOTS3[ TAB%]+\)\([0-9]+\)~        BEGIN DEFINE_ASSOCIATIVE_ARRAY cd_prof_strings BEGIN dots3 => "%MATCH2%" END END ~%MATCH1%%MATCH2%~
    REPLACE_EVALUATE ~^\(STRREF_GUI_MIXED_DOTS4[ TAB%]+\)\([0-9]+\)~        BEGIN DEFINE_ASSOCIATIVE_ARRAY cd_prof_strings BEGIN dots4 => "%MATCH2%" END END ~%MATCH1%%MATCH2%~
    REPLACE_EVALUATE ~^\(STRREF_GUI_MIXED_DOTS5[ TAB%]+\)\([0-9]+\)~        BEGIN DEFINE_ASSOCIATIVE_ARRAY cd_prof_strings BEGIN dots5 => "%MATCH2%" END END ~%MATCH1%%MATCH2%~
    BUT_ONLY

END

OUTER_SPRINT forbidden1 @244001
OUTER_SPRINT forbidden2 @244002
OUTER_SPRINT forbidden3 @244003

ACTION_PHP_EACH cd_prof_strings AS prof => strref BEGIN

  ACTION_GET_STRREF strref raw
  OUTER_INNER_PATCH_SAVE string ~%raw%~ BEGIN
    REPLACE_TEXTUALLY ~%forbidden1%~ ~~
    REPLACE_TEXTUALLY ~%forbidden2%[ %TAB%]+\*~ ~%forbidden2%~
    REPLACE_TEXTUALLY ~[ %TAB%]+%forbidden3%[ %TAB%]+~ ~ ~
  END
  STRING_SET_EVALUATE strref ~%string%~

END

ACTION_CLEAR_ARRAY mod_kit_clabs
ACTION_DEFINE_ASSOCIATIVE_ARRAY mod_kit_clabs BEGIN
  clabfi01 =>  2 // generic fighter
  clabpa01 =>  6 // generic paladin
  clabpa05 =>  6 // fallen paladin
  clabrn01 => 12 // generic ranger
  clabrn05 => 12 // fallen ranger
END

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS ~10~ "rows"
  FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
    READ_2DA_ENTRY %index% 8 10 modclass
    PATCH_IF ((modclass = 2) OR (modclass = 6) OR (modclass = 12)) BEGIN
      READ_2DA_ENTRY %index% 5 10 modclab
      SPRINT $mod_kit_clabs(~%modclab%~)~%modclass%~
    END
  END
BUT_ONLY

ACTION_PHP_EACH mod_kit_clabs AS clab => clas BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%clab%.2da~ THEN BEGIN
    APPEND ~%clab%.2da~ ~ABILITY     ****        ****        ****        ****        ****        ****        AP_D5_NUMAT ****        ****        ****        ****        ****        AP_D5_NUMAT D5REPLACE ~ UNLESS ~AP_D5_NUMAT~
    COPY_EXISTING ~%clab%.2da~ ~override~
      COUNT_2DA_COLS cols
      FOR (index = 15 ; index < cols ; ++index) BEGIN
        REPLACE_TEXTUALLY ~D5REPLACE~ ~**** D5REPLACE~
      END
      REPLACE_TEXTUALLY ~D5REPLACE~ ~****~
      PRETTY_PRINT_2DA
    BUT_ONLY
  END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// PnP Prof restrictions for dual-classes                     \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @245000 DESIGNATED 2450
GROUP @9
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~kitlist.2da~ @25
REQUIRE_PREDICATE NOT GAME_IS ~soa iwd2~ @25 // needs tob until ADD_KIT/luabbr is fixed
LABEL ~cd_tweaks_pnp_prof_restrictions_for_dualclasses~

INCLUDE ~cdtweaks/lib/sod_25stweap_fix.tpa~
INCLUDE ~cdtweaks/lib/iwdee_25stweap_fix.tpa~
INCLUDE ~cdtweaks/lib/remove_lines.tpa~
INCLUDE ~cdtweaks/lib/fl#add_kit_ee.tpa~

COPY ~cdtweaks/lib/pnp_dual_core.tpa~ ~weidu_external/cdtweaks/lib/pnp_dual_core.tpa~

// look up default fighter/ranger stuff
COPY_EXISTING ~weapprof.2da~  ~override~
  REPLACE_TEXTUALLY ~NAME_REF~ ~CDDELETEME NAME_REF~
  FOR (row = 9; row < 33; ++row) BEGIN
    READ_2DA_ENTRY row  5 22 prof_fgt
    READ_2DA_ENTRY row 11 22 prof_rgr
    PATCH_IF prof_fgt > 2 BEGIN SET prof_fgt = 2 END
    PATCH_IF prof_rgr > 2 BEGIN SET prof_rgr = 2 END
    INNER_ACTION BEGIN
      COPY ~weidu_external/cdtweaks/lib/pnp_dual_core.tpa~ ~weidu_external/cdtweaks/lib/pnp_dual_core.tpa~
        REPLACE_TEXTUALLY ~prof_fgt_%row%~ ~%prof_fgt%~
        REPLACE_TEXTUALLY ~prof_rgr_%row%~ ~%prof_rgr%~
    END
  END
  BUT_ONLY

// these strings are the same on bgee/bg2ee/iwdee/vanilla bg2; leaving externalized in case we discover otherwise and need to if/else here
OUTER_SET fgt_strref1 = 10086
OUTER_SET fgt_strref2 = 10174 // 16799, 40921, 40922
OUTER_SET fgt_strref3 = 9556
OUTER_SET rgr_strref1 = 7200
OUTER_SET rgr_strref2 = 10173 // 1077, 2184-5
OUTER_SET rgr_strref3 = 9557

COPY ~weidu_external/cdtweaks/lib/pnp_dual_core.tpa~ ~weidu_external/cdtweaks/lib/pnp_dual_core.tpa~
  REPLACE_TEXTUALLY ~fgt_strref1~ ~%fgt_strref1%~
  REPLACE_TEXTUALLY ~fgt_strref2~ ~%fgt_strref2%~
  REPLACE_TEXTUALLY ~fgt_strref3~ ~%fgt_strref3%~
  REPLACE_TEXTUALLY ~fgt_strref4~ ~%fgt_strref4%~
  REPLACE_TEXTUALLY ~rgr_strref1~ ~%rgr_strref1%~
  REPLACE_TEXTUALLY ~rgr_strref2~ ~%rgr_strref2%~
  REPLACE_TEXTUALLY ~rgr_strref3~ ~%rgr_strref3%~
  REPLACE_TEXTUALLY ~rgr_strref4~ ~%rgr_strref4%~

REINCLUDE ~weidu_external/cdtweaks/lib/pnp_dual_core.tpa~

COPY ~cdtweaks/spl/d5_duafc.spl~ ~override~

COPY ~cdtweaks/cre/d5_dual.cre~ ~override~
     ~cdtweaks/eff/d5_dual.eff~ ~override~

COMPILE ~cdtweaks/baf/d5_dual.baf~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~clabma01.2da~ THEN BEGIN
  COPY ~cdtweaks/2da/clabmaxx.2da~ ~override/clabma01.2da~
END

COPY_EXISTING ~clabdr01.2da~ ~override~
              ~clabma01.2da~ ~override~
              ~clabpr01.2da~ ~override~
              ~clabth01.2da~ ~override~
  LPM remove_blank_lines
  APPEND_FILE ~cdtweaks/2da/dualfc.2da~
  BUT_ONLY

// now for original and mod kits
OUTER_SET cd_kit_count = 0

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row2 = 0 ; row2 < rows ; ++row2) BEGIN
    READ_2DA_ENTRY row2 8 9 class
    READ_2DA_ENTRY row2 0 9 kitno
    PATCH_IF IS_AN_INT class BEGIN // this is to screen out header junk
      PATCH_IF ((class = 2) OR (class = 12)) BEGIN // looking for fighter/ranger kits
        READ_2DA_ENTRY row2 1 9 name
        READ_2DA_ENTRY row2 5 9 clab
        SET ids = (0x4000 + kitno)
        PATCH_IF (class = 2) BEGIN // looking for fighter/ranger kits
          SPRINT letter  f
          SPRINT class ~OR(4) Class(LastSummonerOf(Myself),FIGHTER_THIEF) Class(LastSummonerOf(Myself),FIGHTER_MAGE) Class(LastSummonerOf(Myself),FIGHTER_DRUID) Class(LastSummonerOf(Myself),FIGHTER_CLERIC)~
        END ELSE BEGIN
          SPRINT letter  r
          SPRINT class ~Class(LastSummonerOf(Myself),CLERIC_RANGER)~
        END
        SET sanity_check = 0
        // sanity check before we proceed - make sure kit is not purely internal
        PATCH_FOR_EACH race IN d e g he hl ho BEGIN
          PATCH_IF FILE_EXISTS_IN_GAME ~k_%letter%_%race%.2da~ BEGIN
            INNER_ACTION BEGIN

              COPY_EXISTING ~k_%letter%_%race%.2da~ ~override~
                COUNT_2DA_ROWS 2 rows_inner
                FOR (row_inner = 0 ; row_inner < rows_inner ; ++row_inner) BEGIN
                  READ_2DA_ENTRY row_inner 0 2 index
                  READ_2DA_ENTRY row_inner 1 2 kitcheck
                  PATCH_IF IS_AN_INT kitcheck BEGIN // sanity check
                    PATCH_IF kitcheck = kitno BEGIN
                      SET sanity_check = 1
                      SET row_inner = rows_inner // kill loop
                    END
                  END
                END
                BUT_ONLY
  
            END
          END
        END
        PATCH_IF sanity_check = 1 BEGIN
          INNER_ACTION BEGIN // sanity check two - don't bother if kit doesn't allow > 2 pips in anything

            COPY ~cdtweaks/lib/pnp_dual_kit.tpa~ ~weidu_external/cdtweaks/lib/pnp_dual_working.tpa~

            COPY_EXISTING ~weapprof.2da~  ~override~
              COUNT_2DA_COLS weapprof_cols
              FOR (index_col = 20 ; index_col < weapprof_cols ; ++index_col) BEGIN
                READ_2DA_ENTRY 0 index_col weapprof_cols col_name
                PATCH_IF ("%col_name%" STRING_COMPARE_CASE ~%name%~ = 0) BEGIN
                  FOR (row = 9; row < 33; ++row) BEGIN
                    READ_2DA_ENTRY row index_col weapprof_cols prof_fgt
                    PATCH_IF prof_fgt > 2 BEGIN SET prof_fgt = 2 SET sanity_check = 2 END
                    INNER_ACTION BEGIN

                      COPY ~weidu_external/cdtweaks/lib/pnp_dual_working.tpa~ ~weidu_external/cdtweaks/lib/pnp_dual_working.tpa~
                        REPLACE_TEXTUALLY ~prof_fgt_%row%~ ~%prof_fgt%~

                    END

                  END
                  SET index_col = weapprof_cols // kill loop
                END
              END
              BUT_ONLY

          END
          PATCH_IF sanity_check = 1 BEGIN // if no > 2 pips profs available, simply edit invis creature script
            INNER_ACTION BEGIN

              COPY_EXISTING ~d5_dual.bcs~ ~override~
                DECOMPILE_AND_PATCH BEGIN
                  REPLACE_EVALUATE ~OR(\([0-9]+\))\([ %TAB%%LNL%%MNL%%WNL%]+Kit(LastSummonerOf(Myself),D5FIGHT)\)~ BEGIN
                    SET new_or = (%MATCH1% + 1)
                  END ~OR(%new_or%) %MATCH2% Kit(LastSummonerOf(Myself),%ids%)~
                END
                BUT_ONLY

            END
          END ELSE BEGIN // if we do indeed need shadow kit
            INNER_ACTION BEGIN

              COPY ~weidu_external/cdtweaks/lib/pnp_dual_working.tpa~ ~weidu_external/cdtweaks/lib/pnp_dual_working.tpa~
                REPLACE_TEXTUALLY ~REPLACE_ME_KIT_NAME~ ~%name%~
                REPLACE_TEXTUALLY ~CD_NEW_KIT_NAME~ ~CDSK%cd_kit_count%~

              REINCLUDE ~weidu_external/cdtweaks/lib/pnp_dual_working.tpa~

            END
          END // sanity check 2
        END // sanity check 1
      END // fighter/ranger class check
    END // is_an_int check
  END // for loop
  BUT_ONLY

COPY_EXISTING ~weapprof.2da~  ~override~
  REPLACE_TEXTUALLY ~CDDELETEME~ ~~
  PRETTY_PRINT_2DA

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Exceptional Strength Weight Limit Changes                  \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @250000 DESIGNATED 2500
GROUP @9
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~strmodex.2da~ @25
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_exceptional_strength_weight_limits~

COPY_EXISTING ~strmodex.2da~ ~override~
  REPLACE_EVALUATE ~^\([0-9]+\)\([ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+[0-9]+[ %TAB%]+\)\([0-9]+\)~
  BEGIN SET weight = (%MATCH1% * 2) END
  ~%MATCH1%%MATCH2%%weight%~
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// level-locked spell scrolls                                 \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @251000 DESIGNATED 2510
GROUP @9
REQUIRE_PREDICATE GAME_IS ~soa tob tutu tutu_totsc bgt ca iwd-in-bg2 bgee bg2ee eet iwdee~ @25
LABEL ~cd_tweaks_level_locked_scrolls~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  READ_SHORT 0x24 level
  PATCH_IF ((type = 11) AND (level = 0)) BEGIN // scrolls w/o a min requirement already
    READ_LONG  0x64 abil_off ELSE 0
    READ_SHORT 0x68 abil_num ELSE 0
    READ_LONG  0x6a fx_off   ELSE 0
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      READ_SHORT  (abil_off + 0x1e + (0x38 * index)) abil_fx_num
      READ_SHORT  (abil_off + 0x20 + (0x38 * index)) abil_fx_idx
      FOR (index2 = 0 ; index2 < abil_fx_num ; ++index2) BEGIN
        READ_SHORT (fx_off +        (0x30 * (abil_fx_idx + index2))) opcode
        PATCH_IF ((opcode = 146) OR (opcode = 148)) BEGIN // cast spell
          READ_ASCII (fx_off + 0x14 + (0x30 * (abil_fx_idx + index2))) spell
          PATCH_IF (("%spell%" STRING_COMPARE_REGEXP ~^[Ss][Pp][Ww][Ii][3-9][0-9][0-9]$~ = 0) OR
                    ("%spell%" STRING_COMPARE_REGEXP ~^[Ss][Pp][Pp][Rr][3-7][0-9][0-9]$~ = 0)) BEGIN // levels 1&2 usable by level 0/1, so no need for level requirements
            PATCH_IF ("%spell%" STRING_COMPARE_REGEXP ~^[Ss][Pp][Ww][Ii][3-9][0-9][0-9]$~ = 0) BEGIN SET level_break = 11 END ELSE BEGIN SET level_break = 13 END // account for extra level for priest 6>7 and wizard 5>6
            READ_BYTE  (fx_off + 0x18 + (0x30 * (abil_fx_idx + index2))) level
            SET level = ((level - 0x31) * 2) // 0x31 is ascii 1, 0x32 is ascii 2, etc. This basically uses a level one below the numerical value and then doubles it
            PATCH_IF level < level_break BEGIN
              SET level -= 1
            END
          END
        END
      END
    END
    WRITE_SHORT 0x24 level // still 0 if no match found, otherwise new value
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// loosen shield restrictions                                 \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @252000 DESIGNATED 2520
GROUP @9
REQUIRE_PREDICATE GAME_IS ~soa tob bg1 totsc iwd how totlm tutu tutu_totsc bgt ca iwd-in-bg2 bgee bg2ee eet iwdee~ @25
LABEL ~cd_tweaks_loosen_shield_restrictions~
     
INCLUDE ~cdtweaks/lib/common_armor_list.tpa~

ACTION_PHP_EACH cd_common_armor_list AS params => item BEGIN
  
  ACTION_IF (("%params_2%" STRING_COMPARE_CASE "shield_buckler" = 0) OR
             ("%params_2%" STRING_COMPARE_CASE "shield_small" = 0)) BEGIN

    COPY_EXISTING ~%item%.itm~ ~override~
      READ_SHORT 0x1c type
      PATCH_IF ((type = 12) OR (type = 41) OR (type = 47) OR (type = 49) OR (type = 53)) BEGIN // 12 is bg shield type, all others are iwd
        READ_LONG 0x1e use
        PATCH_IF (("%params_2%" STRING_COMPARE_CASE "shield_buckler" = 0) AND // buckler that's usable by...
                  ((use BAND BIT8)  = 0) AND                                  // cleric-mages and
                  ((use BAND BIT13) = 0) AND                                  // fighter-mages and
                  ((use BAND BIT15) = 0) AND                                  // fighter-mage-clerics and
                  ((use BAND BIT16) = 0) AND                                  // fighter-mage-thieves and
                  ((use BAND BIT19) = 0)) BEGIN                               // mage-thieves
          WRITE_LONG 0x1e (THIS BAND `BIT18)                                  // removes mage flag
        END ELSE
        PATCH_IF (("%params_2%" STRING_COMPARE_CASE "shield_small" = 0) AND // small shield that's usable by...
                  ((use BAND BIT9)  = 0) AND                                // cleric-thieves and
                  ((use BAND BIT16) = 0) AND                                // fighter-mage-thieves and
                  ((use BAND BIT17) = 0)) BEGIN                             // fighter-thieves
          WRITE_LONG 0x1e (((THIS BAND `BIT22) BAND `BIT19) BAND `BIT6)     // removes bard, thief, mage-thief flags
        END
      END  
      BUT_ONLY
      
  END // appearance check
  
END // php_each
  
ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2060~ THEN BEGIN // weapon styles for all

  OUTER_SET $mage_kit(MAGE) = 1
  OUTER_SET $mage_kit(SORCERER) = 1 // shouldn't be in weapprof, but let's be safe
  COPY_EXISTING ~kitlist.2da~ ~override~
    SET col_check = (9 + enhanced_edition)
    COUNT_2DA_ROWS col_check rows
    FOR (row = 1 ; row < rows ; ++row) BEGIN
      READ_2DA_ENTRY row 8 col_check class
      PATCH_IF ((class = 1) OR (class = 19)) BEGIN // mage or sorcerer
        READ_2DA_ENTRY row 1 col_check kit
        SET $mage_kit("%kit%") = 1
      END
    END   
    BUT_ONLY

  COPY_EXISTING ~weapprof.2da~ ~override~
    COUNT_2DA_COLS col_count
    SET col_check = col_count - 1 // header row has one less entry than rest of file
    FOR (col = 3 ; col < col_check ; ++col) BEGIN
      READ_2DA_ENTRY 0 col col_check kit
      PATCH_IF VARIABLE_IS_SET $mage_kit("%kit%") BEGIN
        SET_2DA_ENTRY 29 (col + 1) col_count 2
      END
    END  
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Lightning bolts don't bounce                               \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @253000 DESIGNATED 2530
GROUP @9
REQUIRE_PREDICATE NOT GAME_IS ~pst pstee iwd2~ @25
LABEL ~cd_tweaks_lightning_bolts_pls_no_bouncerino~

OUTER_SET proj_protect = 1
ACTION_IF ((FILE_EXISTS_IN_GAME ~lightb.pro~) OR (FILE_EXISTS_IN_GAME ~idpro40.pro~)) THEN BEGIN // for EE games w/o hardcoded projectile...

  COPY_EXISTING ~idpro40.pro~ ~override~ // iwdee lightning bolt
                ~lightb.pro~  ~override~ // bgee/bg2ee lightning bolt
    READ_SHORT 0x21a bounce
    PATCH_IF bounce BEGIN // if bounce projectile set (basically 2.6+)
      WRITE_SHORT 0x21a 0 // blank bounce projectile
    END ELSE BEGIN  // if no bounce projectile (2.5 or earlier)
      WRITE_LONG  0x02c (THIS BAND `BIT0)  // remove bounce from walls flag
      WRITE_SHORT 0x200 (THIS BAND `BIT4)  // remove secondary projectile flag
      WRITE_SHORT 0x214 0                  // blank secondary projectile reference
    END  
    BUT_ONLY IF_EXISTS

    OUTER_SET cdlightb = 40

END ELSE BEGIN

  ACTION_IF original_bg2 OR tutu_gen OR game_is_bgt OR game_is_ca OR game_is_iwd-in-bg2 BEGIN // engines that support pro files

    ADD_PROJECTILE ~cdtweaks/pro/cdlightb.pro~ // lightning bolt w/o bouncing

    ACTION_IF NOT FILE_EXISTS_IN_GAME ~tra_09.wav~   BEGIN COPY ~cdtweaks/wav/tra_09.wav~   ~override~ END // sound for projectile
    ACTION_IF NOT FILE_EXISTS_IN_GAME ~splightb.bam~ BEGIN COPY ~cdtweaks/bam/splightb.bam~ ~override~ END // BAM for projectile

  END ELSE BEGIN // if engine without projectile files, we'll just change to hardcoded call lightning projectile

    OUTER_SET cdlightb = 23
    OUTER_SET proj_protect = 0

  END

  INCLUDE ~cdtweaks/lib/macro_alter_header.tpa~

  // update anything that is immune/reflects original lightning bolt to do the same with new projectile
  COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
    LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode =  83 match_parameter2 = 39 parameter2 = (cdlightb - 1) END // immunity to projectile
    LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 197 match_parameter2 = 39 parameter2 = (cdlightb - 1) END // reflect projectile
    BUT_ONLY

END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
                          ~^.+\.spl$~ ~override~
  READ_ASCII 0x00 type (3)
  SET counter_offset = 0x70
  READ_LONG  0x64 abil_off ELSE 0
  READ_SHORT 0x68 abil_num ELSE 0
  READ_LONG  0x6a fx_off   ELSE 0
  PATCH_IF ("%type%" STRING_COMPARE_CASE "spl" = 0) BEGIN
    SET abil_length    = 0x28
    SET global_loop    = 0
  END ELSE BEGIN // item
    SET abil_length    = 0x38
    SET global_loop    = 1
  END
  SET pro_off = abil_length = 0x38 ? 0x2a : 0x26
  SET new_fx = 0
  FOR (index = (0 - global_loop) ; index < abil_num ; ++index) BEGIN
    PATCH_IF (index < 0) BEGIN // if loop through globals needed
      SET abil_fx_idx = 0
    END ELSE BEGIN // otherwise normal ability
      SET proj_protect_add = 0
      SET counter_offset = (abil_off + 0x1e + (abil_length * index))
      READ_SHORT  (abil_off + 0x20 + (abil_length * index)) abil_fx_idx
      SET abil_fx_idx += new_fx
      WRITE_SHORT (abil_off + 0x20 + (abil_length * index)) (abil_fx_idx)
    END
    READ_SHORT counter_offset counter // fx_num on global loop, otherwise abil_fx_num
    PATCH_IF (cdlightb != 40) BEGIN // don't bother with updating immunity/reflection if EE game where projectile number doesn't change
      FOR (index2 = 0 ; index2 < counter ; ++index2) BEGIN
        READ_SHORT (fx_off        + ((abil_fx_idx + index2) * 0x30)) opcode
        READ_LONG  (fx_off + 0x08 + ((abil_fx_idx + index2) * 0x30)) param2
        PATCH_IF (((opcode = 83) OR (opcode = 197)) AND (param2 = 39)) BEGIN            // reflect/immunity to existing lightning bolt
          READ_ASCII   (fx_off        + ((abil_fx_idx + index2) * 0x30)) clone (48)     // read existing effect
          INSERT_BYTES (fx_off        + ((abil_fx_idx + index2) * 0x30)) 0x30           // insert bytes
          WRITE_ASCIIE (fx_off        + ((abil_fx_idx + index2) * 0x30)) "%clone%" #48  // write existing effect
          WRITE_LONG   (fx_off + 0x08 + ((abil_fx_idx + index2) * 0x30)) (cdlightb - 1) // update to new projectile
          SET new_fx += 1
          SET index2 += 1
          SET counter += 1
        END
      END
    END
    PATCH_IF (index >= 0) BEGIN // skip on globals loop
      READ_SHORT  (abil_off + pro_off + (abil_length * index)) projectile
      PATCH_IF projectile = 40 BEGIN
        WRITE_SHORT  (abil_off + pro_off + (abil_length * index)) cdlightb
        PATCH_IF proj_protect BEGIN
          INSERT_BYTES (fx_off        + (abil_fx_idx * 0x30)) 0x30           // insert bytes for new effect
          WRITE_SHORT  (fx_off        + (abil_fx_idx * 0x30)) 83             // immunity to projectile
          WRITE_BYTE   (fx_off + 0x02 + (abil_fx_idx * 0x30)) 1              // target:self
          WRITE_LONG   (fx_off + 0x08 + (abil_fx_idx * 0x30)) (cdlightb - 1) // new lightning projectile
          WRITE_LONG   (fx_off + 0x0e + (abil_fx_idx * 0x30)) 2              // duration
          WRITE_BYTE   (fx_off + 0x12 + (abil_fx_idx * 0x30)) 100            // probability
          SET new_fx += 1
          SET counter += 1
        END
      END
    END
    WRITE_SHORT counter_offset counter // fx_num on global loop, otherwise abil_fx_num
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Speed Up de'Arnise Keep Stronghold Quests                  \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @254000 DESIGNATED 2540
GROUP @9
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @25
LABEL ~cd_tweaks_faster_dearnise_keep_timer~

COPY_EXISTING ~cut32b.bcs~   ~override~
              ~cut32c.bcs~   ~override~
              ~cut32d.bcs~   ~override~
              ~cut32e.bcs~   ~override~
              ~cut32e2.bcs~  ~override~
              ~cut32g.bcs~   ~override~
              ~cut32h.bcs~   ~override~
              ~cut32i.bcs~   ~override~
              ~cut32j.bcs~   ~override~
              ~cut32k.bcs~   ~override~
              ~kpdomo01.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_EVALUATE ~SetGlobalTimer("KPPlotTime","GLOBAL",\([^)]+\))~ BEGIN
      PATCH_IF IS_AN_INT MATCH1 BEGIN
        SET time = MATCH1
      END ELSE BEGIN
        SET time = IDS_OF_SYMBOL (~GTIMES~ ~%MATCH1%~) // should convert stuff like EIGHT_HOURS to numeric value
      END
      SET time = ((time * 67) / 100) 
    END ~SetGlobalTimer("KPPlotTime","GLOBAL",%time%)~  
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Allow Monks to Wear Helmets                                \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @256000 DESIGNATED 2560
GROUP @9
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet bgee iwdee tutu tutu_totsc~ @25
LABEL ~cd_tweaks_allow_monks_to_wear_helmets~

// monks generally not listed in unusability block, so skip description updates for oBG2
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c type
  PATCH_IF type = 7 BEGIN // headgear
    READ_LONG 0x1e flags
    PATCH_IF (((flags & BIT7) = 0) AND ((flags & BIT11) = 0)) BEGIN // if usable by single-class clerics and fighters
      WRITE_LONG 0x1e (flags BAND `BIT29) // removes monk flag
    END
  END
  BUT_ONLY

ACTION_IF !MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~10~ THEN BEGIN // skip if 'no helmet animations' installed

  // helmets don't work on monk animation, so replace with cleric animation
  ACTION_CLEAR_ARRAY cd_new_anims
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_new_anims BEGIN
    6000, 6500, CHMB, CHMM => monk_male_human    // clone cleric animations into monk animations
    6010, 6510, CHFB, CHFM => monk_female_human 
  END
    
  ACTION_PHP_EACH cd_new_anims AS anims => entry BEGIN
        
    APPEND ~animate.ids~ ~0x%anims_1% %entry%~ UNLESS ~^0x%anims_1%~

    ACTION_IF enhanced_edition BEGIN

      COPY_EXISTING ~%anims_0%.ini~ ~override/%anims_1%.ini~
        REPLACE_TEXTUALLY ~^\(//[ %TAB%]+[A-Z]+[ %TAB%]+\)cleric_[a-z]+_[a-z]+[ %TAB%]*~ ~\1%entry%~
      
    END ELSE BEGIN
    
      COPY_EXISTING_REGEXP GLOB ~^%anims_2%\(.+\)\.bam$~ ~override/%anims_3%\1.bam~
                                ~^%anims_2%\(.+\)\.plt$~ ~override/%anims_3%\1.plt~
        IF_EXISTS // in case there are no plts
    
    END

  END  
  
END
    


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// max hp at level one                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @299900 DESIGNATED 2999 // maximum at l1
GROUP @4
LABEL ~cd_tweaks_max_hp_at_l1~

ACTION_CLEAR_ARRAY cd_hp_tables

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

END ELSE BEGIN // otherwise use fixed list

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN

    hpbarb   => 0 // barbarian hp table
    hpmonk   => 0 // monk hp table
    hpprs    => 0 // priest hp table
    hprog    => 0 // rogue hp table
    hpwar    => 0 // warrior hp table
    hpwiz    => 0 // mage hp table
  
  END

END

ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%tables%.2da~ THEN BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      READ_2DA_ENTRY 0 2 4 rolls
      PATCH_IF (rolls != 0) BEGIN
        READ_2DA_ENTRY 0 1 4 sides
        READ_2DA_ENTRY 0 3 4 modifier
        SET_2DA_ENTRY  0 2 4 0
        SET_2DA_ENTRY  0 3 4 ((rolls * sides) + modifier)
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Higher HP on Level Up                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// maximum                                          \\\\\
/////                                                  \\\\\

BEGIN @300001 DESIGNATED 3000 // maximum
GROUP @4
SUBCOMPONENT @300000 // Higher HP on Level Up
LABEL ~cd_tweaks_higher_hp_at_levelup_max~

ACTION_CLEAR_ARRAY cd_hp_tables

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

END ELSE BEGIN // otherwise use fixed list

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN

    hpbarb   => 0 // barbarian hp table
    hpmonk   => 0 // monk hp table
    hpprs    => 0 // priest hp table
    hprog    => 0 // rogue hp table
    hpwar    => 0 // warrior hp table
    hpwiz    => 0 // mage hp table
  
  END

END

ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%tables%.2da~ THEN BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      COUNT_2DA_ROWS 4 rows
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 4 rolls
        PATCH_IF (rolls != 0) BEGIN
          READ_2DA_ENTRY index 1 4 sides
          READ_2DA_ENTRY index 3 4 modifier
          SET_2DA_ENTRY  index 2 4 0
          SET_2DA_ENTRY  index 3 4 ((rolls * sides) + modifier)
        END
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END

/////                                                  \\\\\
///// nwn-style                                        \\\\\
/////                                                  \\\\\

BEGIN @300100 DESIGNATED 3001 // nwn-style
GROUP @4
SUBCOMPONENT @300000 // Higher HP on Level Up
LABEL ~cd_tweaks_higher_hp_at_levelup_nwn~

ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2999~ THEN BEGIN // max at level 1

  OUTER_SET start = 1

END ELSE BEGIN

  OUTER_SET start = 0

END

ACTION_CLEAR_ARRAY cd_hp_tables

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

END ELSE BEGIN // otherwise use fixed list

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN

    hpbarb   => 0 // barbarian hp table
    hpmonk   => 0 // monk hp table
    hpprs    => 0 // priest hp table
    hprog    => 0 // rogue hp table
    hpwar    => 0 // warrior hp table
    hpwiz    => 0 // mage hp table
  
  END

END

ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%tables%.2da~ BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      COUNT_2DA_ROWS 4 rows
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 4 rolls
        PATCH_IF (rolls != 0) BEGIN
          READ_2DA_ENTRY index 1 4 sides
          SET_2DA_ENTRY  index 1 4 2
          SET_2DA_ENTRY  index 2 4 (((rolls * sides) + 1) / 2) // +1 here is to force rounding errors in or favor, e.g. 1d7 becomes 4d2 instead of 3d2
        END
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END

/////                                                  \\\\\
///// average rolls                                    \\\\\
/////                                                  \\\\\

BEGIN @300200 DESIGNATED 3002 // average
GROUP @4
SUBCOMPONENT @300000 // Higher HP on Level Up
LABEL ~cd_tweaks_higher_hp_at_levelup_average~

ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2999~ THEN BEGIN // max at level 1

  OUTER_SET start = 1

END ELSE BEGIN

  OUTER_SET start = 0

END

ACTION_CLEAR_ARRAY cd_hp_tables

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

END ELSE BEGIN // otherwise use fixed list

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN

    hpbarb   => 0 // barbarian hp table
    hpmonk   => 0 // monk hp table
    hpprs    => 0 // priest hp table
    hprog    => 0 // rogue hp table
    hpwar    => 0 // warrior hp table
    hpwiz    => 0 // mage hp table
  
  END

END

ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%tables%.2da~ BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      SET round = 0
      COUNT_2DA_ROWS 4 rows
      FOR (index = start ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 4 rolls
        PATCH_IF (rolls != 0) BEGIN
          PATCH_IF round = 0 BEGIN SET round = 1 END ELSE BEGIN SET round = 0 END
          READ_2DA_ENTRY index 1 4 sides
          READ_2DA_ENTRY index 3 4 static
          SET_2DA_ENTRY  index 2 4 0 // no rolls
          SET_2DA_ENTRY  index 3 4 ((((rolls * sides) + rolls + round) / 2) + static) // round fudge factor is to get weidu to alternate rounding up and down for odd results
        END
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END

/////                                                  \\\\\
///// small die roll                                   \\\\\
/////                                                  \\\\\

/*

withdrawing as there's no way to really do this
yep, cam forgot that if a roll occurs, the static modifier is ignored by the engine

BEGIN @300300 DESIGNATED 3003 // small die
GROUP @4
SUBCOMPONENT @300000 // Higher HP on Level Up
LABEL ~cd_tweaks_higher_hp_at_levelup_small~

ACTION_IF MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2999~ THEN BEGIN // max at level 1

  OUTER_SET start = 1

END ELSE BEGIN

  OUTER_SET start = 0

END

ACTION_CLEAR_ARRAY cd_hp_tables

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

END ELSE BEGIN // otherwise use fixed list

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN

    hpbarb   => 0 // barbarian hp table
    hpmonk   => 0 // monk hp table
    hpprs    => 0 // priest hp table
    hprog    => 0 // rogue hp table
    hpwar    => 0 // warrior hp table
    hpwiz    => 0 // mage hp table
  
  END

END

ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%tables%.2da~ BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      COUNT_2DA_ROWS 4 rows
      FOR (index = start ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 4 rolls
        READ_2DA_ENTRY index 1 4 sides
        PATCH_IF ((rolls != 0) AND (sides > 4)) BEGIN // don't bother for no roll, or a d4 roll or smaller
          READ_2DA_ENTRY index 3 4 static
          SET_2DA_ENTRY  index 1 4 4 // d4
          SET_2DA_ENTRY  index 2 4 1 // one roll
          SET_2DA_ENTRY  index 3 4 ((sides - 4) + (((rolls - 1) * sides)) + static)
        END
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END
*/

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Extend HP rolls through level 20                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @300800 DESIGNATED 3008 // maximum
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~iwd2~ @25
LABEL ~cd_tweaks_extend_hp_rolls_to_l20~

ACTION_CLEAR_ARRAY cd_hp_tables

ACTION_IF FILE_EXISTS_IN_GAME hpclass.2da THEN BEGIN // if tobex/ee, look up and patch tables

  ACTION_DEFINE_ARRAY cd_hp_tables BEGIN END

  COPY_EXISTING ~hpclass.2da~ ~override~
    COUNT_2DA_ROWS 2 rows2
    FOR (index2 = 0 ; index2 < rows2 ; ++index2) BEGIN
      READ_2DA_ENTRY index2 1 2 table
      PATCH_IF FILE_EXISTS_IN_GAME ~%table%.2da~ BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN "%table%" => 0 END
      END
    END
    BUT_ONLY

END ELSE BEGIN // otherwise use fixed list

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_hp_tables BEGIN

    hpbarb   => 0 // barbarian hp table
    hpmonk   => 0 // monk hp table
    hpprs    => 0 // priest hp table
    hprog    => 0 // rogue hp table
    hpwar    => 0 // warrior hp table
    hpwiz    => 0 // mage hp table
  
  END

END

ACTION_PHP_EACH cd_hp_tables AS tables => foo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%tables%.2da~ BEGIN

    COPY_EXISTING ~%tables%.2da~ ~override~
      READ_2DA_ENTRY 8 1 4 sides
      READ_2DA_ENTRY 8 2 4 rolls
      READ_2DA_ENTRY 8 3 4 mod
      FOR (index = 9 ; index < 20 ; ++index) BEGIN
        SET_2DA_ENTRY  index 1 4 sides
        SET_2DA_ENTRY  index 2 4 rolls
        SET_2DA_ENTRY  index 3 4 mod
      END
      PRETTY_PRINT_2DA
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Maximum HP for NPCs                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// all npcs                                         \\\\\
/////                                                  \\\\\

BEGIN @301001 DESIGNATED 3010 // maximum hp for npcs
GROUP @4
SUBCOMPONENT @301000 // all npcs
LABEL ~cd_tweaks_maximum_hp_npcs_all~

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY_EXISTING_REGEXP ~^.*\.cre$~ ~override~
    SET "maxhp" = 0
    READ_SHORT 0x24 "curhp"
    READ_SHORT 0x26 "maxhp"
    READ_BYTE  0x8b "barbarian"
    READ_BYTE  0x8c "bard"
    READ_BYTE  0x8d "cleric"
    READ_BYTE  0x8e "druid"
    READ_BYTE  0x8f "fighter"
    READ_BYTE  0x90 "monk"
    READ_BYTE  0x91 "paladin"
    READ_BYTE  0x92 "ranger"
    READ_BYTE  0x93 "rogue"
    READ_BYTE  0x94 "sorceror"
    READ_BYTE  0x95 "wizard"
    SET "maxed_hp" = 12 * "%barbarian%" + 10 * ("%fighter%" + "%paladin%" + "%ranger%")
                   + 8 * ("%cleric%" + "%druid%" + "%monk%") + 6 * ("%bard%" + "%rogue%")
                   + 4 * ("%wizard%" + "%sorceror%")
    PATCH_IF ("%curhp%" < "%maxed_hp%") BEGIN
      WRITE_SHORT 0x24 "%maxed_hp%" * "%curhp%" / "%maxhp%"
      WRITE_SHORT 0x26 "%maxed_hp%"
    END
    BUT_ONLY

END ELSE BEGIN

  // load patch macro
  INCLUDE ~cdtweaks/lib/max_hp_creatures.tpa~
  
  PRINT @1
  COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
        READ_SHORT 0x24 "currenthp" ELSE 0
        READ_SHORT 0x26 "maxhp"     ELSE 0
        PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0)) BEGIN
          LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
        END
      END
    END
    BUT_ONLY

END

/////                                                  \\\\\
///// Maximum HP for Non-Party-Joinable NPCs           \\\\\
/////                                                  \\\\\

BEGIN @301100 DESIGNATED 3011 // maximum hp for npcs
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 iwd how totlm~ @25 // no joinables in iwd/iwd2; at least possible in iwdee/iwd-in-bg2
SUBCOMPONENT @301000 // all npcs
LABEL ~cd_tweaks_maximum_hp_npcs_nonjoinable~

// load patch macro
INCLUDE ~cdtweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
      READ_SHORT 0x24  "currenthp" ELSE 0
      READ_SHORT 0x26  "maxhp"     ELSE 0
      SET joinable = 0
      PATCH_IF (game_includes_pst = 1) BEGIN
        PATCH_IF ((~%SOURCE_RES%~ STRING_COMPARE_CASE ~annah~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~morte~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~nordom~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~dakkon~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~grace~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~ignus~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~vhail~ = 0)) BEGIN
          SET joinable = 1
        END
      END ELSE BEGIN
        READ_LONG  0x1cc "biography" ELSE 0
        PATCH_IF (("%biography%" < 2147483647) AND ("%biography%" > 0)) BEGIN // legit bio string
          SET joinable = 1
        END
      END
      PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0) AND (joinable = 0)) BEGIN
        LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
      END
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// Maximum HP for Party-Joinable NPCs               \\\\\
/////                                                  \\\\\

BEGIN @301200 DESIGNATED 3012 // maximum hp for npcs
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 iwd how totlm~ @25 // no joinables in iwd/iwd2; at least possible in iwdee/iwd-in-bg2
SUBCOMPONENT @301000 // all npcs
LABEL ~cd_tweaks_maximum_hp_npcs_joinable~

// load patch macro
INCLUDE ~cdtweaks/lib/max_hp_creatures.tpa~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
    PATCH_IF (NOT ~%SOURCE_RES%~ STRING_EQUAL_CASE ~riftcr04~) BEGIN // skip creatures meant to have lower hp
      READ_SHORT 0x24  "currenthp" ELSE 0
      READ_SHORT 0x26  "maxhp"     ELSE 0
      SET joinable = 0
      PATCH_IF (game_includes_pst = 1) BEGIN
        PATCH_IF ((~%SOURCE_RES%~ STRING_COMPARE_CASE ~annah~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~morte~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~nordom~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~dakkon~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~grace~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~ignus~ = 0) OR
                  (~%SOURCE_RES%~ STRING_COMPARE_CASE ~vhail~ = 0)) BEGIN
          SET joinable = 1
        END
      END ELSE BEGIN
        READ_LONG  0x1cc "biography" ELSE 0
        PATCH_IF (("%biography%" < 2147483647) AND ("%biography%" > 0)) BEGIN // legit bio string
          SET joinable = 1
        END
      END
      PATCH_IF (("%maxhp%" > 0) AND ("%currenthp%" > 0) AND (joinable = 1)) BEGIN
        LAUNCH_PATCH_MACRO ~max_hp_creatures~ // contains rest of patch
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Identify All Items                               \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @302000 DESIGNATED 3020
GROUP @4
LABEL ~cd_tweaks_id_all_items~

// SoS item needs to be un-ID'd for one quest - part 1
ACTION_IF FILE_EXISTS_IN_GAME ~cbltcnt2.itm~ THEN BEGIN

  COPY_EXISTING ~cbltcnt2.itm~ ~override~
    READ_SHORT 0x42 "sos_lore"
    BUT_ONLY

END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN // protects against invalid files
    WRITE_SHORT 0x42 0
  END
  BUT_ONLY

// SoS item needs to be un-ID'd for one quest - part 2
ACTION_IF FILE_EXISTS_IN_GAME ~cbltcnt2.itm~ THEN BEGIN

  COPY_EXISTING ~cbltcnt2.itm~ ~override~
    WRITE_SHORT 0x42 "%sos_lore%"
    BUT_ONLY

END

// i've learned that GAME_IS checks in a regexp copy are very slooooow, so...
OUTER_SET itm_off_are = 0x078 // are v1.0
OUTER_SET itm_off_cre = 0x2bc // cre v1.0
ACTION_IF game_is_pst BEGIN // pstee uses v1.0
  OUTER_SET itm_off_cre = 0x360 // creV1.2
END
ACTION_IF game_is_iwd2 BEGIN          // V2.2 cre, v9.1 are
  OUTER_SET itm_off_cre = 0x616 // cre v2.2
  OUTER_SET itm_off_are = 0x88  // are v9.1
END
ACTION_IF original_iwd BEGIN // V9.0
  OUTER_SET itm_off_cre = 0x324 // cre v9.0
END

COPY_EXISTING_REGEXP GLOB ~^.+\.[ac]re$~ ~override~
  PATCH_IF ("%SOURCE_FILE%" STRING_COMPARE_REGEXP "^.+\.cre$" = 0) BEGIN // if creature
    READ_LONG (itm_off_cre       ) itm_off ELSE 0
    READ_LONG (itm_off_cre + 0x04) itm_num ELSE 0
  END ELSE BEGIN
    READ_LONG  (itm_off_are       ) itm_off ELSE 0
    READ_SHORT (itm_off_are - 0x02) itm_num ELSE 0
  END
  FOR (index = 0 ; index < itm_num ; ++index) BEGIN
    WRITE_BYTE (itm_off + 0x10 + (0x14 * index)) (THIS BOR 0b00000001) // adds identified flag
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Easy Spell Learning                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// 100% learn spells                                \\\\\
/////                                                  \\\\\

BEGIN @303001 DESIGNATED 3030 // 100% learn spells
GROUP @4
SUBCOMPONENT @303000 // easy spell learning
LABEL ~cd_tweaks_easy_spell_learning_capped~

PRINT @1
COPY_EXISTING ~intmod.2da~ ~override~
  COUNT_2DA_ROWS ~3~ "rows"
  FOR (index = 3; index < rows ; index = index + 1) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  BUT_ONLY

ACTION_IF game_is_iwd THEN BEGIN // allows player to learn lev8-9 spells in non-HoW games

  INCLUDE ~cdtweaks/lib/learn_all.tpa~

END

/////                                                  \\\\\
///// 100% learn spells & no spell caps                \\\\\
/////                                                  \\\\\

BEGIN @303100 DESIGNATED 3031 // 100% learn spells, remove caps
GROUP @4
SUBCOMPONENT @303000 // easy spell learning
LABEL ~cd_tweaks_easy_spell_learning_no_cap~

PRINT @1
COPY_EXISTING ~intmod.2da~ ~override~
  COUNT_2DA_ROWS ~3~ "rows"
  FOR (index = 3; index < rows ; index = index + 1) BEGIN
    SET_2DA_ENTRY_LATER ~intmod~ index 1 ~150~
    SET_2DA_ENTRY_LATER ~intmod~ index 2 ~9~
    SET_2DA_ENTRY_LATER ~intmod~ index 3 ~99~
  END
  SET_2DA_ENTRIES_NOW ~intmod~ 1
  BUT_ONLY

ACTION_IF game_is_iwd THEN BEGIN // allows player to learn lev8-9 spells in non-HoW games

  INCLUDE ~cdtweaks/lib/learn_all.tpa~

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Misc Bags of Holding Bottomless             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @304000 DESIGNATED 3040
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc iwd pst~ @25
LABEL ~cd_tweaks_bottomless_bags~

OUTER_SET type_check = 5 
OUTER_SET stor_off = 0x22
ACTION_IF game_is_how OR game_is_totlm OR game_is_iwd2 BEGIN 
  OUTER_SET type_check = 4 
  OUTER_SET stor_off = 0x9c 
END

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x08 type ELSE 0
  PATCH_IF (type = type_check) BEGIN
    WRITE_SHORT stor_off 32767
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove fatigue from restoration spells           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @305000 DESIGNATED 3050
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~bg1 totsc tutu tutu_totsc iwd how totlm iwd2 pst pstee~ @25 // iwd2 has restoration, but it incurs no fatigue
LABEL ~cd_tweaks_restoration_no_fatigue~

// remove fatigue from restoration spells
ACTION_FOR_EACH spell IN sppr417 sppr713 spwish07 spwish46 ohbresto cdpr417 BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.spl~ BEGIN

    COPY_EXISTING ~%spell%.spl~ ~override~
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 93 END
      BUT_ONLY
      
  END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Remove "You Must Gather..."                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @306000 DESIGNATED 3060
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~iwdee iwd how totlm iwd2 pst pstee~ @25
LABEL ~cd_tweaks_remove_you_must_gather~

COPY  ~cdtweaks/wav/error01.wav~ ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Store-rep tweaks                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// discounts for low rep                            \\\\\
/////                                                  \\\\\

BEGIN @307000 DESIGNATED 3070 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks
LABEL ~cd_tweaks_store_rep_low_rep_discount~

COPY ~cdtweaks/2da/repmodst.2da~ ~override~

/////                                                  \\\\\
///// no rep effect, 100%                              \\\\\
/////                                                  \\\\\

BEGIN @307100 DESIGNATED 3071 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks
LABEL ~cd_tweaks_store_rep_no_rep_effect_100~

COPY ~cdtweaks/2da/repmodst_100.2da~ ~override/repmodst.2da~

/////                                                  \\\\\
///// no rep effect, 80%                               \\\\\
/////                                                  \\\\\

BEGIN @307200 DESIGNATED 3072 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks
LABEL ~cd_tweaks_store_rep_no_rep_effect_80~

COPY ~cdtweaks/2da/repmodst_080.2da~ ~override/repmodst.2da~

/////                                                  \\\\\
///// no rep effect, 60%                               \\\\\
/////                                                  \\\\\

BEGIN @307300 DESIGNATED 3073 // low-rep discounts
GROUP @4
SUBCOMPONENT @307001 // store-rep tweaks
LABEL ~cd_tweaks_store_rep_no_rep_effect_60~

COPY ~cdtweaks/2da/repmodst_060.2da~ ~override/repmodst.2da~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Increase Ammo Stacks                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Unlimited Ammo Stacks                            \\\\\
/////                                                  \\\\\

BEGIN @308000 DESIGNATED 3080
GROUP @4
SUBCOMPONENT @308001 // increase ammo stacks
LABEL ~cd_tweaks_increase_ammo_stacks_infinite~

OUTER_SET stack = 999
INCLUDE ~cdtweaks/lib/increase_ammo.tpa~

/////                                                  \\\\\
///// stack of 40                                      \\\\\
/////                                                  \\\\\

BEGIN @308100 DESIGNATED 3081
GROUP @4
SUBCOMPONENT @308001 // increase ammo stacks
LABEL ~cd_tweaks_increase_ammo_stacks_40~

OUTER_SET stack = 40
INCLUDE ~cdtweaks/lib/increase_ammo.tpa~

/////                                                  \\\\\
///// stack of 80                                      \\\\\
/////                                                  \\\\\

BEGIN @308200 DESIGNATED 3082
GROUP @4
SUBCOMPONENT @308001 // increase ammo stacks
LABEL ~cd_tweaks_increase_ammo_stacks_80~

OUTER_SET stack = 80
INCLUDE ~cdtweaks/lib/increase_ammo.tpa~

/////                                                  \\\\\
///// stack of 120                                     \\\\\
/////                                                  \\\\\

BEGIN @308300 DESIGNATED 3083
GROUP @4
SUBCOMPONENT @308001 // increase ammo stacks
LABEL ~cd_tweaks_increase_ammo_stacks_120~

OUTER_SET stack = 120
INCLUDE ~cdtweaks/lib/increase_ammo.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// increase Gem and Jewelry Stacking                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Unlimited Gem and Jewelry Stacking               \\\\\
/////                                                  \\\\\

BEGIN @309000 DESIGNATED 3090
GROUP @4
SUBCOMPONENT @309001 // increase misc stacks
LABEL ~cd_tweaks_increase_gem_stacks_infinite~

OUTER_SET stack = 999
INCLUDE ~cdtweaks/lib/increase_gem.tpa~

/////                                                  \\\\\
///// stack of 40                                      \\\\\
/////                                                  \\\\\

BEGIN @308100 DESIGNATED 3091
GROUP @4
SUBCOMPONENT @309001 // increase misc stacks
LABEL ~cd_tweaks_increase_gem_stacks_40~

OUTER_SET stack = 40
INCLUDE ~cdtweaks/lib/increase_gem.tpa~

/////                                                  \\\\\
///// stack of 80                                      \\\\\
/////                                                  \\\\\

BEGIN @308200 DESIGNATED 3092
GROUP @4
SUBCOMPONENT @309001 // increase misc stacks
LABEL ~cd_tweaks_increase_gem_stacks_80~

OUTER_SET stack = 80
INCLUDE ~cdtweaks/lib/increase_gem.tpa~

/////                                                  \\\\\
///// stack of 120                                     \\\\\
/////                                                  \\\\\

BEGIN @308300 DESIGNATED 3093
GROUP @4
SUBCOMPONENT @309001 // increase misc stacks
LABEL ~cd_tweaks_increase_gem_stacks_120~

OUTER_SET stack = 120
INCLUDE ~cdtweaks/lib/increase_gem.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// increased Potion Stacking                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Unlimited Potion Stacking                        \\\\\
/////                                                  \\\\\

BEGIN @310000 DESIGNATED 3100
GROUP @4
SUBCOMPONENT @310001 // increase potion stacks
LABEL ~cd_tweaks_increase_potion_stacks_infinite~

OUTER_SET stack = 999
INCLUDE ~cdtweaks/lib/increase_potion.tpa~

/////                                                  \\\\\
///// stack of 40                                      \\\\\
/////                                                  \\\\\

BEGIN @308100 DESIGNATED 3101
GROUP @4
SUBCOMPONENT @310001 // increase potion stacks
LABEL ~cd_tweaks_increase_potion_stacks_40~

OUTER_SET stack = 40
INCLUDE ~cdtweaks/lib/increase_potion.tpa~

/////                                                  \\\\\
///// stack of 80                                      \\\\\
/////                                                  \\\\\

BEGIN @308200 DESIGNATED 3102
GROUP @4
SUBCOMPONENT @310001 // increase potion stacks
LABEL ~cd_tweaks_increase_potion_stacks_80~

OUTER_SET stack = 80
INCLUDE ~cdtweaks/lib/increase_potion.tpa~

/////                                                  \\\\\
///// stack of 120                                     \\\\\
/////                                                  \\\\\

BEGIN @308300 DESIGNATED 3103
GROUP @4
SUBCOMPONENT @310001 // increase potion stacks
LABEL ~cd_tweaks_increase_potion_stacks_120~

OUTER_SET stack = 120
INCLUDE ~cdtweaks/lib/increase_potion.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// increased Scroll Stacking                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Unlimited Scroll Stacking                        \\\\\
/////                                                  \\\\\

BEGIN @311000 DESIGNATED 3110
GROUP @4
SUBCOMPONENT @311001 // increase scroll stacks
LABEL ~cd_tweaks_increase_scroll_stacks_infinite~

OUTER_SET stack = 999
INCLUDE ~cdtweaks/lib/increase_scroll.tpa~

/////                                                  \\\\\
///// stack of 40                                      \\\\\
/////                                                  \\\\\

BEGIN @308100 DESIGNATED 3111
GROUP @4
SUBCOMPONENT @311001 // increase scroll stacks
LABEL ~cd_tweaks_increase_scroll_stacks_40~

OUTER_SET stack = 40
INCLUDE ~cdtweaks/lib/increase_scroll.tpa~

/////                                                  \\\\\
///// stack of 80                                      \\\\\
/////                                                  \\\\\

BEGIN @308200 DESIGNATED 3112
GROUP @4
SUBCOMPONENT @311001 // increase scroll stacks
LABEL ~cd_tweaks_increase_scroll_stacks_80~

OUTER_SET stack = 80
INCLUDE ~cdtweaks/lib/increase_scroll.tpa~

/////                                                  \\\\\
///// stack of 120                                     \\\\\
/////                                                  \\\\\

BEGIN @308300 DESIGNATED 3113
GROUP @4
SUBCOMPONENT @311001 // increase scroll stacks
LABEL ~cd_tweaks_increase_scroll_stacks_120~

OUTER_SET stack = 120
INCLUDE ~cdtweaks/lib/increase_scroll.tpa~


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Unlimited Stacking for Ankheg Carapaces, Winter  \\\\\
///// Wolf Pelts, and Wyvern Heads (borrowed from SCS) \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @500000 DESIGNATED 3115 DEPRECATED @18 // already covered by gem/jewelry 
LABEL ~cd_tweaks_increase_pelt_stacks_infnite~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Happy patch                                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// never angry                                      \\\\\
/////                                                  \\\\\

BEGIN @312001 DESIGNATED 3120
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob iwdee iwd-in-bg2 ca~ @25
SUBCOMPONENT @312000
LABEL ~cd_tweaks_happy_patch_never_angry~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  REPLACE_TEXTUALLY ~-[0-9]+~ ~0~
  PRETTY_PRINT_2DA
  BUT_ONLY
  
// bg1 npc can let shar-teel leave anyway
ACTION_IF game_includes_bg1 THEN BEGIN
  INCLUDE ~cdtweaks/lib/happy_sharteel.tpa~
END

/////                                                  \\\\\
///// angry but never leave                            \\\\\
/////                                                  \\\\\

BEGIN @312100 DESIGNATED 3121
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob iwdee iwd-in-bg2 ca~ @25
SUBCOMPONENT @312000
LABEL ~cd_tweaks_happy_patch_angry_never_leave~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  REPLACE_EVALUATE ~-\([0-9]+\)~ BEGIN
    PATCH_IF ("%MATCH1%" > 160) BEGIN SET new = 160 END ELSE BEGIN SET new = "%MATCH1%" END
  END ~-%new%~
  PRETTY_PRINT_2DA
  BUT_ONLY
  
// bg1 npc can let shar-teel leave anyway
ACTION_IF game_includes_bg1 THEN BEGIN
  INCLUDE ~cdtweaks/lib/happy_sharteel.tpa~
END

/////                                                  \\\\\
///// always neutral                                   \\\\\
/////                                                  \\\\\

BEGIN @312200 DESIGNATED 3122
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob iwdee iwd-in-bg2 ca~ @25
SUBCOMPONENT @312000
LABEL ~cd_tweaks_happy_patch_always_neutral~

// stop rep complaints
COPY_EXISTING ~happy.2da~ ~override~
  FOR (col = 1 ; col < 4 ; col = col + 1) BEGIN
    FOR (row = 0 ; row < 20 ; row = row + 1) BEGIN
      SET_2DA_ENTRY %row% %col% 4 "0"
    END
  END
  PRETTY_PRINT_2DA
  BUT_ONLY
  
// bg1 npc can let shar-teel leave anyway
ACTION_IF game_includes_bg1 THEN BEGIN
  INCLUDE ~cdtweaks/lib/happy_sharteel.tpa~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// NPCs don't fight                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312300 DESIGNATED 3123
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_npcs_never_fight~

// remove NPC conflicts
INCLUDE ~cdtweaks/lib/happy.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stop Haer'Dalis-Aerie romance from starting      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312400 DESIGNATED 3124
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgt soa tob bg2ee eet~ @25 // non-Tutu
LABEL ~cd_tweaks_stop_hd_aerie_romance~

COPY_EXISTING ~haerdali.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Global("RomanceConflict","GLOBAL",0)~ ~False()~
  END
  BUT_ONLY
EXTEND_TOP ~haerdali.bcs~ ~cdtweaks/baf/haerdali.baf~ 


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Happy neutrals                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @312500 DESIGNATED 3125 // happy neutrals
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob iwdee iwd-in-bg2 ca~ @25
LABEL ~cd_tweaks_happy_neutrals~

COPY_EXISTING ~happy.2da~ ~override~
  FOR (row = 6; row < 12; row = row + 1) BEGIN
    SET_2DA_ENTRY "%row%" 2 4 ~80~
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No traps or locks                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @313000 DESIGNATED 3130
GROUP @4
LABEL ~cd_tweaks_no_traps_or_locks~

ACTION_IF game_is_iwd2 BEGIN OUTER_SET off = 0x10 END ELSE BEGIN OUTER_SET off = 0 END // outside of this, patch works for v1.0 and v9.1 areas

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
  READ_SHORT (0x5a + off) trig_num
  READ_LONG  (0x5c + off) trig_off
  READ_SHORT (0x74 + off) cont_num
  READ_LONG  (0x70 + off) cont_off
  READ_LONG  (0xa4 + off) door_num
  READ_LONG  (0xa8 + off) door_off
  FOR (index = 0 ; index < trig_num ; ++index) BEGIN // cycle through triggers
    READ_SHORT (trig_off + 0x6a + (0xc4 * index)) trap_diff
    PATCH_IF ((trap_diff > 0) AND (trap_diff < 100)) BEGIN
      WRITE_SHORT (trig_off + 0x6c + (0xc4 * index)) 0 // is not a trap
    END
  END
  FOR (index2 = 0 ; index2 < cont_num ; ++index2) BEGIN // cycle through containers
    READ_ASCII  (cont_off + 0x78 + (index2 * 0xc0)) key // key y
    PATCH_IF (("%key%" STRING_COMPARE_CASE "" = 0) OR ("%key%" STRING_COMPARE_CASE "None" = 0)) BEGIN
      READ_SHORT  (cont_off + 0x26 + (index2 * 0xc0)) lock_diff // lock difficulty
      PATCH_IF ((lock_diff != 0) AND (lock_diff != 100)) BEGIN
	      WRITE_SHORT (cont_off + 0x26 + (index2 * 0xc0)) 0 // lock difficulty
      END
    END
    READ_SHORT  (cont_off + 0x2c + (index2 * 0xc0)) trap_diff // trap difficulty
    PATCH_IF ((trap_diff != 0) AND (trap_diff != 100)) BEGIN
      WRITE_SHORT (cont_off + 0x30 + (index2 * 0xc0)) 0 // is not a trap
    END
  END
  FOR (index3 = 0 ; index3 < door_num ; ++index3) BEGIN // cycle through doors
    READ_LONG (door_off + 0x28 + (index3 * 0xc8)) flags // door flags
    PATCH_IF ((flags & BIT8) = BIT8) BEGIN  // // if flagged as secret door
      WRITE_LONG (door_off + 0x88 + (index3 * 0xc8)) 0         // detect diff (secret doors)
      WRITE_LONG (door_off + 0x28 + (index3 * 0xc8)) (THIS BAND `BIT8) // remove secret flag
    END  
    READ_SHORT (door_off + 0x6e + (index3 * 0xc8)) trap_diff // detect diff
    PATCH_IF ((trap_diff != 0) AND (trap_diff != 100)) BEGIN
      WRITE_SHORT (door_off + 0x70 + (index3 * 0xc8)) 0 // is not a trap
    END
    READ_ASCII (door_off + 0x78 + (index3 * 0xc8)) key // key 
    PATCH_IF (("%key%" STRING_COMPARE_CASE "" = 0) OR ("%key%" STRING_COMPARE_CASE "None" = 0)) BEGIN
      READ_LONG  (door_off + 0x8c + (index3 * 0xc8)) lock_diff // lock diff
      PATCH_IF ((lock_diff != 0) AND (lock_diff != 100)) BEGIN
        WRITE_LONG  (door_off + 0x8c + (index3 * 0xc8)) 0 // lock diff  
      END  
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Faster chapter 1&2 cutscenes and dreams          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// original EoU stuff                               \\\\\
/////                                                  \\\\\

BEGIN @314001 DESIGNATED 3140 // original EoU
GROUP @4
SUBCOMPONENT @314000 // faster chap 1&2 cutscenes
REQUIRE_PREDICATE GAME_IS ~soa tob bg2ee bgt eet~ @25 // non-Tutu, non-BGT
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~mrimrin1.itm~ @24 // imoen romance compatibility code
LABEL ~cd_tweaks_faster_ch1_cutscenes_eou~

OUTER_SET silly = 1
INCLUDE ~cdtweaks/lib/faster_start.tpa~

/////                                                  \\\\\
///// non-silly version                                \\\\\
/////                                                  \\\\\

BEGIN @314100 DESIGNATED 3141 // original EoU
GROUP @4
SUBCOMPONENT @314000 // faster chap 1&2 cutscenes
REQUIRE_PREDICATE GAME_IS ~soa tob bg2ee~ @25 // non-Tutu, non-BGT
REQUIRE_PREDICATE NOT FILE_EXISTS_IN_GAME ~mrimrin1.itm~ @24 // imoen romance compatibility code
LABEL ~cd_tweaks_faster_ch1_cutscenes_nonsilly~

//OUTER_SPRINT silly "_nonsilly"
OUTER_SET silly = 0
INCLUDE ~cdtweaks/lib/faster_start.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Keep Drizzt's Loot, Disable Malchor Harpell      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @316000 DESIGNATED 3160
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee ca~ @25
LABEL ~cd_tweaks_disable_malchor~

COPY_EXISTING ~baldur.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!Exists("c6harp")~ ~False()~
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// No Drow Avatars On Party In Underdark            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @317000 DESIGNATED 3170
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~bgee bg1 totsc tutu tutu_totsc iwdee iwd how totlm iwd-in-bg2 iwd2 pst pstee ca~ @25
LABEL ~cd_tweaks_no_drow_avatars_in_underdark~

COPY_EXISTING ~ar2102.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~ActionOverride(Player[1-6],ApplySpell(Myself,DROW_CHANGE))~ ~~
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Disable romances                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @317500 DESIGNATED 3175
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgt soa tob bg2ee eet~ @25
LABEL ~cd_tweaks_disable_romances~

ACTION_FOR_EACH script IN baldur baldur25 BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%script%.bcs~ THEN BEGIN
  
    EXTEND_TOP ~%script%.bcs~ ~cdtweaks/baf/romance_disable.baf~

  END 

END    

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Accelerate/decelerate romances                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @317600 DESIGNATED 3176
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bgt soa tob bg2ee eet~ @25
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~3175~ @16
LABEL ~cd_tweaks_change_romance_speed~

ACTION_IF (romance_speed_use_config_values = 1) THEN BEGIN // if set in the config file...

  ACTION_IF ((!IS_AN_INT romance_speed_factor) OR (romance_speed_factor < 20) OR (romance_speed_factor > 500)) THEN BEGIN // but is not a valid integer between 20-500
    
    OUTER_SET prompt = 1 // prompt anyway
  
  END ELSE BEGIN // if it is an integer between 20-500
    
    OUTER_SET prompt = 0 // then skip prompt and use the value
  
  END

END ELSE BEGIN // if not set in the config file

  OUTER_SET prompt = 1 

END
  
ACTION_IF prompt BEGIN
  
  OUTER_WHILE ((!IS_AN_INT romance_speed_factor) OR (romance_speed_factor < 20) OR (romance_speed_factor > 500)) BEGIN // stay here until we have a 20-500
    PRINT @317601
    ACTION_READLN romance_speed_factor
  END

END  

ACTION_IF game_includes_bg1 BEGIN 

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances_timers BEGIN
    //Game, bcs, base dlg, banter dlg, join dlg => var // Notes
    bg, ~dorn%eet_var%~,   ~dorn%eet_var%~,   ~bdorn%eet_var%~,   ~dornj%eet_var%~   => ~dorn_romance[12]~          // dorn bg
    bg, ~neera%eet_var%~,  ~neera%eet_var%~,  ~bneera%eet_var%~,  ~neeraj%eet_var%~  => ~neeraromancetimer~         // neera bg
    bg, ~rasaad%eet_var%~, ~rasaad%eet_var%~, ~brasaad%eet_var%~, ~rasaadj%eet_var%~ => ~rasaad_romance_timer[123]~ // rasaad bg

  END
  
END 

ACTION_IF game_includes_soa BEGIN 

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances_timers BEGIN
    //Game, bcs, base dlg, banter dlg, join dlg => var // Notes
    bg2, aerie,   aerie,   baerie,  aeriej   => aerieromance         // aerie soa
    bg2, anomen,  anomen,  banomen, anomenj  => anomenromance        // anomen soa
    bg2, dorn,    dorn,    bdorn,   dornj    => dornlovetalkstimer   // dorn soa
    bg2, hexxat,  hexxat,  bhexxat, hexxatj  => hexxatlovetalkstimer // hexxat soa
    bg2, jaheira, jaheira, bjaheir, jaheiraj => jaheiraromance       // jaheira soa
    bg2, neera,   neera,   bneera,  neeraj   => neeralovetalkstimer  // neera soa
    bg2, rasaad,  rasaad,  brasaad, rasaadj  => rasaadlovetalkstimer // rasaad soa
    bg2, viconia, viconi,  bviconi, viconij  => viconiaromance       // viconia soa

  END
  
END 

ACTION_IF game_includes_tob BEGIN 

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances_timers BEGIN
    //Game, bcs, base dlg, banter dlg, join dlg => var // Notes
    tob, aeri25,  aerie25a, baerie25, aerie25j => aerieromance            // aerie tob
    tob, anom25,  anome25a, banome25, anome25j => anomenromance           // anomen tob
    tob, dorn25,  dorn25a,  bdorn25,  dorn25j  => dorntoblovetalkstimer   // dorn tob
    tob, hexxa25, hexxa25a, bhexxa25, hexxa25j => hexxattoblovetalkstimer // hexxat tob
    tob, jahe25,  jahei25a, bjahei25, jahei25j => jaheiraromance          // jaheira tob
    tob, neer25,  neera25a, bneera25, neera25j => neeratoblovetalkstimer  // neera tob
    tob, rasa25,  rasaa25a, brasaa25, rasaa25j => rasaadtoblovetalkstimer // rasaad tob
    tob, vico25,  vicon25a, bvicon25, vicon25j => expviconiaromance       // viconia tob

  END
  
END 

ACTION_PHP_EACH cd_romances_timers AS params => variable BEGIN
  
  ACTION_FOR_EACH file IN ~%params_1%.bcs~ ~%params_2%.dlg~ ~%params_3%.dlg~ ~%params_4%.dlg~ BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ THEN BEGIN
    
      COPY_EXISTING ~%file%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_EVALUATE CASE_INSENSITIVE ~\(RealSetGlobalTimer\|SetGlobalTimer\)("\(%variable%\)","\([^"]+\)",\([^)]+\))~ BEGIN
            PATCH_IF IS_AN_INT MATCH4 BEGIN
              SET time = MATCH4
            END ELSE BEGIN
              SET time = IDS_OF_SYMBOL (~GTIMES~ ~%MATCH4%~) // should convert stuff like EIGHT_HOURS to numeric value
            END
            SET time = ((time * romance_speed_factor) / 100) 
          END ~%MATCH1%("%MATCH2%","%MATCH3%",%time%)~  
        END
        BUT_ONLY
      
    END
    
  END

END  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Romance Cheats                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @318300 DESIGNATED 3183
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgt soa tob bg2ee eet~ @25
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~3175~ @16
LABEL ~cd_tweaks_romance_cheats~

// make array for standard bg2 romances
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances BEGIN
  Aerie   => aeri25
  Anomen  => anom25
  Jaheira => jahe25
  Viconia => vico25
END

ACTION_IF enhanced_edition THEN BEGIN

  // add bg2ee npcs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_romances BEGIN
    Dorn    => dorn25
    Hexxat  => hexxa25
    Neera   => neer25
    Rasaad  => rasa25
  END

END

// fixes for Anomen's join dialogue
COMPILE ~cdtweaks/dlg/ano_rom_fix.d~

ACTION_IF ((romance_use_config_values = 1) AND NOT // sanity check; if no changes specified in config, do normal prompting
          ((remove_romance_racial_requirements != 1) AND 
           (remove_romance_gender_requirements != 1) AND 
           (romance_multiple != 1) AND
           ((romance_nothing_kills != 1) OR (romance_multiple != 1)) AND
           ((romance_starts_in_ToB != 1) OR (!game_is_soa))))  THEN BEGIN

  // print mini-summary for debug
  PRINT @26
  PRINT @318309
  ACTION_IF remove_romance_racial_requirements = 1 THEN BEGIN
    PRINT @318302
  END
  ACTION_IF remove_romance_gender_requirements = 1 THEN BEGIN
    PRINT @318304
  END
  ACTION_IF romance_multiple = 1 THEN BEGIN
    PRINT @318306
    ACTION_IF romance_nothing_kills = 1 THEN BEGIN
      PRINT @318308
    END
  END
  ACTION_IF NOT game_is_soa THEN BEGIN
    ACTION_IF romance_starts_in_ToB = 1 THEN BEGIN
      PRINT @318312
    END
  END

END ELSE BEGIN

  // questions to set options
  OUTER_SET romance_proceed = 0
  OUTER_SET remove_romance_racial_requirements = 0
  OUTER_SET remove_romance_gender_requirements = 0
  OUTER_SET romance_multiple = 0
  OUTER_SET romance_nothing_kills = 0
  OUTER_SET romance_starts_in_ToB = 0
  
  OUTER_WHILE (romance_proceed != 1) BEGIN
    OUTER_WHILE ((!IS_AN_INT remove_romance_racial_requirements) OR ((remove_romance_racial_requirements != 1) AND (remove_romance_racial_requirements != 2))) BEGIN // stay here until we have a 1 or a 2
      PRINT @318301
      ACTION_READLN remove_romance_racial_requirements
    END
    OUTER_WHILE ((!IS_AN_INT remove_romance_gender_requirements) OR ((remove_romance_gender_requirements != 1) AND (remove_romance_gender_requirements != 2))) BEGIN // stay here until we have a 1 or a 2
      PRINT @318303
      ACTION_READLN remove_romance_gender_requirements
    END
    OUTER_WHILE ((!IS_AN_INT romance_multiple) OR ((romance_multiple != 1) AND (romance_multiple != 2))) BEGIN // stay here until we have a 1 or a 2
      PRINT @318305
      ACTION_READLN romance_multiple
    END
    ACTION_IF (romance_multiple = 1) THEN BEGIN // only offer no kill if multi-romance selected
      OUTER_WHILE ((!IS_AN_INT romance_nothing_kills) OR ((romance_nothing_kills != 1) AND (romance_nothing_kills != 2))) BEGIN
        PRINT @318307
        ACTION_READLN romance_nothing_kills
      END
    END
    ACTION_IF NOT game_is_soa THEN BEGIN // tob
      OUTER_WHILE ((!IS_AN_INT romance_starts_in_ToB) OR ((romance_starts_in_ToB != 1) AND (romance_starts_in_ToB != 2))) BEGIN
        PRINT @318311
        ACTION_READLN romance_starts_in_ToB
      END
    END
    // sanity check, make sure some change has been selected
    ACTION_IF ((remove_romance_racial_requirements != 1) AND (remove_romance_gender_requirements != 1) AND (romance_multiple != 1) AND (romance_nothing_kills != 1) AND (romance_starts_in_ToB != 1)) BEGIN
      OUTER_SET romance_proceed = 2
      PRINT @321014
    END ELSE BEGIN
      // print summary of options before proceeding
      PRINT @318309
      ACTION_IF remove_romance_racial_requirements = 1 THEN BEGIN
        PRINT @318302
      END
      ACTION_IF remove_romance_gender_requirements = 1 THEN BEGIN
        PRINT @318304
      END
      ACTION_IF romance_multiple = 1 THEN BEGIN
        PRINT @318306
      END
      ACTION_IF romance_nothing_kills = 1 THEN BEGIN
        PRINT @318308
      END
      ACTION_IF romance_starts_in_ToB = 1 THEN BEGIN
        PRINT @318312
      END
      OUTER_WHILE ((!IS_AN_INT romance_proceed) OR ((romance_proceed != 1) AND (romance_proceed != 2))) BEGIN
        PRINT @318310
        ACTION_READLN romance_proceed
      END
    END
    ACTION_IF (romance_proceed = 2) THEN BEGIN
      OUTER_SET romance_proceed = 0
      OUTER_SET remove_romance_racial_requirements = 0
      OUTER_SET remove_romance_gender_requirements = 0
      OUTER_SET romance_multiple = 0
      OUTER_SET romance_nothing_kills = 0
      OUTER_SET romance_starts_in_ToB = 0
    END
  END

END

// take care of tob_start
// this will also take care of any romance requirement adjustments on the ToB side while we're here
ACTION_IF romance_starts_in_ToB AND game_includes_tob THEN BEGIN

  EXTEND_TOP ~aeri25.bcs~ ~cdtweaks/baf/aeri25_start_tob.baf~

  ACTION_IF enhanced_edition THEN BEGIN
    COMPILE ~cdtweaks/dlg/rom_start_tob_bg2ee.d~ // order is different
  END ELSE BEGIN
    COMPILE ~cdtweaks/dlg/rom_start_tob.d~
  END

  ACTION_IF (remove_romance_gender_requirements != 1) THEN BEGIN // if gender reqs intact

    COMPILE ~cdtweaks/dlg/rom_start_tob_gender.d~

  END

  ACTION_IF (remove_romance_racial_requirements != 1) THEN BEGIN // if  race reqs intact

    COMPILE ~cdtweaks/dlg/rom_start_tob_race.d~

  END

  ACTION_IF (romance_multiple != 1) THEN BEGIN // if no multi allowed

    COMPILE ~cdtweaks/dlg/rom_start_tob_multi.d~

  END

END

ACTION_IF ((remove_romance_racial_requirements = 1) OR (remove_romance_gender_requirements = 1)) BEGIN

  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN
    
    EXTEND_TOP ~%npc%.bcs~ ~cdtweaks/baf/%npc%_rom_reqs.baf~
  
  END
      
  ACTION_IF (remove_romance_racial_requirements != 1) BEGIN // if racial requirements intact (ee romances have no racial reqs)
  
    COPY_EXISTING ~aerie.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(5) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING) Race(Player1,GNOME)~
      END
      BUT_ONLY
  
    COPY_EXISTING ~anomen.bcs~  ~override~
                  ~jaheira.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,ELF) Race(Player1,HALFLING)~
      END
      BUT_ONLY
  
    COPY_EXISTING ~viconia.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
        ~\1 OR(4) Race(Player1,HUMAN) Race(Player1,HALF_ELF) Race(Player1,HALFLING) Race(Player1,HALFORC)~
      END
      BUT_ONLY
  
  END
      
  ACTION_IF (remove_romance_gender_requirements != 1) BEGIN // if gender requirements intact
  
    ACTION_FOR_EACH npc IN aerie jaheira neera viconia BEGIN
  
      ACTION_IF FILE_EXISTS_IN_GAME ~%npc%.bcs~ THEN BEGIN
  
        COPY_EXISTING ~%npc%.bcs~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
            ~\1 Gender(Player1,MALE)~
          END
          BUT_ONLY
      
      END
    
    END
  
    ACTION_FOR_EACH npc IN anomen hexxat rasaad BEGIN
  
      ACTION_IF FILE_EXISTS_IN_GAME ~%npc%.bcs~ THEN BEGIN
  
        COPY_EXISTING ~%npc%.bcs~ ~override~
          DECOMPILE_AND_PATCH BEGIN
            REPLACE_TEXTUALLY ~\(Global("CDRomanceFoo","LOCALS",0)\)~
            ~\1 Gender(Player1,FEMALE)~
          END
          BUT_ONLY
      
      END
    
    END
  
  END
  
  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN // cleanup dummy var
    
    COPY_EXISTING ~%npc%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("CDRomanceFoo","LOCALS",0)~ ~~
      END
      BUT_ONLY
  
  END

END

// allow multiple romances
ACTION_IF (romance_multiple = 1) THEN BEGIN

  ACTION_PHP_EACH cd_romances AS npc => tob BEGIN
    
    COPY_EXISTING ~%npc%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        PATCH_PHP_EACH cd_romances AS npc2 => tob2 BEGIN
          PATCH_IF ("%npc%" STRING_COMPARE_CASE "%npc2%") BEGIN // don't self-match
            REPLACE_TEXTUALLY ~\b\(Global("%npc2%RomanceActive","GLOBAL",[12])\)~ ~False()~
          END
        END
        PATCH_IF (romance_nothing_kills = 1) BEGIN // nothing kills romance option
          APPEND_FILE ~cdtweaks/baf/%npc%_rom3.baf~
        END
      END
      BUT_ONLY

    ACTION_IF game_includes_tob THEN BEGIN
    
      COPY_EXISTING ~%tob%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          PATCH_PHP_EACH cd_romances AS npc2 => tob2 BEGIN
            PATCH_IF ("%npc%" STRING_COMPARE_CASE "%npc2%") BEGIN // don't self-match
              REPLACE_TEXTUALLY ~\b\(Global("%npc2%RomanceActive","GLOBAL",[12])\)~ ~False()~
            END
          END
          REPLACE_TEXTUALLY ~GlobalGT("WraithPunish","GLOBAL",0)~ ~False()~
          APPEND_FILE ~cdtweaks/baf/%tob%_rom2.baf~
          PATCH_IF (romance_nothing_kills = 1) BEGIN // nothing kills romance option
            APPEND_FILE ~cdtweaks/baf/%tob%_rom3.baf~
          END
        END
        BUT_ONLY

    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// rest anywhere                                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @319000 DESIGNATED 3190
GROUP @4
LABEL ~cd_tweaks_rest_anywhere~

ACTION_IF game_is_pst BEGIN

  COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
    WRITE_BYTE 0x14 (THIS | BIT1) // this bit is called "Tutorial" in NI, but is really the can_not_sleep one.
    BUT_ONLY

END ELSE BEGIN

  ACTION_IF game_is_pstee BEGIN

    COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
      WRITE_LONG 0x14 ((THIS & `BIT7) & `BIT8) // removes can't rest, too dangerous to rest flags
      BUT_ONLY
  
  END ELSE BEGIN

    COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
      WRITE_BYTE 0x48 (THIS | BIT7)
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Disable Non-Hostile Rest Spawns                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @319100 DESIGNATED 3191
LABEL ~cd_tweaks_rest_spawns_alter_nonhostile_disable~
GROUP @4

OUTER_SET factor = 0 // 0% chance (disable)
OUTER_SET adjust_hostile = 0 // check neutral spawns
INCLUDE ~cdtweaks/lib/rest_spawns.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Alter Hostile Rest Spawns                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Disable completely                               \\\\\
/////                                                  \\\\\

BEGIN @319401 DESIGNATED 3194
GROUP @4
SUBCOMPONENT @319400 // Alter Hostile Rest Spawns
LABEL ~cd_tweaks_rest_spawns_alter_hostile_disable~

OUTER_SET factor = 0 // 0% chance (disable)
OUTER_SET adjust_hostile = 1 // adjust hostile spawns
INCLUDE ~cdtweaks/lib/rest_spawns.tpa~

/////                                                  \\\\\
///// Decrease frequency by 50%                        \\\\\
/////                                                  \\\\\

BEGIN @319500 DESIGNATED 3195
GROUP @4
SUBCOMPONENT @319400 // Alter Hostile Rest Spawns
LABEL ~cd_tweaks_rest_spawns_alter_hostile_50~

OUTER_SET factor = 50 // 50% chance
OUTER_SET adjust_hostile = 1 // adjust hostile spawns
INCLUDE ~cdtweaks/lib/rest_spawns.tpa~

/////                                                  \\\\\
///// Increase frequency by 50%                        \\\\\
/////                                                  \\\\\

BEGIN @319600 DESIGNATED 3196
GROUP @4
SUBCOMPONENT @319400 // Alter Hostile Rest Spawns
LABEL ~cd_tweaks_rest_spawns_alter_hostile_150~

OUTER_SET factor = 150 // 150% chance
OUTER_SET adjust_hostile = 1 // adjust hostile spawns
INCLUDE ~cdtweaks/lib/rest_spawns.tpa~

/////                                                  \\\\\
///// Double frequency                                 \\\\\
/////                                                  \\\\\

BEGIN @319700 DESIGNATED 3197
GROUP @4
SUBCOMPONENT @319400 // Alter Hostile Rest Spawns
LABEL ~cd_tweaks_rest_spawns_alter_hostile_200~

OUTER_SET factor = 200 // 200% chance
OUTER_SET adjust_hostile = 1 // adjust hostile spawns
INCLUDE ~cdtweaks/lib/rest_spawns.tpa~

/////                                                  \\\\\
///// Quadruple frequency                              \\\\\
/////                                                  \\\\\

BEGIN @319800 DESIGNATED 3198
GROUP @4
SUBCOMPONENT @319400 // Alter Hostile Rest Spawns
LABEL ~cd_tweaks_rest_spawns_alter_hostile_400~

OUTER_SET factor = 400 // 400% chance
OUTER_SET adjust_hostile = 1 // adjust hostile spawns
INCLUDE ~cdtweaks/lib/rest_spawns.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// sellable items                                             \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @320000 DESIGNATED 3200
GROUP @4
LABEL ~cd_tweaks_sellable_items~

PRINT @1
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c "type"  ELSE 0 // 17 - mace, 18 - sling, 26 - staff, 37 - book
  READ_LONG  0x34 "price" ELSE 0
  PATCH_IF ((("%type%" = 17) OR ("%type%" = 18) OR ("%type%" = 26) OR ("%type%" = 37)) AND ("%price%" = 0)) BEGIN
    WRITE_LONG 0x34 1 // set price to 1 gp
  END
  BUT_ONLY

// stores which buy arrows now buy bolts (mainly affects BGT/Tutu)
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x2c buy_off   ELSE 0
  READ_LONG 0x30 buy_num   ELSE 0
  SET bolts = 0
  FOR (index = 0 ; index < buy_num ; ++index) BEGIN
    READ_LONG (buy_off + (index * 0x04)) item
    PATCH_IF (item = 5) BEGIN // arrows
      SET bolts = 1
    END ELSE
    PATCH_IF (item = 31) BEGIN // bolts
      SET bolts = 2
      SET index = buy_num // kills loop
    END
  END
  PATCH_IF (bolts = 1) BEGIN // if arrows but no bolts
    INSERT_BYTES buy_off 0x04
      WRITE_LONG buy_off 31
    WRITE_LONG 0x30 (buy_num + 1)
    PATCH_FOR_EACH offset IN 0x34 0x4c 0x70 BEGIN // adjust offsets if needed, thankfully the same in v9, v1, and v1.1
      READ_LONG offset off
      PATCH_IF off > buy_off BEGIN
        WRITE_LONG offset (off + 0x04)
      END
    END
  END
  BUT_ONLY

// get prices from lookup table
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_prices BEGIN
  _blun01  =>    1 // club, tutu
  _blun10  => 2750 // root of the problem, Tutu
  _staf01  =>    1 // generic staff, tutu
  _sw1h13  =>  100 // xan's moonblade, tutu
  blun01   =>    1 // club
  blun10   => 2750 // root of the problem
  deck     => 2500 // deck of many things
  flolth   =>  575 // drow flail +3
  key20    =>   20 // king strohm iii's burial mask
  kuobolt  =>   30 // kuo-toa bolts
  kuobolt2 =>   30 // kuo-toa bolts
  kuobolt3 =>   30 // kuo-toa bolts
  misc3a1  => 3200 // book of infinite spells
  misc3a2  => 3200 // book of infinite spells
  misc3a3  => 3200 // book of infinite spells
  misc3a4  => 3200 // book of infinite spells
  misc3a5  => 3200 // book of infinite spells
  misc3a6  => 3200 // book of infinite spells
  misc3a7  => 3200 // book of infinite spells
  misc3a8  => 3200 // book of infinite spells
  misc3a9  =>  800 // book of infinite spells, final page
  misc3aa  => 3200 // book of infinite spells, 0 lore copy from BG2FP
  misc3c   => 2000 // efreeti bottle
  misc3h   => 1000 // horn of blasting
  misc3l   => 2000 // horn of silence
  misc3o   => 1000 // methlid's harp
  misc9q   =>   55 // habib's scimitar
  npmisc1  =>  200 // jan jansen's goggles
  npmisc2  =>  150 // jan jansen's gloves
  npshld   => 2000 // anomen's shield
  sahbolt  =>   30 // sahuagin bolts
  staf01   =>    1 // generic staff
  sw1h13   =>  100 // xan's moonblade
END

ACTION_PHP_EACH cd_prices AS item => price BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%item%.itm" BEGIN

    COPY_EXISTING "%item%.itm" ~override~
      READ_LONG  0x34 current
      PATCH_IF (current = 0) BEGIN // in case someone else sets a price
        WRITE_LONG 0x34 price
      END
      BUT_ONLY

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// stores buy all items                                       \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @320500 DESIGNATED 3205
GROUP @4
LABEL ~cd_tweaks_stores_buy_all_items~

// item types common to all games
ACTION_CLEAR_ARRAY cd_buyable_item_types
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN
   0 => 0 // miscellaneous
   6 => 0 // bracers and gauntlets
  10 => 0 // rings
  11 => 0 // scrolls
  16 => 0 // daggers
  17 => 0 // maces
  19 => 0 // small swords
  21 => 0 // hammers
  25 => 0 // axes
  27 => 0 // crossbows
  28 => 0 // hand-to-hand weapons
  29 => 0 // greatswords (pst)/spears (non-pst)
  31 => 0 // bolts
//  33 => 1 // copper commons (pst)/gold pieces (bg2)
  35 => 0 // wands
END

ACTION_IF game_includes_pst THEN BEGIN

  ACTION_IF game_is_pst THEN BEGIN
  
    OUTER_SET store_match = "-1" // no containers in PsT
  
  END ELSE BEGIN
  
    OUTER_SET store_match = 4 // containers in PsTEE

  END

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN // pst exclusives
     2 => 0 // armor
     8 => 0 // keys
    36 => 0 // eyeballs (pst)
    37 => 0 // bracelets (pst)
    38 => 0 // earrings (pst)
    39 => 0 // tattoos
    40 => 0 // lenses
    41 => 0 // teeth
  END

END ELSE BEGIN // non-pst

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN // common to everything except pst
     1 => 0 // amulets
     3 => 0 // belts and girdles
     4 => 0 // boots
     5 => 0 // arrows
     7 => 0 // headgear
     9 => 0 // potions
    14 => 0 // bullets
    15 => 0 // bows
    18 => 0 // slings
    20 => 0 // large swords
    22 => 0 // morning stars
    23 => 0 // flails
    24 => 0 // darts
    26 => 0 // staves
    30 => 0 // halberds
    32 => 0 // cloaks and robes
    34 => 0 // gems
  END

  ACTION_IF original_iwd OR game_is_iwd2 THEN BEGIN

    OUTER_SET store_match = 4 // container type in iwd/iwd2
    ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN // iwd & iwd2
       8 => 0 // keys
      36 => 0 // * containers (bg2)
      38 => 0 // * broken weapons (iwd)
      41 => 0 // bucklers
      42 => 0 // candles
      44 => 0 // clubs
      47 => 0 // large shields
      49 => 0 // medium shields
      53 => 0 // small shields
      55 => 0 // telescopes
      56 => 0 // bottles
      57 => 0 // great swords
//      58 => 1 // bags
    END

    ACTION_IF game_is_iwd2 THEN BEGIN

      ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN // iwd2 but not iwd
        50 => 0 // notes
        60 => 0 // leather armor
        61 => 0 // studded leather
        62 => 0 // chain mail
        63 => 0 // splint mail
        64 => 0 // plate mail
        65 => 0 // full plate
        66 => 0 // hide armor
        67 => 0 // robes
        69 => 0 // bastard swords
        70 => 0 // scarves
        71 => 0 // rations
        72 => 0 // hats
        73 => 0 // gloves
      END

    END ELSE BEGIN // iwd but not iwd2

      ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN // iwd but not iwd2
         2 => 0 // armor
      END

    END
    
  END ELSE BEGIN // non-iwd, non-iwd2

    OUTER_SET store_match = 5 // container type in other games
    ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN // common to bg(ee)/bg2(ee)/iwdee/tutu/bgt/ca
       2 => 0 // armor
      12 => 0 // shields
    END

    ACTION_IF NOT (original_bg1) BEGIN

      ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN // common to all but bg1
         8 => 0 // keys
        13 => 0 // food
//        36 => 1 // containers (bg2)
        37 => 0 // books (bg2)
//        38 => 1 // familiars (bg2)
      END

      ACTION_IF game_is_bgee OR game_is_eet BEGIN

        ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN
          39 => 0 // bandit scalps
//          43 => 1 // child bodies
//          45 => 1 // female bodies
//          48 => 1 // male bodies
          50 => 0 // notes
          54 => 0 // spider bodies
          55 => 0 // telescopes
          56 => 0 // bottles
          72 => 0 // hats
        END
      
      END

      ACTION_IF game_is_bg2ee OR game_is_eet BEGIN

        ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN
//          45 => 1 // female bodies
          51 => 0 // rods
        END
      
      END

      ACTION_IF game_is_iwdee OR game_is_eet BEGIN

        ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN
          74 => 0 // broken junk
          56 => 0 // bottles
          59 => 0 // furs and pelts
        END
      
      END
    
    END
 
  END

END

// thankfully store formats are similar enough that there are no game-specific checks here other than the container check
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x08 store_type
  READ_LONG 0x10 flags
  READ_LONG 0x14 markup_sell
  READ_LONG 0x18 markup_buy
  PATCH_IF ((store_type != store_match) AND ((flags & BIT1) = BIT1) AND  // don't bother if store can't buy, or if it's a container
            (markup_sell > 119) AND (markup_buy < 81)) BEGIN // don't mess with stores with exploitable buy/sell markups
    SET delta = 0
    READ_LONG 0x2c purch_off
    READ_LONG 0x30 purch_num
    // first, look at what store already sells and push items into array
    FOR (index = 0 ; index < purch_num ; ++index) BEGIN
      READ_LONG (purch_off + (index * 0x04)) type
      DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN "%type%" => 1 END
    END
    PATCH_PHP_EACH cd_buyable_item_types AS item => val BEGIN
      PATCH_IF val = 0 BEGIN
        SET delta += 1
        INSERT_BYTES purch_off 0x04
        WRITE_LONG   purch_off item
      END ELSE BEGIN
        DEFINE_ASSOCIATIVE_ARRAY cd_buyable_item_types BEGIN "%item%" => 0 END // reset for next store
      END
    END
    WRITE_LONG 0x30 (THIS + delta) // update number of items purchased
    // fix offsets for rest of file to account for new bytes
    PATCH_FOR_EACH off IN 0x34 0x4c 0x70 BEGIN
      READ_LONG off offset
      PATCH_IF offset >= purch_off BEGIN
        WRITE_LONG off (THIS + (delta * 0x04))
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Min Stats Tweak                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @321000 DESIGNATED 3210
GROUP @4
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 pst pstee~ @25
LABEL ~cd_tweaks_minimum_stats~

OUTER_SPRINT minimum_stats_strength_text     @321008
OUTER_SPRINT minimum_stats_dexterity_text    @321009
OUTER_SPRINT minimum_stats_constitution_text @321010
OUTER_SPRINT minimum_stats_intelligence_text @321011
OUTER_SPRINT minimum_stats_wisdom_text       @321012
OUTER_SPRINT minimum_stats_charisma_text     @321013

ACTION_IF ((minimum_stats_use_config_values = 1) AND // if we should use the config file
           (((minimum_stats_strength > 5)     AND (minimum_stats_strength < 16))     OR (minimum_stats_strength = 0))     AND // STR is either 0 or 6-15
           (((minimum_stats_dexterity > 5)    AND (minimum_stats_dexterity < 16))    OR (minimum_stats_dexterity = 0))    AND // DEX is either 0 or 6-15
           (((minimum_stats_constitution > 5) AND (minimum_stats_constitution < 16)) OR (minimum_stats_constitution = 0)) AND // CON is either 0 or 6-15
           (((minimum_stats_intelligence > 5) AND (minimum_stats_intelligence < 16)) OR (minimum_stats_intelligence = 0)) AND // INT is either 0 or 6-15
           (((minimum_stats_wisdom > 5)       AND (minimum_stats_wisdom < 16))       OR (minimum_stats_wisdom = 0))       AND // WIS is either 0 or 6-15
           (((minimum_stats_charisma > 5)     AND (minimum_stats_charisma < 16))     OR (minimum_stats_charisma = 0))     AND // CHR is either 0 or 6-15
           (minimum_stats_strength + minimum_stats_dexterity + minimum_stats_constitution + minimum_stats_intelligence + minimum_stats_wisdom + minimum_stats_charisma != 0)) THEN BEGIN // make sure we have some changes
  // print summary of options for the debug file
  PRINT @26
  PRINT @318309
  ACTION_IF (minimum_stats_strength != 0) THEN BEGIN
    PRINT ~%minimum_stats_strength_text% %minimum_stats_strength%~
  END
  ACTION_IF (minimum_stats_dexterity != 0) THEN BEGIN
    PRINT ~%minimum_stats_dexterity_text% %minimum_stats_dexterity%~
  END
  ACTION_IF (minimum_stats_constitution != 0) THEN BEGIN
    PRINT ~%minimum_stats_constitution_text% %minimum_stats_constitution%~
  END
  ACTION_IF (minimum_stats_intelligence != 0) THEN BEGIN
    PRINT ~%minimum_stats_intelligence_text% %minimum_stats_intelligence%~
  END
  ACTION_IF (minimum_stats_wisdom != 0) THEN BEGIN
    PRINT ~%minimum_stats_wisdom_text% %minimum_stats_wisdom%~
  END
  ACTION_IF (minimum_stats_charisma != 0) THEN BEGIN
    PRINT ~%minimum_stats_charisma_text% %minimum_stats_charisma%~
  END

END ELSE BEGIN

  OUTER_SET minimum_stats_strength = 1
  OUTER_SET minimum_stats_dexterity = 1
  OUTER_SET minimum_stats_constitution = 1
  OUTER_SET minimum_stats_intelligence = 1
  OUTER_SET minimum_stats_wisdom = 1
  OUTER_SET minimum_stats_charisma = 1
  OUTER_SET minimum_stats_proceed = 0

  OUTER_WHILE (minimum_stats_proceed != 1) BEGIN
    OUTER_WHILE ((NOT IS_AN_INT minimum_stats_strength) OR ((minimum_stats_strength < 6) AND (minimum_stats_strength != 0)) OR (minimum_stats_strength > 15)) BEGIN
      PRINT @321002
      ACTION_READLN minimum_stats_strength
    END
    OUTER_WHILE ((NOT IS_AN_INT minimum_stats_dexterity) OR ((minimum_stats_dexterity < 6) AND (minimum_stats_dexterity != 0)) OR (minimum_stats_dexterity > 15)) BEGIN
      PRINT @321003
      ACTION_READLN minimum_stats_dexterity
    END
    OUTER_WHILE ((NOT IS_AN_INT minimum_stats_constitution) OR ((minimum_stats_constitution < 6) AND (minimum_stats_constitution != 0)) OR (minimum_stats_constitution > 15)) BEGIN
      PRINT @321004
      ACTION_READLN minimum_stats_constitution
    END
    OUTER_WHILE ((NOT IS_AN_INT minimum_stats_intelligence) OR ((minimum_stats_intelligence < 6) AND (minimum_stats_intelligence != 0)) OR (minimum_stats_intelligence > 15)) BEGIN
      PRINT @321005
      ACTION_READLN minimum_stats_intelligence
    END
    OUTER_WHILE ((NOT IS_AN_INT minimum_stats_wisdom) OR ((minimum_stats_wisdom < 6) AND (minimum_stats_wisdom != 0)) OR (minimum_stats_wisdom > 15)) BEGIN
      PRINT @321006
      ACTION_READLN minimum_stats_wisdom
    END
    OUTER_WHILE ((NOT IS_AN_INT minimum_stats_charisma) OR ((minimum_stats_charisma < 6) AND (minimum_stats_charisma != 0)) OR (minimum_stats_charisma > 15)) BEGIN
      PRINT @321007
      ACTION_READLN minimum_stats_charisma
    END
    ACTION_IF ((minimum_stats_strength = 0) AND (minimum_stats_dexterity = 0) AND (minimum_stats_constitution = 0) AND (minimum_stats_intelligence = 0) AND (minimum_stats_wisdom = 0) AND (minimum_stats_charisma = 0)) BEGIN
      PRINT @321014
      OUTER_SET minimum_stats_proceed = 2
    END ELSE BEGIN
      // print summary of options before proceeding
      PRINT @318309
      ACTION_IF (minimum_stats_strength != 0) THEN BEGIN
        PRINT ~%minimum_stats_strength_text% %minimum_stats_strength%~
      END
      ACTION_IF (minimum_stats_dexterity != 0) THEN BEGIN
        PRINT ~%minimum_stats_dexterity_text% %minimum_stats_dexterity%~
      END
      ACTION_IF (minimum_stats_constitution != 0) THEN BEGIN
        PRINT ~%minimum_stats_constitution_text% %minimum_stats_constitution%~
      END
      ACTION_IF (minimum_stats_intelligence != 0) THEN BEGIN
        PRINT ~%minimum_stats_intelligence_text% %minimum_stats_intelligence%~
      END
      ACTION_IF (minimum_stats_wisdom != 0) THEN BEGIN
        PRINT ~%minimum_stats_wisdom_text% %minimum_stats_wisdom%~
      END
      ACTION_IF (minimum_stats_charisma != 0) THEN BEGIN
        PRINT ~%minimum_stats_charisma_text% %minimum_stats_charisma%~
      END
      OUTER_WHILE ((NOT IS_AN_INT minimum_stats_proceed) OR ((minimum_stats_proceed != 1) AND (minimum_stats_proceed != 2))) BEGIN
        PRINT @318310
        ACTION_READLN minimum_stats_proceed
      END
    END
    ACTION_IF (minimum_stats_proceed = 2) THEN BEGIN
      OUTER_SET minimum_stats_strength = 1
      OUTER_SET minimum_stats_dexterity = 1
      OUTER_SET minimum_stats_constitution = 1
      OUTER_SET minimum_stats_intelligence = 1
      OUTER_SET minimum_stats_wisdom = 1
      OUTER_SET minimum_stats_charisma = 1
      OUTER_SET minimum_stats_proceed = 0
    END
  END

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_min_stats BEGIN
  1 => "%minimum_stats_strength%"
  2 => "%minimum_stats_dexterity%"
  3 => "%minimum_stats_constitution%"
  4 => "%minimum_stats_intelligence%"
  5 => "%minimum_stats_wisdom%"
  6 => "%minimum_stats_charisma%"
END

COPY_EXISTING ~abclasrq.2da~ ~override~
  COUNT_2DA_ROWS ~7~ "rows"
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    PATCH_PHP_EACH cd_min_stats AS col => val BEGIN
      PATCH_IF val != 0 BEGIN
        READ_2DA_ENTRY index col 7 min
        PATCH_IF (min < val) BEGIN
          SET_2DA_ENTRY index col 7 "%val%"
        END
      END
    END
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Sensible Entrance Points                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @322000 DESIGNATED 3220
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt soa tob~ @25
LABEL ~cd_tweaks_sensible_entrance_points~

ACTION_IF game_includes_bg1 THEN BEGIN // BGT/Tutu/obg changes

  ACTION_IF game_is_bgt THEN BEGIN // BGT
    
    COPY_EXISTING ~%Beregost%.are~ ~override~ // only change for BGT is to move the east beregost entrance
      LPF ALTER_AREA_ENTRANCE INT_VAR x_coord = 4950 y_coord = 3085 orient = 4 STR_VAR entrance_name = "Exit9300" END
      BUT_ONLY

  END ELSE BEGIN // BG/Tutu changes

    COPY_EXISTING ~worldmap.wmp~ ~override~
                  ~no_totsc.wmp~ ~override~
      READ_LONG 0x0c mos_off
      READ_LONG (mos_off + 0x20) area_num
      READ_LONG (mos_off + 0x24) area_off
      READ_LONG (mos_off + 0x28) link_off
      READ_LONG (mos_off + 0x2c) link_num
      FOR (index = 0 ; index < area_num ; ++index) BEGIN
        READ_ASCII (area_off + 0x08 + (index * 0xf0)) "area"
        PATCH_IF ("%area%" STRING_COMPARE_CASE "%Beregost%" = 0) BEGIN // beregost
          SET Beregost_idx = index
        END
        PATCH_IF ("%area%" STRING_COMPARE_CASE "%FishingVillage%" = 0) BEGIN // farm north of fai
          SET FishingVillage_idx = index
        END
        PATCH_IF ("%area%" STRING_COMPARE_CASE "%Farmlands%" = 0) BEGIN // zombie farm
          SET Farmlands_idx = index
        END
        PATCH_IF ("%area%" STRING_COMPARE_CASE "%ShipwrecksCoast%" = 0) BEGIN // shipwreck
          SET ShipwrecksCoast_idx = index
        END
        PATCH_IF ("%area%" STRING_COMPARE_CASE "%Lighthouse%" = 0) BEGIN // lighthouse
          SET Lighthouse_idx = index
        END
      END
      // read links, adjust entry point for links to beregost
      FOR (index = 0 ; index < link_num ; ++index) BEGIN
        READ_LONG (link_off +        (index * 0xd8)) target
        PATCH_IF (target = Lighthouse_idx) BEGIN // if travel link to lighthouse
          READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
          READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
          PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                    (target = 1)) BEGIN // unnamed entrance point, north
            WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDNorthExit~
          END
        END
        PATCH_IF (target = ShipwrecksCoast_idx) BEGIN // if travel link to shipwreck
          READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
          READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
          PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                    (target = 4)) BEGIN // unnamed entrance point, south
            WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDSouthExit~
          END
        END
        PATCH_IF (target = Beregost_idx) BEGIN // if travel link to beregost
          READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
          READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
          PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                    (target = 2)) BEGIN // unnamed entrance point, east
            WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDEastExit~
          END
        END
        PATCH_IF (target = FishingVillage_idx) BEGIN // if travel link to north fai
          READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
          READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
          PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                    (target = 4)) BEGIN // unnamed entrance point, south
            WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDSouthExit~
          END
        END
        PATCH_IF (target = Farmlands_idx) BEGIN // if travel link to zombie farm
          READ_ASCII (link_off + 0x04 + (index * 0xd8)) entrance_name
          READ_BYTE  (link_off + 0x28 + (index * 0xd8)) target
          PATCH_IF (("%entrance_name%" STRING_COMPARE_CASE "" = 0) AND
                    (target = 4)) BEGIN // unnamed entrance point, south
            WRITE_ASCII (link_off + 0x04 + (index * 0xd8)) ~CDSouthExit~
          END
        END
      END
      BUT_ONLY IF_EXISTS
    
    COPY_EXISTING ~%Beregost%.are~ ~override~
      LPF fj_are_structure
        INT_VAR
        fj_loc_x       = 4950
        fj_loc_y       = 3085
        fj_orientation = 4   //W
        STR_VAR
        fj_structure_type = entrance
        fj_name           = CDEastExit
      END
    
    COPY_EXISTING ~%FishingVillage%.are~ ~override~
      LPF fj_are_structure
        INT_VAR
        fj_loc_x       = 791
        fj_loc_y       = 3615
        fj_orientation = 9   //N
        STR_VAR
        fj_structure_type = entrance
        fj_name           = CDSouthExit
      END
  
    COPY_EXISTING ~%Farmlands%.are~ ~override~
      LPF fj_are_structure
        INT_VAR
        fj_loc_x       = 1638
        fj_loc_y       = 3715
        fj_orientation = 8   //N
        STR_VAR
        fj_structure_type = entrance
        fj_name           = CDSouthExit
      END
    
    COPY_EXISTING ~%ShipwrecksCoast%.are~ ~override~
      LPF fj_are_structure
        INT_VAR
        fj_loc_x       = 4957
        fj_loc_y       = 3516
        fj_orientation = 4   //N
        STR_VAR
        fj_structure_type = entrance
        fj_name           = CDSouthExit
      END
    
    COPY_EXISTING ~%Lighthouse%.are~ ~override~
      LPF fj_are_structure
        INT_VAR
        fj_loc_x       = 4941
        fj_loc_y       = 688
        fj_orientation = 4   //N
        STR_VAR
        fj_structure_type = entrance
        fj_name           = CDNorthExit
      END
      
  END

END

ACTION_IF game_includes_soa THEN BEGIN // flipping the NORH in BG2/BGT; already flipped in bg2ee/eet

  DECOMPRESS_BIFF AREA090A.BIF

  OUTER_SET width = 2368
  OUTER_SET sm_width = 148

  COPY_EXISTING ~ar0900.are~ ~override~
    LPF ALTER_AREA_ENTRANCE INT_VAR x_coord = 3259 y_coord = 3451 orient =  2 STR_VAR entrance_name = "Exit0903" END // exchange entrances
    LPF ALTER_AREA_ENTRANCE INT_VAR x_coord = 4147 y_coord = 3342 orient = 14 STR_VAR entrance_name = "Exi0903b" END

  ACTION_FOR_EACH file IN ar0903 BEGIN // in case other mods clone ar0903, we can drop them in this list

    ACTION_IF FILE_EXISTS_IN_GAME ~%file%.are~ THEN BEGIN

      COPY_EXISTING ~%file%.are~ ~override~
        LPF ALTER_AREA_ENTRANCE INT_VAR x_coord = 1964 y_coord = 1520 orient =  6 STR_VAR entrance_name = "Exit0900" END // exchange entrances
        LPF ALTER_AREA_ENTRANCE INT_VAR x_coord =  476 y_coord = 1581 orient = 10 STR_VAR entrance_name = "Exi0900b" END
        READ_LONG  0x54 act_off
        READ_SHORT 0x58 act_num
        FOR (index = 0 ; index < act_num ; ++index) BEGIN
          WRITE_SHORT (act_off + 0x20 + (index * 0x110)) (width - THIS) // current x
          WRITE_SHORT (act_off + 0x24 + (index * 0x110)) (width - THIS) // travel x
          WRITE_SHORT (act_off + 0x34 + (index * 0x110)) (16 - THIS)   // orientation
        END
        READ_SHORT 0x5a trig_num
        READ_LONG  0x5c trig_off
        FOR (index = 0 ; index < trig_num ; ++index) BEGIN
          READ_SHORT  (trig_off + 0x22 + (index * 0xc4)) left  // flip bounding box
          READ_SHORT  (trig_off + 0x26 + (index * 0xc4)) right // flip bounding box
          WRITE_SHORT (trig_off + 0x22 + (index * 0xc4)) (width - right) // flip bounding box
          WRITE_SHORT (trig_off + 0x26 + (index * 0xc4)) (width - left)  // flip bounding box
          WRITE_SHORT (trig_off + 0x70 + (index * 0xc4)) (width - THIS) // launch point
          WRITE_SHORT (trig_off + 0x84 + (index * 0xc4)) (width - THIS) // activation point
        END
        READ_LONG  0x70 cont_off
        READ_SHORT 0x74 cont_num
        FOR (index = 0 ; index < cont_num ; ++index) BEGIN
          WRITE_SHORT (cont_off + 0x20 + (index * 0xc0)) (width - THIS) // location
          WRITE_SHORT (cont_off + 0x34 + (index * 0xc0)) (width - THIS) // launch point
          READ_SHORT  (cont_off + 0x38 + (index * 0xc0)) left  // flip bounding box
          READ_SHORT  (cont_off + 0x3c + (index * 0xc0)) right // flip bounding box
          WRITE_SHORT (cont_off + 0x38 + (index * 0xc0)) (width - right) // flip bounding box
          WRITE_SHORT (cont_off + 0x3c + (index * 0xc0)) (width - left)  // flip bounding box
        END
        READ_SHORT 0x82 amb_num
        READ_LONG  0x84 amb_off
        FOR (index = 0 ; index < amb_num ; ++index) BEGIN
          WRITE_SHORT (amb_off + 0x20 + (index * 0xd4)) (width - THIS) // location
          // go ahead and enable horse ambient while we're here
          READ_ASCII  (amb_off +        (index * 0xd4)) name //
          PATCH_IF ("%name%" STRING_COMPARE_CASE "SS_Horse" = 0) BEGIN
            WRITE_BYTE (amb_off + 0x90 + (index * 0xd4)) (THIS | (BIT0 + BIT3)) // enabled, random order
          END
        END
        READ_LONG  0xac anim_num
        READ_LONG  0xb0 anim_off
        FOR (index = 0 ; index < anim_num ; ++index) BEGIN
          WRITE_SHORT (anim_off + 0x20 + (index * 0x4c)) (width - THIS) // location
        END
        READ_SHORT 0x80 vert_num
        READ_LONG  0x7c vert_off
        FOR (index = 0 ; index < vert_num ; ++index) BEGIN
          WRITE_SHORT (vert_off +        (index * 0x04)) (width - THIS) // location
        END
        READ_LONG  0x64 spwn_num
        READ_LONG  0x60 spwn_off
        FOR (index = 0 ; index < spwn_num ; ++index) BEGIN
          WRITE_SHORT (spwn_off + 0x20 + (index * 0xc8)) (width - THIS) // location
        END
        READ_LONG  0xc8 note_num
        READ_LONG  0xc4 note_off
        FOR (index = 0 ; index < note_num ; ++index) BEGIN
          WRITE_SHORT (note_off +        (index * 0x34)) (width - THIS) // location
        END
        READ_LONG  0xd0 proj_num
        READ_LONG  0xcc proj_off
        FOR (index = 0 ; index < proj_num ; ++index) BEGIN
          WRITE_SHORT (proj_off + 0x14 + (index * 0x1c)) (width - THIS) // location
        END
        // doors get special treatment
        READ_LONG 0xa4 door_num
        READ_LONG 0xa8 door_off
        FOR (index = 0 ; index < door_num ; ++index) BEGIN
          READ_SHORT  (door_off + 0x38 + (index * 0xc8)) left  // flip bounding box (open)
          READ_SHORT  (door_off + 0x3c + (index * 0xc8)) right // flip bounding box (open)
          WRITE_SHORT (door_off + 0x38 + (index * 0xc8)) (width - right) // flip bounding box (open)
          WRITE_SHORT (door_off + 0x3c + (index * 0xc8)) (width - left)  // flip bounding box (open)
          READ_SHORT  (door_off + 0x40 + (index * 0xc8)) left  // flip bounding box (closed)
          READ_SHORT  (door_off + 0x44 + (index * 0xc8)) right // flip bounding box (closed)
          WRITE_SHORT (door_off + 0x40 + (index * 0xc8)) (width - right) // flip bounding box (closed)
          WRITE_SHORT (door_off + 0x44 + (index * 0xc8)) (width - left)  // flip bounding box (closed)
          WRITE_SHORT (door_off + 0x74 + (index * 0xc8)) (width - THIS) // launch point
          WRITE_SHORT (door_off + 0x90 + (index * 0xc8)) (width - THIS) // open location
          WRITE_SHORT (door_off + 0x94 + (index * 0xc8)) (width - THIS) // closed location
          // since impeded vertices use search map coordinates, need to undo the adjustment above and re-do
          READ_LONG  (door_off + 0x48 + (index * 0xc8)) vert_idx  // impeded/open
          READ_SHORT (door_off + 0x4c + (index * 0xc8)) vert_num  // impeded/open
          FOR (index2 = vert_idx ; index2 < (vert_idx + vert_num) ; ++index2) BEGIN
            WRITE_SHORT (vert_off +        (index2 * 0x04)) (sm_width - (width - THIS)) // location
          END
          READ_LONG  (door_off + 0x50 + (index * 0xc8)) vert_idx  // impeded/closed
          READ_SHORT (door_off + 0x4e + (index * 0xc8)) vert_num  // impeded/closed
          FOR (index2 = vert_idx ; index2 < (vert_idx + vert_num) ; ++index2) BEGIN
            WRITE_SHORT (vert_off +        (index2 * 0x04)) (sm_width - (width - THIS)) // location
          END
        END
        LPF fj_are_structure
          INT_VAR
          fj_loc_x          = 1723
          fj_loc_y          = 1553
          fj_dest_x         = 1723
          fj_dest_y         = 1553
          fj_animation      = 45312 // horse
          fj_orientation    = 6      //SSE
          STR_VAR
          fj_structure_type = actor
          fj_name           = "Atta Girl"
          fj_cre_resref     = cdhorse1
        END
        LPF fj_are_structure
          INT_VAR
          fj_loc_x          = 2000
          fj_loc_y          = 1349
          fj_dest_x         = 2000
          fj_dest_y         = 1349
          fj_animation      = 45312 //horse
          fj_orientation    = 6      //SSE
          STR_VAR
          fj_structure_type = actor
          fj_name           = Angel
          fj_cre_resref     = cdhorse2
        END

    END

  END

  COPY_EXISTING ~horse.cre~ ~override/cdhorse1.cre~
    SAY 0x08 @322001
    SAY 0x0c @322001

  COPY_EXISTING ~horse.cre~ ~override/cdhorse2.cre~
    SAY 0x08 @322002
    SAY 0x0c @322002

  ACTION_FOR_EACH file IN ar0903 cut11a c6spwn1 c6spwn2 c#bjcut1 BEGIN // compatibility

    ACTION_IF FILE_EXISTS_IN_GAME ~%file%.bcs~ THEN BEGIN

      COPY_EXISTING ~%file%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_EVALUATE ~\[\([0-9]+\)\.\([0-9]+\)\]~
            BEGIN SET "RESULT" = (width - %MATCH1%) END
            ~[%RESULT%.%MATCH2%]~
          REPLACE_EVALUATE ~Color(\[\([0-9]+\)\.\([0-9]+\)\]~ // don't mess up area fades
            BEGIN SET "RESULT" = (width - %MATCH1%) END
            ~Color([%RESULT%.%MATCH2%]~
        END
    END

  END
  
  // re compatibiity
  ACTION_IF FILE_EXISTS_IN_GAME ~arre07.are~ THEN BEGIN

    COPY_EXISTING ~arre07.are~ ~override~
      READ_LONG  0xac anim_num
      READ_LONG  0xb0 anim_off
      FOR (index = 0 ; index < anim_num ; ++index) BEGIN
        WRITE_SHORT (anim_off + 0x20 + (index * 0x4c)) (width - THIS) // location
      END

  END

  COMPILE ~cdtweaks/dlg/ar0903.d~

  LAF HANDLE_TILESETS END // decompresses ar0903.tiz from cdtweaks/tiz to the override (only one tiz and paths are defaults, so no need to specify parameters)

  COPY ~cdtweaks/bmp/ar0903ht.bmp~ ~override~
       ~cdtweaks/bmp/ar0903lm.bmp~ ~override~
       ~cdtweaks/bmp/ar0903sr.bmp~ ~override~
       ~cdtweaks/mos/ar0903.mos~   ~override~
       ~cdtweaks/wed/ar0903.wed~   ~override~
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow Taerom to Make Additional Ankheg Armors    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @323000 DESIGNATED 3230
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt~ @25
LABEL ~cd_tweaks_taerom_makes_multiple_ankheg_armor~

ACTION_IF NOT (original_bg1) THEN BEGIN // change fenten/taerom to only take one shell at a time
  COMPILE ~cdtweaks/dlg/ankheg.d~ EVALUATE_BUFFER // use TakePartyItemNum when we can
END ELSE BEGIN
  COMPILE ~cdtweaks/dlg/ankheg_bg1.d~ // otherwise stuck with TakePartyItem
END

EXTEND_TOP ~%tutu_var%taerom.bcs~ ~cdtweaks/baf/taerom.baf~ EVALUATE_BUFFER // reset timer/vars after completing armor

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// friendly random drops                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// re-randomize on reload                           \\\\\
/////                                                  \\\\\

BEGIN @324001 DESIGNATED 3240
GROUP @4
REQUIRE_PREDICATE GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
SUBCOMPONENT @324000 // friendly random drops
LABEL ~cd_tweaks_friendly_random_drops_reload~

OUTER_SET tfg_bows = 1 // randomize bows in TotFG (skip on merchant option)
INCLUDE ~cdtweaks/lib/random_common.tpa~ // ar9200 fix, UB detection

ACTION_IF enhanced_edition THEN BEGIN

  INCLUDE ~cdtweaks/lib/iwdee_rndtres.tpa~ // replaces IWD references with IWDEE's RNDTRE schema

END

COPY ~weidu_external/cdtweaks/random/random.2da~ ~weidu_external/cdtweaks/random/random.2da~
  PATCH_IF NOT (game_is_iwd OR game_is_how) BEGIN
    REPLACE_TEXTUALLY ~ShadowedElvenPriest~ ~Priest_0~ // dv changes with totlm installed
  END
  COUNT_2DA_ROWS 3 "rows"
  FOR (index = 2 ; index < rows ; ++index) BEGIN // start at 2 to skip orktres
    READ_2DA_ENTRY index 0 3 "area"
    READ_2DA_ENTRY index 1 3 "item"
    READ_2DA_ENTRY index 2 3 "cont"
    PATCH_IF FILE_EXISTS_IN_GAME ~%area%.are~ BEGIN
      INNER_ACTION BEGIN

        COPY_EXISTING ~%table%.2da~ ~override~
          REPLACE_EVALUATE CASE_INSENSITIVE ~^\(%item%[ %TAB%]\)\(.+\)\([ %TAB%]*\)$~
          BEGIN SPRINT "list" "%MATCH2%" END ~%MATCH1%%MATCH2%%MATCH3%~
          BUT_ONLY

        COPY ~cdtweaks/baf/areabottom.baf~ ~weidu_external/cdtweaks/random/%item%.baf~

        COPY ~cdtweaks/lib/template.tpa~ ~weidu_external/cdtweaks/random/%item%.tpa~
          REPLACE_TEXTUALLY ~replacemelist~ ~%list%~ EVALUATE_BUFFER
          REPLACE_TEXTUALLY ~drop~ ~drop%item%~ EVALUATE_BUFFER
          REPLACE_TEXTUALLY ~\([ %TAB%]+\*\)*~ ~~
          REPLACE_TEXTUALLY ~multiply~ ~*~
          REPLACE_TEXTUALLY ~percentage~ ~%~
      
        INCLUDE ~weidu_external/cdtweaks/random/%item%.tpa~

        COPY_EXISTING ~%area%.are~ ~override~
          WRITE_ASCIIE 0x94 ~%area%~ #8
          BUT_ONLY
      
        COPY ~weidu_external/cdtweaks/random/%item%.baf~ ~weidu_external/cdtweaks/random/%item%.baf~
          REPLACE_TEXTUALLY ~replaceme[12]~ ~~
      
        EXTEND_TOP    ~%area%.bcs~ ~cdtweaks/baf/areatop.baf~ EVALUATE_BUFFER
        EXTEND_BOTTOM ~%area%.bcs~ ~weidu_external/cdtweaks/random/%item%.baf~  EVALUATE_BUFFER

      END

    END
  END
  BUT_ONLY

ACTION_IF game_is_iwd-in-bg2 BEGIN
  
  COPY_EXISTING ~ar3501.bcs~ ~override~
                ~ar3502.bcs~ ~override~
                ~ar3503.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_EVALUATE ~"CDKT\([123]\)Tres"~ BEGIN
        SET new_ref = (%MATCH1% + 17)
      END ~"CDRNDTRE%new_ref%"~
    END
    BUT_ONLY

END

/////                                                  \\\\\
///// pick via dialogue                                \\\\\
/////                                                  \\\\\

BEGIN @324100 DESIGNATED 3241
GROUP @4
REQUIRE_PREDICATE GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
SUBCOMPONENT @324000 // friendly random drops
LABEL ~cd_tweaks_friendly_random_drops_tokens~

OUTER_SET tfg_bows = 1 // randomize bows in TotFG (skip on merchant option)
INCLUDE ~cdtweaks/lib/random_common.tpa~ // ar9200 fix, UB detection

COPY ~cdtweaks/bam/cdtokeni.bam~ ~override~
COPY_EXISTING ~de1tres.itm~ ~override~
  SAY 0x08 @324102
  SAY 0x0c @324102
  SAY 0x50 @324103
  SAY 0x54 @324103
  WRITE_ASCII 0x3a ~cdtokeni~
  WRITE_ASCII 0x44 ~gtreas01~
  
COPY ~cdtweaks/dlg/cdrandom.d~   ~weidu_external/cdtweaks/random/cdrandom.d~
COPY ~cdtweaks/baf/cdrandom.baf~ ~weidu_external/cdtweaks/random/cdrandom.baf~

ACTION_IF game_is_iwdee OR game_is_iwd-in-bg2 THEN BEGIN // if modern engine

  ACTION_IF game_is_iwd-in-bg2 THEN BEGIN // if IWD-in-BG2 instead of true IWD...

    COPY ~weidu_external/cdtweaks/random/cdrandom.d~ ~weidu_external/cdtweaks/random/cdrandom.d~
      REPLACE_TEXTUALLY ~SAY #288~ ~SAY #74388~ // normally we'd replace_eval, but only one replacement

  END ELSE BEGIN

    INCLUDE ~cdtweaks/lib/iwdee_rndtres.tpa~ // replaces IWD references with IWDEE's RNDTRE schema in random.2da

  END

  COPY ~weidu_external/cdtweaks/random/cdrandom.baf~ ~weidu_external/cdtweaks/random/cdrandom.baf~
    REPLACE_TEXTUALLY ~EquipItem("gopoof",1)~ ~DestroyItem("gopoof")~
    REPLACE_TEXTUALLY ~EquipItem("mring1",0)~ ~EquipItem("mring1")~
    REPLACE_TEXTUALLY ~SetDialogueRange(300)[ %TAB%%LNL%%MNL%%WNL]+Dialogue(~ ~StartDialogueNoSet(~

  OUTER_SET rand_num = 1

END

COPY_EXISTING ~telanis.cre~ ~override/cdrandom.cre~
  SAY 0x08 @324101
  SAY 0x0c @324101
  WRITE_ASCII 0x248 ~cdrandom~ #8
  PATCH_IF original_iwd BEGIN
    WRITE_BYTE  0x2d9 1 // undead > humanoid
    WRITE_ASCII 0x2e8 ~cdrandom~ #32
    WRITE_ASCII 0x334 ~cdrandom~ #8
  END ELSE BEGIN
    WRITE_BYTE  0x271 1 // undead > humanoid
    WRITE_ASCII 0x280 ~cdrandom~ #32
    WRITE_ASCII 0x2cc ~cdrandom~ #8
  END
  REPLACE_CRE_ITEM ~gopoof~ #0 #0 #0 ~NONE~ ~RRING~
  ADD_CRE_ITEM ~mring1~ #0 #0 #0 ~NONE~ ~INV1~

ACTION_IF enhanced_edition THEN BEGIN

  // add unused random drop to one of yxonmei's champions (halberd or 2h axe); see also ar4005.are patch; replaces generic yuan-ti halberd
  COPY_EXISTING ~cdyucham.cre~ ~override~
    ADD_CRE_ITEM ~u2hAx3a~ #0 #0 #0 ~UNDROPPABLE~ ~WEAPON1~ EQUIP TWOHANDED // give undroppable axe, push token to inventory
  
  // replace generic composite long bow with long bow +3: repeater or long bow +3: defender
  COPY_EXISTING ~kelly.cre~ ~override~
    ADD_CRE_ITEM ~uLBow4a~ #0 #0 #0 ~UNDROPPABLE~ ~WEAPON1~ EQUIP TWOHANDED // give undroppable bow, push token to inventory

  // add unused random drop to one of alpheus (choice of two mage staves)
  COPY_EXISTING ~alpheus.cre~  ~override~
    ADD_CRE_ITEM ~quost~ #0 #0 #0 ~UNDROPPABLE~ ~WEAPON1~ EQUIP TWOHANDED // give undroppable staff, push token to inventory

END

COPY ~weidu_external/cdtweaks/random/random.2da~ ~weidu_external/cdtweaks/random/random.2da~
  PATCH_IF NOT (game_is_iwd OR game_is_how) BEGIN
    REPLACE_TEXTUALLY ~ShadowedElvenPriest~ ~Priest_0~ // dv changes with totlm installed
  END
  COUNT_2DA_ROWS 3 "rows"
  FOR (index = 1 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 3 "area"
    READ_2DA_ENTRY index 1 3 "item"
    READ_2DA_ENTRY index 2 3 "cont"
    PATCH_IF FILE_EXISTS_IN_GAME ~%area%.are~  BEGIN
      INNER_ACTION BEGIN

        ACTION_IF game_is_iwdee BEGIN // iwdee will swap any RNDTRExx even if it's not on the table, so in iwdee we have to swap the rndtreXX item ref for a different item
          OUTER_SPRINT token_item ~CDRAND%rand_num%~
          OUTER_SET rand_num += 1
        END ELSE BEGIN
          OUTER_SPRINT token_item ~%item%~
        END

        OUTER_FOR (index2 = 1 ; index2 < 7 ; ++index2) BEGIN

          EXTEND_BOTTOM ~%area%.bcs~ ~cdtweaks/baf/cdrandom_extend.baf~ EVALUATE_BUFFER

        END

        ACTION_IF index = 1 BEGIN // special for orrick's random-via-dialogue

          COMPILE ~cdtweaks/dlg/dorrick.d~ EVALUATE_BUFFER
        
        END

        COPY_EXISTING ~%table%.2da~ ~override~
          PATCH_IF game_is_iwdee BEGIN
            REPLACE_EVALUATE CASE_INSENSITIVE ~^\(%item%[ %TAB%]\)\(.+\)\([ %TAB%]*[%LNL%%MNL%%WNL%]+\)~
              BEGIN SPRINT "list" "%MATCH2%" END ~%MATCH1%%MATCH2%%MATCH3%~ // preserve line
          END ELSE BEGIN
            REPLACE_EVALUATE CASE_INSENSITIVE ~^\(%item%[ %TAB%]\)\(.+\)\([ %TAB%]*[%LNL%%MNL%%WNL%]+\)~
            BEGIN SPRINT "list" "%MATCH2%" END ~~ // delete line from table
          END
          BUT_ONLY

        COPY_EXISTING ~de1tres.itm~ ~override/%token_item%.itm~

        COPY ~weidu_external/cdtweaks/random/cdrandom.d~ ~weidu_external/cdtweaks/random/cdrandom.d~
          REPLACE_TEXTUALLY ~insert_reply~ ~~
          REPLACE_TEXTUALLY ~~~~~insert_state~~~~~ ~~~~~
IF ~PartyHasItem("%token_item%")~ THEN BEGIN CDState%token_item% SAY @324104
  insert_reply
END
insert_state
~~~~~

        COPY ~cdtweaks/lib/template2.tpa~ ~weidu_external/cdtweaks/random/%item%.tpa~
          REPLACE_TEXTUALLY ~replacemelist~ ~%list%~ //EVALUATE_BUFFER
//          REPLACE_TEXTUALLY ~drop~ ~drop%item%~ EVALUATE_BUFFER
          REPLACE_TEXTUALLY ~\([ %TAB%]+\*\)*~ ~~
          REPLACE_TEXTUALLY ~multiply~ ~*~
          REPLACE_TEXTUALLY ~percentage~ ~%~

        INCLUDE ~weidu_external/cdtweaks/random/%item%.tpa~
          
        COPY_EXISTING ~%area%.are~ ~override~
          WRITE_ASCIIE 0x94 ~%area%~ #8
          BUT_ONLY

      END

    END
  END
  BUT_ONLY

COPY ~weidu_external/cdtweaks/random/cdrandom.d~ ~weidu_external/cdtweaks/random/cdrandom.d~
  REPLACE_TEXTUALLY ~insert_state~ ~~
  REPLACE_TEXTUALLY ~insert_reply~ ~~

COMPILE ~weidu_external/cdtweaks/random/cdrandom.d~
        ~weidu_external/cdtweaks/random/cdrandom.baf~

ACTION_IF game_is_iwd-in-bg2 BEGIN

  COPY_EXISTING ~ar3501.bcs~ ~override~
                ~ar3502.bcs~ ~override~
                ~ar3503.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_EVALUATE ~"CDKT\([123]\)Tres"~ BEGIN
        SET new_ref = (%MATCH1% + 17)
      END ~"CDRNDTRE%new_ref%"~
    END
    BUT_ONLY

END

/////                                                  \\\\\
///// wandering merchant style                         \\\\\
/////                                                  \\\\\

BEGIN @324200 DESIGNATED 3242
GROUP @4
REQUIRE_PREDICATE GAME_IS ~iwdee iwd how totlm iwd-in-bg2~ @25
SUBCOMPONENT @324000 // friendly random drops
LABEL ~cd_tweaks_friendly_random_drops_merchant~

OUTER_SET tfg_bows = 0 // randomize bows in TotFG (skip on merchant option)
INCLUDE ~cdtweaks/lib/random_common.tpa~ // ar9200 fix, UB detection

COPY_EXISTING ~katown1.cre~ ~override/cdrndm1.cre~
  SAY 0x08 @324201
  SAY 0x0c @324202
  WRITE_ASCII 0x248 ~eftwnchk~ #8
  WRITE_ASCII 0x250 ~kuatkfle~ #32 // add script, blank other fields
  PATCH_IF original_iwd BEGIN
    WRITE_BYTE  0x270 0 // make visible
    WRITE_ASCII 0x2e8 ~cdrndm1~ #32 // dv
    WRITE_ASCII 0x334 ~cdrndm1~ #8  // dialogue
  END ELSE BEGIN
    WRITE_ASCII 0x280 ~cdrndm1~ #32 // dv
    WRITE_ASCII 0x2cc ~cdrndm1~ #8  // dialogue
  END

EXTEND_BOTTOM ~ar2100.bcs~ ~cdtweaks/baf/ar2100.baf~

ACTION_IF game_is_iwdee THEN BEGIN

  INCLUDE ~cdtweaks/lib/iwdee_rndtres.tpa~ // replaces IWD references with IWDEE's RNDTRE schema in random.2da

END

COPY ~cdtweaks/dlg/cdrndm1.d~ ~weidu_external/cdtweaks/random/cdrndm1.d~

COPY ~weidu_external/cdtweaks/random/random.2da~ ~weidu_external/cdtweaks/random/random.2da~
  PATCH_IF NOT (game_is_iwd OR game_is_how) BEGIN
    REPLACE_TEXTUALLY ~ShadowedElvenPriest~ ~Priest_0~ // dv changes with totlm installed
  END
  COUNT_2DA_ROWS 3 "rows"
  FOR (index = 1 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 1 3 "item"
    INNER_ACTION BEGIN

      COPY_EXISTING ~%table%.2da~ ~override~
        REPLACE_EVALUATE CASE_INSENSITIVE ~^\(%item%[ %TAB%]\)\(.+\)\([ %TAB%]*\)$~
        BEGIN SPRINT "list" "%MATCH2%" END ~%MATCH1%%MATCH2%%MATCH3%~
        BUT_ONLY

      COPY ~cdtweaks/lib/template3.tpa~ ~weidu_external/cdtweaks/random/%item%.tpa~
        REPLACE_TEXTUALLY ~replacemelist~ ~%list%~ //EVALUATE_BUFFER
        REPLACE_TEXTUALLY ~\([ %TAB%]+\*\)*~ ~~
        REPLACE_TEXTUALLY ~multiply~ ~*~
        REPLACE_TEXTUALLY ~percentage~ ~%~

      INCLUDE ~weidu_external/cdtweaks/random/%item%.tpa~

    END
  END
  BUT_ONLY

COPY ~weidu_external/cdtweaks/random/cdrndm1.d~ ~weidu_external/cdtweaks/random/cdrndm1.d~
  REPLACE_TEXTUALLY ~insert_eof~ ~END~

ACTION_IF NOT game_is_iwd THEN BEGIN // add lonelywood merchant

  COPY_EXISTING ~cdrndm1.cre~ ~override/cdrndm2.cre~
    SAY 0x08 @324203
    SAY 0x0c @324202
    WRITE_ASCII 0x248 ~efpikwrn~ #8
    WRITE_ASCII 0x250 ~eftwnchk~ #8
    WRITE_ASCII 0x258 ~efflee00~ #24
    PATCH_IF original_iwd BEGIN
      WRITE_ASCII 0x2e8 ~cdrndm2~ #32 // dv
      WRITE_ASCII 0x334 ~cdrndm2~ #8  // dialogue
      WRITE_BYTE  0x270 0 // make visible
    END ELSE BEGIN
      WRITE_ASCII 0x280 ~cdrndm2~ #32 // dv
      WRITE_ASCII 0x2cc ~cdrndm2~ #8  // dialogue
    END

  EXTEND_BOTTOM ~ar9100.bcs~ ~cdtweaks/baf/ar9100.baf~

  COPY ~weidu_external/cdtweaks/random/cdrndm1.d~ ~weidu_external/cdtweaks/random/cdrndm2.d~
    REPLACE_TEXTUALLY ~31426\([0-9]\)~ ~31428\1~
    REPLACE_TEXTUALLY ~CDRNDM1~ ~CDRNDM3~
    REPLACE_TEXTUALLY ~CDRNDM2~ ~CDRNDM1~
    REPLACE_TEXTUALLY ~CDRNDM3~ ~CDRNDM2~

  COMPILE ~weidu_external/cdtweaks/random/cdrndm2.d~

END

COMPILE ~weidu_external/cdtweaks/random/cdrndm1.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// never lose access to Orrick's trade goods        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @325000 DESIGNATED 3250
GROUP @4
REQUIRE_PREDICATE GAME_IS ~iwd how totlm iwd-in-bg2~ @25 // functionality built into iwdee already
LABEL ~cd_tweaks_orrick_stock~

COMPILE ~cdtweaks/dlg/orrickadd.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Recoverable ammunition                           \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// 25% chance to recover after a successful hit     \\\\\
/////                                                  \\\\\

BEGIN @326001 DESIGNATED 3260 // 25% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_25~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 25
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 50% chance to recover after a successful hit     \\\\\
/////                                                  \\\\\

BEGIN @326100 DESIGNATED 3261 // 50% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_50~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 50
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 75% chance to recover after a successful hit     \\\\\
/////                                                  \\\\\

BEGIN @326200 DESIGNATED 3262 // 75% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_75~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 75
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 100% chance to recover after a successful hit    \\\\\
/////                                                  \\\\\

BEGIN @326300 DESIGNATED 3263 // 100% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_100~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 100
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 25% chance to recover after a successful hit,    \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326400 DESIGNATED 3264 // 25% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_25_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 25
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 50% chance to recover after a successful hit,    \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326500 DESIGNATED 3265 // 50% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_50_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 50
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 75% chance to recover after a successful hit,    \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326600 DESIGNATED 3266 // 75% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_75_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 75
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 100% chance to recover after a successful hit,   \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326700 DESIGNATED 3267 // 100% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @326000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_ammo_100_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_AMMO_RECOVERABLE
  INT_VAR
    probability       = 100
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Recoverable throwing weapons                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// 25% chance to recover after a successful hit     \\\\\
/////                                                  \\\\\

BEGIN @326001 DESIGNATED 3270 // 25% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_25~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 25
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 50% chance to recover after a successful hit     \\\\\
/////                                                  \\\\\

BEGIN @326100 DESIGNATED 3271 // 50% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_50~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 50
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 75% chance to recover after a successful hit     \\\\\
/////                                                  \\\\\

BEGIN @326200 DESIGNATED 3272 // 75% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_75~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 75
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 100% chance to recover after a successful hit    \\\\\
/////                                                  \\\\\

BEGIN @326300 DESIGNATED 3273 // 100% chance to recover after a successful hit
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_100~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 100
    ids_target        = 2
    ids_value         = 0
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 25% chance to recover after a successful hit,    \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326400 DESIGNATED 3274 // 25% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_25_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 25
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 50% chance to recover after a successful hit,    \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326500 DESIGNATED 3275 // 50% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_50_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 50
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 75% chance to recover after a successful hit,    \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326600 DESIGNATED 3276 // 75% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_75_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 75
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END

/////                                                  \\\\\
///// 100% chance to recover after a successful hit,   \\\\\
///// vs. enemies only                                 \\\\\
/////                                                  \\\\\

BEGIN @326700 DESIGNATED 3277 // 100% chance to recover after a successful hit, vs. enemies only
GROUP @4
SUBCOMPONENT @327000 // Recoverable ammunition
REQUIRE_PREDICATE NOT GAME_IS ~bg1 iwd how totlm iwd2 pst~ @25 // no eff files
LABEL ~cd_tweaks_recover_throwing_weapons_100_enemy_only~

INCLUDE ~cdtweaks/lib/recoverable_weapons.tpa~

LAF MAKE_WEAPONS_RECOVERABLE
  INT_VAR
    probability       = 100
    ids_target        = 2
    ids_value         = 200
  STR_VAR
    eff_template_file = ~cdtweaks/eff/a7_hitfx.eff~
END


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Four weapon slots for all                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @328000 DESIGNATED 3280
GROUP @4
REQUIRE_PREDICATE GAME_IS ~eet bgee bg2ee iwdee~ @25 // EEs (minus pstee)
LABEL ~cd_tweaks_universal_four_weapon_slots~

COPY_EXISTING ~numwslot.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]+\)[123][ %TAB%%MNL%]*$~ ~\14~ // thanks argent77!
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Personalize automatic save names                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Use scheme: 000000000-Protagonist-Save-Name      \\\\\
/////                                                  \\\\\

BEGIN @329001 DESIGNATED 3290
GROUP @4
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~savename.2da~) @25 // EEs (since patch 2.0)
SUBCOMPONENT @329000 // Personalize automatic save names
LABEL ~cd_tweaks_personalize_savename_opt1~

INCLUDE ~cdtweaks/lib/personalized_save_names.tpa~

LAF PERSONALIZE_SAVENAMES
  STR_VAR
    charname_string = ~<CHARNAME>-~
END

/////                                                  \\\\\
///// Use scheme: 000000000-Protagonist Save-Name      \\\\\
/////                                                  \\\\\

BEGIN @329100 DESIGNATED 3291
GROUP @4
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~savename.2da~) @25 // EEs (since patch 2.0)
SUBCOMPONENT @329000 // Personalize automatic save names
LABEL ~cd_tweaks_personalize_savename_opt2~

INCLUDE ~cdtweaks/lib/personalized_save_names.tpa~

LAF PERSONALIZE_SAVENAMES
  STR_VAR
    charname_string = ~<CHARNAME> ~
END

/////                                                  \\\\\
///// Use scheme: 000000000-(Protagonist)-Save-Name    \\\\\
/////                                                  \\\\\

BEGIN @329200 DESIGNATED 3292
GROUP @4
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~savename.2da~) @25 // EEs (since patch 2.0)
SUBCOMPONENT @329000 // Personalize automatic save names
LABEL ~cd_tweaks_personalize_savename_opt3~

INCLUDE ~cdtweaks/lib/personalized_save_names.tpa~

LAF PERSONALIZE_SAVENAMES
  STR_VAR
    charname_string = ~(<CHARNAME>)-~
END

/////                                                  \\\\\
///// Use scheme: 000000000-(Protagonist) Save-Name    \\\\\
/////                                                  \\\\\

BEGIN @329300 DESIGNATED 3293
GROUP @4
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~savename.2da~) @25 // EEs (since patch 2.0)
SUBCOMPONENT @329000 // Personalize automatic save names
LABEL ~cd_tweaks_personalize_savename_opt4~

INCLUDE ~cdtweaks/lib/personalized_save_names.tpa~

LAF PERSONALIZE_SAVENAMES
  STR_VAR
    charname_string = ~(<CHARNAME>) ~
END

/////                                                  \\\\\
///// Use scheme: 000000000-[Protagonist]-Save-Name    \\\\\
/////                                                  \\\\\

BEGIN @329400 DESIGNATED 3294
GROUP @4
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~savename.2da~) @25 // EEs (since patch 2.0)
SUBCOMPONENT @329000 // Personalize automatic save names
LABEL ~cd_tweaks_personalize_savename_opt5~

INCLUDE ~cdtweaks/lib/personalized_save_names.tpa~

LAF PERSONALIZE_SAVENAMES
  STR_VAR
    charname_string = ~\[<CHARNAME>\]-~
END

/////                                                  \\\\\
///// Use scheme: 000000000-[Protagonist] Save-Name    \\\\\
/////                                                  \\\\\

BEGIN @329500 DESIGNATED 3295
GROUP @4
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~savename.2da~) @25 // EEs (since patch 2.0)
SUBCOMPONENT @329000 // Personalize automatic save names
LABEL ~cd_tweaks_personalize_savename_opt6~

INCLUDE ~cdtweaks/lib/personalized_save_names.tpa~

LAF PERSONALIZE_SAVENAMES
  STR_VAR
    charname_string = ~\[<CHARNAME>\] ~
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// death cam                                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @330000 DESIGNATED 3300
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @25
LABEL ~cd_tweaks_death_cam~

COPY_EXISTING_REGEXP GLOB ~^.+\.are$~ ~override~
  WRITE_LONG 0x14 (THIS | BIT4) // add player1-can-die bit
  BUT_ONLY

ACTION_FOR_EACH script IN baldur baldur25 bdbaldur BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%script%.bcs~ THEN BEGIN
  
    EXTEND_TOP ~%script%.bcs~ ~cdtweaks/baf/deathcam.baf~

  END 

END 

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// new games start with party ai turned off         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @331000 DESIGNATED 3310
GROUP @4
REQUIRE_PREDICATE GAME_IS ~soa tob iwd2 pstee bg1 totsc iwd how totlm tutu tutu_totsc bgt ca iwd-in-bg2 bgee bg2ee eet iwdee~ @25   
LABEL ~cd_tweaks_new_game_disable_party_ai~

COPY_EXISTING ~baldur.gam~   ~override~ // all others
              ~icewind.gam~  ~override~ // iwd
              ~icewind2.gam~ ~override~ // iwd2
              ~torment.gam~  ~override~ // pstee
  WRITE_LONG 0x60 (THIS | BIT0) // set party ai disabled bit
  BUT_ONLY IF_EXISTS

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// no store depreciation                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @332000 DESIGNATED 3320
GROUP @4
LABEL ~cd_tweaks_no_store_depreciation~

COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  WRITE_LONG 0x1c 0 // depreciation
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// reduced chance of chunking                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @502000 DESIGNATED 3330
GROUP @4
REQUIRE_PREDICATE GAME_IS ~bg2 tob tutu tutu_totsc bgt ca bgee bg2ee eet~ @25 // requires op 295, so EEs or bg2 only
LABEL ~cd_tweaks_reduced_party_chunking~

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  READ_LONG 0x1cc bio
  PATCH_IF bio >= 0 BEGIN // don't bother if no bio
    LPF ADD_CRE_EFFECT INT_VAR opcode = 295 parameter2 = 1 target = 1 timing = 9 END
  END
  BUT_ONLY
  
ACTION_IF game_includes_soa BEGIN

  COPY_EXISTING ~spin770.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR check_globals = 0 timing = 4 duration = 1 END
    BUT_ONLY
    
  COPY_EXISTING ~ar2904.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~ReallyForceSpell(\([^,]+\),HELL_EXPLODE)~
        ~ReallyForceSpell(\1,HELL_EXPLODE)ActionOverride(\1,LeaveParty())Wait(1)~
    END
    BUT_ONLY
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Increase party movement speed and/or casting     \\\\\
///// speed outside of combat                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Movement speed by 50 percent                     \\\\\
/////                                                  \\\\\

BEGIN @333001 DESIGNATED 3340
GROUP @4
REQUIRE_PREDICATE (NOT GAME_IS ~pstee~ && VALID_SCRIPT_TRIGGERS ~NextTriggerObject(Player1)~) @25
SUBCOMPONENT @333000
LABEL ~cd_tweaks_noncombat_speed_increase_50~

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = 0
    movement_speed = 1
    speed_percent = 150
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END

/////                                                  \\\\\
///// Movement speed by 100 percent                    \\\\\
/////                                                  \\\\\

BEGIN @333002 DESIGNATED 3341
GROUP @4
REQUIRE_PREDICATE (NOT GAME_IS ~pstee~ && VALID_SCRIPT_TRIGGERS ~NextTriggerObject(Player1)~) @25
SUBCOMPONENT @333000
LABEL ~cd_tweaks_noncombat_speed_increase_100~

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = 0
    movement_speed = 1
    speed_percent = 200
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END

/////                                                  \\\\\
///// Movement speed by 150 percent                    \\\\\
/////                                                  \\\\\

BEGIN @333003 DESIGNATED 3342
GROUP @4
REQUIRE_PREDICATE (NOT GAME_IS ~pstee~ && VALID_SCRIPT_TRIGGERS ~NextTriggerObject(Player1)~) @25
SUBCOMPONENT @333000
LABEL ~cd_tweaks_noncombat_speed_increase_150~

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = 0
    movement_speed = 1
    speed_percent = 250
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END

/////                                                  \\\\\
///// Movement speed by 50 percent and casting speed   \\\\\
/////                                                  \\\\\

BEGIN @333004 DESIGNATED 3343
GROUP @4
REQUIRE_PREDICATE (NOT GAME_IS ~pstee~ && VALID_SCRIPT_TRIGGERS ~NextTriggerObject(Player1)~) @25
SUBCOMPONENT @333000
LABEL ~cd_tweaks_noncombat_speed_casting_increase_50~

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = 1
    movement_speed = 1
    speed_percent = 150
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END

/////                                                  \\\\\
///// Movement speed by 100 percent and casting speed  \\\\\
/////                                                  \\\\\

BEGIN @333005 DESIGNATED 3344
GROUP @4
REQUIRE_PREDICATE (NOT GAME_IS ~pstee~ && VALID_SCRIPT_TRIGGERS ~NextTriggerObject(Player1)~) @25
SUBCOMPONENT @333000
LABEL ~cd_tweaks_noncombat_speed_casting_increase_100~

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = 1
    movement_speed = 1
    speed_percent = 200
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END

/////                                                  \\\\\
///// Movement speed by 150 percent and casting speed  \\\\\
/////                                                  \\\\\

BEGIN @333006 DESIGNATED 3345
GROUP @4
REQUIRE_PREDICATE (NOT GAME_IS ~pstee~ && VALID_SCRIPT_TRIGGERS ~NextTriggerObject(Player1)~) @25
SUBCOMPONENT @333000
LABEL ~cd_tweaks_noncombat_speed_casting_increase_150~

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = 1
    movement_speed = 1
    speed_percent = 250
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END

/////                                                  \\\\\
///// Casting speed only                               \\\\\
/////                                                  \\\\\

BEGIN @333007 DESIGNATED 3346
GROUP @4
REQUIRE_PREDICATE (NOT GAME_IS ~pstee~ && VALID_SCRIPT_TRIGGERS ~NextTriggerObject(Player1)~) @25
SUBCOMPONENT @333000
LABEL ~cd_tweaks_noncombat_casting_increase_only~

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = 1
    movement_speed = 0
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Create interval saves                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Every 15 minutes (one save only)                 \\\\\
/////                                                  \\\\\

BEGIN @335001 DESIGNATED 3350
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_one_save_15~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 900
    count = 1
END

/////                                                  \\\\\
///// Every 30 minutes (one save only)                 \\\\\
/////                                                  \\\\\

BEGIN @335002 DESIGNATED 3351
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_one_save_30~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 1800
    count = 1
END

/////                                                  \\\\\
///// Every 60 minutes (one save only)                 \\\\\
/////                                                  \\\\\

BEGIN @335003 DESIGNATED 3352
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_one_save_60~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 3600
    count = 1
END

/////                                                  \\\\\
///// Every 120 minutes (one save only)                \\\\\
/////                                                  \\\\\

BEGIN @335004 DESIGNATED 3353
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_one_save_120~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 7200
    count = 1
END

/////                                                  \\\\\
///// Every 15 minutes (cycle through four saves)      \\\\\
/////                                                  \\\\\

BEGIN @335005 DESIGNATED 3354
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~ && FILE_EXISTS_IN_GAME ~savename.2da~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_four_saves_15~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 900
    count = 4
END

/////                                                  \\\\\
///// Every 30 minutes (cycle through four saves)      \\\\\
/////                                                  \\\\\

BEGIN @335006 DESIGNATED 3355
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~ && FILE_EXISTS_IN_GAME ~savename.2da~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_four_saves_30~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 1800
    count = 4
END

/////                                                  \\\\\
///// Every 60 minutes (cycle through four saves)      \\\\\
/////                                                  \\\\\

BEGIN @335007 DESIGNATED 3356
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~ && FILE_EXISTS_IN_GAME ~savename.2da~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_four_saves_60~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 3600
    count = 4
END

/////                                                  \\\\\
///// Every 120 minutes (cycle through four saves)     \\\\\
/////                                                  \\\\\

BEGIN @335008 DESIGNATED 3357
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~ && FILE_EXISTS_IN_GAME ~savename.2da~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_four_saves_120~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

LAF a7_auto_save
  INT_VAR
    interval = 7200
    count = 4
END

/////                                                  \\\\\
///// Customize                                        \\\\\
/////                                                  \\\\\

BEGIN @335009 DESIGNATED 3358
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee pstee~ && FILE_EXISTS_IN_GAME ~savename.2da~) @25
SUBCOMPONENT @335000
LABEL ~cd_tweaks_create_interval_save_custom~

INCLUDE ~cdtweaks/lib/interval_saves.tpa~

ACTION_IF (FILE_EXISTS ~cdtweaks/cdtweaks.txt~) THEN BEGIN
  INCLUDE ~cdtweaks/cdtweaks.txt~ // config file
END
ACTION_IF (NOT IS_AN_INT ~interval_save_enabled~) BEGIN OUTER_SET interval_save_enabled = 1 END
ACTION_IF (NOT IS_AN_INT ~interval_save_delay~) BEGIN OUTER_SET interval_save_delay = 1800 END
ACTION_IF (NOT IS_AN_INT ~interval_save_combat~) BEGIN OUTER_SET interval_save_combat = 0 END
ACTION_IF (NOT IS_AN_INT ~interval_save_count~) BEGIN OUTER_SET interval_save_count = 1 END
ACTION_IF (NOT IS_AN_INT ~interval_save_custom_slot~) BEGIN OUTER_SET interval_save_custom_slot = 1 END
ACTION_IF (NOT IS_AN_INT ~interval_save_feedback~) BEGIN OUTER_SET interval_save_feedback = 1 END
ACTION_IF (NOT IS_AN_INT ~interval_save_ini~) BEGIN OUTER_SET interval_save_ini = 1 END

// Show configuration info
OUTER_SPRINT msg @335103
OUTER_PATCH_SAVE msg ~%msg%~ BEGIN
  PATCH_IF (interval_save_enabled) BEGIN
    REPLACE_TEXTUALLY ~a7_interval_enable~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_interval_enable~ ~no~
  END
  REPLACE_TEXTUALLY ~a7_interval_delay~ ~%interval_save_delay%~
  PATCH_IF (interval_save_combat) BEGIN
    REPLACE_TEXTUALLY ~a7_interval_combat~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_interval_combat~ ~no~
  END
  REPLACE_TEXTUALLY ~a7_interval_slots~ ~%interval_save_count%~
  PATCH_IF (interval_save_custom_slot) BEGIN
    REPLACE_TEXTUALLY ~a7_interval_custom~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_interval_custom~ ~no~
  END
  PATCH_IF (interval_save_feedback) BEGIN
    REPLACE_TEXTUALLY ~a7_interval_feedback~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_interval_feedback~ ~no~
  END
  PATCH_IF (interval_save_ini) BEGIN
    REPLACE_TEXTUALLY ~a7_interval_ini~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_interval_ini~ ~no~
  END
END
PRINT ~%msg%~

LAF a7_auto_save
  INT_VAR
    enabled     = interval_save_enabled
    combat      = interval_save_combat
    interval    = interval_save_delay
    custom_slot = interval_save_custom_slot
    count       = interval_save_count
    feedback    = interval_save_feedback
    use_ini     = interval_save_ini
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Reset UnderSigil Fog of War                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @336000 DESIGNATED 3360
GROUP @4
REQUIRE_PREDICATE (GAME_IS ~pst pstee~) @25
LABEL ~cd_tweaks_reset_undersigil_fow~

INCLUDE ~cdtweaks/lib/undersigil_fow.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Adjust Evil joinable NPC reaction rolls          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @400000 DESIGNATED 4000
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // Tutu or BGT only
LABEL ~cd_tweaks_adjust_evil_npc_reactions~

COMPILE ~cdtweaks/dlg/evilnpc.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Improved Fate Spirit Summoning                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @401000 DESIGNATED 4010
GROUP @0
REQUIRE_PREDICATE     GAME_IS ~tob bg2ee bgt~   @25 // ToB required
LABEL ~cd_tweaks_improved_fate_spirit_summoning~

// end of SoA cutscenes set extra variable
COPY_EXISTING ~cut100a.bcs~ ~override~
              ~cut100b.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~MultiPlayerSync()~ ~SetGlobal("G#G3.CompletedSoA","GLOBAL",1) MultiPlayerSync()~
  END
  UNLESS ~SetGlobal("G#G3.CompletedSoA","GLOBAL",1)~
  BUT_ONLY

ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_fate_spirit BEGIN
  ~aerie~    => ~aerie~
  ~anomen~   => ~anomen~
  ~cernd~    => ~cernd~
  ~d#silver~ => ~silverstar~
  ~d0alassa~ => ~alassa~
  ~dorn~     => ~dorn~
  ~edwin~    => ~edwin~
  ~fwghar~   => ~ghareth~
  ~haerdali~ => ~HaerDalis~
  ~hexxat~   => ~hexxat~
  ~imoen~    => ~imoen~
  ~imoen2~   => ~imoen~
  ~j#klsy~   => ~kelsey~
  ~jaheira~  => ~jaheira~
  ~jan~      => ~jan~
  ~jcbru~    => ~JCBruce~
  ~keldorn~  => ~keldorn~
  ~kido~     => ~kido~
  ~kindrek~  => ~kindrek~
  ~korgan~   => ~korgan~
  ~mazzy~    => ~mazzy~
  ~minsc~    => ~minsc~
  ~nalia~    => ~nalia~
  ~neera~    => ~neera~
  ~o#xans~   => ~xan~
  ~p#deh~    => ~deheriana~
  ~p#kiv01~  => ~kivan~
  ~rasaad~   => ~rasaad~
  ~sc#hub~   => ~hubelpot~
  ~sola~     => ~Solaufein~
  ~subru~    => ~BonsBruce~
  ~tashia~   => ~tashia~
  ~valygar~  => ~valygar~
  ~vanim~    => ~vanim~
  ~viconia~  => ~viconia~
  ~wilson~  =>  ~wilson~
END

ACTION_PHP_EACH cd_fate_spirit AS script => var BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME "%script%.bcs" BEGIN

    COPY_EXISTING ~%script%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        APPEND_FILE ~cdtweaks/baf/fatespirit.baf~
        REPLACE_TEXTUALLY ~"G3Met_VARIABLE_NAME"~ ~"G3Met_%var%"~
      END
      BUT_ONLY
    
  END

END

COPY_EXISTING ~fatesp.dlg~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~~~~~!Dead("\([^"]+\)")[ |%TAB%|%LNL%|%MNL%|%WNL%]*Global("\([^"]+\)","GLOBAL",0)~ THEN REPLY #\([0-9]+\)~~~~~
    ~~~~~Dead("\1")~ THEN REPLY #\3 DO ~SetGlobal("\2","GLOBAL",2)~ GOTO 7
    IF ~!Dead("\1") Global("\2","GLOBAL",0)~ THEN REPLY #\3~~~~~
  END
  BUT_ONLY

// add extra triggers to fate spirit dialogue
COMPILE ~cdtweaks/dlg/fatesp.d~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// ToB-Style NPCs                                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @402000 DESIGNATED 4020
GROUP @0
REQUIRE_PREDICATE NOT GAME_IS ~iwd2 iwd how totlm pst pstee~ @25 // no joinables in iwd/iwd2; at least possible in iwdee/iwd-in-bg2; pst only uses one creature file per
LABEL ~cd_tweaks_tob_style_npcs~

MKDIR ~weidu_external/cdtweaks~

<<<<<<<<./inlined/tob_lookup.txt
>>>>>>>>

// load patch macro
INCLUDE ~cdtweaks/lib/tob_style_npcs.tpa~

COPY_EXISTING ~pdialog.2da~ ~override~
  COUNT_2DA_COLS col_min
  COUNT_2DA_ROWS col_min rows
  FOR (index = 0 ; index < rows ; ++index) BEGIN
    READ_2DA_ENTRY index 0 col_min curdv //
    DEFINE_ASSOCIATIVE_ARRAY cd_dv_array BEGIN "cdtob_%curdv%" => "%curdv%" END
    INNER_ACTION BEGIN
    
      COPY ~./inlined/tob_lookup.txt~ ~weidu_external/cdtweaks/cdtob_%curdv%.txt~

    END
  END
  BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  READ_LONG 0x1cc bio ELSE "-1"
  PATCH_IF (("%SOURCE_RES%" STRING_COMPARE_CASE "BDIMOEN") AND  // exclude special imoen files
            ("%SOURCE_RES%" STRING_COMPARE_CASE "BDSHIMOE") AND // exclude special imoen files
            ("%SOURCE_RES%" STRING_COMPARE_CASE "FINIMOEN") AND // exclude special imoen files
            (bio >= 0)) BEGIN // don't bother if no bio
    READ_ASCII 0x280 "dvcre" (32) NULL
    PHP_EACH cd_dv_array AS file => dv BEGIN
      PATCH_IF ("%dv%" STRING_COMPARE_CASE "%dvcre%" = 0) BEGIN
        READ_LONG 0x018 xp
        READ_BYTE 0x273 class
        INNER_ACTION BEGIN
          APPEND_OUTER ~weidu_external/cdtweaks/%file%.txt~ ~%SOURCE_RES% %xp% %class%~
        END
      END
    END
  END
  BUT_ONLY

// for creatuers like imoen, split files for cre files with same dv but different classes
ACTION_PHP_EACH cd_dv_array AS file => dv BEGIN

  COPY ~weidu_external/cdtweaks/%file%.txt~ ~weidu_external/cdtweaks/%file%.txt~
    SET split = 0
    COUNT_2DA_ROWS 3 rows
    PATCH_IF (rows > 1) BEGIN // don't bother if only one creature
      READ_2DA_ENTRY 0 2 3 test_class
      FOR (index = 1 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 3 class
        PATCH_IF class != test_class BEGIN
          SET split = 1
          DEFINE_ASSOCIATIVE_ARRAY cd_dv_array_split BEGIN "%class%"      => "%file%_%class%" END
          DEFINE_ASSOCIATIVE_ARRAY cd_dv_array_split BEGIN "%test_class%" => "%file%"         END
        END
      END
    END
    BUT_ONLY

  ACTION_IF split BEGIN

    ACTION_PHP_EACH cd_dv_array_split AS class => dest_file BEGIN

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~%dest_file%.txt~ BEGIN
    
        ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_dv_array BEGIN "%dest_file%" => "%dv%" END

        COPY ~weidu_external/cdtweaks/%file%.txt~ ~weidu_external/cdtweaks/%dest_file%.txt~
          COUNT_2DA_ROWS 3 rows
          FOR (index = 0 ; index < rows ; ++index) BEGIN
            READ_2DA_ENTRY index 2 3 class_check
            PATCH_IF class != class_check BEGIN
              SET_2DA_ENTRY index 2 3 999
            END
          END
          REPLACE_TEXTUALLY ~^.+999[%LNL%%MNL%%WNL%]+~ ~~
          BUT_ONLY

      END

    END

    COPY ~weidu_external/cdtweaks/%file%.txt~ ~weidu_external/cdtweaks/%file%.txt~
      COUNT_2DA_ROWS 3 rows
      READ_2DA_ENTRY 0 2 3 class_check
      FOR (index = 1 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 2 3 class
        PATCH_IF class != class_check BEGIN
          SET_2DA_ENTRY index 2 3 999
        END
      END
      REPLACE_TEXTUALLY ~^.+999[%LNL%%MNL%%WNL%]+~ ~~
      BUT_ONLY
  
    END

END

ACTION_PHP_EACH cd_dv_array AS file => dv BEGIN

  COPY ~weidu_external/cdtweaks/%file%.txt~ ~weidu_external/cdtweaks/%file%.txt~
    COUNT_2DA_ROWS 3 rows
    PATCH_IF (rows > 1) BEGIN // don't bother if only one creature
      READ_2DA_ENTRY 0 1 2 xp   // take first row as comparison point
      READ_2DA_ENTRY 0 0 2 orig
      FOR (index = 1 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 1 2 xp_test
        PATCH_IF (xp_test < xp) BEGIN   // if lower XP creature, use it as comparison instead
          READ_2DA_ENTRY index 0 2 orig
          SET xp = xp_test
        END
      END
      FOR (index = 0 ; index < rows ; ++index) BEGIN
        READ_2DA_ENTRY index 0 2 dest
        INNER_ACTION BEGIN

          ACTION_IF (((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) AND // both exist
                      (~%orig%~ STRING_COMPARE_CASE ~%dest%~)) THEN BEGIN                            // skip self-copy 
  
            COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
              PATCH_PRINT " -- copying %orig% to %dest%..."
              LAUNCH_PATCH_MACRO ~tob_style_npcs~
              BUT_ONLY
  
          END
          
        END
      END
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Allow NPC pairs to separate (SCS borrowing)      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @501000 DESIGNATED 4025
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
LABEL ~cd_tweaks_allow_npc_pairs_to_separate~

ACTION_IF !original_bg1 BEGIN // originally modified a bunch of junk here, but easier just to do original bg1 as a one-off
  
  APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~^0x80101FEF[ %TAB]+CD_STATE_NOTVALID[ %TAB%%LNL%%MNL%%WNL%]~

  WITH_TRA "%tra_location%/english/dw_components.tra" "%tra_location%/%LANGUAGE%/dw_components.tra" BEGIN
    INCLUDE ~cdtweaks/lib/npc_separate.tpa~
  END
  
END ELSE BEGIN
  
  APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~^0x80101FEF[ %TAB]+CD_STATE_NOTVALID[ %TAB%%LNL%%MNL%%WNL%]~
  
  COMPILE ~cdtweaks/dlg/pairsep_bg1.d~ USING "%tra_location%/english/dw_components.tra" "%tra_location%/%LANGUAGE%/dw_components.tra"
  
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Edwin                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4030 // Use BG Stats
GROUP @0
SUBCOMPONENT @403000 // Stat Changes: Edwin
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_edwin_bg~

COPY_EXISTING_REGEXP GLOB ~^_?edwin[0-9_]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x238 9 // 9 STR
    WRITE_BYTE 0x23b 9 // 9 WIS
  END
  BUT_ONLY

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4031 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @403000 // Stat Changes: Edwin
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_edwin_bg2~

COPY_EXISTING_REGEXP GLOB ~^_?edwin[0-9_]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x238 10 // 10 STR
    WRITE_BYTE 0x23b 10 // 10 WIS
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Jaheira                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4040 // Use BG Stats
GROUP @0
SUBCOMPONENT @404000 // Stat Changes: Jaheira
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_jaheira_bg~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08 "strref" ELSE 0
  PATCH_IF (("%strref%" = 9456) OR ("%strref%" = 9475)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x23c 14 // 14 DEX
  END
  BUT_ONLY

ACTION_IF ((FILE_EXISTS_IN_GAME misc5x.itm) AND (!enhanced_edition)) BEGIN // ee restricts this with 319

  COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
    WRITE_BYTE 0x2c 14 // min dex
    BUT_ONLY

END

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4041 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @404000 // Stat Changes: Jaheira
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_jaheira_bg2~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08 "strref" ELSE 0
  PATCH_IF (("%strref%" = 9456) OR ("%strref%" = 9475)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x23c 17 // 17 DEX
  END
  BUT_ONLY

ACTION_IF ((FILE_EXISTS_IN_GAME misc5x.itm) AND (!enhanced_edition)) BEGIN // ee restricts this with 319

  COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
    WRITE_BYTE 0x2c 17 // min dex
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Jaheira to NG                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @405000 DESIGNATED 4050
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_ng_jaheira~

COPY_EXISTING_REGEXP GLOB ~^_?jahei.*\.cre$~ ~override~
  READ_LONG 0x08  "strref" ELSE 0
  READ_BYTE 0x27b "align"  ELSE 0
  PATCH_IF ((("%strref%" = 9456) OR ("%strref%" = 9475)) AND ("%align%" = 34)) THEN BEGIN // exclude harper cre files
    WRITE_BYTE 0x27b 33 // neutral good
  END
  BUT_ONLY

ACTION_IF FILE_EXISTS_IN_GAME misc5x.itm BEGIN

  COPY_EXISTING ~misc5x.itm~ ~override~ // harper pin
    PATCH_IF GAME_IS enhanced_edition BEGIN
      WRITE_LONG 0x1e 0 // nukes all flags since 319 is in effect; shouldn't have any anyway
    END ELSE BEGIN
      READ_BYTE  0x1e "use1"
      READ_BYTE  0x21 "use2"
      WRITE_BYTE 0x1e (("%use1%" BOR 0b00001000) BAND 0b11111011) // adds GE-Neutral flag, removes good flag
      WRITE_BYTE 0x21 ("%use2%" BOR 0b10000000) // adds half-orc flag
    END
    BUT_ONLY

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Minsc                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4060 // Use BG Stats
GROUP @0
SUBCOMPONENT @406000 // Stat Changes: Minsc
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_minsc_bg~

COPY_EXISTING_REGEXP GLOB ~^_?minsc[0-9_]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x23c 15 // 15 DEX
    WRITE_BYTE 0x23d 15 // 15 CON
  END
  BUT_ONLY

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4061 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @406000 // Stat Changes: Minsc
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_minsc_bg2~

COPY_EXISTING_REGEXP GLOB ~^_?minsc[0-9_]*\.cre$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x2d3) THEN BEGIN // protects against invalid files
    WRITE_BYTE 0x23c 16 // 16 DEX
    WRITE_BYTE 0x23d 16 // 16 CON
  END
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Stat changes: Viconia                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// BG Values                                        \\\\\
/////                                                  \\\\\

BEGIN @403001 DESIGNATED 4070 // Use BG Stats
GROUP @0
SUBCOMPONENT @407000 // Stat Changes: Viconia
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_viconia_bg~

COPY_EXISTING_REGEXP GLOB ~^\(bg\|_\)?viconi[0-9]*_?\.cre$~ ~override~
  WRITE_BYTE 0x5d 50
  READ_BYTE  0x23b wis ELSE 15
  PATCH_IF (wis != 15) BEGIN
    READ_LONG 0x2b0 mem_off
    WRITE_BYTE 0x23b 15 // 15 WIS
    READ_LONG  0x2a8 mem_info
    SET spell_delta = 0
    FOR (index = 1 ; index < 17 ; ++index) BEGIN // removes level 2, 3, 4 bonus spell for 18 >> 15 WIS
      WRITE_LONG (mem_info + 0x08 + (index * 0x10)) (THIS + spell_delta)
      PATCH_IF (index < 4) BEGIN // level to delete spells
        READ_LONG  (mem_info + 0x08 + (index * 0x10)) mem_idx
        READ_LONG  (mem_info + 0x0c + (index * 0x10)) mem_num
        PATCH_IF (mem_num > 1) BEGIN
          WRITE_LONG (mem_info + 0x0c + (index * 0x10)) (mem_num - 1)
          SET spell_delta -= 1
          DELETE_BYTES (mem_off + ((mem_idx + mem_num - 1) * 0x0c)) 0x0c
        END
      END
    END
    PATCH_IF spell_delta != 0 BEGIN // don't bother if nothing missing
      PATCH_FOR_EACH offset IN 0x2a0 0x2b0 0x2b8 0x2bc 0x2c4 BEGIN
        READ_LONG offset offset_val
        PATCH_IF (mem_off < offset_val) BEGIN
          WRITE_LONG offset (offset_val + (spell_delta * 0x0c))
        END
      END
    END
  END
  BUT_ONLY

/////                                                  \\\\\
///// BG2 Values                                       \\\\\
/////                                                  \\\\\

BEGIN @403100 DESIGNATED 4071 // Use BG2 Stats
GROUP @0
SUBCOMPONENT @407000 // Stat Changes: Viconia
REQUIRE_PREDICATE GAME_IS ~bgee bg1 totsc tutu tutu_totsc eet bgt bg2ee soa tob~ @25
LABEL ~cd_tweaks_consistent_stats_viconia_bg2~

COPY_EXISTING_REGEXP GLOB ~^\(bg\|_\)?viconi[0-9]*_?\.cre$~ ~override~
  WRITE_BYTE 0x5d 65
  READ_BYTE  0x23b wis ELSE 18
  PATCH_IF (wis != 18) BEGIN
    READ_LONG 0x2b0 mem_off
    WRITE_BYTE 0x23b 18 // 15 WIS
    READ_LONG  0x2a8 mem_info
    SET spell_delta = 0
    FOR (index = 1 ; index < 17 ; ++index) BEGIN // adds level 2, 3, 4 bonus spell for 18 >> 15 WIS
      WRITE_LONG (mem_info + 0x08 + (index * 0x10)) (THIS + spell_delta)
      PATCH_IF (index < 4) BEGIN // level to add spells
        READ_LONG  (mem_info + 0x08 + (index * 0x10)) mem_idx
        READ_LONG  (mem_info + 0x0c + (index * 0x10)) mem_num
        PATCH_IF (mem_num > 0) BEGIN
          WRITE_LONG (mem_info + 0x0c + (index * 0x10)) (mem_num + 1)
          SET spell_delta += 1
          READ_ASCII (mem_off + ((mem_idx + mem_num - 1) * 0x0c)) spell (12)
          INSERT_BYTES (mem_off + ((mem_idx + mem_num - 1) * 0x0c)) 12
          WRITE_ASCIIE (mem_off + ((mem_idx + mem_num - 1) * 0x0c)) ~%spell%~ #12
        END
      END
    END
    PATCH_IF spell_delta != 0 BEGIN // don't bother if nothing missing
      PATCH_FOR_EACH offset IN 0x2a0 0x2b0 0x2b8 0x2bc 0x2c4 BEGIN
        READ_LONG offset offset_val
        PATCH_IF (mem_off < offset_val) BEGIN
          WRITE_LONG offset (offset_val + (spell_delta * 0x0c))
        END
      END
    END
  END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Khalid a Fighter-Mage                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @408000 DESIGNATED 4080
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25
LABEL ~cd_tweaks_make_khalid_fighter_mage~

ACTION_IF original_bg1 BEGIN

  COPY_EXISTING ~khalid.cre~  ~override~
                ~khalid2.cre~ ~override~
                ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    SAY BIO @408001
    PATCH_IF (%SOURCE_SIZE% > 724) BEGIN //ensure file is at least header size
      WRITE_BYTE 0x70 2    // raise longbow prof
      WRITE_BYTE 0x74 0    // remove axe prof
      WRITE_BYTE 0x236 0   // remove 3rd class level
      WRITE_BYTE 0x23a 15  // increase intelligence
      WRITE_BYTE 0x273 7   // change class to fighter/mage
    READ_LONG 0x2a8 memoff
    READ_LONG 0x2ac memnum
    FOR (x=0;x<%memnum%;x+=1) BEGIN
      READ_SHORT (%memoff% + (0x10 * %x%) + 0x0) level
      READ_SHORT (%memoff% + (0x10 * %x%) + 0x6) type
      PATCH_IF ((%level% = 0) AND (%type% = 1)) BEGIN //add info to have a mem spell slot available
        WRITE_SHORT (%memoff% + (0x10 * %x%) + 0x2) 1
        WRITE_SHORT (%memoff% + (0x10 * %x%) + 0x4) 1
      END
    END
    READ_LONG 0x2b0 memspellsoff
      PATCH_IF (%memspellsoff% >= %memoff%) BEGIN
        WRITE_LONG 0x2b0 (%memspellsoff% - 0x10)
      END
    READ_LONG 0x2b8 itemslots
      PATCH_IF (%itemslots% >= %memoff%) BEGIN
        WRITE_LONG 0x2b8 (%itemslots% - 0x10)
      END
    READ_LONG 0x2bc itemoff
      PATCH_IF (%itemoff% >= %memoff%) BEGIN
        WRITE_LONG 0x2bc (%itemoff% - 0x10)
      END
    READ_LONG 0x2c4 fxoff
      PATCH_IF (%fxoff% >= %memoff%) BEGIN
        WRITE_LONG 0x2c4 (%fxoff% - 0x10)
      END
    END
    BUT_ONLY
  
  // add in spells and items
  COPY_EXISTING ~khalid.cre~  ~override~
                ~khalid2.cre~ ~override~
                ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    WRITE_LONG 0x18 4888  // raise XP
    WRITE_SHORT 0x24 10   // current hp
    WRITE_SHORT 0x26 10   // max hp
    WRITE_BYTE 0x52 19    // lower thac0
    WRITE_BYTE 0x66 3     // lower lore
    WRITE_BYTE 0x234 2    // 1st class level
    WRITE_BYTE 0x235 1    // 2nd class level
    REMOVE_CRE_ITEMS
    ADD_CRE_ITEM ~helm01~ #1  #0 #0 ~IDENTIFIED~ ~HELMET~
    ADD_CRE_ITEM ~sw1h01~ #1  #0 #0 ~IDENTIFIED~ ~WEAPON1~
    ADD_CRE_ITEM ~bow03~  #1  #0 #0 ~IDENTIFIED~ ~WEAPON2~ EQUIP TWOHANDED
    ADD_CRE_ITEM ~arow01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER1~
    ADD_CRE_ITEM ~potn08~ #1  #0 #0 ~IDENTIFIED~ ~QITEM1~
    ADD_KNOWN_SPELL ~SPWI114~ #0 ~wizard~
    ADD_KNOWN_SPELL ~SPWI116~ #0 ~wizard~
    ADD_KNOWN_SPELL ~SPWI108~ #0 ~wizard~
    ADD_KNOWN_SPELL ~SPWI112~ #0 ~wizard~
    ADD_KNOWN_SPELL ~SPWI119~ #0 ~wizard~
    ADD_KNOWN_SPELL ~SPWI110~ #0 ~wizard~
    ADD_KNOWN_SPELL ~SPWI102~ #0 ~wizard~
    ADD_MEMORIZED_SPELL ~SPWI102~ #0 ~wizard~ (1)

  COPY_EXISTING ~khalid2.cre~ ~override~
                ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    WRITE_LONG 0x18 9944  // raise XP
    WRITE_SHORT 0x24 12   // current hp
    WRITE_SHORT 0x26 12   // max hp
    WRITE_BYTE 0x52 18    // lower thac0
    WRITE_BYTE 0x54 13    // improve save (khalid2)
    WRITE_BYTE 0x66 6     // lower lore
    WRITE_BYTE 0x72 1     // raise blunt prof (khalid2, 4, 6)
    WRITE_BYTE 0x234 3    // 1st class level
    WRITE_BYTE 0x235 2    // 2nd class level
    ADD_CRE_ITEM ~brac01~ #0  #0 #0 ~IDENTIFIED~ ~GLOVES~
    ADD_MEMORIZED_SPELL ~SPWI112~ #0 ~wizard~ (1)

  COPY_EXISTING ~khalid4.cre~ ~override~
                ~khalid6.cre~ ~override~
    WRITE_LONG 0x18 18822 // raise XP
    WRITE_SHORT 0x24 16   // current hp
    WRITE_SHORT 0x26 16   // max hp
    WRITE_BYTE 0x66 9     // lower lore
    WRITE_BYTE 0x6e 2     // lower large sword prof (khalid4, 6)
    WRITE_BYTE 0x72 1     // raise blunt prof (khalid2, 4, 6)
    WRITE_BYTE 0x234 4    // 1st class level
    WRITE_BYTE 0x235 3    // 2nd class level
    ADD_CRE_ITEM ~clck14~ #0  #0 #0 ~IDENTIFIED~ ~ARMOR~
    ADD_KNOWN_SPELL ~SPWI212~ #0 ~wizard~
    ADD_MEMORIZED_SPELL ~SPWI212~ #1 ~wizard~ (1)

  COPY_EXISTING ~khalid6.cre~ ~override~
    WRITE_LONG 0x18 43588 // raise XP
    WRITE_SHORT 0x24 20   // current hp
    WRITE_SHORT 0x26 20   // max hp
    WRITE_BYTE 0x52 16    // lower thac0
    WRITE_BYTE 0x66 15    // lower lore
    WRITE_BYTE 0x6e 2     // lower large sword prof (khalid4, 6)
    WRITE_BYTE 0x72 1     // raise blunt prof (khalid2, 4, 6)
    WRITE_BYTE 0x234 5    // 1st class level
    WRITE_BYTE 0x235 5    // 2nd class level
    ADD_CRE_ITEM ~ring06~ #0  #0 #0 ~IDENTIFIED~ ~LRING~
    ADD_KNOWN_SPELL ~SPWI213~ #0 ~wizard~
    ADD_KNOWN_SPELL ~SPWI303~ #0 ~wizard~
    ADD_MEMORIZED_SPELL ~SPWI112~ #0 ~wizard~ (2)
    ADD_MEMORIZED_SPELL ~SPWI213~ #1 ~wizard~ (1)
    ADD_MEMORIZED_SPELL ~SPWI303~ #2 ~wizard~ (1)

END ELSE BEGIN // tutu, bgt, bgee

  COPY_EXISTING ~%tutu_var%khalid2.cre~ ~override~
    READ_ASCII 0x034 portrait (16) // read portrait from existing cre file
    READ_ASCII 0x0a4 soundset (396) // read soundset from existing cre file
    // detect if walking speed altered
    READ_BYTE  0x033 fx_type
    READ_LONG  0x2c4 fx_off
    READ_LONG  0x2c8 fx_num
    FOR (index = 0 ; index < fx_num ; ++index) BEGIN
      READ_SHORT (fx_off + (0x08 * fx_type) + (index * ((fx_type * 0xd8) + 0x30))) op
      PATCH_IF ((op = 176) OR (op = 126)) BEGIN
        SET index = fx_num // kill loop
        SET fx_type = 4
      END
    END
    BUT_ONLY
  
  COPY ~cdtweaks/cre/khalid.cre~  ~override/%tutu_var%khalid.cre~
       ~cdtweaks/cre/khalid2.cre~ ~override/%tutu_var%khalid2.cre~
       ~cdtweaks/cre/khalid4.cre~ ~override/%tutu_var%khalid4.cre~
       ~cdtweaks/cre/khalid6.cre~ ~override/%tutu_var%khalid6.cre~
    WRITE_EVALUATED_ASCII 0x34 "%portrait%" #16
    WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
    SAY BIO @408001
    WRITE_ASCIIE 0x248 ~%tutu_var%khalid~  // override script
    WRITE_ASCIIE 0x250 ~%tutu_var%shout~   // class script
    WRITE_ASCIIE 0x260 ~%tutu_scriptw%tasight~// general script
    WRITE_ASCIIE 0x268 ~%tutu_var%dplayer~ // default script
    WRITE_ASCIIE 0x2cc ~%tutu_var%khalid~  // dialogue file
    ADD_CRE_ITEM ~%tutu_var%BOW03~  #0  #0 #0 ~IDENTIFIED~ ~WEAPON2~ EQUIP TWOHANDED
    ADD_CRE_ITEM ~%tutu_var%AROW01~ #40 #0 #0 ~IDENTIFIED~ ~QUIVER1~
    ADD_CRE_ITEM ~%tutu_var%HELM01~ #0  #0 #0 ~IDENTIFIED~ ~HELMET~
    ADD_CRE_ITEM ~%tutu_var%SW1H01~ #0  #0 #0 ~IDENTIFIED~ ~WEAPON1~
    ADD_CRE_ITEM ~%tutu_var%POTN08~ #0  #0 #0 ~IDENTIFIED~ ~QITEM1~
    READ_LONG 0x18 xp
    PATCH_IF (xp > 5000) BEGIN // all but khalid
      ADD_CRE_ITEM ~%tutu_var%BRAC03~ #0 #0 #0 ~IDENTIFIED~ ~GLOVES~
      PATCH_IF (xp > 10000) BEGIN // now exclude khalid2
        ADD_CRE_ITEM ~%tutu_var%CLCK14~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~
        PATCH_IF (xp > 20000) BEGIN // now exclude khalid4
          ADD_CRE_ITEM ~%tutu_var%RING06~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
        END
      END        
    END
    PATCH_IF fx_type = 4 BEGIN
      LPF ADD_CRE_EFFECT INT_VAR opcode = 176 target = 1 parameter1 = 0xfffffffd timing = 9 END
    END
  
  COMPILE ~cdtweaks/dlg/p#khalid.d~ EVALUATE_BUFFER

  ACTION_IF GAME_IS enhanced_edition BEGIN
  
    COPY_EXISTING ~%KHALID_BCS%.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,32000,SET)\)~ ~\1 AddSpecialAbility("SPWI211") AddSpecialAbility("SPWI212")~
        REPLACE_TEXTUALLY ~\(ChangeStat(Myself,XP,16000,SET)\)~ ~\1 AddSpecialAbility("SPWI212")~
      END 
      BUT_ONLY
      
    ACTION_IF FILE_EXISTS_IN_GAME ~khalid7.cre~ BEGIN // sod khalid
  
      COPY ~cdtweaks/cre/khalid7.cre~ ~override~
        SAY BIO @408001
        PATCH_IF fx_type = 4 BEGIN
          LPF ADD_CRE_EFFECT INT_VAR opcode = 176 target = 1 parameter1 = 0xfffffffd timing = 9 END
        END
    
    END
    
  END  

  EXTEND_TOP    ~%KHALID_BCS%.bcs~ ~cdtweaks/baf/p#khalid.baf~
  
  // if bg-style or iwd-style proficiencies are installed, adjust proficiencies to match new system
  ACTION_IF (((MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2161~) OR (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2162~)) OR // bg-style profs
             ((MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2163~) OR (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2164~))) THEN BEGIN // iwd-style profs
    
    ACTION_IF ((MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2161~) OR (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2162~)) THEN BEGIN // bg-style profs
    
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_khalid_profs BEGIN
        104 => 100 // longbow > bow
         89 => 103 // bastard sword > large swords
//      106 => 106 // dart > missile
      END 

    END ELSE BEGIN // iwd-style profs  
    
      ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_khalid_profs BEGIN
        104 =>  95 // longbow > bow
         89 =>  99 // bastard sword > great swords
        106 => 104 // dart > missile
      END 
      
    END 

    COPY_EXISTING ~%tutu_var%khalid.cre~  ~override~
                  ~%tutu_var%khalid2.cre~ ~override~
                  ~%tutu_var%khalid4.cre~ ~override~
                  ~%tutu_var%khalid6.cre~ ~override~
                  ~khalid7.cre~           ~override~
      PATCH_PHP_EACH cd_khalid_profs AS old => new BEGIN
        LPF ALTER_EFFECT INT_VAR match_opcode = 233 match_parameter2 = old parameter2 = new silent = 1 END
      END
      BUT_ONLY IF_EXISTS
      
  END

END // end bg1 vs. bg2 check
  
// if tob-style npcs are installed, then adjust higher level creatures
ACTION_IF (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~4020~) THEN BEGIN

  // load patch macro
  INCLUDE ~cdtweaks/lib/tob_style_npcs.tpa~
  
  ACTION_CLEAR_ARRAY cd_tob_style_npcs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_tob_style_npcs BEGIN
    ~_khalid2~ => ~_khalid~
    ~_khalid4~ => ~_khalid~
    ~_khalid6~ => ~_khalid~
    ~khalid2~  => ~khalid~
    ~khalid4~  => ~khalid~
    ~khalid6~  => ~khalid~
  END
  
  ACTION_PHP_EACH cd_tob_style_npcs AS dest => orig BEGIN
  
    ACTION_IF ((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) THEN BEGIN

      COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
        LAUNCH_PATCH_MACRO ~tob_style_npcs~
        BUT_ONLY
  
    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Montaron an Assassin                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @409000 DESIGNATED 4090
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bgee tutu tutu_totsc eet bgt~ @25
LABEL ~cd_tweaks_make_montaron_assassin~

COPY_EXISTING ~%tutu_var%montar.cre~ ~override~
  READ_ASCII 0x0a4 soundset (396) // read soundset from existing cre file
  // detect if walking speed altered
  READ_BYTE  0x033 fx_type
  READ_LONG  0x2c4 fx_off
  READ_LONG  0x2c8 fx_num
  FOR (index = 0 ; index < fx_num ; ++index) BEGIN
    READ_SHORT (fx_off + (0x08 * fx_type) + (index * ((fx_type * 0xd8) + 0x30))) op
    PATCH_IF ((op = 176) OR (op = 126)) BEGIN
      SET index = fx_num // kill loop
      SET fx_type = 4
    END
  END
  BUT_ONLY

COPY ~cdtweaks/cre/_montar.cre~  ~override/%tutu_var%montar.cre~
     ~cdtweaks/cre/_montar2.cre~ ~override/%tutu_var%montar2.cre~
     ~cdtweaks/cre/_montar4.cre~ ~override/%tutu_var%montar4.cre~
     ~cdtweaks/cre/_montar6.cre~ ~override/%tutu_var%montar6.cre~
  SAY NAME1 #2425
  SAY NAME2 #2425
  WRITE_EVALUATED_ASCII 0xa4 "%soundset%" #396
  WRITE_ASCIIE 0x34  ~%tutu_scriptm%ontars~ #8   // small portrait
  WRITE_ASCIIE 0x248 ~%tutu_scriptm%ontaron~  // Override Script
  WRITE_ASCIIE 0x250 ~%tutu_var%shout~        // Class Script
  WRITE_ASCII  0x258 ~~ #8                    // Race Script
  WRITE_ASCIIE 0x260 ~%tutu_scriptw%tasight~  // General Script
  WRITE_ASCIIE 0x268 ~%tutu_var%dplayer~ #8   // default Script
  WRITE_ASCII  0x280 ~montaron~ #18           // Death Variable
  WRITE_ASCIIE 0x2cc ~%tutu_var%montar~ #8    // Dialogue file
  PATCH_IF tutu_gen BEGIN
    WRITE_ASCII 0x3c ~_ontarl~   // large portrait
  END ELSE BEGIN
    WRITE_ASCII 0x3c  ~montarm~ #8   // large portrait
    // use BGT item references instead of Tutu
    READ_LONG  0x2bc "itm_off" ELSE 0
    READ_LONG  0x2c0 "itm_num" ELSE 0
    FOR (index = 0 ; index < itm_num ; index = index + 1) BEGIN // searches through items
      READ_ASCII ("%itm_off%" + (0x14 * "%index%")) "item"
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_leat04" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "leat04" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_sw1h07" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "sw1h07" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_potn08" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "potn08" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_dagg05" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "dagg05" #8 // corrected resref
      END ELSE
      PATCH_IF ("%item%" STRING_COMPARE_CASE "_potn14" = 0) BEGIN // find invalid resref
        WRITE_ASCII ("%itm_off%" + (0x14 * "%index%")) "potn14" #8 // corrected resref
      END
    END
  END
  PATCH_IF fx_type = 4 BEGIN
    LPF ADD_CRE_EFFECT INT_VAR opcode = 176 target = 1 parameter1 = 0xfffffffd timing = 9 END
  END

// if bg-style proficiencies are installed, adjust proficiencies to match new system
ACTION_IF ((MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2161~) OR (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2162~)) THEN BEGIN

  COPY_EXISTING ~%tutu_var%montar.cre~  ~override~
                ~%tutu_var%montar2.cre~ ~override~
                ~%tutu_var%montar4.cre~ ~override~
                ~%tutu_var%montar6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 233 END
      LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 102 timing = 9 END // 1* dagger > 1* small sword
      LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 103 timing = 9 END // 1* short sword > 1* large sword
    END
    BUT_ONLY

  COPY_EXISTING ~%tutu_var%montar4.cre~ ~override~
                ~%tutu_var%montar6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      PATCH_IF (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2162~) BEGIN // no weapon styles
        LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 105 timing = 9 END // 1* single weapon > 1* blunt
      END ELSE BEGIN
        LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 113 timing = 9 END // 1* single weapon
      END
    END
    BUT_ONLY

END

// if iwd-style proficiencies are installed, adjust proficiencies to match new system
ACTION_IF ((MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2163~) OR (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2164~)) THEN BEGIN

  COPY_EXISTING ~%tutu_var%montar.cre~  ~override~
                ~%tutu_var%montar2.cre~ ~override~
                ~%tutu_var%montar4.cre~ ~override~
                ~%tutu_var%montar6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode = 233 END
      LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 115 timing = 9 END // 1* dagger
      LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 105 timing = 9 END // 1* short sword
    END
    BUT_ONLY

  COPY_EXISTING ~%tutu_var%montar4.cre~ ~override~
                ~%tutu_var%montar6.cre~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
      PATCH_IF (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2164~) BEGIN // no weapon styles
        LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 102 timing = 9 END // 1* single weapon > 1* large sword
      END ELSE BEGIN
        LPF ADD_CRE_EFFECT INT_VAR opcode = 233 parameter1 = 1 parameter2 = 113 timing = 9 END // 1* single weapon
      END
    END
    BUT_ONLY

END

// if tob-style npcs are installed, then adjust higher level creatures
ACTION_IF (MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~4020~) THEN BEGIN

  // load patch macro
  INCLUDE ~cdtweaks/lib/tob_style_npcs.tpa~
  
  ACTION_CLEAR_ARRAY cd_tob_style_npcs
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_tob_style_npcs BEGIN
    _montar2 => _montar
    _montar4 => _montar
    _montar6 => _montar
    montar2  => montar
    montar4  => montar
    montar6  => montar
  END
  
  ACTION_PHP_EACH cd_tob_style_npcs AS dest => orig BEGIN
  
    ACTION_IF ((FILE_EXISTS_IN_GAME ~%orig%.cre~) AND (FILE_EXISTS_IN_GAME ~%dest%.cre~)) THEN BEGIN

      COPY_EXISTING ~%orig%.cre~ ~override/%dest%.cre~
        LAUNCH_PATCH_MACRO ~tob_style_npcs~
        BUT_ONLY
  
    END

  END

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Change Korgan to NE                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @410000 DESIGNATED 4100
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bgt soa tob bg2ee eet~ @25 // bg2 only
LABEL ~cd_tweaks_ne_korgan~

COPY_EXISTING_REGEXP GLOB ~^korgan[0-9]*.*\.cre$~ ~override~
  WRITE_BYTE 0x27b 35 // neutral evil
  BUT_ONLY
  
ACTION_IF FILE_EXISTS_IN_GAME ~korga25p.dlg~ BEGIN
  
  COMPILE ~cdtweaks/dlg/ne_korgan.d~

END  

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Kagain legal CON                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @411000 DESIGNATED 4110
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // Tutu or BGT only
LABEL ~cd_tweaks_kagain_legal_con~

COPY_EXISTING_REGEXP GLOB ~^_?kagain[0-9]*.*\.cre$~ ~override~
  WRITE_BYTE 0x23d 19 // constitution
  BUT_ONLY

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Coran Legal DEX                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @412000 DESIGNATED 4120
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // Tutu or BGT only
LABEL ~cd_tweaks_coran_legal_dex~

COPY_EXISTING_REGEXP GLOB ~^_?\(bd\)?coran[0-9]*.*\.cre$~ ~override~
  WRITE_BYTE 0x23c 19 // 19 DEX
  BUT_ONLY


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Xan a Generalist Mage                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @413000 DESIGNATED 4130
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // Tutu or BGT only
LABEL ~cd_tweaks_make_xan_generic_mage~

ACTION_INCLUDE "%MOD_FOLDER%/lib/make_generalist.tpa"

COPY_EXISTING_REGEXP "^%tutu_scriptbg%xan.cre$" "override"
                     "^_?xan[0-9_]*.cre$" "override"
  LAUNCH_PATCH_MACRO mh_make_generalist
  BUT_ONLY IF_EXISTS


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Dynaheir a Generalist Mage                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @413100 DESIGNATED 4131
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // Tutu or BGT only
LABEL ~cd_tweaks_make_dynaheir_generic_mage~

ACTION_INCLUDE "%MOD_FOLDER%/lib/make_generalist.tpa"

COPY_EXISTING_REGEXP "^_?dynahe[0-9]*.cre$" "override"
  LAUNCH_PATCH_MACRO mh_make_generalist
  BUT_ONLY


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Xzar a Generalist Mage                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @413200 DESIGNATED 4132
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // Tutu or BGT only
LABEL ~cd_tweaks_make_xzar_generic_mage~

ACTION_INCLUDE "%MOD_FOLDER%/lib/make_generalist.tpa"

COPY_EXISTING_REGEXP "^%tutu_scriptbg%xzar.cre$" "override"
                     "^_?xzar[0-9_]*.cre$" "override"
  LAUNCH_PATCH_MACRO mh_make_generalist
  BUT_ONLY


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Make Edwin a Generalist Mage                     \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @413300 DESIGNATED 4133
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bg2 tob bgt eet bgee bg2ee~ @25 // All BG games
LABEL ~cd_tweaks_make_edwin_generic_mage~

ACTION_INCLUDE "%MOD_FOLDER%/lib/make_generalist.tpa"

COPY_EXISTING_REGEXP "^_?edwin[0-9_]*.cre$" "override"
  LAUNCH_PATCH_MACRO mh_make_generalist
  BUT_ONLY


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Move Boo out of Minsc's quick-access             \\\\\
///// (SCS borrowing)                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @503000 DESIGNATED 4150
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bg2 tob bgt eet bgee bg2ee~ @25 // All BG games
LABEL ~cd_tweaks_move_boo_into_inventory~

COPY_EXISTING_REGEXP ~^_?minsc.*\.cre$~ ~override~
  SET boo = "-1"
  READ_LONG 0x2bc item_off
  READ_LONG 0x2c0 item_num
  FOR (index = 0 ; index < item_num ; ++index) BEGIN
    READ_ASCII (item_off + 0x00 + (index * 0x14)) item
    PATCH_IF ("%item%" STRING_COMPARE_CASE "misc84" = 0) BEGIN
      SET boo = index
      SET index = item_num // kill loop
    END
  END
  PATCH_IF boo >= 0 BEGIN
    READ_LONG 0x2b8 slot_off
    FOR (index = 0 ; index < 37 ; ++index) BEGIN
      READ_SHORT (slot_off + (index * 0x02)) item_idx
      PATCH_IF item_idx = boo BEGIN
        WRITE_SHORT (slot_off + (index * 0x02)) "-1" // delete all existing boo references
      END
    END    
    FOR (index = 21 ; index < 37 ; ++index) BEGIN  
      READ_SHORT (slot_off + (index * 0x02)) item_idx
      PATCH_IF item_idx = 0xffff BEGIN              // if empty slot...
        WRITE_SHORT (slot_off + (index * 0x02)) boo // add boo
        SET index = 37 // kill loop
      END
    END    
  END
  BUT_ONLY  

// bg2 ub compat
ACTION_FOR_EACH spell IN wwboo wwfboo wwnoboo BEGIN

  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.spl~ BEGIN
  
    COPY_EXISTING "%spell%.spl" override
      LPF ALTER_EFFECT STR_VAR check_globals = 0 match_opcode = 143 opcode = 122 parameter1 = 0 END
      BUT_ONLY

  END

END


/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Let Yeslick use axes                             \\\\\
///// (SCS borrowing)                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @504000 DESIGNATED 4160
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // BG content games
LABEL ~cd_tweaks_yeslick_can_use_axes~

INCLUDE ~cdtweaks/lib/yeslick_axes.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Protect Shar-Teel                                \\\\\
///// (SCS borrowing)                                  \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @505000 DESIGNATED 4170
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bgt eet bgee~ @25 // Tutu or BGT only
LABEL ~cd_tweaks_protect_sharteel~

ACTION_IF original_bg1 BEGIN // original bg - no min hp effect, so overcharge her HP and end fight when she's back to her normal HP
    
  ADD_SPELL ~cdtweaks/spl/cdshart.spl~ 3 1 CD_SHARTEEL_HP_REDUCTION 
  
  COPY_EXISTING ~shartel2.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~HPPercentLT(Myself,50)~ ~HPPercentLT(Myself,66)~
      REPLACE_TEXTUALLY ~HPPercentLT(Myself,75)~ ~HPPercentLT(Myself,90)~
    END
  
  <<<<<<<< ./inline/cdsharteel.baf
    IF
      InParty(Myself)
      Global("CD_SHARTEEL_HP_REDUCTION","GLOBAL",0)
    THEN
      RESPONSE #100
        ApplySpell(Myself,CD_SHARTEEL_HP_REDUCTION)
        SetGlobal("CD_SHARTEEL_HP_REDUCTION","GLOBAL",1)
    END
  >>>>>>>>
  
END ELSE BEGIN 

  <<<<<<<< ./inline/cdsharteel.baf
    IF
      InParty(Myself)
      HasItem("monhp1",Myself)
    THEN
      RESPONSE #100
        DestroyItem("monhp1")
    END
  >>>>>>>>

END   

COPY_EXISTING ~%tutu_var%sharte.cre~  ~override~
              ~%tutu_var%sharte4.cre~ ~override~
              ~%tutu_var%sharte6.cre~ ~override~
  PATCH_IF original_bg1 BEGIN
    WRITE_BYTE 0x24 ((THIS * 3) / 2) // increase by 50%
    WRITE_BYTE 0x26 ((THIS * 3) / 2) // increase by 50%
  END ELSE BEGIN    
    ADD_CRE_ITEM monhp1 #0 #0 #0 none ~RRING LRING AMULET BELT BOOTS CLOAK GLOVES ARMOR HELMET~
  END  
  
EXTEND_TOP ~%tutu_scripts%harteel.bcs~ ~./inline/cdsharteel.baf~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Disable joinable AI scripts                      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @414000 DESIGNATED 4140
GROUP @0
REQUIRE_PREDICATE GAME_IS ~eet bgee bg2ee~ @25 // EEs with joinables
LABEL ~cd_tweaks_disable_joinable_ai_scripts~

COPY_EXISTING ~partyai.2da~ ~override~
  REPLACE_TEXTUALLY ~\([ %TAB%]+\)BD[A-Z]+[ %TAB%]*~ ~\1DEFAULT~
  BUT_ONLY

COPY_EXISTING ~ar0602.bcs~ ~override~ // irenicus's dungeon
              ~ar2600.bcs~ ~override~ // candlekeep
              ~ar4000.bcs~ ~override~ // tob heads
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~[ %TAB%]Global("BD_DEFAI","\(MYAREA\|GLOBAL\)",0)~ ~ False()~ // nuke block that assigns 'advanced ai' to player1
  END
  BUT_ONLY IF_EXISTS

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Removable NPC Items                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @418000 DESIGNATED 4180
GROUP @0
REQUIRE_PREDICATE GAME_IS ~bg1 totsc tutu tutu_totsc bg2 tob bgt eet bgee bg2ee~ @25 // All BG games
LABEL ~cd_tweaks_removable_npc_items~

// some special stuff for edwin; move the amulet effects directly to edwin
COPY_EXISTING ~misc89.itm~                         ~override~ // bg2
              ~%tutu_scriptbg%misc89%eet_var%.itm~ ~override~ // tutu or eet version
  LPF DELETE_EFFECT INT_VAR match_opcode = 42 check_headers = 0 END
  PATCH_IF enhanced_edition BEGIN
    SAY 0x50 @418001 // on EEs, amulet descript mentions extra spells granted, so remove
  END  
  BUT_ONLY IF_EXISTS
  
COPY_EXISTING_REGEXP ~^_?edwin[0-9_]*\.cre$~ ~override~
  FOR (index = 0 ; index < 9 ; ++index) BEGIN
    LPF ADD_CRE_EFFECT INT_VAR opcode = 42 target = 1 parameter1 = 2 parameter2 = (2 ** index) timing = 9 END
  END  
  BUT_ONLY
  
COPY_EXISTING ~npring01.itm~ ~override~ // remove 'cannot be removed' description from nalia's ring
  SAY 0x54 @418002
  BUT_ONLY IF_EXISTS 

ACTION_IF game_is_bgt BEGIN // bgt edwin has special scripting to keep wearing his amulet; disable it

  COPY_EXISTING ~bgedwin.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~!HasItemEquipedReal("MISC89",Myself)~ ~False()~
    END
    BUT_ONLY

END    

ACTION_IF game_includes_bg1 BEGIN // obg, tutu, bgt, bgee, eet
  ACTION_IF !tutu_gen BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_removable_npc_items BEGIN
      alora, ~%tutu_var%alora~, ~%ALORA_BCS%~, bdalora, cdalorab => ~%tutu_var%misc88~               // alora's lucky rabbit foot; bdalora.bcs shouldn't exist
      edwin, ~%tutu_var%edwin~, ~%EDWIN_BCS%~, bdedwin, cdedamul => ~%tutu_scriptbg%misc89%eet_var%~ // edwin's amulet (bg/sod)
    END
  END ELSE BEGIN // tutu breaks its own _foo naming convention, so can't rely on CPM variables here
    ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_removable_npc_items BEGIN
      alora, ~%tutu_var%alora~, ~%ALORA_BCS%~, bdalora, cdalorab => ~misc88~ // alora's lucky rabbit foot; bdalora.bcs shouldn't exist
      edwin, ~%tutu_var%edwin~, ~%EDWIN_BCS%~, bdedwin, cdedamul => ~misc89~ // edwin's amulet (bg/sod)
    END
  END  
END

ACTION_IF game_includes_soa BEGIN // obg2, bgt, bg2ee, eet
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_removable_npc_items BEGIN
    nalia, nalia15, nalia, nali25, cdnalrng => npring01 // nalia's de'Arnise Signet Ring
    edwin, edwin13, edwin, edwi25, cdedamul => misc89   // edwin's amulet (bg2/tob)
  END
END
  

ACTION_PHP_EACH cd_removable_npc_items AS params => item BEGIN // dv, cre file, script, expansion script, cloned item => item

  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ AND FILE_EXISTS_IN_GAME ~%params_1%.cre~ BEGIN
  
    COPY_EXISTING ~%params_1%.cre~ ~override~
      READ_LONG  0x08 name
      READ_ASCII 0x248 script
      PATCH_IF !enhanced_edition BEGIN
        READ_BYTE 0x238 str
        READ_BYTE 0x23a int
        READ_BYTE 0x23b wis
        READ_BYTE 0x23c dex
        READ_BYTE 0x23d con
        READ_BYTE 0x23e chr
        READ_BYTE 0x272 race
        READ_BYTE 0x273 class
        READ_BYTE 0x27b align
      END
      BUT_ONLY      
    
    COPY_EXISTING ~%item%.itm~ ~override~
      WRITE_LONG 0x18 (THIS BOR BIT2) // adds droppable flag
      WRITE_LONG 0x34 0 // make unsellable
      PATCH_IF enhanced_edition BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 power = 1 parameter2 = 11 timing = 2 special = name STR_VAR resource = EVAL ~%params_0%~ END
      END ELSE BEGIN
        WRITE_SHORT 0x26 str
        WRITE_BYTE  0x2a int
        WRITE_BYTE  0x2c dex
        WRITE_BYTE  0x2e wis
        WRITE_BYTE  0x30 con
        WRITE_SHORT 0x32 chr
        SET usability = 0xffffffff
        PATCH_MATCH race WITH
          1 BEGIN SET usability -= BIT27 END // human
          2 BEGIN SET usability -= BIT23 END // elf
          4 BEGIN SET usability -= BIT24 END // dwarf
          3 BEGIN SET usability -= BIT25 END // half-elf
          5 BEGIN SET usability -= BIT26 END // halfling
          6 BEGIN SET usability -= BIT28 END // gnome
          7 BEGIN SET usability -= BIT31 END // half-orc
          DEFAULT  
        END
        PATCH_MATCH class WITH
           1 BEGIN SET usability -= BIT18 END // mage
           2 BEGIN SET usability -= BIT11 END // fighter
           3 BEGIN SET usability -= BIT7  END // cleric
           4 BEGIN SET usability -= BIT22 END // thief
           5 BEGIN SET usability -= BIT6  END // bard
           6 BEGIN SET usability -= BIT20 END // paladin
           7 BEGIN SET usability -= BIT13 END // fighter_mage
           8 BEGIN SET usability -= BIT14 END // fighter_cleric
           9 BEGIN SET usability -= BIT17 END // fighter_thief
          10 BEGIN SET usability -= BIT16 END // fighter_mage_thief
          11 BEGIN SET usability -= BIT30 END // druid
          12 BEGIN SET usability -= BIT21 END // ranger
          13 BEGIN SET usability -= BIT19 END // mage_thief
          14 BEGIN SET usability -= BIT8  END // cleric_mage
          15 BEGIN SET usability -= BIT9  END // cleric_thief
          16 BEGIN SET usability -= BIT12 END // fighter_druid
          17 BEGIN SET usability -= BIT15 END // fighter_mage_cleric
          18 BEGIN SET usability -= BIT10 END // cleric_ranger
          19 BEGIN SET usability -= BIT18 END // sorcerer
          20 BEGIN SET usability -= BIT29 END // monk
          21 BEGIN SET usability -= BIT30 END // shaman
          DEFAULT  
        END
        PATCH_MATCH align WITH
          17 BEGIN SET usability -= (BIT4 + BIT2) END // lawful_good
          18 BEGIN SET usability -= (BIT4 + BIT3) END // lawful_neutral
          19 BEGIN SET usability -= (BIT4 + BIT1) END // lawful_evil
          33 BEGIN SET usability -= (BIT5 + BIT2) END // neutral_good
          34 BEGIN SET usability -= (BIT5 + BIT3) END // neutral
          35 BEGIN SET usability -= (BIT5 + BIT1) END // neutral_evil
          49 BEGIN SET usability -= (BIT0 + BIT2) END // chaotic_good
          50 BEGIN SET usability -= (BIT0 + BIT3) END // chaotic_neutral
          51 BEGIN SET usability -= (BIT0 + BIT1) END // chaotic_evil        
          DEFAULT  
        END
        WRITE_LONG 0x1e usability
      END
      BUT_ONLY

    ACTION_IF enhanced_edition BEGIN 
    
      COPY_EXISTING ~item_use.2da~ ~override~
        REPLACE_TEXTUALLY ~^\(%item%[ %TAB%]+[^ %TAB%]+[ %TAB%]+[0-9]+[ %TAB%]+\)[0-9]+~ ~\12~
        BUT_ONLY
    
    END ELSE BEGIN
    
      COPY_EXISTING ~%item%.itm~ ~override/%params_4%.itm~ // clone item
      
      COPY_EXISTING_REGEXP ~^_?%params_0%[0-9_]*\.cre$~ ~override~
        READ_LONG 0x2bc item_off
        READ_LONG 0x2c0 item_num
        FOR (index = 0 ; index < item_num ; ++index) BEGIN
          READ_ASCII (item_off + 0x00 + (index * 0x14)) item_check
          PATCH_IF ("%item_check%" STRING_COMPARE_CASE "%item%" = 0) BEGIN
            WRITE_ASCIIE (item_off + 0x00 + (index * 0x14)) ~%params_4%~ #8
          END
        END
        BUT_ONLY
        
      OUTER_SPRINT item ~%params_4%~ // so that the script extensions, below, use the replacement item
        
    END    
  
    ACTION_FOR_EACH script IN ~%params_2%~ ~%params_3%~ BEGIN    
      ACTION_IF FILE_EXISTS_IN_GAME ~%script%.bcs~ BEGIN      
        EXTEND_TOP ~%script%.bcs~ ~cdtweaks/baf/removable_npc_items.baf~ EVALUATE_BUFFER        
      END
    END      
      
  END // file check
  
END // php_each
