/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Weapon Styles for All (Idobek)                   \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

ACTION_CLEAR_ARRAY style_kits
OUTER_SET $style_kits(MAGE) = 1
OUTER_SET $style_kits(SORCERER) = 19 // shouldn't be in weapprof, but let's be safe
OUTER_SET $style_kits(MONK) = 20
COPY_EXISTING ~kitlist.2da~ ~override~
  SET col_check = (9 + enhanced_edition)
  COUNT_2DA_ROWS col_check rows
  FOR (row = 1 ; row < rows ; ++row) BEGIN
    READ_2DA_ENTRY row 8 col_check class
    PATCH_IF ((class = 1) OR (class = 19) OR (class = 20)) BEGIN // mage or sorcerer or monk
      READ_2DA_ENTRY row 1 col_check kit
      SET $style_kits("%kit%") = class
    END
  END
  BUT_ONLY

COPY_EXISTING ~weapprof.2da~ ~override~
  REPLACE_TEXTUALLY ~\(ID[ %TAB%]+NAME_REF\)~ ~CD_DELETE_ME \1~ // adding column to header column
  COUNT_2DA_COLS col_count
  READ_2DA_ENTRY 30  4 col_count mage_sns
  READ_2DA_ENTRY 30 32 col_count kensai_sns
  SET single  = 2 // if a kit/class ever needs exceptions here, move these into the loop
  SET dual    = 3
  // if mages can use bucklers installed, or some other mod has given mages points for sns
  PATCH_IF mage_sns OR MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2520~ BEGIN
    SET mage_sns = 2
  END ELSE BEGIN
    SET mage_sns = 0
  END
  FOR (col = 4 ; col < col_count ; ++col) BEGIN
    READ_2DA_ENTRY 0 col col_check kit
    SET 2handed = 2
    SET sns     = 2
    PATCH_IF VARIABLE_IS_SET $style_kits("%kit%") BEGIN
      PATCH_IF $style_kits("%kit%") = 20 BEGIN
        SET 2handed = 0
        SET sns     = 0
      END ELSE BEGIN // for mage or sorcerer
        SET sns     = mage_sns
      END
    END
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 29 col 2handed
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 col sns
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 31 col single
    SET_2DA_ENTRY_LATER ~s2el_weapprof~ 32 col dual
  END
  SET_2DA_ENTRY_LATER ~s2el_weapprof~ 30 32 kensai_sns
  SET_2DA_ENTRIES_NOW ~s2el_weapprof~ col_count
  REPLACE_TEXTUALLY ~CD_DELETE_ME~ ~~ // removing it
  PRETTY_PRINT_2DA
  BUT_ONLY
