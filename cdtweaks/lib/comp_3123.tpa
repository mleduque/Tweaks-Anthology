/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// NPCs don't fight                                 \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\


// bogstandard bg2 conflicts; don't bother if Tutu/BGEE game
ACTION_IF game_includes_soa THEN BEGIN

  //anomen-keldorn
  COPY_EXISTING ~anomen.bcs~  ~override~
                ~keldorn.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("AnomenKeldornFight","GLOBAL",1)~ ~False()~
      REPLACE_TEXTUALLY ~Global("AnomenHatesKeldorn","GLOBAL",1)~ ~False()~
    END
    BUT_ONLY
  EXTEND_TOP ~anomen.bcs~  ~cdtweaks/baf/anomkeld.baf~
  EXTEND_TOP ~keldorn.bcs~ ~cdtweaks/baf/anomkeld.baf~

  //edwin-minsc
  COPY_EXISTING ~edwin.bcs~ ~override~
                ~minsc.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("EdwinMinscFight","GLOBAL",1)~ ~False()~
    END
    BUT_ONLY
  EXTEND_TOP ~edwin.bcs~   ~cdtweaks/baf/edwimins.baf~
  EXTEND_TOP ~minsc.bcs~   ~cdtweaks/baf/edwimins.baf~

  //edwin-keldorn
  COPY_EXISTING ~edwin.bcs~   ~override~
                ~keldorn.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("EdwinKeldornFight","GLOBAL",1)~ ~False()~
    END
    BUT_ONLY
  EXTEND_TOP ~edwin.bcs~   ~cdtweaks/baf/edwikeld.baf~
  EXTEND_TOP ~keldorn.bcs~ ~cdtweaks/baf/edwikeld.baf~

  //edwin-valygar
  COPY_EXISTING ~edwin.bcs~   ~override~
                ~valygar.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("EdwinValygarFight","GLOBAL",1)~ ~False()~
    END
    BUT_ONLY
  EXTEND_TOP ~edwin.bcs~   ~cdtweaks/baf/edwivaly.baf~
  EXTEND_TOP ~valygar.bcs~ ~cdtweaks/baf/edwivaly.baf~

  //viconia-keldorn
  COPY_EXISTING ~viconia.bcs~ ~override~
                ~keldorn.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~Global("ViconiaKeldornFight","GLOBAL",1)~ ~False()~
    END
    BUT_ONLY
  EXTEND_TOP ~viconia.bcs~ ~cdtweaks/baf/vicokeld.baf~
  EXTEND_TOP ~keldorn.bcs~ ~cdtweaks/baf/vicokeld.baf~

  // removes korgan-aerie grumpiness
  EXTEND_TOP ~korgan.bcs~ ~cdtweaks/baf/korgan.baf~

  ACTION_IF enhanced_edition THEN BEGIN // for new npcs

    // viconia-rasaad
    COMPILE ~cdtweaks/dlg/happy_bg2ee.d~

    // edwin-neera ultimatum
    EXTEND_BOTTOM ~edwin.bcs~  ~cdtweaks/baf/edwineer.baf~
    EXTEND_BOTTOM ~edwi25.bcs~ ~cdtweaks/baf/edwineer.baf~

    // anomen-dorn
    COPY_EXISTING ~anomen.bcs~ ~override~
                  ~dorn.bcs~   ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("OHD_anomenfight","GLOBAL",1)~ ~False()~
        APPEND_FILE ~cdtweaks/baf/anomdorn.baf~
      END
      BUT_ONLY

    // hexxat-aerie
    COPY_EXISTING ~ohhaeri.bcs~ ~override~
                  ~hexxa25.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("OHH_aeriefight","GLOBAL",1)~ ~False()~
        APPEND_FILE ~cdtweaks/baf/aerihexx.baf~
      END
      BUT_ONLY

    // hexxat-mazzy
    COPY_EXISTING ~ohhmaz.bcs~  ~override~
                  ~ohhmaz2.bcs~ ~override~
                  ~hexxat.bcs~  ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("OHH_mazzyfight","GLOBAL",1)~ ~False()~
        APPEND_FILE ~cdtweaks/baf/hexxmazz.baf~
      END
      BUT_ONLY

    // hexxat-valygar
    COPY_EXISTING ~ohhval.bcs~  ~override~
                  ~ohhval2.bcs~ ~override~
                  ~hexxat.bcs~  ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("OHH_valygarfight","GLOBAL",1)~ ~False()~
        APPEND_FILE ~cdtweaks/baf/hexxvaly.baf~
      END
      BUT_ONLY

    // hexxat-keldorn
    COPY_EXISTING ~ohhkeld.bcs~  ~override~
                  ~ohhkeld2.bcs~ ~override~
                  ~hexxat.bcs~   ~override~
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~Global("OHH_keldornfight","GLOBAL",1)~ ~False()~
        APPEND_FILE ~cdtweaks/baf/hexxkeld.baf~
      END
      BUT_ONLY

  END

END

// bg fights
ACTION_IF game_includes_bg1 THEN BEGIN

  EXTEND_TOP ~%EDWIN_BCS%.bcs~    ~cdtweaks/baf/edwin.baf~
  EXTEND_TOP ~%MINSC_BCS%.bcs~    ~cdtweaks/baf/edwin.baf~
  EXTEND_TOP ~%DYNAHEIR_BCS%.bcs~ ~cdtweaks/baf/edwin.baf~
  EXTEND_TOP ~%YESLICK_BCS%.bcs~  ~cdtweaks/baf/yeslick.baf~
  EXTEND_TOP ~%KAGAIN_BCS%.bcs~   ~cdtweaks/baf/yeslick.baf~
  EXTEND_TOP ~%KHALID_BCS%.bcs~   ~cdtweaks/baf/xzar.baf~
  EXTEND_TOP ~%JAHEIRA_BCS%.bcs~  ~cdtweaks/baf/xzar.baf~
  EXTEND_TOP ~%MONTARON_BCS%.bcs~ ~cdtweaks/baf/xzar.baf~
  EXTEND_TOP ~%XZAR_BCS%.bcs~     ~cdtweaks/baf/xzar.baf~
  EXTEND_TOP ~%KIVAN_BCS%.bcs~    ~cdtweaks/baf/kivan.baf~
  EXTEND_TOP ~%VICONIA_BCS%.bcs~  ~cdtweaks/baf/kivan.baf~
  EXTEND_TOP ~%ELDOTH_BCS%.bcs~   ~cdtweaks/baf/eldoth.baf~
  EXTEND_TOP ~%SHARTEEL_BCS%.bcs~ ~cdtweaks/baf/eldoth.baf~
  EXTEND_TOP ~%TIAX_BCS%.bcs~     ~cdtweaks/baf/tiax.baf~
  EXTEND_TOP ~%QUAYLE_BCS%.bcs~   ~cdtweaks/baf/tiax.baf~

  ACTION_IF enhanced_edition THEN BEGIN

    // %*&#( dorn fights with everyone... sigh
    EXTEND_TOP ~%DORN_BCS%.bcs~ ~cdtweaks/baf/dorndorn.baf~
    ACTION_FOR_EACH script IN
      ~%AJANTIS_BCS%~ ~%ALORA_BCS%~ ~%BRANWEN_BCS%~ ~%CORAN_BCS%~ ~%DYNAHEIR_BCS%~ ~%EDWIN_BCS%~
      ~%ELDOTH_BCS%~ ~%FALDORN_BCS%~ ~%GARRICK_BCS%~ ~%IMOEN_BCS%~ ~%JAHEIRA_BCS%~ ~%KAGAIN_BCS%~
      ~%KHALID_BCS%~ ~%KIVAN_BCS%~ ~%MINSC_BCS%~ ~%MONTARON_BCS%~ ~%NEERA_BCS%~ ~%QUAYLE_BCS%~
      ~%RASAAD_BCS%~ ~%SAFANA_BCS%~ ~%SHARTEEL_BCS%~ ~%SKIE_BCS%~ ~%TIAX_BCS%~ ~%VICONIA_BCS%~
      ~%XAN_BCS%~ ~%XZAR_BCS%~ ~%YESLICK_BCS%~ BEGIN
      EXTEND_TOP ~%script%.bcs~ ~cdtweaks/baf/dorn.baf~

    END

    ACTION_IF FILE_EXISTS_IN_GAME ~bdneera.bcs~ BEGIN // neera-edwin SoD ultimatum
      EXTEND_BOTTOM ~bdneera.bcs~ ~cdtweaks/baf/bdneera.baf~
    END

  END

END
