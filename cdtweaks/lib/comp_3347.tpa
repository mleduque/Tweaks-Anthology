/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Increase party movement speed and/or casting     \\\\\
///// speed outside of combat                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Customize                                        \\\\\
/////                                                  \\\\\

INCLUDE ~cdtweaks/lib/increased_party_movement_speed.tpa~

ACTION_IF (NOT VARIABLE_IS_SET ~party_movement_speed_enabled~) BEGIN
  OUTER_SET party_movement_speed_enabled = 1
END
ACTION_IF (NOT VARIABLE_IS_SET ~party_movement_speed_value~ || party_movement_speed_value <= 0) BEGIN
  OUTER_SET party_movement_speed_value = 200
END
ACTION_IF (NOT VARIABLE_IS_SET ~party_casting_speed_enabled~) BEGIN
  OUTER_SET party_casting_speed_enabled = 0
END
ACTION_IF (NOT VARIABLE_IS_SET ~party_movement_speed_display_icons~) BEGIN
  OUTER_SET party_movement_speed_display_icons = 1
END

// Show configuration details
OUTER_SPRINT msg @333009
OUTER_PATCH_SAVE msg ~%msg%~ BEGIN
  PATCH_IF (party_movement_speed_enabled) BEGIN
    REPLACE_TEXTUALLY ~a7_movement_speed_enable~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_movement_speed_enable~ ~no~
  END
  REPLACE_TEXTUALLY ~a7_movement_speed_value~ ~%party_movement_speed_value%~
  PATCH_IF (party_casting_speed_enabled) BEGIN
    REPLACE_TEXTUALLY ~a7_casting_speed_enable~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_casting_speed_enable~ ~no~
  END
  PATCH_IF (party_movement_speed_display_icons) BEGIN
    REPLACE_TEXTUALLY ~a7_display_portrait_icons~ ~yes~
  END ELSE BEGIN
    REPLACE_TEXTUALLY ~a7_display_portrait_icons~ ~no~
  END
END
PRINT ~%msg%~

LAF A7_INSTALL_AUTOHASTE
  INT_VAR
    casting_speed = party_casting_speed_enabled
    movement_speed = party_movement_speed_enabled
    speed_percent = party_movement_speed_value
    display_icons = party_movement_speed_display_icons
  RET success
END

ACTION_IF (NOT success) BEGIN
  ABORT @28
END