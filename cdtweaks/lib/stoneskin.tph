DEFINE_ACTION_FUNCTION "MORE_STONESKIN_FEEDBACK"
BEGIN
	OUTER_SET "feedback_strref_stoneskin" = RESOLVE_STR_REF (@0)
	OUTER_SET "feedback_strref_skins_left" = RESOLVE_STR_REF (@1)
	//
	WITH_SCOPE BEGIN
		CREATE "spl" "gtstnskn"
		COPY_EXISTING "gtstnskn.spl" "override"
			WRITE_LONG NAME1 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG DESC "-1"
			WRITE_LONG NAME2 "-1"
			WRITE_LONG 0x18 (BIT14 BOR BIT25) // ignore dead/wild magic, castable when silenced
			WRITE_SHORT 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			//
			LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "range" = 30 END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 1 STR_VAR "resource" = "%DEST_RES%" END // invoke lua
		BUT_ONLY
	END
	//
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Misc Tweaks" "sourceFileSpec" = "cdtweaks\luke\lua\tweaks\stoneskin.lua" "destRes" = "m_gttwks" END
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Lua Tools" "sourceFileSpec" = "cdtweaks\luke\lua\tool\find_greatest_int.lua" "destRes" = "m_gttool" END
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Utility Functions / Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\utility\display_sprite_message.lua" "destRes" = "m_gtutil" END
END